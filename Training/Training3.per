;The purpose of this AI is to write an extremely thoroughly documented AI for
;training purposes.
;
;Author:       BearTheGreat
;Last Updated: Feb. 11, 2007
;
;PREREQUISITES:  Be comfortable with the concepts in the preceding AIs.  They 
;will not necessarily be repeated in this AI, but any new changes will be noted
;with "New in Training3".
;
;******************************************************************************
;
; Defconst Rules
;
;******************************************************************************
;
;There are two large advantages to defining constants.  One is that it makes
;the script easier to read.  The other is that it makes the script easier to
;modify.  Let's settle in on some conventions.  All goal numbers will be 
;preceded by "g-", all goal values will be preceded by "gv-".  All regular
;constants will not be preceded by either.

;Need to set a limit on the number of Dark Age Villagers.  Otherwise it may 
;take too long to get out of the Dark Age!

(defconst max-num-dark-villagers 30) 

;It is useful to know your age status for many reasons.

(defconst g-age-status 1) ;Goal 1
(defconst gv-dark-age 0)
(defconst gv-advancing-to-feudal 1)
(defconst gv-feudal-age 2)
(defconst gv-advancing-to-castle 3)
(defconst gv-castle-age 4)
(defconst gv-advancing-to-imperial 5)
(defconst gv-imperial-age 6)

;The following will also prove valuable for a number of future goals

(defconst gv-off 0)
(defconst gv-on 1)
(defconst gv-no 0)
(defconst gv-yes 1)

(defrule
   (true)
   =>
   (set-goal g-age-status gv-dark-age)
   (disable-self)
)

;******************************************************************************
;
; Starting Strategic Numbers
;
;******************************************************************************

(defrule
   (true)
   =>
   (set-strategic-number sn-camp-max-distance 8)
   (set-strategic-number sn-initial-exploration-required 0)
   (set-strategic-number sn-food-dropsite-distance -1)
   (set-strategic-number sn-wood-dropsite-distance -1)
   (set-strategic-number sn-stone-dropsite-distance 100)
   (set-strategic-number sn-gold-dropsite-distance 35)
;New in Training3 - Don't let your villagers walk all the way across the map to
;gather resources.
   (set-strategic-number sn-maximum-food-drop-distance 15)
   (set-strategic-number sn-maximum-wood-drop-distance 15)
   (set-strategic-number sn-maximum-stone-drop-distance 25)
   (set-strategic-number sn-maximum-gold-drop-distance 25)
   (set-strategic-number sn-maximum-hunt-drop-distance 15)
   (set-strategic-number sn-maximum-fish-boat-drop-distance 15)
   (set-strategic-number sn-mill-max-distance 15)
   (disable-self)
)

;New in Training3 - Set your defend priorities.  My thinking on this is that
;the town center is very able to defend itself.  What you want to defend is 
;your ability to gather gold.  Unfortunately, defending wood is not an option.
;This is in preparation for building towers.

(defrule
   (true)
   =>
   (set-strategic-number sn-gold-defend-priority 1)
   (set-strategic-number sn-stone-defend-priority 0)
   (set-strategic-number sn-forage-defend-priority 0)
   (set-strategic-number sn-relic-defend-priority 0)
   (set-strategic-number sn-town-defend-priority 0)
   (disable-self)
)

;******************************************************************************
;
; Market Rules
;
;******************************************************************************

;******************************************************************************
;
; Economy Rules
;
;******************************************************************************

;New in Training3 - the 60F/40W Dark Age gatherer ratio penalizes the Chinese
;because they start with no food.  It is not optimum for the Aztecs because
;they get loom free so their town center sits idle when it goes under 50 food.
;Teuton farms cost less, so they don't need as many lumberjacks.  Celts chop
;wood faster, so they don't need as many lumberjacks.

#load-if-not-defined AZTEC-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined TEUTONIC-CIV


(defrule
   (goal g-age-status gv-dark-age)
   =>
   (set-strategic-number sn-food-gatherer-percentage 60)
   (set-strategic-number sn-wood-gatherer-percentage 40)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

#end-if
#end-if
#end-if
#end-if

#load-if-defined AZTEC-CIV

(defrule
   (goal g-age-status gv-dark-age)
   =>
   (set-strategic-number sn-food-gatherer-percentage 100)
   (set-strategic-number sn-wood-gatherer-percentage 0)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

(defrule
   (goal g-age-status gv-dark-age)
   (unit-type-count villager > 9)
   =>
   (set-strategic-number sn-food-gatherer-percentage 60)
   (set-strategic-number sn-wood-gatherer-percentage 40)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

#end-if

#load-if-defined CELTIC-CIV

(defrule
   (goal g-age-status gv-dark-age)
   =>
   (set-strategic-number sn-food-gatherer-percentage 60)
   (set-strategic-number sn-wood-gatherer-percentage 40)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

(defrule
   (goal g-age-status gv-dark-age)
   (building-type-count-total lumber-camp > 0)
   =>
   (set-strategic-number sn-food-gatherer-percentage 65)
   (set-strategic-number sn-wood-gatherer-percentage 35)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

#end-if

#load-if-defined CHINESE-CIV

(defrule
   (goal g-age-status gv-dark-age)
   =>
   (set-strategic-number sn-food-gatherer-percentage 100)
   (set-strategic-number sn-wood-gatherer-percentage 0)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

(defrule
   (goal g-age-status gv-dark-age)
   (unit-type-count villager > 9)
   =>
   (set-strategic-number sn-food-gatherer-percentage 60)
   (set-strategic-number sn-wood-gatherer-percentage 40)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

#end-if

#load-if-defined TEUTONIC-CIV

(defrule
   (goal g-age-status gv-dark-age)
   =>
   (set-strategic-number sn-food-gatherer-percentage 60)
   (set-strategic-number sn-wood-gatherer-percentage 40)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

(defrule
   (goal g-age-status gv-dark-age)
   (building-type-count-total lumber-camp > 0)
   =>
   (set-strategic-number sn-food-gatherer-percentage 70)
   (set-strategic-number sn-wood-gatherer-percentage 30)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

#end-if

;While advancing to Feudal Age, wood for buildings is suddenly very
;important.  Switch more villagers to wood.  A very useful fact for setting 
;gatherer percentages is that the last valid rule in the file is the one
;that will be implemented.  The AI can read through several valid rules.
;It will only implement the last valid rule.

(defrule
   (goal g-age-status gv-advancing-to-feudal)
   =>
   (set-strategic-number sn-food-gatherer-percentage 40)
   (set-strategic-number sn-wood-gatherer-percentage 50)
   (set-strategic-number sn-gold-gatherer-percentage 10)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

;New in Training3 - Once we have two barracks, let's switch our economy towards
;training villagers and swordsmen.

(defrule
   (goal g-age-status gv-advancing-to-feudal)  
   (building-type-count-total barracks > 1)
   =>
   (set-strategic-number sn-food-gatherer-percentage 55)
   (set-strategic-number sn-wood-gatherer-percentage 35)
   (set-strategic-number sn-gold-gatherer-percentage 10)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

;New in Training3 - Initial gatherer rules for the Feudal Age!

(defrule
   (goal g-age-status gv-feudal-age)  
   =>
   (set-strategic-number sn-food-gatherer-percentage 55)
   (set-strategic-number sn-wood-gatherer-percentage 35)
   (set-strategic-number sn-gold-gatherer-percentage 10)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

;New in Training3 - Add stone miners

(defrule
   (goal g-age-status gv-feudal-age)  
   (building-type-count-total blacksmith > 0)
   (or(building-type-count-total archery-range > 0)
      (building-type-count-total market > 0))
   =>
   (set-strategic-number sn-food-gatherer-percentage 55)
   (set-strategic-number sn-wood-gatherer-percentage 30)
   (set-strategic-number sn-gold-gatherer-percentage 10)
   (set-strategic-number sn-stone-gatherer-percentage 5)
)

;New in Training3 - Noticed I was a little heavy on wood.  Shift some towards
;food and gold.

(defrule
   (goal g-age-status gv-feudal-age)  
   (building-type-count-total blacksmith > 0)
   (or(building-type-count-total archery-range > 0)
      (building-type-count-total market > 0))
   (wood-amount > 200)
   =>
   (set-strategic-number sn-food-gatherer-percentage 60)
   (set-strategic-number sn-wood-gatherer-percentage 20)
   (set-strategic-number sn-gold-gatherer-percentage 15)
   (set-strategic-number sn-stone-gatherer-percentage 5)
)

;******************************************************************************
;
; Research Rules
;
;******************************************************************************

(defrule
   (goal g-age-status gv-advancing-to-feudal)
   (research-available feudal-age)
   =>
   (chat-to-allies "Age research misfire!")
   (set-goal g-age-status gv-dark-age)
)

(defrule
   (can-research feudal-age)
   =>
   (research feudal-age)
   (set-goal g-age-status gv-advancing-to-feudal)
   (chat-to-allies "Researching Feudal Age")
)

(defrule
   (current-age == feudal-age)
   =>
   (set-goal g-age-status gv-feudal-age)
   (disable-self)
)

(defrule
   (goal g-age-status gv-advancing-to-castle)
   (research-available castle-age)
   =>
   (chat-to-allies "Age research misfire!")
   (set-goal g-age-status gv-feudal-age)
)

(defrule
   (can-research castle-age)
   =>
   (research castle-age)
   (set-goal g-age-status gv-advancing-to-castle)
   (chat-to-allies "Researching Castle Age")
)

(defrule
   (current-age == castle-age)
   =>
   (set-goal g-age-status gv-castle-age)
   (disable-self)
)

(defrule
   (goal g-age-status gv-advancing-to-imperial)
   (research-available imperial-age)
   =>
   (chat-to-allies "Age research misfire!")
   (set-goal g-age-status gv-castle-age)
)

(defrule
   (can-research imperial-age)
   =>
   (research imperial-age)
   (set-goal g-age-status gv-advancing-to-imperial)
   (chat-to-allies "Researching Imperial Age")
)

(defrule
   (current-age == imperial-age)
   =>
   (set-goal g-age-status gv-imperial-age)
   (disable-self)
)

(defrule
   (food-amount < 50)
   (can-research ri-loom)
   =>
   (research ri-loom)
   (chat-to-allies "Researching loom.")
)

;New in Training3 - Feudal Age Research Rules!

;***************************** Feudal Age Research ****************************

(defrule
   (can-research ri-wheel-barrow)
   =>
   (research ri-wheel-barrow)
   (chat-to-allies "Researching wheel barrow.")
)

(defrule
   (can-research ri-man-at-arms)
   =>
   (research ri-man-at-arms)
   (chat-to-allies "Researching man at arms.")
)

(defrule
   (not(research-available ri-wheel-barrow))
   (not(research-available ri-man-at-arms))
   (can-research ri-double-bit-axe)
   =>
   (research ri-double-bit-axe)
   (chat-to-allies "Researching double bit axe.")
)

(defrule
   (not(research-available ri-double-bit-axe))
   (can-research ri-gold-mining)
   =>
   (research ri-gold-mining)
   (chat-to-allies "Researching gold mining.")
)

(defrule
   (not(research-available ri-gold-mining))
   (can-research ri-horse-collar)
   =>
   (research ri-horse-collar)
   (chat-to-allies "Researching horse collar.")
)

(defrule
   (not(research-available ri-man-at-arms))
   (can-research ri-scale-mail)
   =>
   (research ri-scale-mail)
   (chat-to-allies "Researching scale mail armor.")
)

(defrule
   (research-completed ri-scale-mail)
   (can-research ri-forging)
   =>
   (research ri-forging)
   (chat-to-allies "Researching forging.")
)

;New in Training3 - The following rule will greatly aid in the TC being able
;to defend itself.

(defrule
   (town-under-attack)
   (can-research ri-fletching)
   =>
   (research ri-fletching)
   (chat-to-allies "Researching fletching.")
)

(defrule
   (or(current-age > feudal-age)
       (goal g-age-status gv-advancing-to-castle))
   (can-research ri-stone-mining)
   =>
   (research ri-stone-mining)
   (chat-to-allies "Researching stone mining.")
)
;******************************************************************************
;
; Escrow Rules
;
;******************************************************************************

;******************************************************************************
;
; Build Rules
;
;******************************************************************************

#load-if-not-defined HUN-CIV

(defrule
   (housing-headroom < 4)
   (population-headroom > 3)
   (can-build house)
   =>
   (build house)
)

;New in Training3 - Increase housing headroom so that your housing will not
;limit your troop production.

(defrule
   (or(current-age > dark-age)
      (and(goal g-age-status gv-advancing-to-feudal)
          (building-type-count-total barracks > 1)))
   (housing-headroom < 10)
   (population-headroom > 3)
   (can-build house)
   =>
   (build house)
)

#end-if

;New in Training3 - Not all maps have forage bushes.  To be able to progress
;with this situation, stop doing your initial exploring after you've built
;a mill.

(defrule
   (game-time < 600)
   (sheep-and-forage-too-far)
   (not(resource-found food)) ;Haven't found the berries yet.
   (building-type-count-total mill == 0)
   =>
   (set-strategic-number sn-percent-civilian-explorers 100)
   (set-strategic-number sn-minimum-civilian-explorers 100)
   (set-strategic-number sn-cap-civilian-explorers 100)
   (set-strategic-number sn-total-number-explorers 101)
   (set-strategic-number sn-number-explore-groups 101)
   (set-strategic-number sn-minimum-explore-group-size 1)
   (set-strategic-number sn-maximum-explore-group-size 1)
)

(defrule
   (or(not(sheep-and-forage-too-far))
   (or(resource-found food)
      (building-type-count-total mill > 0)))
;New in Training3 - Added the following line.  Why implement the following
;actions every rule pass?
   (strategic-number sn-percent-civilian-explorers != 0)
   =>
   (set-strategic-number sn-percent-civilian-explorers 0)
   (set-strategic-number sn-minimum-civilian-explorers 0)
   (set-strategic-number sn-cap-civilian-explorers 0)
   (set-strategic-number sn-total-number-explorers 1)
   (set-strategic-number sn-number-explore-groups 1)
)

;Now let's build a mill.
;New in Training3 - Added the provision for maps with no berries.

(defrule
   (or(building-type-count-total house > 0)
      (civ-selected hun))
   (building-type-count-total mill < 1)
   (or(resource-found food)
      (and(cc-players-unit-type-count 0 59 == 0) ;The map has no berries.
          (not(sheep-and-forage-too-far))))      ;You've found your first sheep.
   (can-build mill)
   =>
   (build mill)
)

;New in Training3 - Increase your mill-max-distance for the second mill.

(defrule
   (building-type-count-total mill > 0)
   =>
   (set-strategic-number sn-mill-max-distance 25)
   (set-strategic-number sn-maximum-hunt-drop-distance 15)
   (disable-self)
)

;Now we'll try to also build a lumber camp.  The default strategic numbers do
;not always work well for the initial camp placement, so hang with me here.
;I copied the following from Elite Raider and it hardly ever fails on a
;standard map.

;START of first lumber camp placement rules.

;The theory is that the amount of time it takes for your scout to see trees is
;related to how far away they are from your camp.

(defrule
   (resource-found wood)
   (strategic-number sn-camp-max-distance == 8)
   (game-time < 4)
   =>
   (set-strategic-number sn-camp-max-distance 11)
   (chat-local-to-self "Camp distance 11.")
   (disable-self)
)

(defrule
   (resource-found wood)
   (strategic-number sn-camp-max-distance == 8)
   (game-time < 7)
   =>
   (set-strategic-number sn-camp-max-distance 13)
   (chat-local-to-self "Camp distance 13.")
   (disable-self)
)

(defrule
   (resource-found wood)
   (strategic-number sn-camp-max-distance == 8)
   (game-time < 12)
   =>
   (set-strategic-number sn-camp-max-distance 15)
   (chat-local-to-self "Camp distance 15.")
   (disable-self)
)

(defrule
   (resource-found wood)
   (strategic-number sn-camp-max-distance == 8)
   (game-time < 16)
   =>
   (set-strategic-number sn-camp-max-distance 17)
   (chat-local-to-self "Camp distance 17.")
   (disable-self)
)

(defrule
   (resource-found wood)
   (strategic-number sn-camp-max-distance == 8)
   (game-time < 25)
   =>
   (set-strategic-number sn-camp-max-distance 20)
   (chat-local-to-self "Camp distance 20.")
   (disable-self)
)

(defrule
   (resource-found wood)
   (strategic-number sn-camp-max-distance == 8)
   (game-time > 25)
   =>
   (set-strategic-number sn-camp-max-distance 22)
   (chat-local-to-self "Camp distance 22.")
   (disable-self)
)

;I added the next two rules as acts of desperation.  They rarely fire on a
;standard map.

(defrule
   (current-age == dark-age)
   (wood-amount > 200)
   (population > 19)
   (building-type-count-total lumber-camp == 0)
   =>
   (set-strategic-number sn-camp-max-distance 35)
   (chat-to-allies "Wood further away than 22 tiles!")
   (chat-local-to-self "Camp distance 35.")
   (disable-self)
)

(defrule
   (current-age == dark-age)
   (wood-amount > 200)
   (population > 21)
   (building-type-count-total lumber-camp == 0)
   =>
   (set-strategic-number sn-camp-max-distance 45)
   (chat-to-allies "Wood further away than 35 tiles!")
   (chat-local-to-self "Camp distance 45.")
   (disable-self)
)

;END of first lumber camp placement rules

;Now that we've hopefully found the trees at a reasonable distance
;from the town center, let's build that lumber camp.

(defrule
   (current-age == dark-age)
   (building-type-count-total mill > 0)
   (resource-found wood)
   (building-type-count-total lumber-camp < 1)
   (can-build lumber-camp)
   =>
   (build lumber-camp)
)

(defrule
   (building-type-count-total lumber-camp > 0)
   (strategic-number sn-camp-max-distance < 35)
   =>
   (set-strategic-number sn-camp-max-distance 35)
   (chat-local-to-self "Camp distance 35.")
   (disable-self)
)

;New in Training3 - one of the saddest things that can happen to an AI is for
;its first lumber camp to be poorly placed.  Be prepared to detect this
;situation and correct it.  The next 3 rules address this situation.

(defrule
   (current-age <= feudal-age)
   (building-type-count lumber-camp == 1)
   (dropsite-min-distance wood > 5)
   =>
   (chat-to-allies "Bad lumber camp.")
   (set-strategic-number sn-camp-max-distance 45)
   (chat-local-to-self "Camp distance 45.")
   (set-strategic-number sn-maximum-wood-drop-distance -1)
   (disable-self)
)

(defrule
   (building-type-count-total lumber-camp == 2)
   =>
   (set-strategic-number sn-maximum-wood-drop-distance 15)
   (disable-self)
)

(defrule
   (or(current-age >= feudal-age)
      (strategic-number sn-camp-max-distance == 45))
   (dropsite-min-distance wood > 5)
   (can-build lumber-camp)
   =>
   (build lumber-camp)
)

;New in Training3 - Build a second lumber camp in the Feudal Age after you
;have a blacksmith and an archery range or a market.  This will be needed
;to prevent crowding at your first lumber camp.  It will also help you out
;if your first lumber camp comes under attack.

(defrule
   (current-age == feudal-age)
   (building-type-count-total blacksmith > 0)
   (or(building-type-count-total archery-range > 0)
      (building-type-count-total market > 0))
   (building-type-count-total lumber-camp < 2)
   (can-build lumber-camp)
   =>
   (build lumber-camp)
)

(defrule
   (or(current-age > dark-age)
      (goal g-age-status gv-advancing-to-feudal))
;New in Training3 - Next line increases odds of getting the first mine at the
;gold.
   (or(unit-type-count 579 > 0)   ;Male gold miner
      (unit-type-count 581 > 0))  ;Female gold miner 
   (resource-found gold)
   (building-type-count mining-camp < 4)
;New in Training3 - There next line detects if there is still any gold on the
;map.
   (cc-players-unit-type-count 0 66 > 0) 
   (dropsite-min-distance gold > 5)
;New in Training3 - The following line will not allow you to build mining camps
;on your island if all the remaining gold is on somebody else's island.
   (dropsite-min-distance gold < 255)
   (can-build mining-camp)
   =>
   (build mining-camp)
)

;New in Training3 - Build a stone mining camp.

(defrule
   (current-age >= feudal-age)
   (building-type-count-total blacksmith > 0)
   (or(building-type-count-total archery-range > 0)
      (building-type-count-total market > 0))
   (resource-found stone)
   (building-type-count mining-camp < 4)
   (dropsite-min-distance stone > 5)
   (building-type-count-total mining-camp < 2)
   (can-build mining-camp)
   =>
   (build mining-camp)
)

(defrule
   (building-type-count-total mining-camp > 0)
   (dropsite-min-distance gold <= 5)
   =>
   (set-strategic-number sn-gold-dropsite-distance -1)
   (set-strategic-number sn-stone-dropsite-distance -1)
   (disable-self)
)

(defrule
   (or(current-age > dark-age)
      (goal g-age-status gv-advancing-to-feudal))
   (building-type-count-total mining-camp > 0)
   (building-type-count-total barracks < 2)
   (can-build barracks)
   =>
   (build barracks)
)

;****************************** Feudal Buildings ******************************

;New in Training3 - Build a blacksmith to upgrade your military!

(defrule
   (current-age >= feudal-age)
   (building-type-count-total barracks > 1)
   (building-type-count-total blacksmith < 1)
   (can-build blacksmith)
   =>
   (build blacksmith)
)

;New in Training3 - If you haven't found any gold, build a market and get ready
;to go to the castle age.

(defrule
   (current-age == feudal-age)
   (building-type-count-total blacksmith > 0)
   (unit-type-count 579 == 0)  ;Male gold miner
   (unit-type-count 581 == 0)  ;Female gold miner 
   (building-type-count-total market < 1)
   (can-build market)
   =>
   (build market)
)

;New in Training3 - Otherwise build an archery range.  Many AIs will counter
;your infantry with archers, so have an archery range ready to counter their 
;archers with skirmishers.

(defrule
   (current-age == feudal-age)
   (building-type-count-total blacksmith > 0)
   (or(unit-type-count 579 > 0)   ;Male gold miner
      (unit-type-count 581 > 0))  ;Female gold miner 
   (building-type-count-total archery-range < 1)
   (can-build archery-range)
   =>
   (build archery-range)
)

;New in Training3 - Build towers once you have a blacksmith if you've found 
;gold.  Otherwise let's save that stone since the plan will be to go for a fast
;castle age.

(defrule
   (current-age == feudal-age)
   (building-type-count-total blacksmith > 0)
   (or(unit-type-count 579 > 0)   ;Male gold miner
      (unit-type-count 581 > 0))  ;Female gold miner 
   (can-build watch-tower)
   =>
   (build watch-tower)
)

;******************************* Farm Building ********************************

(defrule
   (goal g-age-status gv-dark-age)
   (building-type-count-total mill > 0)
   (building-type-count-total lumber-camp > 0)
;New in Training3 - If (dropsite-min-distance wood > 5) then the first lumber
;camp was placed poorly.  Save the wood for a new lumber camp.
   (dropsite-min-distance wood < 6)
   (or(building-type-count-total farm < 15)
      (idle-farm-count == 0))
   (can-build farm)
   =>
   (build farm)
)

(defrule
   (or(current-age > dark-age)
      (goal g-age-status gv-advancing-to-feudal))
   (idle-farm-count == 0)
   (can-build farm)
   =>
   (build farm)
)


;******************************************************************************
;
; Training Rules
;
;******************************************************************************

(defrule
   (current-age == dark-age)
   (unit-type-count-total villager < max-num-dark-villagers)
   (can-train villager)
   =>
   (train villager)
)

(defrule
   (current-age > dark-age) 
   (unit-type-count-total villager < 100)
   (can-train villager)
   =>
   (train villager)
)

(defrule
   (goal g-age-status gv-advancing-to-feudal)
   (can-train militiaman-line)
   =>
   (train militiaman-line)
)

(defrule
   (current-age > dark-age)
   (not(research-available ri-man-at-arms))
   (can-train militiaman-line)
   =>
   (train militiaman-line)
)

;******************************************************************************
;
; Taunting Rules
;
;******************************************************************************

;******************************************************************************
;
; Resignation Rules
;
;******************************************************************************

;******************************************************************************
;
; Town-Size Rules
;
;******************************************************************************

;******************************************************************************
;
; Defend Rules
;
;******************************************************************************

;******************************************************************************
;
; Counter-Attack Rules
;
;******************************************************************************

;******************************************************************************
;
; Attack Rules
;
;******************************************************************************







