(defconst super-traitor-chat-id 22034); I have thought better of my previous merciful plan.
(defconst traitor-chat-id 22097); You cannot possibly defeat my mighty forces.
(defconst betrayed-chat-id 22071); From henceforth we are the bitterest of foes!
(defconst wonder-game-time 3000)
(defconst camp-raid-time-variance 400)
(defconst nomad-tc-up-chat "Me")
(defconst gg-chat "gg")

#load-if-not-defined UP-SCENARIO-GAME

#load-if-not-defined DEATH-MATCH
#load-if-defined CHINESE-CIV
(defrule
	(game-time > 45)
	(game-time < 55)
	(building-type-count-total town-center == 0)
=>
	(chat-to-all "I can't handle this many villagers.")
	(disable-self)
)
#end-if
#end-if

#load-if-defined ARENA-MAP
(defrule
	(up-compare-const gv-2-player-game == 1)
	(civ-selected persian)
	(players-civ any-enemy aztec)
	(game-time > 30)
=>
	(chat-to-all "Expected.")
	(disable-self)
)
#end-if

(defrule
	(game-time < 210)
	(map-type arabia)
	(current-age == dark-age)
	(up-compare-goal threat-time < 3000)
	(up-compare-goal threat-player > 0)
	(or	(up-compare-goal threat-source == scout-cavalry-class)
		(up-compare-goal threat-source == infantry-class))
	(player-human focus-player)
=>
	(up-store-player-name focus-player)
	(up-chat-data-to-all "%s pls" c: 7031232)
	(disable-self)
)

(defrule
	(game-time > 1800)
	(goal MILITARY 1)
	(current-score > 5000)
	(players-score any-ally > 4000)
	(players-score any-enemy > 4000)
	(players-score focus-player < 2700)
	(player-human focus-player)
=>
	(up-store-player-name focus-player)
	(up-chat-data-to-all "Nice score %s" c: 7031232)
	(disable-self)
)

(defrule
	(not	(death-match-game))
	(game-time < 1800)
	(goal gl-rush-suitability-on-map GOOD)
	(building-type-count wall-class < 5)
	(players-building-type-count every-ally stone-wall-line < 5)
	(players-building-type-count every-ally palisade-wall < 5)
	(or	(players-building-type-count target-player stone-wall-line > 13)
		(players-building-type-count target-player palisade-wall > 13))
=>
	(chat-to-all "Are you feeling confident with those walls?")
	(disable-self)
)

(defrule
	(strategic-number sn-nomad-style-start == 0)
	(up-compare-const gv-x-vs-one-challenge == 1)
	(up-players-in-game any >= preferred-player-number)
	(nor	(goal gl-uncommon-settings YES)
		(map-type custom))
	(starting-age == dark-age)
	(player-valid 6)
	(game-time > 150)
	(difficulty <= hard)
	(strategic-number sn-minimum-water-body-size-for-dock == land-map-style)
=>
	(chat-to-all "I hope you're ready for what it takes.")
	(disable-self)
)

#load-if-defined VICTORY-STANDARD
(defrule
	(strategic-number sn-allow-resource-spending > 5)
	(population > almost-full-pop)
	(hold-relics)
	(players-building-type-count wonder every-enemy == 0)
	(up-compare-goal gl-gathered-relics > 4)
=>
	(chat-to-all "Let's see how well you can lay siege.")
	(disable-self)
)

(defrule
	(up-compare-goal victory-time > 0)
	(up-compare-goal victory-time < 7)
	(not	(hold-relics))
	(players-building-type-count every-ally wonder == 0)
	(building-type-count wonder == 0)
	(or	(players-building-type-count any-enemy wonder > 0)
		(enemy-captured-relics))
=>
	(chat-to-all "*ggggggg")
	(disable-self)
)

(defrule
	(up-compare-goal victory-time > 0)
	(up-compare-goal victory-time < 6)
	(or	(players-building-type-count any-ally wonder > 0)
		(or	(building-type-count wonder > 0)
			(hold-relics)))
	(nor	(players-building-type-count any-enemy wonder > 0)
		(enemy-captured-relics))
=>
	(chat-to-all "*gg!")
	(disable-self)
)
#end-if

(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == land-map-style)
	(players-civ focus-player viking)
	(players-current-age focus-player == imperial-age)
	(players-current-age-time focus-player > 4381)
	(current-age == imperial-age)
	(population > almost-full-pop)
	(strategic-number sn-allow-resource-spending > 5)
	(not	(civ-selected viking))
=>
	(chat-to-all "Are you enjoying your Vikings?")
	(disable-self)
)

(defrule
	(players-civ focus-player turkish)
	(game-time > 1500)
	(players-current-age focus-player >= castle-age)
	(players-unit-type-count focus-player spearman-line > 14)
=>
	(chat-to-all "Turkish spearmen, so strong.")
	(disable-self)
)

(defrule
	(up-research-status c: ri-herbal-medicine == research-pending)
=>
	(chat-to-all "It's time to take the most useful upgrade in the game:")
	(chat-to-all "Herbal medicide!")
	(disable-self)
)

(defrule
	(game-time > 20)
	(up-allied-goal any-computer-ally gl-identity == Promi)
=>
	(chat-to-all "Hi Promi, nice to have you as an ally!")
	(disable-self)
)

(defrule
	(or	(players-tribute-memory any-ally food >= 1000)
		(or	(players-tribute-memory any-ally wood >= 1000)
			(or	(players-tribute-memory any-ally gold >= 1000)
				(players-tribute-memory any-ally stone >= 500))))
=>
	(chat-to-allies "Thanks!")
	(disable-self)
)
#end-if