
(defrule
	(up-compare-goal gl-strategy != eagle-monk-rush)
=>
	(up-jump-rule 75)
)

(defrule
	(game-time > 2400)
	(or	(and	(up-compare-goal gl-current-age <= gv-castle)
			(game-time > 2700))
		(or	(players-current-age focus-player == imperial-age)
			(game-time > 3000)))
=>
	(set-goal gl-strategy default)
)

(defrule
	(map-size tiny)
	(strategic-number sn-stone-gatherer-percentage == 0)
	(strategic-number sn-low-eco-push != 1)
	(game-time > 1500)
	(building-type-count-total town-center == 2)
	(stone-amount == 100)
	(goal MILITARY 0)
=>
	(set-goal TC 1)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)

#load-if-defined TINY-MAP
(defrule
	(strategic-number sn-stone-gatherer-percentage == 0)
	(strategic-number sn-low-eco-push != 1)
	(or	(and	(game-time > 1500)
			(and	(stone-amount == 0)
				(goal MILITARY 0)))
		(or	(game-time > 2100)
			(stone-amount == 75)))
=>
	(set-goal TC 1)
	(set-strategic-number sn-stone-gatherer-percentage 5)
	(set-strategic-number sn-stone-dropsite-distance 3)
)
#else
(defrule
	(goal TC 0)
	(strategic-number sn-low-eco-push != 1)
	(game-time > 1200)
=>
	(set-goal TC 1)
	(set-strategic-number sn-stone-gatherer-percentage 5)
	(set-strategic-number sn-stone-dropsite-distance 3)
	(disable-self)
)
#end-if

(defrule
	(goal gl-current-age gv-dark-up)
	(nor	(goal gl-DRUSH YES)
		(goal gl-DRUSH-CONTROL DRUSH-OVER))
=>
	(set-strategic-number sn-food-gatherer-percentage 60)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-add-rams-in-attack 0)
	(set-strategic-number sn-skip-first-mining-camp 1)
)

(defrule
	(goal gl-current-age gv-dark-up)
	(or	(goal gl-DRUSH YES)
		(goal gl-DRUSH-CONTROL DRUSH-OVER))
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 12)
	(set-strategic-number sn-add-rams-in-attack 0)
	(set-strategic-number sn-skip-first-mining-camp 0)
)

(defrule
	(goal gl-current-age gv-dark-up)
	(gold-amount == 100)
	(strategic-number sn-gold-gatherer-percentage == 0)
=>
	(set-strategic-number sn-maximum-gold-drop-distance 20)
	(set-strategic-number sn-gold-gatherer-percentage 5)
)

(defrule
	(up-compare-goal gl-current-age <= gv-feudal)
	(dropsite-min-distance gold > 3)
	(gold-amount >= 112)
	(strategic-number sn-gold-gatherer-percentage != 0)
	(nor	(goal gl-DRUSH YES)
		(goal gl-DRUSH-CONTROL DRUSH-OVER))
=>
	(set-strategic-number sn-maximum-gold-drop-distance 6)
	(set-strategic-number sn-gold-gatherer-percentage 0)
)

(defrule
	(up-compare-goal gl-current-age >= gv-dark-up)
	(up-compare-goal gl-current-age <= gv-feudal)
	(or	(building-type-count-total blacksmith > 0)
		(wood-amount > 360))
=>
	(set-strategic-number sn-food-gatherer-percentage 67)
	(set-strategic-number sn-wood-gatherer-percentage 33)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(building-type-count-total mining-camp > 0)
	(dropsite-min-distance gold < 4)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-maximum-gold-drop-distance 6)
	(set-goal target-system raid)
	(disable-self)
)

(defrule
	(up-compare-goal gl-current-age >= gv-castle)
	(building-type-count-total monastery > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 32)
	(disable-self)
)

(defrule
	(map-size tiny)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(game-time < 1200)
	(building-type-count-total barracks > 0)
	(players-military-population every-enemy > 3)
	(players-current-age every-enemy <= feudal-age)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 39)
	(set-strategic-number sn-gold-gatherer-percentage 21)
	(set-strategic-number sn-monk-trained 1)
	(disable-self)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(goal TC 0)
	(or	(strategic-number sn-low-eco-push != 1)
		(building-type-count-total barracks > 3))
	(building-type-count-total monastery > 1)
=>
	(set-strategic-number sn-food-gatherer-percentage 34)
	(set-strategic-number sn-wood-gatherer-percentage 24)
	(set-strategic-number sn-gold-gatherer-percentage 42)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(goal TC 0)
	(or	(unit-type-count-total spearman-line > 3)
		(goal gl-enemy-strategy KNIGHT-RUSH))
=>
	(set-strategic-number sn-food-gatherer-percentage 44)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 21)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(goal TC 0)
	(strategic-number sn-low-eco-push == 1)
	(building-type-count-total barracks > 2)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 27)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(goal TC 1)
	(building-type-count-total town-center < 3)
	(building-type-count-total monastery > 1)
=>
	(set-strategic-number sn-food-gatherer-percentage 34)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 28)
)

(defrule
	(goal TC 1)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 1)
	(gold-amount < 300)
	(food-amount < 300)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 33)
)

(defrule
	(goal TC 1)
	(building-type-count-total town-center > 2)
	(building-type-count-total monastery > 1)
	(gold-amount < 300)
	(food-amount > 300)
=>
	(set-strategic-number sn-food-gatherer-percentage 32)
	(set-strategic-number sn-wood-gatherer-percentage 20)
	(set-strategic-number sn-gold-gatherer-percentage 43)
)

(defrule
	(goal TC 1)
	(building-type-count-total town-center > 2)
	(gold-amount > 300)
=>
	(set-strategic-number sn-food-gatherer-percentage 41)
	(set-strategic-number sn-wood-gatherer-percentage 31)
	(set-strategic-number sn-gold-gatherer-percentage 23)
)

(defrule
	(goal TC 1)
	(building-type-count-total town-center < 3)
	(or	(unit-type-count-total spearman-line > 3)
		(goal gl-enemy-strategy KNIGHT-RUSH))
=>
	(set-strategic-number sn-food-gatherer-percentage 41)
	(set-strategic-number sn-wood-gatherer-percentage 39)
	(set-strategic-number sn-gold-gatherer-percentage 15)
)

(defrule
	(goal TC 1)
	(building-type-count-total town-center > 2)
	(or	(unit-type-count-total spearman-line > 3)
		(goal gl-enemy-strategy KNIGHT-RUSH))
=>
	(set-strategic-number sn-food-gatherer-percentage 43)
	(set-strategic-number sn-wood-gatherer-percentage 34)
	(set-strategic-number sn-gold-gatherer-percentage 18)
)

(defrule
	(goal gl-current-age gv-castle)
	(game-time < 1700)
	(goal gl-enemy-strategy XBOW-RUSH)
=>
	(set-strategic-number sn-food-gatherer-percentage 29)
	(set-strategic-number sn-wood-gatherer-percentage 44)
	(set-strategic-number sn-gold-gatherer-percentage 27)
)

(defrule
	(goal TC 1)
	(game-time > 2100)
	(food-amount > 600)
	(wood-amount < 200)
	(strategic-number sn-wood-gatherer-percentage < 32)
=>
	(set-strategic-number sn-wood-gatherer-percentage 32)
)

(defrule
	(goal TC 1)
	(game-time > 2100)
	(food-amount > 1200)
	(wood-amount < 200)
	(strategic-number sn-wood-gatherer-percentage < 40)
=>
	(set-strategic-number sn-wood-gatherer-percentage 40)
)

(defrule
	(goal TC 1)
	(building-type-count-total town-center > 3)
	(game-time > 2100)
	(wood-amount > 500)
	(food-amount < 1000)
	(strategic-number sn-wood-gatherer-percentage >= 32)
=>
	(set-strategic-number sn-wood-gatherer-percentage 26)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(up-compare-goal gl-gathered-relics > 0)
	(or	(and	(building-type-count-total monastery > 1)
			(building-type-count-total barracks > 2))
		(or	(unit-type-count-total spearman-line > 3)
			(goal gl-enemy-strategy KNIGHT-RUSH)))
	(nand	(food-amount > 500)
		(gold-amount < 400))
	(strategic-number sn-gold-gatherer-percentage > 18)
=>
	(up-modify-sn sn-gold-gatherer-percentage g:- gl-gathered-relics)
)

(defrule
	(current-age <= castle-age)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(up-compare-goal gl-current-age <= gv-castle)
	(players-military-population focus-player > 4)
	(players-current-age focus-player == feudal-age)
=>
	(set-goal gl-eagles flush-counter-eagles)
	(chat-to-player my-player-number "Enemy flushing!")
	(disable-self)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(goal gl-eagles flush-counter-eagles)
	(up-compare-goal gl-eagle-warrior-count-total > 10)
=>
	(set-goal gl-eagles 0)
	(disable-self)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(current-age == imperial-age)
	(not	(research-available ri-elite-eagle-warrior))
=>
	(set-goal gl-eagles heavy-eagles)
	(disable-self)
)

(defrule
	(not	(goal gl-advanced-archery backup-xbows))
	(players-current-age focus-player == castle-age)
	(current-age == castle-age)
	(research-completed ri-chain-mail)
	(game-time < 1560)
	(unit-type-count-total monk > 3)
	(strategic-number sn-threat-level > -2)
=>
	(set-strategic-number sn-threat-level -2)
)

(defrule
	(current-age >= castle-age)
	(or	(players-current-age every-enemy <= feudal-age)
		(and	(research-completed ri-elite-eagle-warrior)
			(and	(research-completed ri-theocracy)
				(and	(research-completed ri-block-printing)
					(players-current-age focus-player <= castle-age)))))
	(research-completed ri-chain-mail)
	(strategic-number sn-threat-level > -4)
=>
	(set-strategic-number sn-threat-level -4)
)

(defrule
	(players-current-age focus-player == castle-age)
	(or	(and	(strategic-number sn-threat-level == -4)
			(game-time < 1830))
		(or	(and	(or	(strategic-number sn-threat-level == -2)
					(strategic-number sn-threat-level == 0))
				(game-time > 1560))
			(and	(goal gl-advanced-archery backup-xbows)
				(unit-type-count-total archer-line > 12))))
	(current-age == castle-age)
=>
	(set-strategic-number sn-threat-level -1)
)

(defrule
	(or	(goal gl-advanced-archery backup-xbows)
		(and	(research-available ri-pikeman)
			(and	(or	(up-compare-goal gl-enemy-heavy-cavalry-count > 3)
					(goal gl-enemy-strategy KNIGHT-RUSH))
				(goal MILITARY 0))))
	(current-age == castle-age)
=>
	(set-strategic-number sn-threat-level 1)
)

(defrule
	(players-current-age focus-player == castle-age)
	(or	(and	(strategic-number sn-threat-level < 0)
			(or	(up-research-status c: ri-chain-mail != research-complete)
				(and	(research-available ri-pikeman)
					(or	(up-compare-goal gl-enemy-heavy-cavalry-count > 3)
						(goal gl-enemy-strategy KNIGHT-RUSH)))))
		(and	(strategic-number sn-threat-level == 1)
			(up-research-status c: ri-pikeman >= research-pending)))
=>
	(set-strategic-number sn-threat-level 0)
)

(defrule
	(players-current-age focus-player == castle-age)
	(current-age == castle-age)
	(strategic-number sn-threat-level < 0)
	(or	(and	(game-time > 1830)
			(or	(up-compare-goal gl-enemy-heavy-cavalry-count > 3)
				(or	(players-unit-type-count focus-player scout-cavalry-line > 3)
					(players-unit-type-count focus-player cavalry-archer-line > 3))))
		(players-building-type-count target-player watch-tower > 3))
=>
	(set-strategic-number sn-threat-level 0)
)

(defrule
	(players-current-age focus-player > castle-age)
	(current-age == castle-age)
	(up-compare-goal gl-assisting-ally != YES)
	(strategic-number sn-threat-level < 2)
=>
	(set-strategic-number sn-threat-level 2)
)

(defrule
	(up-compare-goal gl-current-age >= gv-castle-up)
=>
	(set-strategic-number sn-allow-resource-spending 2)
	(disable-self)
)

(defrule
	(research-completed ri-elite-eagle-warrior)
	(research-completed ri-theocracy)
	(research-completed ri-block-printing)
	(or	(strategic-number sn-allow-resource-spending < 6)
		(and	(or	(strategic-number sn-threat-level == -4)
				(strategic-number sn-threat-level > -1))
			(players-current-age focus-player == imperial-age)))
=>
	(set-strategic-number sn-allow-resource-spending 6)
	(set-strategic-number sn-threat-level -1)
)

; ================= AGE UP

(defrule
	(current-age == dark-age)
	(goal POSITION FLANK)
	(food-amount > 430)
	(or	(up-compare-goal gl-lured-deer-count < 3)
		(up-compare-goal gl-my-villager-count-total > 27))
	(up-compare-goal gl-my-villager-count-total > 26)
=>
	(set-strategic-number sn-enable-training-queue 0)
	(disable-self)
)

(defrule
	(or	(and	(current-age == feudal-age)
			(current-age-time < 45))
		(or	(unit-type-count-total villager < 27)
			(and	(player-valid 3)
				(and	(unit-type-count-total villager < 30)
					(goal POSITION POCKET)))))
	(can-train villager)
=>
	(train villager)
)

(defrule
	(goal gl-current-age gv-dark-up)
=>
	(set-goal gl-FC-type 2-villagers)
	(set-goal gl-required-current-age-time 45)
	(disable-self)
)

; ================= RESEARCHES

(defrule
	(or	(map-size tiny)
		(game-time > 1600))
	(nand	(building-type-count-total town-center > 1)
		(building-type-count-total farm < 15))
	(current-age == castle-age)
	(research-available ri-chain-mail)
	(or	(and	(goal MILITARY 1)
			(game-time > 1300))
		(goal gl-eagles flush-counter-eagles))
=>
	(up-modify-escrow food c:max 200)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(or	(and	(can-research ri-scale-mail)
			(or	(current-age-time > 160)
				(current-age >= castle-age)))
		(can-research-with-escrow ri-chain-mail))
=>
	(release-escrow food)
	(release-escrow gold)
	(research ri-scale-mail)
	(research ri-chain-mail)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(up-compare-goal gl-eagle-warrior-count-total > 3)
	(nand	(research-available ri-pikeman)
		(unit-type-count-total spearman-line > 5))
	(research-completed ri-chain-mail)
	(or	(can-research ri-forging)
		(can-research ri-iron-casting))
=>
	(research ri-forging)
	(research ri-iron-casting)
)

(defrule
	(or	(up-compare-goal gl-current-age >= gv-castle-up)
		(or	(town-under-attack)
			(goal MILITARY 1)))
	(nand	(research-available ri-pikeman)
		(unit-type-count-total spearman-line > 5))
	(research-completed ri-iron-casting)
	(up-compare-goal gl-eagle-warrior-count-total > 5)
	(can-research ri-squires)
=>
	(research ri-squires)
)

(defrule
	(or	(unit-type-count-total spearman-line > 4)
		(goal gl-enemy-strategy KNIGHT-RUSH))
	(can-research-with-escrow ri-pikeman)
=>
	(release-escrow food)
	(release-escrow gold)
	(set-escrow-percentage food 0)
	(set-escrow-percentage gold 0)
	(research ri-pikeman)
)

(defrule
	(or	(unit-type-count-total monk > 2)
		(and	(or	(players-unit-type-count any-enemy knight-line > 0)
				(town-under-attack))
			(unit-type-count-total monk > 0)))
	(can-research ri-sanctity)
=>
	(research ri-sanctity)
)

(defrule
	(or	(players-unit-type-count focus-player battering-ram-line > 0)
		(or	(up-compare-goal gl-enemy-onager-count > 0)
			(or	(up-compare-goal gl-enemy-scorpion-count > 0)
				(or	(players-building-type-count target-player watch-tower > 0)
					(or	(players-military-population focus-player < 5)
						(and	(goal MILITARY 0)
							(enemy-buildings-in-town)))))))
	(can-research ri-redemption)
=>
	(research ri-redemption)
)

(defrule
	(or	(current-age-time > 200)
		(goal gl-eagles heavy-eagles))
	(unit-type-count-total monk > 1)
	(or	(can-research ri-fervor)
		(or	(can-research ri-theocracy)
			(or	(can-research ri-block-printing)
				(can-research ri-illumination))))
=>
	(research ri-fervor)
	(research ri-theocracy)
	(research ri-block-printing)
	(research ri-illumination)
)

(defrule
	(building-type-count-total monastery > 1)
	(or	(can-research-with-escrow ri-heavy-plow)
		(and	(can-research-with-escrow ri-gold-shaft-mining)
			(nand	(goal MILITARY 1)
				(unit-type-count-total battering-ram-line < 1))))
=>
	(release-escrow food)
	(release-escrow wood)
	(research ri-heavy-plow)
	(research ri-gold-shaft-mining)
)

; ================= BUILDINGS

(defrule
	(goal MILITARY 0)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(strategic-number sn-add-rams-in-attack == 0)
=>
	(set-strategic-number sn-add-rams-in-attack 1)
	(disable-timer 2)
)

(defrule
	(goal MILITARY 1)
	(game-time > 1400)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(strategic-number sn-add-rams-in-attack == 1)
=>
	(enable-timer 2 120)
	(set-strategic-number sn-add-rams-in-attack 0)
)

(defrule
	(goal MILITARY 1)
	(timer-triggered 2)
	(game-time > 1400)
	(building-type-count-total farm > 9)
	(strategic-number sn-add-rams-in-attack == 0)
	(building-type-count-total siege-workshop < 1)
	(wood-amount < 200)
=>
	(up-modify-escrow wood c:max 200)
	(set-strategic-number sn-add-rams-in-attack 2)
)

(defrule
	(strategic-number sn-add-rams-in-attack == 2)
	(or	(building-type-count-total siege-workshop > 0)
		(or	(wood-amount >= 200)
			(or	(goal MILITARY 0)
				(building-type-count-total farm <= 7))))
=>
	(release-escrow wood)
	(set-strategic-number sn-add-rams-in-attack 0)
)

(defrule
	(or	(and	(goal MILITARY 1)
			(and	(timer-triggered 2)
				(game-time > 1400)))
		(and	(game-time < 1700)
			(goal gl-enemy-strategy XBOW-RUSH)))
	(building-type-count-total siege-workshop < 1)
	(can-build-with-escrow siege-workshop)
=>
	(release-escrow wood)
	(set-strategic-number sn-placement-fail-delta 0)
	(up-set-placement-data my-player-number -1 c: 0)
	(up-modify-sn sn-placement-zone-size g:= gl-my-maximum-town-size)
	(up-build place-control 0 c: siege-workshop)
)

(defrule
	(up-compare-goal gl-current-age >= gv-dark-up)
	(or	(up-compare-goal gl-current-age >= gv-feudal-up)
		(wood-amount > 450))
	(or	(and	(building-type-count-total mining-camp < 2)
			(dropsite-min-distance gold > 3))
		(and	(game-time > 2100)
			(wood-amount > 450)))
	(building-type-count-total mining-camp < relative-6-camps)
	(can-build mining-camp)
=>
	(set-strategic-number sn-allow-adjacent-dropsites 0)
	(build mining-camp)
)

(defrule
	(goal gl-advanced-archery backup-xbows)
	(building-type-count-total town-center > 1)
	(or	(building-type-count-total town-center > 2)
		(building-type-count-total archery-range < 2))
	(building-type-count-total archery-range < 4)
=>
	(up-modify-goal gl-range-build-priority c:max 900)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(building-type-count-total mining-camp > 0)
	(or	(building-type-count-total barracks < 2)
		(building-type-count-total monastery > 1))
	(or	(building-type-count-total barracks < 3)
		(building-type-count-total monastery > 2))
	(building-type-count-total barracks < 5)
	(or	(goal gl-advanced-archery 0)
		(building-type-count-total town-center > 2))
=>
	(up-modify-goal gl-barracks-build-priority c:max 900)
)

(defrule
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(building-type-count-total barracks > 0)
	(building-type-count-total monastery < 1)
	(nand	(goal gl-eagles flush-counter-eagles)
		(building-type-count-total barracks < 2))
=>
	(up-modify-goal gl-monastery-build-priority c:max 900)
)

(defrule
	(goal gl-resources-on-map regular)
	(strategic-number sn-minimum-water-body-size-for-dock == land-map-style)
	(building-type-count-total farm < 5)
	(current-age == dark-age)
	(building-type-count-total lumber-camp > 1)
=>
	(set-goal gl-farming 1)
)

(defrule
	(up-compare-goal gl-current-age < gv-feudal-up)
=>
	(up-jump-rule 1)
)

(defrule
	(or	(research-completed ri-horse-collar)
		(current-age-time g:> gl-current-age-time-for-farms))
	(or	(building-type-count-total monastery > 0)
		(building-type-count-total farm < 8))
	(or	(food-amount < high-castle-food)
		(or	(gold-amount < high-castle-gold)
			(or	(research-available imperial-age)
				(up-compare-goal gl-current-age != gv-castle))))
	(building-type-count-total farm g:< gl-max-farm-count)
=>
	(set-goal gl-farming 1)
)

(defrule
	(or	(building-type-count-total town-center > 1)
		(game-time > 1620))
	(building-type-count-total mining-camp < 3)
	(resource-found gold)
	(can-build mining-camp)
=>
	(set-strategic-number sn-allow-adjacent-dropsites 0)
	(set-strategic-number sn-camp-max-distance 32)
	(build mining-camp)
)

; ================= UNITS

(defrule
	(current-age == castle-age)
	(nand	(goal MILITARY 1)
		(unit-type-count-total battering-ram-line < 1))
	(or	(players-unit-type-count focus-player archer-line > 7)
		(goal gl-enemy-strategy XBOW-RUSH))
	(or	(players-unit-type-count focus-player archer-line > 13)
		(game-time < 1700))
	(unit-type-count-total scorpion-line < 8)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)

(defrule
	(or	(unit-type-count-total monk-set < 12)
		(strategic-number sn-low-eco-push == 1))
	(unit-type-count-total monk-set < 24)
	(nand	(unit-type-count-total monk-set > 9)
		(and	(goal gl-enemy-strategy EAGLERUSH)
			(and	(current-age == castle-age)
				(game-time < 2100))))
	(can-train monk)
=>
	(train monk)
	(set-strategic-number sn-monk-trained 1)
)

(defrule
	(or	(food-amount < high-castle-food)
		(or	(town-under-attack)
			(up-compare-goal gl-current-age >= gv-castle-up)))
	(or	(goal gl-eagles heavy-eagles)
		(current-age == castle-age))
	(or	(goal gl-advanced-archery backup-xbows)
		(players-unit-type-count focus-player jaguar-man-line > 2))
	(unit-type-count-total archer-line < 35)
	(can-train archer-line)
=>
	(train archer-line)
)

(defrule
	(or	(and	(goal gl-eagles flush-counter-eagles)
			(and	(food-amount > 60)
				(current-age == castle-age)))
		(or	(up-compare-goal gl-current-age >= gv-castle-up)
			(and	(current-age-time < 250)
				(players-military-population every-enemy > 9))))
	(up-compare-goal gl-eagle-warrior-count-total < 17)
	(can-train eagle-warrior-line)
=>
	(train eagle-warrior-line)
)

(defrule
	(food-amount > 84)
	(players-current-age focus-player >= castle-age)
	(nand	(research-available ri-pikeman)
		(unit-type-count-total spearman-line > 5))
	(nand	(up-compare-goal gl-eagle-warrior-count-total < 16)
		(unit-type-count-total spearman-line > 14))
	(unit-type-count-total spearman-line < 35)
	(or	(players-unit-type-count focus-player scout-cavalry-line > 5)
		(or	(up-compare-goal gl-enemy-heavy-cavalry-count > 3)
			(goal gl-enemy-strategy KNIGHT-RUSH)))
	(can-train spearman-line)
=>
	(train spearman-line)
)

(defrule
	(current-age == castle-age)
	(food-amount >= 60)
	(or	(up-compare-goal gl-eagle-warrior-count-total < 6)
		(and	(strategic-number sn-monk-trained == 1)
			(or	(food-amount < high-castle-food)
				(or	(town-under-attack)
					(up-compare-goal gl-current-age >= gv-castle-up)))))
	(nand	(unit-type-count-total spearman-line > 5)
		(research-available ri-pikeman))
	(can-train eagle-warrior-line)
=>
	(train eagle-warrior-line)
)

; ================= MARKET

(defrule
	(goal gl-current-age gv-feudal)
	(gold-amount < 200)
	(stone-amount >= 200)
	(can-sell-commodity stone)
=>
	(sell-commodity stone)
)

(defrule
	(current-age >= castle-age)
	(food-amount > 400)
	(gold-amount < 100)
	(can-sell-commodity food)
	(or	(commodity-selling-price food >= 45)
		(and	(commodity-selling-price food >= 35)
			(food-amount > 650)))
=>
	(sell-commodity food)
)

(defrule
	(or	(soldier-count > 6)
		(research-completed ri-chain-mail))
	(current-age >= castle-age)
=>
	(enable-timer 1 0)
	(enable-timer 7 0)
	(set-strategic-number sn-percent-attack-soldiers 100)
	(disable-self)
)

; ================= PUSHING WITH LOW ECO

(defrule
	(goal gl-advanced-archery 0)
	(current-age == castle-age)
	(game-time < 1600)
	(not	(player-valid 3))
	(or	(players-building-type-count focus-player castle > 0)
		(players-unit-type-count focus-player jaguar-man-line > 2))
	(players-civ focus-player aztec)
=>
	(chat-to-player my-player-number "1Add xbows against jaguars")
	(set-goal gl-advanced-archery backup-xbows)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(goal gl-advanced-archery backup-xbows)
	(current-age == castle-age)
	(game-time < 2130)
	(players-current-age focus-player == imperial-age)
	(players-unit-type-count focus-player eagle-warrior-line > 5)
	(players-civ any-enemy aztec)
=>
	(chat-to-player my-player-number "24Give up with xbows against fast imp+eew")
	(set-goal gl-advanced-archery 0)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(or	(current-age <= feudal-age)
		(game-time < 60))
	(building-type-count-total castle > 0)
=>
	(set-strategic-number sn-low-eco-push 2)
	(disable-self)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(current-age >= castle-age)
	(strategic-number sn-low-eco-push == 2)
=>
	(set-goal TC 1)
	(set-strategic-number sn-stone-gatherer-percentage 5)
	(set-strategic-number sn-low-eco-push 0)
	(set-strategic-number sn-stone-dropsite-distance 3)
	(disable-self)
)

#load-if-defined TINY-MAP
(defrule
	(goal gl-strategy eagle-monk-rush)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(building-type-count-total barracks > 1)
	(or	(building-type-count-total monastery < 2)
		(building-type-count-total barracks > 2))
	(building-type-count-total monastery < 3)
	(or	(building-type-count-total town-center > 1)
		(or	(goal MILITARY 1)
			(strategic-number sn-low-eco-push == 1)))
=>
	(up-modify-goal gl-monastery-build-priority c:max 900)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(game-time < 2160)
	(strategic-number sn-low-eco-push != 1)
	(players-civilian-population any-enemy > 45)
	(or	(unit-type-count-total villager < 40)
		(players-civilian-population any-enemy > 52))
	(or	(unit-type-count-total villager < 45)
		(players-civilian-population any-enemy > 58))
	(unit-type-count-total villager < 50)
=>
	(set-strategic-number sn-low-eco-push 1)
	(set-goal TC 0)
	(chat-to-player my-player-number "1Push with low eco")
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(strategic-number sn-low-eco-push == 1)
	(unit-type-count-total monk-set > 15)
	(players-current-age every-enemy == castle-age)
	(strategic-number sn-threat-level > -3)
=>
	(set-strategic-number sn-threat-level -3)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(strategic-number sn-low-eco-push == 1)
	(unit-type-count-total monk-set > 22)
	(players-current-age every-enemy == castle-age)
	(strategic-number sn-threat-level > -4)
=>
	(set-strategic-number sn-threat-level -4)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(strategic-number sn-low-eco-push == 1)
	(unit-type-count-total villager > 90)
=>
	(set-strategic-number sn-low-eco-push 0)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(or	(and	(strategic-number sn-low-eco-push == 1)
			(game-time > 2100))
		(or	(players-building-type-count any-enemy castle > 0)
			(and	(building-type-count-total town-center == 1)
				(stone-amount == 0))))
	(strategic-number sn-stone-gatherer-percentage == 0)
=>
	(set-goal TC 1)
	(set-strategic-number sn-stone-gatherer-percentage 5)
	(set-strategic-number sn-low-eco-push 0)
	(set-strategic-number sn-stone-dropsite-distance 3)
)

(defrule
	(goal gl-strategy eagle-monk-rush)
	(strategic-number sn-low-eco-push == 1)
	(goal TC 0)
	(building-type-count-total barracks > 4)
=>
	(set-strategic-number sn-food-gatherer-percentage 34)
	(set-strategic-number sn-wood-gatherer-percentage 22)
	(set-strategic-number sn-gold-gatherer-percentage 44)
)
#else
(defrule
	(goal gl-strategy eagle-monk-rush)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(building-type-count-total barracks > 1)
	(or	(building-type-count-total monastery < 2)
		(building-type-count-total barracks > 2))
	(building-type-count-total monastery < 3)
	(building-type-count-total town-center > 1)
=>
	(up-modify-goal gl-monastery-build-priority c:max 900)
)
#end-if











