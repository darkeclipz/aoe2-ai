; The Horde version 3.9
; An Artificial Intelligence Script written by Archon 2009-2017

; Settings: (preferred settings)
; Game Version: UP 1.4, AoFE
; Game Type: Any (RM and PIDM)
; Location: Any [doesn't play maps where transports of villagers is required (e.g. Migration); requires ai_info_map_type BLACK_FOREST 0 1 0 to detect MICHI, small amounts of each resource type required]
; Map Size: Any
; Teams: Yes
; Resources: Any (standard)
; Difficulty: Any (hard)
; Population: Any (200-250)
; Starting Age: Any (dark age or PI)
; Victory: Any (conquest)
; All Techs: No
; Civilisation: Any
; Get the latest version at: http://forums.aiscripters.com/viewtopic.php?f=8&t=2176


;List of taunts it reacts to:
;3 - 6: Gives resources away if it has enough.
;31 - Attack an Enemy: If requested by an AI, it targets player stored by sn-target-player-number.
;33 - Stop slinging resources. If slinging is requested early and is immediately canceled it may mess up the strategy.
;38 - Sling Resources. Human player only, stops any unit production except for villagers.
;222 - Help Allies. If requested by an AI, it targets player stored in goal 82. If there is a flare on the map the AI will patrol to it.
;230 - If issued during the first 5 min, it changes the strategy to mush, if the civ has full monk tech tree.
;249 - Chats current resource amount
;250 - Chats current strategy


;Reuse obsolete strategic numbers that have no effect.
(defconst sn-current-age 193);tracks our current age
(defconst sn-military-level 190); compares our military with enemy
(defconst sn-resource-control 191); 0=spend res free, 1=navy, 2=research only; > 2 specific items
(defconst sn-infantry-threat 53)
(defconst sn-archer-threat 64)
(defconst sn-cavalry-threat 65)

;Goals
(defconst increase-town-size-goal 1); this increases town size when needed, 0 = no increase, 2 = building placed this rule pass, else increase
(defconst attack-goal 2)
(defconst strategy-goal 3)
(defconst unit-goal 4)
(defconst train-civ-goal 5);1=train villagers, !=1 no villagers
(defconst control-goal 6); 6 = allow to be shot, 7 = shot, also controls if stone for a castle is needed.
(defconst anti-cavalry-threat-goal 7)
(defconst monk-threat-goal 8)
(defconst enemy-goal 9); keep track of what our foe is doing
(defconst farming-goal 10);enable/disable farming
(defconst under-attack-goal 11); town  under attack or just a little harass?
(defconst enemy-boats-goal 12)
(defconst ffa-game-goal 13)
(defconst need-loom-goal 14)
(defconst escrow-purpose-goal 15); keeps track on what we use our escrow
(defconst spread-military-goal 16); spread military every couple of minutes to prevent villagers stuck
(defconst uu-up-goal 17); use escrow to upgrade uu
(defconst hostilities-goal 18); set this flag when we're attacked or attacking - avoid rule too long error
(defconst ranged-unit-type-goal 19);shows the ranged unit we use
(defconst retreat-now-goal 20); use to retreat with UP, condition: 1=always, 2=when attacking, 3=attack conditions false
(defconst housing-goal 21); controls building houses
(defconst anti-monk-threat-goal 22)
(defconst enemy-sighted-goal 23); when enemy activities detected, split the attack groups
(defconst closest-enemy-goal 24); stores player number of nearest enemy
(defconst second-enemy-goal 25); 2nd nearest enemy/nearest player to closest enemy flank
(defconst enemy-fortifications-goal 26); delay attacks if enemy has many castles and walled with castle
(defconst forward-threat-goal 27); enemy tower rush threat
(defconst save-wood-goal 28); controls spending wood
(defconst meso-enemy-goal 29); enemy has a meso civ; saves elements in a rule
(defconst nr-map-goal 30); focus on eco in easily wall-able maps vs humans
(defconst tribute-goal 31); ask for resources
(defconst patrol-goal 32); resets patrol attacks sent by the direct unit feature
(defconst split-rule-goal 33); split rule if we need more than 16 elements
(defconst avoid-tc-goal 34); move scout out of TC fire
(defconst kill-scouts-goal 35); kill enemy scouts
(defconst kill-villager-goal 36); kill low health villagers
(defconst gather-inside-goal 37); spawn units in buildings if we're too far behind; currently unused
(defconst boar-hunting-goal 38); controls hunting
(defconst deer-luring-goal 39)
(defconst raid-goal 40); raiding weak player's eco.
(defconst force-res-drop-goal 41)
(defconst threat-player-goal 42); stores player number of the attacker
(defconst threat-source-goal 43); unit class of the last enemy attacker
(defconst threat-target-goal 44); unit class of the our last attacked unit
(defconst winning-player-goal 45); player number of enemy winning by specifal victory condition
(defconst position-goal 46); detect if we're pocket (=2) or flank (=1) player; was here before that many goals were used, shared with other AI
(defconst ally-under-attack-goal 47)
(defconst ally-attacking-goal 48)
(defconst threat-time-goal 49); stores time in miliseconds we were last attacked
(defconst temp-goal 50); temporary stored values
(defconst temp-goal2 51)
(defconst temp-goal3 52)
(defconst temp-goal4 53)
(defconst temp-goal5 54)
(defconst temp-goal6 55)
(defconst temp-goal7 56)
(defconst temp-goal8 57)
(defconst dark-build-order-goal 58); 1 = dock first, 2 = double mill
(defconst closest-ally-goal 59); stores closest flank if pocket instead
(defconst aofe-game-goal 60); set to 1 if forgotten empires expansion is detected
(defconst p1x-goal 61); various goals to store coordinates for DUC
(defconst p1y-goal 62)
(defconst p2x-goal 63)
(defconst p2y-goal 64)
(defconst object-point-goal 65)
(defconst object-point-y-goal 66)
(defconst patrol-point-goal 67); stores the destination of patroling units
(defconst patrol-point-y-goal 68)
(defconst point-distance-goal 69)
(defconst cut-point1-goal 70)
(defconst cut-point1-y-goal 71)
(defconst cut-point2-goal 72)
(defconst cut-point2-y-goal 73)
(defconst cut-point3-goal 74)
(defconst cut-point3-y-goal 75)
(defconst cut-point4-goal 76)
(defconst cut-point4-y-goal 77)
(defconst tower-point-goal 78)
(defconst tower-point-y-goal 79)
(defconst object-id-goal 80)
(defconst object-id2-goal 81)
(defconst attacking-enemy-goal 82); stores player number of the attacker; shared with Barbarian
(defconst local-total-goal 83)
(defconst local-last-goal 84)
(defconst remote-total-goal 85)
(defconst remote-last-goal 86)
(defconst save-point-goal 87)
(defconst save-point-y-goal 88)
(defconst waypoint-goal 89)
(defconst waypoint-y-goal 90)
(defconst waypoint2-goal 91)
(defconst waypoint2-y-goal 92)
(defconst target-goal 93)
(defconst target-y-goal 94)
(defconst victory-type-goal 95); winning type
(defconst victory-time-goal 96); time left
(defconst duc-control-goal 97); controls DUC attacks to avoid towers.
(defconst sheep-count-goal 98)
(defconst sheep-count-last-goal 99)
(defconst sheep-point-goal 100)
(defconst current-sheep-goal 101)
(defconst next-sheep-goal 102)
(defconst switch-sheep-goal 103)
(defconst current-shepherd-goal 104)
(defconst last-shepherd-goal 105)
(defconst explore-sheep-goal 106)
(defconst explore-sheep2-goal 107)
(defconst my-player-number-goal 108)
(defconst max-coordinate-goal 109)
(defconst max-coordinate-y-goal 110)
(defconst enemy-score-goal 111)
(defconst enemy-previous-score-goal 112)
(defconst position-center-goal 113)
(defconst position-center-y-goal 114)
(defconst enemy-camp-goal 115)
(defconst enemy-previous-camp-goal 116)
(defconst civ-explore-goal 117)

(defconst team-coordination-goal 392); shared with Barbarian; 1 = attack in progress
(defconst id-goal 420); ID other AI
(defconst position-self-goal 478); stores own position, used as point for ally when ordered help via DUC
(defconst position-self-y-goal 479)
(defconst cut-goal 499); shared with barbie, 4535 = cut


;SN/Goal Values
(defconst dark 78)
(defconst df-transit 79)
(defconst feudal 80)
(defconst fc-transit 81)
(defconst castle 82)
(defconst ci-transit 83)
(defconst imperial 84)
(defconst flush 200)
(defconst rush 201)
(defconst boom 202)
(defconst fast-imp 203)
(defconst sling 204)
(defconst drush 205)
(defconst belated-flush-defense 5)
(defconst accept-slinging 6)
(defconst aggressive-rush 7); go 1 TC eco
(defconst shot 7)
(defconst uu 8)
(defconst navy 4000)
(defconst flank 1)
(defconst pocket 2)
(defconst groups 2)
(defconst tsa 3)
(defconst duc 4)
(defconst retreat -2)
(defconst move-wp1 1)
(defconst pat-wp1 2)
(defconst move-wp2 3)
(defconst pat-wp2 4)
(defconst move-target 5)
(defconst pat-target 6)
(defconst jugger 275); ally ID values for goal 420
(defconst promi 420)
(defconst horde 666)
(defconst bruteforce 1001)
(defconst barbarian 4651)


;Timers
(defconst increase-town-size-timer 1)
(defconst reset-town-size-timer 2)
(defconst scouting-timer 3)
(defconst attack-timer 4)
(defconst spread-military-timer 5)
(defconst under-attack-timer 7)
(defconst lumber-timer 8)
(defconst one-minute-timer 9)
(defconst tribute-timer 10)
(defconst micro-timer 11)
(defconst navy-attack-timer 12)
(defconst gather-units-timer 13)
(defconst villager-timer 14); timer for forcedrop to continuously train villager
(defconst trebuchet-timer 15); timer to avoid looping retreat unit commands
(defconst hunting-timer 16)
(defconst garrison-timer 17)
(defconst disable-defend-groups-timer 18); always enable defending again
(defconst monk-micro-timer 19)
(defconst misc-micro-timer 20)
(defconst five-seconds-timer 21)
(defconst cut-timer 22)
(defconst reset-units-timer 23)
(defconst duc-timer 24)
(defconst retreat-timer 25)
(defconst civ-explore-timer 26)

;Unit IDs
(defconst male-hunter 122)
(defconst female-hunter 216)
(defconst male-forager 120)
(defconst female-forager 354)
(defconst male-shepherd 592)
(defconst female-shepherd 590)
(defconst male-lumberjack 123)
(defconst female-lumberjack 218)
(defconst male-gold-miner 581)
(defconst female-gold-miner 579)
(defconst male-fisher 56)
(defconst female-fisher 57)
(defconst male-farmer 259)
(defconst female-farmer 214)
(defconst hunter 976)
(defconst ri-herbal-medicine 441)
(defconst ri-perfusion 457)
(defconst ri-anarchy 16)
(defconst unpacked-trebuchet 42)
(defconst rax-husky-line 759)
(defconst ranged-unit-class 900)
(defconst building-class 903)
(defconst villager-class 904)
(defconst infantry-class 906)
(defconst cavalry-class 912)
(defconst siege-unit-class 913)
(defconst monastery-class 918)
(defconst mounted-gun-class 923)
(defconst cavalry-archer-class 936)
(defconst gunpowder-class 944)
(defconst scorpion-class 955)
(defconst tower-class 952)
(defconst warship-class 922)
(defconst tree-class 915)
(defconst wall-class 927)
(defconst deer 65)
(defconst forage-bush 59)
(defconst gold-mine 66)
(defconst stone-mine 102)
(defconst shore-fish 933)
(defconst sea-fish 905)
(defconst sheep 958)
(defconst relic 285)
(defconst revealer 112)
(defconst flare 274)
(defconst tc-arrow 54)
(defconst tc-arrow-chemistry 328)
(defconst watch-tower-arrow 786)
(defconst castle-arrow 746)
(defconst castle-arrow-chemistry 747)

;User Patch
(defconst game-time 0); FactId Constants
(defconst population-cap 1)
(defconst population-headroom 2)
(defconst housing-headroom 3)
(defconst idle-farm-count 4)
(defconst food-amount 5)
(defconst wood-amount 6)
(defconst stone-amount 7)
(defconst gold-amount 8)
(defconst escrow-amount 9)
(defconst commodity-buying-price 10)
(defconst commodity-selling-price 11)
(defconst dropsite-min-distance 12)
(defconst soldier-count 13)
(defconst attack-soldier-count 14)
(defconst defend-soldier-count 15)
(defconst warboat-count 16)
(defconst attack-warboat-count 17)
(defconst defend-warboat-count 18)
(defconst current-age 19)
(defconst current-score 20)
(defconst civilization 21)
(defconst player-number 22)
(defconst player-in-game 23)
(defconst unit-count 24)
(defconst unit-type-count 25)
(defconst unit-type-count-total 26)
(defconst building-count 27)
(defconst building-type-count 28)
(defconst building-type-count-total 29)
(defconst population 30)
(defconst military-population 31)
(defconst civilian-population 32)
(defconst random-number 33)
(defconst resource-amount 34)
(defconst player-distance 35)
(defconst allied-goal 36)
(defconst allied-sn 37)
(defconst resource-percent 38)
(defconst enemy-buildings-in-town 39)
(defconst enemy-units-in-town 40)
(defconst enemy-villagers-in-town 41)
(defconst players-in-game 42)
(defconst defender-count 43)
(defconst building-type-in-town 44)
(defconst unit-type-in-town 45)
(defconst villager-type-in-town 46)
(defconst gaia-type-count 47)
(defconst gaia-type-count-total 48)
(defconst cc-gaia-type-count 49)
(defconst aggressive 0); Define AttackStance Constants
(defconst defensive 1)
(defconst stand-ground 2)
(defconst no-attack 3)
(defconst projectile-town-center 0); Define ProjectileType Constants
(defconst projectile-castle 1)
(defconst projectile-watch-tower 2)
(defconst projectile-bombard-tower 3)
(defconst projectile-ship 4)
(defconst projectile-siege 5)
(defconst projectile-fortification 6)
(defconst projectile-any 7)
(defconst find-attacker 0); Define FindPlayerMethod Constants
(defconst find-random 1)
(defconst find-closest 2)
(defconst find-ordered 3)
(defconst research-unavailable 0); Define ResearchState Constants
(defconst research-available 1)
(defconst research-pending 2)
(defconst research-complete 3)
(defconst timer-disabled 0); Define TimerState Constants
(defconst timer-triggered 1)
(defconst timer-running 2)
(defconst group-type-land-attack 100);Define GroupType Constants
(defconst group-type-land-explore 101)
(defconst group-type-land-trade 109)
(defconst group-type-water-attack 102)
(defconst group-type-water-explore 103)
(defconst group-type-water-trade 106)
(defconst group-type-transport-ship 104)
(defconst group-type-fishing-ship 105)
(defconst group-type-forward-builder 107)
(defconst group-type-monk 108)
(defconst place-normal 0);Define PlacementType Constants
(defconst place-forward 1)
(defconst place-control 2)
(defconst place-point 3)
(defconst amount-tribute-inefficiency 46)
(defconst position-center 0)
(defconst position-opposite 1)
(defconst position-corner 2)
(defconst position-enemy 3)
(defconst position-border 4)
(defconst position-mirror 5)
(defconst position-flank 6)
(defconst position-zero 7)
(defconst position-map-size 8)
(defconst position-self 9)
(defconst position-target 10)
(defconst position-focus 11)
(defconst position-object 12)
(defconst position-point 13)
(defconst search-local 1); my units
(defconst search-remote 2); focus player's units
(defconst action-default 0)
(defconst action-move 1)
(defconst action-patrol 2)
(defconst action-guard 3)
(defconst action-follow 4)
(defconst action-stop 5)
(defconst action-ground 6)
(defconst action-garrison 7)
(defconst action-delete 8)
(defconst formation-line 2)
(defconst formation-box 4)
(defconst formation-stagger 7)
(defconst formation-flank 8)
(defconst cmdid-flag 0)
(defconst cmdid-livestock-gaia 1)
(defconst cmdid-civilian-building 2)
(defconst cmdid-villager 3)
(defconst cmdid-military 4)
(defconst cmdid-trade 5)
(defconst cmdid-monk 6)
(defconst cmdid-transport 7)
(defconst cmdid-relic 8)
(defconst cmdid-unknown 9)
(defconst cmdid-military-building 10)
(defconst actionid-attack 600)
(defconst actionid-defend 601)
(defconst actionid-build 602)
(defconst actionid-heal 603)
(defconst actionid-convert 604)
(defconst actionid-explore 605)
(defconst actionid-stop 606)
(defconst actionid-runaway 607)
(defconst actionid-retreat 608)
(defconst actionid-gather 609)
(defconst actionid-move 610)
(defconst actionid-patrol 611)
(defconst actionid-follow 612)
(defconst actionid-hunt 613)
(defconst actionid-transport 614)
(defconst actionid-trade 615)
(defconst actionid-evade 616)
(defconst actionid-enter 617)
(defconst actionid-repair 618)
(defconst actionid-train 619)
(defconst actionid-research 620)
(defconst actionid-unload 621)
(defconst orderid-attack 700)
(defconst orderid-defend 701)
(defconst orderid-build 702)
(defconst orderid-heal 703)
(defconst orderid-convert 704)
(defconst orderid-explore 705)
(defconst orderid-stop 706)
(defconst orderid-runaway 707)
(defconst orderid-retreat 708)
(defconst orderid-gather 709)
(defconst orderid-move 710)
(defconst orderid-patrol 711)
(defconst orderid-follow 712)
(defconst orderid-hunt 713)
(defconst orderid-transport 714)
(defconst orderid-trade 715)
(defconst orderid-evade 716)
(defconst orderid-enter 717)
(defconst orderid-repair 718)
(defconst orderid-train 719)
(defconst orderid-research 720)
(defconst orderid-unload 721)
(defconst orderid-relic 731)
(defconst object-data-id 0)
(defconst object-data-type 1)
(defconst object-data-class 2)
(defconst object-data-category 3)
(defconst object-data-cmdid 4)
(defconst object-data-action 5)
(defconst object-data-order 6)
(defconst object-data-target 7)
(defconst object-data-point-x 8)
(defconst object-data-point-y 9)
(defconst object-data-hitpoints 10)
(defconst object-data-maxhp 11)
(defconst object-data-range 12)
(defconst object-data-speed 13)
(defconst object-data-dropsite 14)
(defconst object-data-resource 15)
(defconst object-data-carry 16)
(defconst object-data-garrisoned 17)
(defconst object-data-garrison-count 18)
(defconst object-data-status 19)
(defconst sort-targets 0)
(defconst sort-random 1)
(defconst sort-distance 2)
(defconst sort-hitpoints 3)
(defconst sort-range 4)
(defconst find-attacker 0)
(defconst find-random 1)
(defconst find-closest 2)
(defconst find-ordered 3)
(defconst ally 0)
(defconst neutral 1)
(defconst any 2)
(defconst enemy 3)
(defconst terrain-grass 0); terrain constants
(defconst terrain-grass2 12)
(defconst terrain-grass3 9)
(defconst terrain-grass-snow 34)
(defconst terrain-grass-old 16)
(defconst terrain-water 1)
(defconst terrain-water-deep 22)
(defconst terrain-water-medium 23)
(defconst terrain-water-bridge 28)
(defconst terrain-water-old 15)
(defconst terrain-water-beach 2)
(defconst terrain-shallows 4)
(defconst terrain-ice 26)
(defconst terrain-ice2 35)
(defconst terrain-ice-beach 37)
(defconst terrain-desert 14)
(defconst terrain-dirt 6)
(defconst terrain-dirt2 11)
(defconst terrain-dirt3 3)
(defconst terrain-dirt-snow 33)
(defconst terrain-farm 7)
(defconst terrain-farm1 29)
(defconst terrain-farm2 30)
(defconst terrain-farm3 31)
(defconst terrain-farm-dead 8)
(defconst terrain-forest 10)
(defconst terrain-forest-bamboo 18)
(defconst terrain-forest-jungle 17)
(defconst terrain-forest-oak 20)
(defconst terrain-forest-palm 13)
(defconst terrain-forest-pine 19)
(defconst terrain-forest-snow 21)
(defconst terrain-foundation 27)
(defconst terrain-foundation-snow 36)
(defconst terrain-koh 40)
(defconst terrain-leaves 5)
(defconst terrain-road 24)
(defconst terrain-road-broken 25)
(defconst terrain-road-snow 38)
(defconst terrain-road-fungus 39)
(defconst terrain-snow 32)
(defconst terrain-unknown 41)


;AoFE item list
(defconst italian 19)
(defconst indian 20)
(defconst incan 21)
(defconst magyar 22)
(defconst slavic 23)
(defconst stable-tarkan 886)
(defconst stable-elite-tarkan 887)
(defconst boyar 876)
(defconst elite-boyar 878)
(defconst kamayuk 879)
(defconst elite-kamayuk 881)
(defconst slinger 185)
(defconst ri-elite-magyar-huszar 472)
(defconst magyar-huszar 869)
(defconst elite-magyar-huszar 871)
(defconst ri-elite-elephant-archer 481)
(defconst elephant-archer 873)
(defconst elite-elephant-archer 875)
(defconst imperial-camel 897)
(defconst ri-elite-genoese-crossbow 468)
(defconst genoese-crossbowman 866)
(defconst elite-genoese-crossbowman 868)
(defconst heavy-eagle-warrior 753)
(defconst condottiero 882)
(defconst ri-chivalry 493)
(defconst ri-inquisition 492)
(defconst ri-sipahi 491)
(defconst ri-madrasah 490)
(defconst ri-ironclad 489)
(defconst ri-boiling-oil 488)
(defconst ri-nomads 487)
(defconst ri-panokseon 486)
(defconst ri-yasama 484)
(defconst ri-marauders 483)
(defconst ri-atlatl 460)
(defconst ri-war-wolf 461)
(defconst ri-greek-fire 464)
(defconst ri-chieftains 463)
(defconst ri-stronghold 482)
(defconst ri-great-wall 462)
(defconst ri-orthodoxy 512)
(defconst ri-druzhina 513)
(defconst ri-elite-boyar 504)
(defconst ri-andean-sling 516)
(defconst ri-couriers 517)
(defconst ri-elite-kamayuk 509)
(defconst ri-sultans 506)
(defconst ri-shatagni 507)
(defconst ri-imperial-camel 521)
(defconst ri-mercenaries 514)
(defconst ri-recurve-bow 515)
(defconst ri-silk-road 499)
(defconst ri-pavise 494)
(defconst ri-revetments 525)
(defconst ri-eagle-warrior 384)
(defconst ri-gillnets 65)
(defconst ri-hunting-dogs 526)
(defconst ri-obsidian-arrows 485)
(defconst siege-tower 885)
(defconst upgraded-palisade-wall 119)
(defconst sea-tower 785)
(defconst sea-wall 788)

; Other SN
(defconst sn-food-modifier-percentage 156)
(defconst sn-wood-modifier-percentage 157)
(defconst sn-stone-modifier-percentage 158)
(defconst sn-gold-modifier-percentage 159)

;civ specific
#load-if-defined AZTEC-CIV
(defconst dark-age-villager 28)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 200)
(defconst pocket-strategy 202)
(defconst default-unit archer)
(defconst unique-unit-food 110);minimum food amount
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit eagle-warrior)
(defconst get-lightcav 0)
(defconst scout-unit eagle-warrior-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 3)
(defconst villager-stable 3)
(defconst villager-archery-range 9)
(defconst villager-monastery 7)
(defconst villager-town-center 9)
(defconst villager-castle 6)
(defconst villager-siege-workshop 9)
(defconst number-barracks 10)
(defconst number-stables 5)
(defconst number-archery-ranges 7)
(defconst pidm-food 10)
(defconst pidm-wood 10)
(defconst pidm-gold 75)
(defconst pidm-stone 5)
(defconst pidm-food2 34)
(defconst pidm-wood2 24)
(defconst pidm-gold2 37)
(defconst pidm-stone2 5)
(defconst uu-food2 36)
(defconst uu-wood2 30)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst ci-uu-switch-food 38)
(defconst ci-uu-switch-wood 28)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 33)
(defconst michi-wood 32)
(defconst max-food-escrow 1000)
(defconst max-gold-escrow 500)
#end-if
#load-if-defined BRITON-CIV
(defconst dark-age-villager 28)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
(defconst default-unit archer)
(defconst unique-unit-food 0)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit archer)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 3)
(defconst villager-stable 12)
(defconst villager-archery-range 12)
(defconst villager-monastery 7)
(defconst villager-town-center 8)
(defconst villager-castle 3)
(defconst villager-siege-workshop 16)
(defconst number-barracks 6)
(defconst number-stables 0)
(defconst number-archery-ranges 9)
(defconst pidm-food 0)
(defconst pidm-wood 47)
(defconst pidm-gold 48)
(defconst pidm-stone 5)
(defconst pidm-food2 26)
(defconst pidm-wood2 34)
(defconst pidm-gold2 35)
(defconst pidm-stone2 5)
(defconst uu-food2 30)
(defconst uu-wood2 36)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst ci-uu-switch-food 30)
(defconst ci-uu-switch-wood 40)
(defconst ci-uu-switch-gold 26)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 20)
(defconst michi-wood 44)
(defconst max-food-escrow 850)
(defconst max-gold-escrow 850)
#end-if
#load-if-defined BYZANTINE-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit skirmisher)
(defconst unique-unit-food 120)
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit skirmisher)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 1)
(defconst sling-number 70)
(defconst villager-barracks 7)
(defconst villager-stable 3)
(defconst villager-archery-range 12)
(defconst villager-monastery 12)
(defconst villager-town-center 15)
(defconst villager-castle 4)
(defconst villager-siege-workshop 16)
(defconst number-barracks 6)
(defconst number-stables 5)
(defconst number-archery-ranges 7)
(defconst pidm-food 10)
(defconst pidm-wood 10)
(defconst pidm-gold 75)
(defconst pidm-stone 5)
(defconst pidm-food2 37)
(defconst pidm-wood2 32)
(defconst pidm-gold2 27)
(defconst pidm-stone2 4)
(defconst uu-food2 39)
(defconst uu-wood2 39)
(defconst uu-gold2 18)
(defconst uu-stone2 3)
(defconst ci-uu-switch-food 38)
(defconst ci-uu-switch-wood 26)
(defconst ci-uu-switch-gold 26)
(defconst ci-uu-switch-stone 10)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 1600)
(defconst max-gold-escrow 800)
#end-if
#load-if-defined CELTIC-CIV
(defconst dark-age-villager 29)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
(defconst default-unit skirmisher)
(defconst unique-unit-food 115)
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged 0)
(defconst pocket-ranged scorpion)
(defconst pocket-unit scorpion)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 6)
(defconst villager-stable 3)
(defconst villager-archery-range 16)
(defconst villager-monastery 7)
(defconst villager-town-center 8)
(defconst villager-castle 9)
(defconst villager-siege-workshop 5)
(defconst number-barracks 9)
(defconst number-stables 3)
(defconst number-archery-ranges 4)
(defconst pidm-food 20)
(defconst pidm-wood 30)
(defconst pidm-gold 45)
(defconst pidm-stone 5)
(defconst pidm-food2 32)
(defconst pidm-wood2 33)
(defconst pidm-gold2 30)
(defconst pidm-stone2 5)
(defconst uu-food2 40)
(defconst uu-wood2 28)
(defconst uu-gold2 28)
(defconst uu-stone2 4)
(defconst ci-uu-switch-food 38)
(defconst ci-uu-switch-wood 28)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 20)
(defconst michi-wood 45)
(defconst max-food-escrow 1000)
(defconst max-gold-escrow 800)
#end-if
#load-if-defined CHINESE-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 201)
(defconst pocket-strategy 202)
(defconst default-unit knight)
(defconst unique-unit-food 0)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit my-unique-unit-line)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 9)
(defconst villager-stable 3)
(defconst villager-archery-range 16)
(defconst villager-monastery 12)
(defconst villager-town-center 15)
(defconst villager-castle 5)
(defconst villager-siege-workshop 16)
(defconst number-barracks 6)
(defconst number-stables 8)
(defconst number-archery-ranges 6)
(defconst pidm-food 0)
(defconst pidm-wood 47)
(defconst pidm-gold 48)
(defconst pidm-stone 5)
(defconst pidm-food2 26)
(defconst pidm-wood2 34)
(defconst pidm-gold2 35)
(defconst pidm-stone2 5)
(defconst uu-food2 32)
(defconst uu-wood2 38)
(defconst uu-gold2 24)
(defconst uu-stone2 6)
(defconst ci-uu-switch-food 34)
(defconst ci-uu-switch-wood 34)
(defconst ci-uu-switch-gold 26)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 24)
(defconst michi-wood 41)
(defconst max-food-escrow 760)
(defconst max-gold-escrow 760)
#end-if
#load-if-defined FRANKISH-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit knight)
(defconst unique-unit-food 0)
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 1)
(defconst sling-number 100)
(defconst villager-barracks 7)
(defconst villager-stable 3)
(defconst villager-archery-range 16)
(defconst villager-monastery 12)
(defconst villager-town-center 18)
(defconst villager-castle 8)
(defconst villager-siege-workshop 14)
(defconst number-barracks 8)
(defconst number-stables 8)
(defconst number-archery-ranges 4)
(defconst pidm-food 45)
(defconst pidm-wood 0)
(defconst pidm-gold 50)
(defconst pidm-stone 5)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 44)
(defconst uu-wood2 26)
(defconst uu-gold2 25)
(defconst uu-stone2 5)
(defconst ci-uu-switch-food 43)
(defconst ci-uu-switch-wood 26)
(defconst ci-uu-switch-gold 25)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 1000)
(defconst max-gold-escrow 850)
#end-if
#load-if-defined GOTHIC-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
(defconst default-unit militiaman-line)
(defconst unique-unit-food 100)
(defconst default-flush-unit militiaman-line)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 1)
(defconst sling-number 100)
(defconst villager-barracks 3)
(defconst villager-stable 20)
(defconst villager-archery-range 12)
(defconst villager-monastery 12)
(defconst villager-castle 10)
(defconst villager-town-center 18)
(defconst villager-siege-workshop 15)
(defconst number-barracks 14)
(defconst number-stables 1)
(defconst number-archery-ranges 4)
(defconst pidm-food 70)
(defconst pidm-wood 0)
(defconst pidm-gold 30)
(defconst pidm-stone 0)
(defconst pidm-food2 44)
(defconst pidm-wood2 23)
(defconst pidm-gold2 30)
(defconst pidm-stone2 3)
(defconst uu-food2 51)
(defconst uu-wood2 28)
(defconst uu-gold2 19)
(defconst uu-stone2 2)
(defconst ci-uu-switch-food 46)
(defconst ci-uu-switch-wood 28)
(defconst ci-uu-switch-gold 20)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 1200)
(defconst max-gold-escrow 550)
#end-if
#load-if-defined HUN-CIV
(defconst dark-age-villager 29)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit knight)
(defconst unique-unit-food 0)
(defconst default-flush-unit scout-cavalry)
(defconst no-gold-flush-unit scout-cavalry)
(defconst default-ranged cavalry-archer)
(defconst pocket-ranged cavalry-archer)
(defconst pocket-unit knight)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 0)
(defconst sling-number 100)
#load-if-not-defined UP-HUN-CIV-ENEMY
(defconst villager-barracks 7)
(defconst villager-siege-workshop 5)
(defconst villager-castle 10)
(defconst villager-town-center 15)
#else
(defconst villager-barracks 5)
(defconst villager-siege-workshop 7)
(defconst villager-castle 15)
(defconst villager-town-center 10)
#end-if
(defconst villager-stable 3)
(defconst villager-archery-range 7)
(defconst villager-monastery 14)
(defconst number-barracks 9)
(defconst number-stables 8)
(defconst number-archery-ranges 12)
(defconst pidm-food 0)
(defconst pidm-wood 30)
(defconst pidm-gold 67)
(defconst pidm-stone 3)
(defconst pidm-food2 30)
(defconst pidm-wood2 32)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 30)
(defconst uu-wood2 36)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst ci-uu-switch-food 38)
(defconst ci-uu-switch-wood 28)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst max-food-escrow 1000)
(defconst max-gold-escrow 500)
#end-if
#load-if-defined JAPANESE-CIV
(defconst dark-age-villager 29)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
(defconst default-unit archer)
(defconst unique-unit-food 110)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit archer)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 1)
(defconst sling-number 70)
(defconst villager-barracks 3)
(defconst villager-stable 12)
(defconst villager-archery-range 9)
(defconst villager-monastery 7)
(defconst villager-town-center 8)
(defconst villager-castle 5)
(defconst villager-siege-workshop 16)
(defconst number-barracks 9)
(defconst number-stables 1)
(defconst number-archery-ranges 7)
(defconst pidm-food 30)
(defconst pidm-wood 10)
(defconst pidm-gold 58)
(defconst pidm-stone 2)
(defconst pidm-food2 38)
(defconst pidm-wood2 30)
(defconst pidm-gold2 27)
(defconst pidm-stone2 5)
(defconst uu-food2 38)
(defconst uu-wood2 28)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst michi-food 30)
(defconst michi-wood 35)
(defconst ci-uu-switch-food 38)
(defconst ci-uu-switch-wood 28)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst max-food-escrow 950)
(defconst max-gold-escrow 875)
#end-if
#load-if-defined KOREAN-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
(defconst default-unit my-unique-unit-line)
(defconst unique-unit-food 0)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit knight)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 1)
(defconst sling-number 50)
(defconst villager-barracks 4)
(defconst villager-stable 16)
(defconst villager-archery-range 13)
(defconst villager-monastery 9)
(defconst villager-town-center 8)
(defconst villager-castle 3)
(defconst villager-siege-workshop 6)
(defconst number-barracks 6)
(defconst number-stables 1)
(defconst number-archery-ranges 7)
(defconst pidm-food 0)
(defconst pidm-wood 58)
(defconst pidm-gold 37)
(defconst pidm-stone 5)
(defconst pidm-food2 26)
(defconst pidm-wood2 39)
(defconst pidm-gold2 30)
(defconst pidm-stone2 5)
(defconst uu-food2 20)
(defconst uu-wood2 52)
(defconst uu-gold2 25)
(defconst uu-stone2 3)
(defconst ci-uu-switch-food 24)
(defconst ci-uu-switch-wood 40)
(defconst ci-uu-switch-gold 26)
(defconst ci-uu-switch-stone 10)
(defconst michi-food 20)
(defconst michi-wood 45)
(defconst max-food-escrow 0)
(defconst max-gold-escrow 800)
#end-if
#load-if-defined MAYAN-CIV
(defconst dark-age-villager 28)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit archer)
(defconst unique-unit-food 0)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit archer)
(defconst get-lightcav 0)
(defconst scout-unit eagle-warrior-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 3)
(defconst villager-stable 3)
(defconst villager-archery-range 14)
(defconst villager-monastery 15)
(defconst villager-town-center 15)
(defconst villager-castle 5)
(defconst villager-siege-workshop 12)
(defconst number-barracks 7)
(defconst number-stables 0)
(defconst number-archery-ranges 7)
(defconst pidm-food 0)
(defconst pidm-wood 47)
(defconst pidm-gold 48)
(defconst pidm-stone 5)
(defconst pidm-food2 26)
(defconst pidm-wood2 34)
(defconst pidm-gold2 35)
(defconst pidm-stone2 5)
(defconst uu-food2 33)
(defconst uu-wood2 39)
(defconst uu-gold2 23)
(defconst uu-stone2 4)
(defconst ci-uu-switch-food 28)
(defconst ci-uu-switch-wood 38)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 20)
(defconst michi-wood 45)
(defconst max-food-escrow 500)
(defconst max-gold-escrow 0)
#end-if
#load-if-defined MONGOL-CIV
(defconst dark-age-villager 29)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
(defconst default-unit knight)
(defconst default-flush-unit scout-cavalry)
(defconst no-gold-flush-unit scout-cavalry)
(defconst unique-unit-food 0)
(defconst default-ranged archer)
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 14)
(defconst villager-stable 3)
(defconst villager-archery-range 14)
(defconst villager-monastery 7)
(defconst villager-town-center 8)
(defconst villager-castle 4)
(defconst villager-siege-workshop 8)
(defconst number-barracks 6)
(defconst number-stables 5)
(defconst number-archery-ranges 7)
(defconst pidm-food 0)
(defconst pidm-wood 30)
(defconst pidm-gold 65)
(defconst pidm-stone 5)
(defconst pidm-food2 26)
(defconst pidm-wood2 34)
(defconst pidm-gold2 35)
(defconst pidm-stone2 5)
(defconst uu-food2 29)
(defconst uu-wood2 38)
(defconst uu-gold2 29)
(defconst uu-stone2 4)
(defconst ci-uu-switch-food 35)
(defconst ci-uu-switch-wood 33)
(defconst ci-uu-switch-gold 29)
(defconst ci-uu-switch-stone 3)
(defconst michi-food 20)
(defconst michi-wood 45)
(defconst max-food-escrow 1100)
(defconst max-gold-escrow 675)
#end-if
#load-if-defined PERSIAN-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit knight)
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
(defconst unique-unit-food 0)
(defconst default-ranged archer)
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 1)
(defconst sling-number 100)
(defconst villager-barracks 5)
(defconst villager-stable 3)
(defconst villager-archery-range 16)
(defconst villager-monastery 12)
(defconst villager-town-center 18)
(defconst villager-castle 5)
(defconst villager-siege-workshop 7)
(defconst number-barracks 7)
(defconst number-stables 6)
(defconst number-archery-ranges 7)
(defconst pidm-food 37)
(defconst pidm-wood 0)
(defconst pidm-gold 58)
(defconst pidm-stone 5)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 38)
(defconst uu-wood2 27)
(defconst uu-gold2 33)
(defconst uu-stone2 3)
(defconst ci-uu-switch-food 38)
(defconst ci-uu-switch-wood 28)
(defconst ci-uu-switch-gold 24)
(defconst ci-uu-switch-stone 10)
(defconst michi-food 33)
(defconst michi-wood 32)
(defconst max-food-escrow 1600)
(defconst max-gold-escrow 1200)
#end-if
#load-if-defined SARACEN-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
#load-if-not-defined POST-IMPERIAL-AGE-START
(defconst default-unit knight)
#else
(defconst default-unit my-unique-unit-line)
#end-if
(defconst unique-unit-food 0); changed for more impact early
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit archer)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 1)
(defconst sling-number 50)
(defconst villager-barracks 9)
(defconst villager-stable 3)
(defconst villager-archery-range 9)
(defconst villager-monastery 15)
(defconst villager-town-center 18)
(defconst villager-castle 9)
(defconst villager-siege-workshop 16)
(defconst number-barracks 6)
(defconst number-stables 9)
(defconst number-archery-ranges 8)
(defconst pidm-food 10)
(defconst pidm-wood 10)
(defconst pidm-gold 75)
(defconst pidm-stone 5)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 36)
(defconst uu-wood2 30)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst michi-food 30)
(defconst michi-wood 35)
(defconst ci-uu-switch-food 35)
(defconst ci-uu-switch-wood 27)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 10)
(defconst max-food-escrow 600)
(defconst max-gold-escrow 500)
#end-if
#load-if-defined SPANISH-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit knight)
#load-if-not-defined ARENA-MAP
(defconst unique-unit-food 0); changed for more impact as pocket early
#else
(defconst unique-unit-food 110)
#end-if
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged skirmisher)
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 1)
(defconst sling-number 100)
(defconst villager-barracks 6)
(defconst villager-stable 3)
(defconst villager-archery-range 16)
(defconst villager-monastery 12)
(defconst villager-town-center 18)
(defconst villager-castle 5)
(defconst villager-siege-workshop 16)
(defconst number-barracks 8)
(defconst number-stables 6)
(defconst number-archery-ranges 4)
(defconst pidm-food 45)
(defconst pidm-wood 0)
(defconst pidm-gold 50)
(defconst pidm-stone 5)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 42)
(defconst uu-wood2 24)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst ci-uu-switch-food 42)
(defconst ci-uu-switch-wood 24)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst max-food-escrow 1200)
(defconst max-gold-escrow 600)
#end-if
#load-if-defined TEUTONIC-CIV
(defconst dark-age-villager 29)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit knight)
(defconst unique-unit-food 0)
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 1)
(defconst sling-number 100)
(defconst villager-barracks 6)
(defconst villager-stable 3)
(defconst villager-archery-range 16)
(defconst villager-monastery 7)
(defconst villager-town-center 9)
(defconst villager-castle 12)
(defconst villager-siege-workshop 5)
(defconst number-barracks 8)
(defconst number-stables 6)
(defconst number-archery-ranges 4)
(defconst pidm-food 45)
(defconst pidm-wood 0)
(defconst pidm-gold 50)
(defconst pidm-stone 5)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 42)
(defconst uu-wood2 24)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst ci-uu-switch-food 42)
(defconst ci-uu-switch-wood 24)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst max-food-escrow 1200)
(defconst max-gold-escrow 600)
(defconst farm-wood 40)
#else
(defconst farm-wood 60)
#end-if
#load-if-defined TURKISH-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit knight)
(defconst unique-unit-food 110)
(defconst default-flush-unit scout-cavalry)
(defconst no-gold-flush-unit scout-cavalry)
#load-if-not-defined POST-IMPERIAL-AGE-START
(defconst default-ranged archer)
#else
(defconst default-ranged cavalry-archer)
#end-if
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 1)
(defconst sling-number 100)
(defconst villager-barracks 14)
(defconst villager-stable 3)
(defconst villager-archery-range 3)
(defconst villager-monastery 7)
(defconst villager-town-center 8)
(defconst villager-castle 5)
(defconst villager-siege-workshop 16)
(defconst number-barracks 6)
(defconst number-stables 7)
(defconst number-archery-ranges 4)
(defconst pidm-food 12)
(defconst pidm-wood 13)
(defconst pidm-gold 70)
(defconst pidm-stone 5)
(defconst pidm-food2 42)
(defconst pidm-wood2 20)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 40)
(defconst uu-wood2 26)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst ci-uu-switch-food 42)
(defconst ci-uu-switch-wood 24)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 850)
(defconst max-gold-escrow 750)
#end-if
#load-if-defined VIKING-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit militiaman-line)
(defconst unique-unit-food 115)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit knight)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 4)
(defconst villager-stable 3)
(defconst villager-archery-range 9)
(defconst villager-monastery 9)
(defconst villager-town-center 7)
(defconst villager-castle 5)
(defconst villager-siege-workshop 16)
(defconst number-barracks 9)
(defconst number-stables 1)
(defconst number-archery-ranges 7)
(defconst pidm-food 20)
(defconst pidm-wood 25)
(defconst pidm-gold 50)
(defconst pidm-stone 5)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 38)
(defconst uu-wood2 28)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst ci-uu-switch-food 46)
(defconst ci-uu-switch-wood 26)
(defconst ci-uu-switch-gold 22)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 1300)
(defconst max-gold-escrow 550)
#end-if
#load-if-defined INCAN-CIV
(defconst dark-age-villager 29)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 200)
(defconst pocket-strategy 202)
(defconst default-unit archer)
(defconst unique-unit-food 110)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit eagle-warrior)
(defconst get-lightcav 0)
(defconst scout-unit eagle-warrior-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 3)
(defconst villager-stable 3)
(defconst villager-archery-range 9)
(defconst villager-monastery 7)
(defconst villager-town-center 16)
(defconst villager-castle 5)
(defconst villager-siege-workshop 16)
(defconst number-barracks 9)
(defconst number-stables 1)
(defconst number-archery-ranges 7)
(defconst pidm-food 30)
(defconst pidm-wood 10)
(defconst pidm-gold 58)
(defconst pidm-stone 2)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 41)
(defconst uu-wood2 27)
(defconst uu-gold2 28)
(defconst uu-stone2 4)
(defconst ci-uu-switch-food 36)
(defconst ci-uu-switch-wood 32)
(defconst ci-uu-switch-gold 26)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 1300)
(defconst max-gold-escrow 550)
#end-if
#load-if-defined INDIAN-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit skirmisher)
(defconst unique-unit-food 160)
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged 0)
(defconst pocket-unit knight)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 1)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 9)
(defconst villager-stable 3)
(defconst villager-archery-range 9)
(defconst villager-monastery 7)
(defconst villager-town-center 18)
(defconst villager-castle 5)
(defconst villager-siege-workshop 16)
(defconst number-barracks 6)
(defconst number-stables 9)
(defconst number-archery-ranges 7)
(defconst pidm-food 30)
(defconst pidm-wood 10)
(defconst pidm-gold 58)
(defconst pidm-stone 2)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 38)
(defconst uu-wood2 28)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst ci-uu-switch-food 42)
(defconst ci-uu-switch-wood 24)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 1300)
(defconst max-gold-escrow 550)
#end-if
#load-if-defined ITALIAN-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 201)
(defconst pocket-strategy 201)
(defconst default-unit archer)
(defconst unique-unit-food 115)
(defconst default-flush-unit archer)
(defconst no-gold-flush-unit skirmisher)
(defconst default-ranged archer)
(defconst pocket-ranged archer)
(defconst pocket-unit knight)
(defconst get-lightcav 0)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 1)
(defconst sling-number 100)
(defconst villager-barracks 12)
(defconst villager-stable 3)
(defconst villager-archery-range 9)
(defconst villager-monastery 7)
(defconst villager-town-center 18)
(defconst villager-castle 3)
(defconst villager-siege-workshop 16)
(defconst number-barracks 9)
(defconst number-stables 1)
(defconst number-archery-ranges 7)
(defconst pidm-food 30)
(defconst pidm-wood 10)
(defconst pidm-gold 58)
(defconst pidm-stone 2)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 38)
(defconst uu-wood2 28)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst ci-uu-switch-food 38)
(defconst ci-uu-switch-wood 28)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst max-food-escrow 1300)
(defconst max-gold-escrow 550)
#end-if
#load-if-defined MAGYAR-CIV
(defconst dark-age-villager 30)
(defconst villager-wonder-dark 32)
(defconst villager-wonder-feudal 34)
(defconst default-strategy 200)
(defconst pocket-strategy 201)
(defconst default-unit knight)
(defconst unique-unit-food 130)
(defconst default-flush-unit scout-cavalry)
(defconst no-gold-flush-unit scout-cavalry)
#load-if-not-defined POST-IMPERIAL-AGE-START
(defconst default-ranged archer)
#else
(defconst default-ranged cavalry-archer)
#end-if
(defconst pocket-ranged archer)
(defconst pocket-unit knight)
(defconst get-lightcav 0); uu instead
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 7)
(defconst villager-stable 3)
(defconst villager-archery-range 16)
(defconst villager-monastery 12)
(defconst villager-town-center 18)
(defconst villager-castle 12)
(defconst villager-siege-workshop 14)
(defconst number-barracks 8)
(defconst number-stables 6)
(defconst number-archery-ranges 4)
(defconst pidm-food 30)
(defconst pidm-wood 10)
(defconst pidm-gold 58)
(defconst pidm-stone 2)
(defconst pidm-food2 34)
(defconst pidm-wood2 28)
(defconst pidm-gold2 33)
(defconst pidm-stone2 5)
(defconst uu-food2 42)
(defconst uu-wood2 24)
(defconst uu-gold2 28)
(defconst uu-stone2 6)
(defconst michi-food 31)
(defconst michi-wood 34)
(defconst ci-uu-switch-food 32)
(defconst ci-uu-switch-wood 36)
(defconst ci-uu-switch-gold 26)
(defconst ci-uu-switch-stone 6)
(defconst max-food-escrow 1300)
(defconst max-gold-escrow 550)
#end-if
#load-if-defined SLAVIC-CIV
(defconst dark-age-villager 29)
(defconst villager-wonder-dark 31)
(defconst villager-wonder-feudal 33)
(defconst default-strategy 201)
(defconst pocket-strategy 202)
(defconst default-unit knight)
(defconst unique-unit-food 115)
(defconst default-flush-unit skirmisher)
(defconst no-gold-flush-unit skirmisher)
#load-if-not-defined DEATH-MATCH
(defconst default-ranged monk)
#else
(defconst default-ranged scorpion)
#end-if
(defconst pocket-ranged scorpion)
(defconst pocket-unit scorpion)
(defconst get-lightcav 1)
(defconst scout-unit scout-cavalry-line)
(defconst allow-forward-castle 0)
(defconst gunpowder 0)
(defconst sling-number 100)
(defconst villager-barracks 2)
(defconst villager-stable 16)
(defconst villager-archery-range 16)
(defconst villager-monastery 6)
(defconst villager-town-center 16)
(defconst villager-castle 4)
(defconst villager-siege-workshop 6)
(defconst number-barracks 4)
(defconst number-stables 4)
(defconst number-archery-ranges 4)
(defconst pidm-food 30)
(defconst pidm-wood 10)
(defconst pidm-gold 58)
(defconst pidm-stone 2)
(defconst pidm-food2 34)
(defconst pidm-wood2 24)
(defconst pidm-gold2 37)
(defconst pidm-stone2 5)
(defconst uu-food2 36)
(defconst uu-wood2 31)
(defconst uu-gold2 30)
(defconst uu-stone2 3)
(defconst michi-food 25)
(defconst michi-wood 40)
(defconst ci-uu-switch-food 42)
(defconst ci-uu-switch-wood 24)
(defconst ci-uu-switch-gold 28)
(defconst ci-uu-switch-stone 6)
(defconst max-food-escrow 1300)
(defconst max-gold-escrow 550)
#end-if

#load-if-defined BYZANTINE-CIV
(defconst imp-food 667)
(defconst imp-gold 536)
(defconst imp-f2 517)
(defconst imp-g2 400)
#else
#load-if-defined ITALIAN-CIV
(defconst imp-food 900)
(defconst imp-f2 730)
(defconst imp-g2 600)
(defconst imp-gold 720)
#else
(defconst imp-food 1000)
(defconst imp-f2 800)
(defconst imp-g2 650)
(defconst imp-gold 800)
#end-if
#end-if
#load-if-defined UP-GAME-AGE2-X2
(defconst mangonel-upgrade-cut ri-onager)
#else
(defconst mangonel-upgrade-cut ri-siege-onager)
#end-if
#load-if-defined POST-IMPERIAL-AGE-START
(defconst pop-house 20)
#else
(defconst pop-house 50)
#end-if
#load-if-defined DEATH-MATCH
(defconst camp-distance 45)
(defconst food-distance 0)
(defconst wood-distance -1)
#load-if-defined POST-IMPERIAL-AGE-START
(defconst market-time 180)
(defconst home-exploration-time 1)
#else
(defconst market-time 600)
(defconst home-exploration-time 30)
#end-if
(defconst number-markets 4)
(defconst number-blacksmiths 2)
(defconst mill-distance 15)
(defconst hunt-distance 0)
(defconst spread-interval 40)
(defconst town-center-placement 0)
(defconst special-attack-type2 town-center)
#else
(defconst camp-distance 15)
(defconst food-distance 20)
(defconst wood-distance 0)
(defconst market-time 1800)
(defconst number-blacksmiths 1)
(defconst number-markets 3)
#load-if-not-defined SCANDANAVIA-MAP
(defconst mill-distance 15)
(defconst hunt-distance 0)
#else
(defconst mill-distance 29)
(defconst hunt-distance 12)
#end-if
(defconst spread-interval 180)
#load-if-defined UP-MICHI-STYLE
(defconst home-exploration-time 900)
#else
#load-if-not-defined WONDER-RACE
(defconst home-exploration-time 480)
#else
(defconst home-exploration-time 32768)
#end-if
#end-if
(defconst town-center-placement lumber-camp)
(defconst special-attack-type2 lumber-camp)
#end-if
#load-if-defined NOMAD-MAP
(defconst wood-distance2 -1)
#else
(defconst wood-distance2 30)
#end-if
#load-if-defined SALT-MARSH-MAP
(defconst dock-placement 1)
#else
(defconst dock-placement 0)
#end-if
#load-if-defined YUCATAN-MAP
(defconst max-warships 20)
#end-if
#load-if-defined HIGHLAND-MAP
(defconst max-warships 20)
#end-if
#load-if-not-defined HIGHLAND-MAP
#load-if-not-defined YUCATAN-MAP
(defconst max-warships 35)
#end-if
#end-if
#load-if-defined ARENA-MAP
(defconst camp-distance-tc 15)
#else
(defconst camp-distance-tc 22)
#end-if
#load-if-defined REGICIDE
#load-if-defined FORTRESS-MAP
(defconst lumber-delay 40)
#else
(defconst lumber-delay 180)
#end-if
#else
(defconst lumber-delay 180)
#end-if
#load-if-defined TINY-MAP
(defconst min-fishing 24)
(defconst heavy-wood 7000)
(defconst max-trade-cart 20)
(defconst trade-distance 54)
#end-if
#load-if-defined SMALL-MAP
(defconst min-fishing 40)
(defconst heavy-wood 8000)
(defconst max-trade-cart 20)
(defconst trade-distance 72)
#end-if
#load-if-defined MEDIUM-MAP
(defconst min-fishing 50)
(defconst heavy-wood 10000)
(defconst max-trade-cart 33)
(defconst trade-distance 90)
#end-if
#load-if-defined NORMAL-MAP
(defconst min-fishing 60)
(defconst heavy-wood 14000)
(defconst max-trade-cart 37)
(defconst trade-distance 120)
#end-if
#load-if-defined LARGE-MAP
(defconst min-fishing 70)
(defconst heavy-wood 18000)
(defconst max-trade-cart 45)
(defconst trade-distance 150)
#end-if
#load-if-defined GIANT-MAP
(defconst min-fishing 80)
(defconst heavy-wood 22000)
#load-if-defined TEAM-ISLANDS-MAP
(defconst max-trade-cart 40)
#else
(defconst max-trade-cart 50)
#end-if
(defconst trade-distance 192)
#end-if
#load-if-defined HIGHLAND-MAP
(defconst water-mixed 1000)
(defconst water-islands 1001)
#else
#load-if-defined TINY-MAP
(defconst water-mixed 1000)
(defconst water-islands 1001)
#end-if
#load-if-defined SMALL-MAP
(defconst water-mixed 2000)
(defconst water-islands 2001)
#end-if
#load-if-defined MEDIUM-MAP
(defconst water-mixed 4000)
(defconst water-islands 4001)
#end-if
#load-if-defined NORMAL-MAP
(defconst water-mixed 4000)
(defconst water-islands 4001)
#end-if
#load-if-defined LARGE-MAP
(defconst water-mixed 4000)
(defconst water-islands 4001)
#end-if
#load-if-defined GIANT-MAP
(defconst water-mixed 4000)
(defconst water-islands 4001)
#end-if
#end-if
#load-if-defined DIFFICULTY-EASIEST
(defconst repair-level 1); castles only
(defconst wood-distance-farm 13)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst repair-level 4097); castles + towers
(defconst wood-distance-farm 13)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst repair-level 20481); castles + towers + siege
(defconst wood-distance-farm 6)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst repair-level 20481); castles + towers + siege
(defconst wood-distance-farm 6)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst repair-level 20481); castles + towers + siege
(defconst wood-distance-farm 6)
#end-if
#load-if-not-defined TURBO-RANDOM-MAP
#load-if-not-defined POPULATION-CAP-25
(defconst number-rams 3)
(defconst number-garrison-units 4)
#load-if-defined DIFFICULTY-EASIEST
(defconst villager-feudal 16)
(defconst villager-flush 16)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst villager-feudal 19)
(defconst villager-flush 18)
#end-if
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-defined LOW-RESOURCES-START
#load-if-defined DIFFICULTY-MODERATE
(defconst villager-flush 27)
(defconst villager-feudal 29)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst villager-flush 24)
(defconst villager-feudal 26)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst villager-flush 22)
(defconst villager-feudal 24)
#end-if
#end-if
#load-if-defined MEDIUM-RESOURCES-START
#load-if-not-defined DIFFICULTY-HARDEST
(defconst villager-flush 20)
(defconst villager-feudal 24)
#else
(defconst villager-feudal 20)
(defconst villager-flush 16)
#end-if
#end-if
#load-if-defined HIGH-RESOURCES-START
(defconst villager-flush 16)
#load-if-not-defined DIFFICULTY-HARDEST
(defconst villager-feudal 16)
#else
(defconst villager-feudal 10)
#end-if
#end-if
#end-if ;NOT EASY
#end-if ;NOT EASIEST
#else
(defconst number-rams 2)
(defconst villager-feudal 24)
(defconst villager-flush 24)
#end-if ; = POP25
#else ; = TURBO
#load-if-not-defined POPULATION-CAP-25
(defconst number-rams 3)
#else
(defconst number-rams 2)
#end-if
(defconst villager-flush 15)
(defconst villager-feudal 16)
#end-if
#load-if-defined GOLD-RUSH-MAP
#load-if-defined TINY-MAP
(defconst camp-distance2 48); high camp distance to reach center gold
#end-if
#load-if-defined SMALL-MAP
(defconst camp-distance2 48)
#end-if
#load-if-defined MEDIUM-MAP
(defconst camp-distance2 64)
#end-if
#load-if-defined NORMAL-MAP
(defconst camp-distance2 90)
#end-if
#load-if-defined LARGE-MAP
(defconst camp-distance2 124)
#end-if
#load-if-defined GIANT-MAP
(defconst camp-distance2 124)
#end-if
#else
(defconst camp-distance2 35)
#end-if ; not gold-rush-map
#load-if-not-defined UP-MICHI-STYLE
(defconst scorpion-gold-min 170)
(defconst safe-town 40)
#else
(defconst scorpion-gold-min 120)
(defconst safe-town 255)
#end-if
#load-if-defined POPULATION-CAP-25
(defconst max-fleet 20)
(defconst twenty-five-percent-pop 6)
(defconst thirty-percent-pop 7)
(defconst fourty-percent-pop 10)
(defconst fifty-percent-pop 13)
(defconst sixty-percent-pop 15)
(defconst seventy-five-percent-pop 19)
(defconst eighty-five-percent-pop 21)
(defconst max-civ 12)
(defconst max-civ-pop 20)
(defconst del-civ-pop 24)
(defconst max-pop 25)
(defconst tc-level-one 1)
(defconst tc-level-two 1)
(defconst tc-level-three 1)
(defconst tc-level-four 1)
(defconst tc-level-five 1)
(defconst min-number-upgrade 1)
(defconst mid-number-upgrade 2)
(defconst high-number-upgrade 4)
(defconst max-trade-pop 0)
#end-if
#load-if-defined POPULATION-CAP-50
(defconst max-fleet 30)
(defconst twenty-five-percent-pop 13)
(defconst thirty-percent-pop 15)
(defconst fourty-percent-pop 20)
(defconst fifty-percent-pop 25)
(defconst sixty-percent-pop 30)
(defconst seventy-five-percent-pop 38)
(defconst eighty-five-percent-pop 42)
(defconst max-civ 28)
(defconst max-civ-pop 45)
(defconst del-civ-pop 48)
(defconst max-pop 50)
(defconst tc-level-one 1)
(defconst tc-level-two 1)
(defconst tc-level-three 1)
(defconst tc-level-four 2)
(defconst tc-level-five 2)
(defconst min-number-upgrade 2)
(defconst mid-number-upgrade 5)
(defconst high-number-upgrade 7)
(defconst max-trade-pop 0)
#end-if
#load-if-defined POPULATION-CAP-75
(defconst max-fleet 40)
(defconst twenty-five-percent-pop 19)
(defconst thirty-percent-pop 22)
(defconst fourty-percent-pop 30)
(defconst fifty-percent-pop 38)
(defconst sixty-percent-pop 45)
(defconst seventy-five-percent-pop 57)
(defconst eighty-five-percent-pop 64)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-civ 40)
#else
(defconst max-civ 50)
#end-if
(defconst max-civ-pop 65)
(defconst del-civ-pop 72)
(defconst max-pop 75)
(defconst tc-level-one 1)
(defconst tc-level-two 1)
(defconst tc-level-three 1)
(defconst tc-level-four 2)
(defconst tc-level-five 2)
(defconst min-number-upgrade 5)
(defconst mid-number-upgrade 8)
(defconst high-number-upgrade 12)
(defconst max-trade-pop 7)
#end-if
#load-if-defined POPULATION-CAP-100
(defconst max-fleet 40)
(defconst twenty-five-percent-pop 25)
(defconst thirty-percent-pop 30)
(defconst fourty-percent-pop 40)
(defconst fifty-percent-pop 50)
(defconst sixty-percent-pop 60)
(defconst seventy-five-percent-pop 75)
(defconst eighty-five-percent-pop 85)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-civ 40)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst max-civ 52)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst max-civ 65)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst max-civ 65)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst max-civ 65)
#end-if
(defconst max-civ-pop 95)
(defconst del-civ-pop 98)
(defconst max-pop 100)
(defconst tc-level-one 1)
(defconst tc-level-two 2)
(defconst tc-level-three 2)
(defconst tc-level-four 2)
(defconst tc-level-five 3)
(defconst min-number-upgrade 5)
(defconst mid-number-upgrade 8)
(defconst high-number-upgrade 15)
(defconst max-trade-pop 10)
#end-if
#load-if-defined POPULATION-CAP-125
(defconst max-fleet 50)
(defconst twenty-five-percent-pop 32)
(defconst thirty-percent-pop 37)
(defconst fourty-percent-pop 50)
(defconst fifty-percent-pop 63)
(defconst sixty-percent-pop 75)
(defconst seventy-five-percent-pop 94)
(defconst eighty-five-percent-pop 106)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-civ 40)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst max-civ 52)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst max-civ 75)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst max-civ 75)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst max-civ 75)
#end-if
(defconst max-civ-pop 115)
(defconst del-civ-pop 123)
(defconst max-pop 125)
(defconst tc-level-one 1)
(defconst tc-level-two 2)
(defconst tc-level-three 2)
(defconst tc-level-four 3)
(defconst tc-level-five 3)
(defconst min-number-upgrade 5)
(defconst mid-number-upgrade 8)
(defconst high-number-upgrade 15)
(defconst max-trade-pop 12)
#end-if
#load-if-defined POPULATION-CAP-150
(defconst max-fleet 56)
(defconst twenty-five-percent-pop 38)
(defconst thirty-percent-pop 45)
(defconst fourty-percent-pop 60)
(defconst fifty-percent-pop 75)
(defconst sixty-percent-pop 90)
(defconst seventy-five-percent-pop 113)
(defconst eighty-five-percent-pop 128)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-trade-pop 5)
(defconst max-civ 40)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst max-trade-pop 12)
(defconst max-civ 52)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst max-trade-pop 15)
(defconst max-civ 80)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst max-trade-pop 15)
(defconst max-civ 80)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst max-trade-pop 15)
(defconst max-civ 80)
#end-if
(defconst max-civ-pop 140)
(defconst del-civ-pop 148)
(defconst max-pop 150)
(defconst tc-level-one 1)
(defconst tc-level-two 2)
(defconst tc-level-three 3)
(defconst tc-level-four 3)
(defconst tc-level-five 4)
(defconst min-number-upgrade 5)
(defconst mid-number-upgrade 8)
(defconst high-number-upgrade 15)
#end-if
#load-if-defined POPULATION-CAP-175
(defconst max-fleet 66)
(defconst twenty-five-percent-pop 44)
(defconst thirty-percent-pop 52)
(defconst fourty-percent-pop 70)
(defconst fifty-percent-pop 88)
(defconst sixty-percent-pop 105)
(defconst seventy-five-percent-pop 131)
(defconst eighty-five-percent-pop 149)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-trade-pop 5)
(defconst max-civ 40)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst max-trade-pop 12)
(defconst max-civ 52)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst max-trade-pop 20)
(defconst max-civ 80)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst max-trade-pop 35)
(defconst max-civ 100)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst max-trade-pop 35)
(defconst max-civ 100)
#end-if
(defconst max-civ-pop 165)
(defconst del-civ-pop 173)
(defconst max-pop 175)
(defconst tc-level-one 1)
(defconst tc-level-two 2)
(defconst tc-level-three 3)
(defconst tc-level-four 4)
(defconst tc-level-five 5)
(defconst min-number-upgrade 5)
(defconst mid-number-upgrade 8)
(defconst high-number-upgrade 15)
#end-if
#load-if-defined POPULATION-CAP-200
#load-if-not-defined POPULATION-CAP-EXTENDED
(defconst max-fleet 75)
(defconst twenty-five-percent-pop 50)
(defconst thirty-percent-pop 60)
(defconst fourty-percent-pop 80)
(defconst fifty-percent-pop 100)
(defconst sixty-percent-pop 120)
(defconst seventy-five-percent-pop 150)
(defconst eighty-five-percent-pop 170)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-trade-pop 5)
(defconst max-civ 40)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst max-trade-pop 12)
(defconst max-civ 52)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst max-trade-pop 20)
(defconst max-civ 90)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst max-trade-pop 45)
(defconst max-civ 125)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst max-trade-pop 45)
(defconst max-civ 120)
#end-if
(defconst max-civ-pop 185)
#load-if-not-defined GOTHIC-CIV
(defconst del-civ-pop 196)
(defconst max-pop 200)
#else
(defconst del-civ-pop 206)
(defconst max-pop 210)
#end-if
#end-if
(defconst tc-level-one 1)
(defconst tc-level-two 2)
(defconst tc-level-three 3)
(defconst tc-level-four 4)
(defconst tc-level-five 5)
(defconst min-number-upgrade 5)
(defconst mid-number-upgrade 8)
(defconst high-number-upgrade 15)
#end-if
#load-if-defined POPULATION-CAP-225
(defconst max-fleet 78)
(defconst twenty-five-percent-pop 56)
(defconst thirty-percent-pop 66)
(defconst fourty-percent-pop 90)
(defconst fifty-percent-pop 112)
(defconst sixty-percent-pop 135)
(defconst seventy-five-percent-pop 169)
(defconst eighty-five-percent-pop 191)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-trade-pop 5)
(defconst max-civ 40)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst max-trade-pop 12)
(defconst max-civ 52)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst max-trade-pop 20)
(defconst max-civ 90)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst max-trade-pop 50)
(defconst max-civ 135)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst max-trade-pop 50)
(defconst max-civ 135)
#end-if
(defconst max-civ-pop 210)
(defconst del-civ-pop 221)
(defconst max-pop 225)
#end-if
#load-if-defined POPULATION-CAP-EXTENDED; Load for Pop >= 250 to prevent error message
#load-if-not-defined POPULATION-CAP-225
(defconst max-fleet 90)
(defconst twenty-five-percent-pop 63)
(defconst thirty-percent-pop 75)
(defconst fourty-percent-pop 100)
(defconst fifty-percent-pop 125)
(defconst sixty-percent-pop 150)
(defconst seventy-five-percent-pop 187)
(defconst eighty-five-percent-pop 213)
#load-if-defined DIFFICULTY-EASIEST
(defconst max-trade-pop 5)
(defconst max-civ 40)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst max-trade-pop 12)
(defconst max-civ 52)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst max-trade-pop 20)
(defconst max-civ 90)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst max-trade-pop 50)
(defconst max-civ 140)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst max-trade-pop 50)
(defconst max-civ 140)
#end-if
(defconst max-civ-pop 220)
(defconst del-civ-pop 246)
(defconst max-pop 250)
#end-if
#end-if
#load-if-defined UP-POCKET-POSITION
(defconst scouting-style position-mirror)
#else
#load-if-defined UP-MULTIPLE-ENEMIES
(defconst scouting-style position-flank)
#else
(defconst scouting-style position-opposite)
#end-if
#end-if
#load-if-defined DIFFICULTY-EASIEST
(defconst enable-patrol 0)
#end-if
#load-if-defined DIFFICULTY-EASY
(defconst enable-patrol 0)
#end-if
#load-if-defined DIFFICULTY-MODERATE
(defconst enable-patrol 0)
#end-if
#load-if-defined DIFFICULTY-HARD
(defconst enable-patrol 1)
#end-if
#load-if-defined DIFFICULTY-HARDEST
(defconst enable-patrol 1)
#end-if
#load-if-not-defined UP-HUMAN-IN-GAME
(defconst chat-player every-ally)
(defconst taunt-player any-computer)
(defconst this-any-taunt-player this-any-computer)
#else
(defconst chat-player my-player-number)
(defconst taunt-player any-human-ally)
(defconst this-any-taunt-player this-any-human-ally)
#end-if


(defrule
	(true)
=>
	(set-strategic-number sn-task-ungrouped-soldiers 0); spread military 0=no,1=yes
	(set-strategic-number sn-total-number-explorers 10);start with all civilians as explorer and turn them off when sheeps or berries are found
	(set-strategic-number sn-percent-civilian-explorers 100)
	(set-strategic-number sn-minimum-civilian-explorers 12)
	(set-strategic-number sn-number-explore-groups 1);explore with scouts
	(set-difficulty-parameter ability-to-dodge-missiles 0);0=dodge,100=stay
	(set-strategic-number sn-percent-enemy-sighted-response 100);fight with all men
	(set-strategic-number sn-enemy-sighted-response-distance 6)
	(set-strategic-number sn-group-commander-selection-method 0)
	(set-strategic-number sn-ignore-attack-group-under-attack 0)
	(set-strategic-number sn-attack-group-size-randomness 0)
	(set-strategic-number sn-scale-minimum-attack-group-size 0)
	(set-strategic-number sn-scale-maximum-attack-group-size 0)
	(set-strategic-number sn-group-leader-defense-distance 3); increases chance to make a few big groups instead of many medium sized ones
	(disable-self)
)
(defrule
	(true)
=>
	(set-strategic-number sn-maximum-town-size 8)
	(set-strategic-number sn-intelligent-gathering 1);drop resources when switched to another type
	(set-strategic-number sn-retask-gather-amount 0);minimum amount to collect before allowed to switch	
	(set-strategic-number sn-camp-max-distance camp-distance);small distances at the start
	(set-strategic-number sn-mill-max-distance mill-distance)
	(set-strategic-number sn-cap-civilian-builders 100); maximum villagers that can be tasked as builder
	(set-strategic-number sn-cap-civilian-explorers 100); maximum villagers that can be tasked as explorer
	(set-strategic-number sn-maximum-hunt-drop-distance 0); disable all, enable as required
	(set-strategic-number sn-maximum-food-drop-distance 0)
	(set-strategic-number sn-maximum-wood-drop-distance 0)
	(set-strategic-number sn-maximum-stone-drop-distance 0)
	(set-strategic-number sn-maximum-gold-drop-distance 0)
	(disable-self)
)
(defrule
	(true)
=>
	(set-strategic-number sn-group-form-distance 30)
	(set-strategic-number sn-consecutive-idle-unit-limit 0)
	(set-strategic-number sn-percent-attack-soldiers 100);attack with all men
	(set-strategic-number sn-coop-share-information 1); gives information to an AI ally
	(set-strategic-number sn-garrison-rams 0); use UP loading instead
	(set-strategic-number sn-current-age dark); initilisation dark-age
	(set-strategic-number sn-minimum-water-body-size-for-dock 999); don't build docks in small puddles
	(disable-self)
)
(defrule
	(true)
=>
	(set-strategic-number sn-special-attack-type2 special-attack-type2)
	(set-strategic-number sn-minimum-attack-group-size 1)
	(set-strategic-number sn-zero-priority-distance 200)
	(set-strategic-number sn-attack-group-gather-spacing 5)
	(set-strategic-number sn-do-not-scale-for-difficulty-level 30)
	(set-strategic-number sn-initial-exploration-required 0); set this to 0 to build immediately
	(set-strategic-number sn-gather-defense-units 0)
	(set-strategic-number sn-object-repair-level repair-level)
	(set-strategic-number sn-ignore-tower-elevation 1)
	(set-strategic-number sn-dock-proximity-factor -3000)
	(set-strategic-number sn-dock-placement-mode dock-placement)
	(set-goal escrow-purpose-goal 0)
	(generate-random-number 100)
	(disable-self)
)
(defrule
	(true)
=>
	(set-strategic-number sn-local-targeting-mode 1)
	(set-strategic-number sn-forage-defend-priority 1)
	(set-strategic-number sn-gold-defend-priority 1)
	(set-strategic-number sn-stone-defend-priority 1)
	(set-strategic-number sn-livestock-defend-priority 1); required to up-find-remote gaia stuff
	(up-get-point position-map-size max-coordinate-goal)
	(up-get-point position-center position-center-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(disable-self)
)
(defrule
	(true)
=>
	(set-goal control-goal 0); secondary unit
	(set-goal enemy-goal 0); enemy strategy unkonwn
	(set-goal increase-town-size-goal 0); disable TS increase
	(set-goal strategy-goal default-strategy)
	(set-goal unit-goal default-unit)
	(set-goal ranged-unit-type-goal default-ranged)
	(set-goal under-attack-goal 0); not under attack
	(set-goal spread-military-goal 0)
	(set-goal id-goal horde)
	(enable-timer civ-explore-timer 1)
	(enable-timer attack-timer 1)
	(enable-timer scouting-timer 1); debug
	(enable-timer villager-timer 1)
	(enable-timer spread-military-timer 220)
	(disable-self)
)

(defrule
	(true)
=>
	(up-set-defense-priority c: house c: 1)
	(up-set-defense-priority c: watch-tower c: 500); default = 2000, 500 = standard military buildings
	(up-set-defense-priority c: guard-tower c: 500)
	(up-set-defense-priority c: keep c: 500)
	(up-set-defense-priority c: bombard-tower c: 500)
	(up-set-defense-priority c: town-center c: 900)
	(up-set-defense-priority c: market c: 700)
	(up-set-defense-priority c: monastery c: 750)
	(up-set-defense-priority c: mining-camp c: 1000)
	(up-set-defense-priority c: lumber-camp c: 1000)
	(up-set-defense-priority c: barracks c: 100)
	(up-set-defense-priority c: archery-range c: 100)
	(up-set-defense-priority c: stable c: 100)
	(up-set-defense-priority c: siege-workshop c: 500)
	(disable-self)
)
(defrule
	(true)
=>
	(up-set-offense-priority c: building-class c: -1)
	(up-set-offense-priority c: 949 c: -1); farms
	(up-set-offense-priority c: 902 c: 0); trade carts
	(up-set-offense-priority c: 951 c: 11)
	(up-set-offense-priority c: 954 c: 11)
	(chat-to-all "The Horde Artificial Intelligence Script 3.9")
	(disable-self)
)
(defrule
	(true)
=>
	(set-strategic-number sn-percent-building-cancellation 25)
	(set-strategic-number sn-wall-targeting-mode 1)
	(set-strategic-number sn-enable-new-building-system 1)
	(set-strategic-number sn-home-exploration-time home-exploration-time)
	(set-strategic-number sn-minimum-boar-hunt-group-size 0)
	(set-strategic-number sn-town-center-placement town-center-placement)
	(set-strategic-number sn-enable-boar-hunting 2)
	(set-strategic-number sn-minimum-boar-lure-group-size 200); block boar hunting
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-livestock-to-town-center 1)
	(set-strategic-number sn-enable-offensive-priority 1)
	(set-strategic-number sn-disable-builder-assistance 1)
	(set-strategic-number sn-number-civilian-militia 0)
	(set-strategic-number sn-enable-patrol-attack enable-patrol); 1 for hard, hardest and 0 for rest
	(disable-self)
)
(defrule
	(true)
=>
	(enable-timer villager-timer 1)
	(enable-timer navy-attack-timer 1)
	(enable-timer micro-timer 1)
	(set-goal raid-goal 0)
	(set-goal current-shepherd-goal 0)
	(set-goal last-shepherd-goal 0)
	(up-get-player-fact my-player-number player-number 0 my-player-number-goal)
	(disable-self)
)
(defrule
	(true); clear these goals/SNs each rule pass
=>
	(set-strategic-number sn-archer-threat 0)
	(set-strategic-number sn-infantry-threat 0)
	(set-strategic-number sn-cavalry-threat 0)
	(set-goal anti-cavalry-threat-goal 0)
	(set-goal anti-monk-threat-goal 0)
	(set-goal monk-threat-goal 0)
	(set-goal enemy-sighted-goal 0)
	(set-goal attack-goal 0)
	(set-goal farming-goal 0)
	(set-goal housing-goal 0)
	(set-goal enemy-boats-goal 0)
	(set-goal hostilities-goal 0)
	(set-goal save-wood-goal 0)
	(set-goal forward-threat-goal -1)
)
(defrule
	(true)
=>
	(set-goal temp-goal 0)
	(set-goal temp-goal2 0)
	(set-goal temp-goal3 0)
	(set-goal temp-goal4 0)
	(set-goal temp-goal5 0)
	(set-goal temp-goal6 0)
	(set-goal temp-goal7 0)
	(set-goal temp-goal8 0)
	(set-goal split-rule-goal 0)
	(set-goal point-distance-goal 0)
	(set-goal ally-under-attack-goal 0)
	(set-goal ally-attacking-goal 0)
)
(defrule
	(true)
=>
	(set-goal tribute-goal 0)
	(set-goal threat-source-goal 0)
	(set-goal threat-target-goal 0)
	(set-goal team-coordination-goal 0)
	(set-strategic-number sn-enable-training-queue 1)
	(up-get-threat-data threat-time-goal threat-player-goal threat-source-goal threat-target-goal)
	(up-find-player enemy find-attacker attacking-enemy-goal)
)
(defrule
	(building-type-count town-center > 0)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: town-center c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object position-self-goal)
	(up-modify-goal position-self-goal c:- 2)
)
(defrule
	(players-building-count any-enemy > 0)
=>
	(up-find-player enemy find-closest closest-enemy-goal)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(game-time > 12)
	(game-time < 15)
=>
	(up-find-player ally find-closest closest-ally-goal)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(disable-self)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(game-time > 12)
	(game-time < 15)
	(up-allied-goal focus-player position-goal == pocket)
=>
	(up-find-player ally find-closest closest-ally-goal)
	(up-find-next-player ally find-closest closest-ally-goal)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(set-goal split-rule-goal 22)
	(disable-self)
)
#load-if-not-defined UP-4-PLAYER-TEAM
(defrule
	(goal split-rule-goal 22)
	(up-compare-goal unit-goal != knight)
	(game-time > 12)
	(game-time < 15)
	(up-allied-goal focus-player position-goal == pocket)
=>
	(set-goal nr-map-goal 1); choose a boom strategy
	(chat-to-allies "super pocket position detected")
	(disable-self)
)
#end-if
#end-if
#load-if-defined UP-ALLY-IN-GAME
(defrule
	(or
		(up-allied-goal any-ally team-coordination-goal == 1)
		(up-allied-goal any-ally under-attack-goal == 1)
	)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
)
(defrule
	(strategic-number sn-focus-player-number g:= closest-ally-goal)
	(up-allied-goal focus-player team-coordination-goal == 1)
=>
	(set-goal ally-attacking-goal 1)
	(chat-local-to-self "ally attacking")
)
(defrule
	(up-allied-goal any-ally under-attack-goal == 1)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
)
(defrule
	(strategic-number sn-focus-player-number g:= closest-ally-goal)
	(up-allied-goal focus-player under-attack-goal == 1)
=>
	(set-goal ally-under-attack-goal 1)
	(chat-local-to-self "ally under attack")
)
#load-if-defined UP-POCKET-POSITION
#load-if-defined UP-4-PLAYER-TEAM
(defrule
	(up-allied-goal any-ally under-attack-goal == 1)
	(goal ally-under-attack-goal 0)
=>
	(up-get-player-fact this-any-ally allied-goal position-goal temp-goal2)
)
(defrule
	(up-allied-goal every-ally id-goal > 0)
	(up-allied-goal any-ally under-attack-goal == 1)
	(goal ally-under-attack-goal 0)
	(goal temp-goal2 pocket)
=>
	(set-goal ally-under-attack-goal 2)
)
#end-if
#end-if
(defrule
	(or
		(up-allied-goal any-ally team-coordination-goal == 1)
		(up-allied-goal any-ally under-attack-goal == 1)
	)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)
#end-if
(defrule
	(not (town-under-attack))
	(up-compare-goal threat-time-goal > 3000)
	(goal under-attack-goal 1)
	(current-age == feudal-age)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-set-target-point position-self-goal)
	(up-target-point position-self-goal action-move -1 no-attack)
	(enable-timer reset-units-timer 2)
	(chat-to-player chat-player "retreat to prevent being lured")
)
; check if out town under attack and not an annoying wolf
(defrule
	(goal under-attack-goal 0)
	(strategic-number sn-maximum-town-size < 40)
	(players-military-population any-enemy > 1)
	(town-under-attack)
=>
	(set-goal under-attack-goal 2)
	(enable-timer under-attack-timer 18)
)
(defrule
	(strategic-number sn-maximum-town-size < 40)
	(town-under-attack)
	(not (timer-triggered under-attack-timer))
	(goal under-attack-goal 3)
=>
	(set-goal under-attack-goal 1)
	(chat-local-to-self "attack was not over yet")
)
(defrule
	(not (town-under-attack))
	(goal under-attack-goal 1)
=>
	(enable-timer under-attack-timer 10)
	(set-goal under-attack-goal 3)
)
(defrule
	(timer-triggered under-attack-timer)
	(town-under-attack)
	(population > 25)
	(or
		(or
			(strategic-number sn-military-level < -1)
			(up-compare-goal threat-player-goal g:!= closest-enemy-goal)
		)
		(up-players-in-game ally > 2)
	)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(chat-to-player focus-player "222")
	(chat-to-player-using-id focus-player 22115)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)
(defrule
	(timer-triggered under-attack-timer)
	(town-under-attack)
	(population > 25)
=>
	(disable-timer under-attack-timer)
	(enable-timer under-attack-timer 30)
	(set-goal under-attack-goal 1)
)
(defrule
	(timer-triggered under-attack-timer)
	(town-under-attack)
	(or
		(population < 26)
		(not (player-in-game any-ally))
	)
=>
	(disable-timer under-attack-timer)
	(enable-timer under-attack-timer 30)
	(set-goal under-attack-goal 1)
)

; React to enemy stuff
(defrule
	(goal duc-control-goal 0)
	(up-compare-goal enemy-camp-goal != -1)
=>
	(set-goal enemy-camp-goal -1)
	(set-goal enemy-previous-camp-goal -1)
)
(defrule
	(up-compare-goal duc-control-goal > 0)
=>
	(up-modify-goal enemy-previous-camp-goal g:= enemy-camp-goal)
	(up-get-target-fact building-type-count mining-camp enemy-camp-goal)
	(up-get-target-fact building-type-count lumber-camp temp-goal)
	(up-modify-goal enemy-camp-goal g:+ temp-goal)
	(up-get-target-fact building-type-count mill temp-goal)
	(up-modify-goal enemy-camp-goal g:+ temp-goal)
	(up-modify-goal temp-goal g:= enemy-camp-goal)
	(up-modify-goal temp-goal g:- enemy-previous-camp-goal)
)
(defrule
	(up-compare-goal duc-control-goal > 0)
	(up-compare-goal enemy-previous-camp-goal > 0)
	(up-compare-goal temp-goal > 0)
=>
	(set-goal enemy-sighted-goal 1)
	(up-chat-data-to-self "New enemy camp sighted: %d camps" g: temp-goal)
)
(defrule
	(up-compare-goal threat-time-goal < 2000)
	(up-compare-goal threat-target-goal != warship-class)
	(up-compare-goal threat-source-goal != warship-class)
	(up-compare-goal threat-source-goal != tower-class)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(set-difficulty-parameter ability-to-maintain-distance 0); hit and run
	(set-goal enemy-sighted-goal 1)
)
(defrule
	(game-time < 1500)
	(up-compare-goal threat-time-goal < 2000)
	(up-compare-goal threat-target-goal != warship-class)
	(up-compare-goal threat-source-goal != warship-class)
	(or
		(goal unit-goal archer)
		(current-age == feudal-age)
	)
	(up-research-status c: ri-ballistics < research-complete)
	(or
		(players-unit-type-count any-enemy knight-line > 6)
		(players-unit-type-count any-enemy eagle-warrior-line > 10)
	)
	(players-unit-type-count every-enemy mangonel-line == 0)
=>
	(set-difficulty-parameter ability-to-maintain-distance 100); stay
)
(defrule
	(up-compare-goal threat-time-goal < 2000)
	(up-compare-goal threat-target-goal != warship-class)
	(up-compare-goal threat-source-goal != warship-class)
	(up-compare-goal threat-source-goal != tower-class)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal);restore
)
(defrule
	(unit-type-count unpacked-trebuchet > 0)
	(or
		(unit-type-count scorpion-line > 20)
		(or
			(unit-type-count ranged-unit-class > 50)
			(unit-type-count war-wagon-line > 50)
		)
	)
=>
	(set-difficulty-parameter ability-to-maintain-distance 100)
)
(defrule
	(unit-type-count fire-ship-line > 0)
=>
	(set-difficulty-parameter ability-to-maintain-distance 100)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(players-building-count every-enemy > 0)
	(timer-triggered one-minute-timer)
	(up-compare-goal second-enemy-goal == -1)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-reset-filters)
	(up-reset-search 1 1 1 1)
	(up-find-remote c: building-class c: 1)
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-filters)
	(up-reset-search 1 1 1 1)
	(set-strategic-number sn-focus-player-number 1)
	(set-goal temp-goal 40)
	(set-goal remote-total-goal 0)
)
(defrule
	(players-building-count every-enemy > 0)
	(timer-triggered one-minute-timer)
	(strategic-number sn-focus-player-number g:!= closest-enemy-goal)
	(stance-toward focus-player enemy)
	(up-compare-goal second-enemy-goal == -1)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 g: temp-goal)
	(up-find-remote c: building-class c: 200)
	(up-get-search-state local-total-goal)
)
(defrule
	(players-building-count every-enemy > 0)
	(timer-triggered one-minute-timer)
	(up-compare-goal remote-total-goal > 0)
	(player-valid focus-player)
	(stance-toward focus-player enemy)
	(strategic-number sn-focus-player-number g:!= closest-enemy-goal)
	(up-compare-goal second-enemy-goal == -1)
=>
	(up-modify-goal second-enemy-goal s:= sn-focus-player-number)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-chat-data-to-player every-ally "Enemy pocket on my side is player %d" g: second-enemy-goal)
	(up-jump-rule 2)
)
(defrule
	(players-building-count every-enemy > 0)
	(timer-triggered one-minute-timer)
	(up-compare-goal second-enemy-goal == -1)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-jump-rule -3)
)
(defrule
	(players-building-count every-enemy > 0)
	(timer-triggered one-minute-timer)
	(up-compare-goal second-enemy-goal == -1)
	(goal remote-total-goal 0)
	(up-compare-goal temp-goal < 150)
	(strategic-number sn-focus-player-number == 8)
=>
	(up-modify-goal temp-goal c:+ 10)
	(set-strategic-number sn-focus-player-number 1)
	(up-jump-rule -4)
)
#else
(defrule
	(timer-triggered one-minute-timer)
	(players-building-count every-enemy > 0)
=>
	(up-find-player enemy find-closest closest-enemy-goal)
	(up-find-next-player enemy find-closest second-enemy-goal)
)
(defrule
	(goal under-attack-goal 1)
	(up-compare-goal attacking-enemy-goal != closest-enemy-goal)
	(up-compare-goal attacking-enemy-goal != second-enemy-goal)
=>
	(up-modify-goal second-enemy-goal g:= attacking-enemy-goal)
)
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(game-time > 1)
	(goal closest-enemy-goal -1)
=>
	(set-strategic-number sn-focus-player-number 1)
)
(defrule
	(game-time > 1)
	(goal closest-enemy-goal -1)
	(or
		(not (player-valid focus-player))
		(not (stance-toward focus-player enemy))
	)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-jump-rule -1)
)
(defrule
	(game-time > 1)
	(goal closest-enemy-goal -1)
	(stance-toward focus-player enemy)
=>
	(up-modify-goal closest-enemy-goal s:= sn-focus-player-number)
	(up-chat-data-to-self "enemy is player %d" s: sn-focus-player-number)
)
#end-if
#end-if
(defrule
	(or
		(or
			(and
				(strategic-number sn-maximum-town-size >= 40)
				(building-type-count town-center > 0)
			)
			(or
				(strategic-number sn-number-attack-groups > 0)
				(strategic-number sn-number-boat-attack-groups > 0)
			)
		)
		(up-compare-goal duc-control-goal > 0)
	)
	(goal team-coordination-goal 0)
=>
	(set-goal team-coordination-goal 1)
)
(defrule
	(or
		(or
			(goal under-attack-goal 1)
			(or
				(up-compare-goal patrol-goal >= 0)
				(up-compare-goal duc-control-goal > 0)
			)
		)
		(or
			(and
				(strategic-number sn-maximum-town-size >= 40)
				(strategic-number sn-minimum-water-body-size-for-dock < water-islands); TSA is not used on islands
			)
			(strategic-number sn-number-attack-groups > 0)
		)
	)
	(building-type-count-total town-center > 0)
=>
	(set-goal hostilities-goal 1)
)
(defrule
	(or
		(and
			(game-time < 1200); don't block early villagers in DM
			(current-age >= feudal-age)
		)
		(nor
			(and
				(goal train-civ-goal -1)
				(population >= max-civ-pop)
			)
			(goal train-civ-goal 2)
		)
	)
=>
	(set-goal train-civ-goal 1)
)
(defrule
	(or
		(or
			(goal team-coordination-goal 0)
			(game-time > 2100)
		)
		(timer-triggered attack-timer)
	)
	(up-compare-goal raid-goal != 0)
=>
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 0)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal train-civ-goal -1)
	(or
		(and
			(unit-type-count-total trebuchet-set > 0)
			(or
				(unit-type-count-total trebuchet-set > 2)
				(goal enemy-fortifications-goal -1)
			)
		)
		(unit-type-count battering-ram-line > 5)
	)
	(civilian-population < 90)
=>
	(set-goal train-civ-goal 1)
)
(defrule
	(taunt-detected any-ally 33)
	(game-time > 600)
=>
	(acknowledge-taunt this-any-ally 33)
	(chat-to-allies-using-id 22157)
	(set-goal train-civ-goal 1)
)
(defrule
	(current-age == imperial-age)
	(population >= max-civ-pop)
	(taunt-detected any-ally 32)
=>
	(acknowledge-taunt this-any-ally 32)
	(chat-to-allies-using-id 22154)
	(set-goal train-civ-goal -1)
)
(defrule
	(game-time > 1800)
	(or
		(or
			(dropsite-min-distance wood == -1); wood gone
			(dropsite-min-distance wood == 255); or remaining wood is on other islands
		)
		(and
			(unit-type-count-total villager > thirty-percent-pop)
			(game-type == 8)
		)
	)
=>
	(set-goal train-civ-goal -1)
)
(defrule
	(current-age == imperial-age)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(or
		(and
			(unit-type-count villager > twenty-five-percent-pop)
			(game-time > 2700)
		)
		(and
			(and
				(players-unit-type-count every-enemy galley-line < 15)
				(players-unit-type-count every-enemy fire-ship-line < 15)
			)
			(warboat-count > 25)
		)
	)
=>
	(set-goal train-civ-goal -1)
)
(defrule
	(current-age == imperial-age)
	(strategic-number sn-resource-control < 1)
	(goal escrow-purpose-goal -1)
	(civilian-population > max-civ); more than enough civ, usually achieved by adding trade carts
	(gold-amount > 500)
	(wood-amount > 800)
	(food-amount > 800)
	(population > del-civ-pop)
	(population-cap <= 250)
=>
	(delete-unit villager)
	(set-goal train-civ-goal -1)
	(chat-local-to-self "delete excess villager")
)
(defrule
	(current-age == imperial-age)
	(strategic-number sn-resource-control < 1)
	(civilian-population > max-civ); more than enough civ, usually achieved by adding trade carts
	(gold-amount > 600); and enough of all resources
	(population > del-civ-pop)
	(goal unit-goal monk)
=>
	(delete-unit villager)
	(set-goal train-civ-goal -1)
	(chat-local-to-self "delete excess villager")
)
#load-if-defined DEFEND-WONDER
(defrule
	(unit-type-count-total villager > fourty-percent-pop)
=>
	(set-goal train-civ-goal -1)
)
#end-if
(defrule
	(strategic-number sn-resource-control != 3)
	(nand
		(strategic-number sn-current-age == feudal)
		(and
			(strategic-number sn-resource-control == 2)
			(goal under-attack-goal 0)
		)
	)
=>
	(set-strategic-number sn-resource-control 0)
)
#load-if-not-defined DEATH-MATCH
(defrule
	(up-timer-status scouting-timer != timer-running)
	(game-time < 600)
	(goal avoid-tc-goal -1)
	(up-gaia-type-count c: sheep > 0)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-set-target-point position-self-goal)
	(set-strategic-number sn-focus-player-number 0)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 24)
	(up-find-remote c: sheep c: 1)
	(up-get-search-state local-total-goal)
)
(defrule
	(up-timer-status scouting-timer != timer-running)
	(game-time < 600)
	(goal avoid-tc-goal -1)
	(up-gaia-type-count c: sheep > 0)
	(up-compare-goal remote-total-goal >= 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal)
	(up-find-local c: scout-unit c: 1)
	(up-target-point p1x-goal action-move -1 -1)
	(enable-timer scouting-timer 10)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)
#end-if
#load-if-defined WONDER-RACE
(defrule
	(true)
=>
	(set-goal strategy-goal fast-imp);overwrite all other settings
	(set-goal unit-goal wonder)
	(set-goal control-goal wonder)
)
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total university < 1)
	(current-age == castle-age)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal university)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
=>
	(enable-timer 6 60)(disable-self)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(timer-triggered 6)
	(food-amount < 300); ri-stonecutting
	(can-buy-commodity food)
	(research-available ri-stonecutting)
=>
	(buy-commodity food)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(not (research-available ri-stonecutting))
=>
	(sell-commodity food)
	(delete-building farm)
	(enable-timer lumber-timer 1200)
)
(defrule
	(current-age == imperial-age)
	(wood-amount > 1100)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
)
(defrule
	(current-age == imperial-age)
	(wood-amount < 1000)
	(gold-amount > 1150)
	(commodity-buying-price wood <= 150)
=>
	(buy-commodity wood)
)
(defrule
	(current-age == imperial-age)
	(stone-amount > 1100)
=>
	(sell-commodity stone)
)
(defrule
	(current-age == imperial-age)
	(stone-amount < 1000)
	(gold-amount > 1174)
	(commodity-buying-price stone <= 175)
=>
	(buy-commodity stone)
)
(defrule
	(can-build wonder)
=>
	(build wonder)
)
(defrule
	(building-type-count-total wonder > 0)
=>
	(delete-building blacksmith)
	(delete-building market)
	(delete-building university)
	(delete-building monastery)
	(delete-building town-center)
	(set-goal increase-town-size-goal 2); block build system
)
#else
;******************** NAVY INITIALISATION ********************

(defrule
	(or
		(goal enemy-boats-goal 1)
		(players-building-type-count any-enemy dock > 0)
	)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age >= castle-age)
=>
	(enable-wall-placement 2)
	(set-strategic-number sn-minimum-water-body-size-for-dock water-mixed)
	(set-strategic-number sn-percent-attack-soldiers 0)
	(set-strategic-number sn-number-boat-explore-groups 0)
;	(chat-to-player chat-player "enemy boats sighted... pretend this map has water")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(taunt-detected any-human 34)
=>
	(acknowledge-taunt this-any-human 34)
	(enable-wall-placement 2)
	(set-strategic-number sn-minimum-water-body-size-for-dock water-mixed)
	(set-strategic-number sn-percent-attack-soldiers 0)
	(set-strategic-number sn-number-boat-explore-groups 0)
)
(defrule
	(game-time < 1200)
	(building-type-count dock > 0)
	(goal strategy-goal flush)
	(or
		(or
			(map-type mediterranean)
			(map-type baltic)
		)
		(or
			(map-type rivers)
			(map-type highland)
		)
	)
=>
	(set-goal enemy-boats-goal 1); pretend this for grush
	(set-strategic-number sn-dock-training-filter 0)
)
#load-if-not-defined FORTRESS-MAP
#load-if-not-defined OASIS-MAP
#load-if-not-defined SALT-MARSH-MAP
(defrule
	(or
		(cc-players-unit-type-count 0 sea-fish >= min-fishing)
		(cc-players-unit-type-count 0 shore-fish >= min-fishing)
	)
=>
	(enable-wall-placement 2)
	(set-strategic-number sn-minimum-water-body-size-for-dock water-mixed)
	(set-strategic-number sn-percent-attack-soldiers 0)
	(set-strategic-number sn-number-boat-explore-groups 0)
	(chat-to-player chat-player "some fish found on map")
	(disable-self))
#else
(defrule
	(true)
=>
	(enable-wall-placement 2)
	(set-strategic-number sn-maximum-fish-boat-drop-distance 14)
	(set-strategic-number sn-minimum-water-body-size-for-dock water-mixed)
	(set-strategic-number sn-percent-attack-soldiers 0)
	(set-strategic-number sn-number-boat-explore-groups 0)
	(chat-to-player chat-player "some fish found on map")
	(disable-self))
#end-if
#end-if
#end-if
#load-if-not-defined COASTAL-MAP
#load-if-not-defined MEDITERRANEAN-MAP
#load-if-not-defined RIVERS-MAP
#load-if-not-defined BALTIC-MAP
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(wall-completed-percentage 2 == 100)
	(players-building-type-count my-player-number stone-wall < 15); this will be only true on islands
	(players-building-type-count my-player-number palisade-wall < 15)
=>
	(set-strategic-number sn-minimum-water-body-size-for-dock water-islands)
	(set-strategic-number sn-home-exploration-time 1200)
	(set-goal strategy-goal rush)
	(chat-to-player chat-player "water map detected!")
	(disable-self)
)
#end-if
#end-if
#end-if
#end-if
(defrule
	(or
		(or
			(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
			(game-time < 1200)
		)
		(up-timer-status five-seconds-timer == timer-running)
	)
=>
	(up-jump-rule 7)
)
(defrule
	(true)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(set-strategic-number sn-focus-player-number 1)
	(set-goal remote-total-goal 0); initialize
)
(defrule
	(strategic-number sn-focus-player-number < 8)
	(stance-toward focus-player enemy)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include -1 -1 -1 1); search same island only
	(up-find-remote c: building-class c: 1)
	(up-get-search-state local-total-goal)
)
(defrule
	(goal remote-total-goal 0)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-jump-rule -2)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(goal remote-total-goal 0)
=>
	(set-strategic-number sn-minimum-water-body-size-for-dock water-islands)
	(chat-to-player chat-player "no enemy on same land left")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(up-compare-goal remote-total-goal > 0)
=>
	(up-modify-sn sn-target-player-number s:= sn-focus-player-number)
	(set-strategic-number sn-minimum-water-body-size-for-dock water-mixed)
	(chat-to-player chat-player "enemy on same land")
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-current-age == dark)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(building-type-count lumber-camp > 0)
	(building-type-count mill > 0)
	(building-type-count dock < 1)
	(wood-amount > 210)
	(game-time > 300)
	(up-pending-placement c: dock)
=>
	(up-reset-placement c: dock)
	(set-strategic-number sn-minimum-water-body-size-for-dock 999)
	(chat-local-to-self "dock placement failed, assume land map")
	(set-goal strategy-goal pocket-strategy)
	(set-goal unit-goal pocket-unit)
	(disable-self)
)
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(building-type-count lumber-camp > 0)
	(building-type-count mill > 0)
	(building-type-count dock < 1)
	(or
		(wood-amount > 210)
		(strategic-number sn-current-age == df-transit)
	)
	(game-time > 300)
=>
	(set-strategic-number sn-minimum-water-body-size-for-dock 999)
	(chat-local-to-self "no dock placemed, assume land map")
	(set-goal strategy-goal pocket-strategy)
	(set-goal unit-goal pocket-unit)
	(disable-self)
)
#else
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(building-type-count lumber-camp > 0)
	(building-type-count mill > 0)
	(building-type-count dock < 1)
	(or
		(wood-amount > 210)
		(strategic-number sn-current-age == df-transit)
	)
	(game-time > 300)
	(up-pending-placement c: dock)
=>
	(up-reset-placement c: dock)
	(set-strategic-number sn-minimum-water-body-size-for-dock 999)
	(chat-local-to-self "dock placement failed, assume land map")
	(disable-self)
)
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(building-type-count lumber-camp > 0)
	(building-type-count mill > 0)
	(building-type-count-total dock == 0)
	(or
		(wood-amount > 210)
		(strategic-number sn-current-age == df-transit)
	)
	(game-time > 300)
=>
	(set-strategic-number sn-minimum-water-body-size-for-dock 999)
	(chat-local-to-self "no dock placemed, assume land map")
	(disable-self)
)
#end-if
#load-if-not-defined VIKING-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(or
		(goal unit-goal skirmisher)
		(goal unit-goal archer)
	)
	(up-compare-goal strategy-goal != flush)
	(current-age < castle-age)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal 0)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(up-compare-goal strategy-goal != flush)
	(current-age < castle-age)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal knight)
)
#end-if
#load-if-not-defined MAYAN-CIV
#load-if-not-defined PERSIAN-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(or
		(goal unit-goal skirmisher)
		(goal unit-goal archer)
	)
	(current-age < castle-age)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal militiaman-line)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(or
		(goal unit-goal skirmisher)
		(goal unit-goal archer)
	)
	(current-age < castle-age)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal knight)
)
#end-if
#end-if
(defrule
	(or
		(players-unit-type-count any-enemy longboat-line > 0)
		(or
			(players-unit-type-count any-enemy galley-line > 0)
			(players-unit-type-count any-enemy fire-ship-line > 0)
		)
	)
=>
	(set-goal enemy-boats-goal 1)
)
(defrule
	(building-type-count dock > 0)
	(or
		(players-building-type-count any-enemy dock > 0)
		(players-unit-type-count any-enemy fishing-ship > 0)
	)
=>
	(set-goal enemy-boats-goal 1)
)
#load-if-not-defined NOMAD-MAP
#load-if-not-defined UP-ALLY-IN-GAME
#load-if-not-defined UP-2-PLAYER-GAME
(defrule
	(true)
=>
	(set-goal nr-map-goal 1)
	(set-goal ffa-game-goal 1)
	(disable-self)
)
#end-if
#end-if
#end-if
#load-if-not-defined DEATH-MATCH
(defrule
	(building-type-count town-center > 0)
	(or
		(players-building-type-count any-enemy watch-tower > 0)
		(players-building-type-count any-enemy castle > 0)
	)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-get-focus-fact player-distance 0 temp-goal)
)
(defrule
	(building-type-count town-center > 0)
	(or
		(players-building-type-count focus-player watch-tower > 0)
		(players-building-type-count focus-player castle > 0)
	)
	(up-compare-goal temp-goal < 30)
=>
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 30)
)
(defrule
	(building-type-count town-center > 0)
	(or
		(players-building-type-count focus-player watch-tower > 0)		
		(players-building-type-count focus-player castle > 0)
	)
	(up-compare-goal temp-goal < 30)
	(or
		(up-find-remote c: tower-class c: 1)
		(up-find-remote c: castle c: 1)
	)
=>
	(set-goal forward-threat-goal 1)
	(chat-local-to-self "forwarded")
)
(defrule
	(game-time < 1320)
	(building-type-count town-center > 0)
	(players-building-type-count focus-player watch-tower > 0)
	(up-compare-goal temp-goal < 25)
=>
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(game-time < 1320)
	(building-type-count town-center > 0)
	(players-building-type-count focus-player watch-tower > 0)
	(up-compare-goal temp-goal < 25); enemy close
	(not (up-find-remote c: tower-class c: 1)); but no enemy tower found anywhere
=>
	(set-goal forward-threat-goal 2); assume tower is under construction
	(chat-local-to-self "maybe forwarded")
)
#end-if
(defrule
	(true)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)

;******************** MILITARY SUPERIORITY CODE ********************

#load-if-not-defined UP-1-PLAYER-TEAM
(defrule
	(goal second-enemy-goal -1)
	(game-time < 1500)
	(goal strategy-goal flush)
	(or
		(up-player-distance focus-player < 70)
		(goal position-goal pocket)
	)
	(stance-toward focus-player enemy)
=>
	(set-strategic-number sn-military-level 0)
	(up-get-fact military-population 0 temp-goal3)
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-modify-goal temp-goal g:= temp-goal3)
	(up-modify-goal temp-goal g:- temp-goal2)
	(up-jump-rule 1)
)
#end-if
(defrule
	(true)
=>
	(set-strategic-number sn-military-level 0)
	(up-get-fact military-population 0 temp-goal3)
	(up-get-fact-max any-enemy military-population 0 temp-goal2)
	(up-modify-goal temp-goal g:= temp-goal3)
	(up-modify-goal temp-goal g:- temp-goal2)
)
(defrule
	(up-compare-goal temp-goal > 5)
	(or
		(unit-type-count war-wagon-line > 12)
		(unit-type-count cavalry-class > 12)
	)
=>
	(set-strategic-number sn-military-level 1)
	(up-modify-goal temp-goal3 c:* 10)
	(up-modify-goal temp-goal2 c:* 13)
	(up-jump-rule 1)
)
(defrule
	(up-compare-goal temp-goal > 5)
=>
	(set-strategic-number sn-military-level 1)
	(up-modify-goal temp-goal3 c:* 10)
	(up-modify-goal temp-goal2 c:* 15)
)
(defrule
	(military-population > 8)
	(up-compare-goal temp-goal > 7)
	(up-compare-goal temp-goal3 g:> temp-goal2)
=>
	(set-strategic-number sn-military-level 2)
	(up-modify-goal temp-goal3 c:/ 10)
	(up-modify-goal temp-goal2 c:/ 7)
)
(defrule
	(military-population > 12)
	(up-compare-goal temp-goal > 10)
	(up-compare-goal temp-goal3 g:> temp-goal2)
=>
	(set-strategic-number sn-military-level 3)
)
(defrule
	(up-compare-goal temp-goal < -1)
	(military-population < 10)
=>
	(set-strategic-number sn-military-level -1)
)
(defrule
	(up-compare-goal temp-goal < -4)
=>
	(up-modify-goal temp-goal3 c:* 6)
	(up-modify-goal temp-goal2 c:* 5)
)
(defrule
	(up-compare-goal temp-goal < -5)
	(up-compare-goal temp-goal3 g:< temp-goal2)
=>
	(set-strategic-number sn-military-level -1)
	(up-modify-goal temp-goal3 c:* 6)
	(up-modify-goal temp-goal2 c:* 5)
)
(defrule
	(up-compare-goal temp-goal < -5)
	(up-compare-goal temp-goal3 g:< temp-goal2)
	(strategic-number sn-military-level < 0)
=>
	(set-strategic-number sn-military-level -2)
	(up-modify-goal temp-goal3 c:* 6)
	(up-modify-goal temp-goal2 c:* 5)
	(set-strategic-number sn-enemy-sighted-response-distance 6)
)
(defrule
	(up-compare-goal temp-goal < -5)
	(up-compare-goal temp-goal3 g:< temp-goal2)
	(strategic-number sn-military-level < -2)
=>
	(set-strategic-number sn-military-level -3)
	(up-modify-goal temp-goal3 c:* 7)
	(up-modify-goal temp-goal2 c:* 5)
)
(defrule
	(up-compare-goal temp-goal < -5)
	(up-compare-goal temp-goal3 g:< temp-goal2)
	(strategic-number sn-military-level < -3)
=>
	(set-strategic-number sn-military-level -4)
)
(defrule
	(game-time < 1380)
	(goal strategy-goal flush)
	(up-player-distance focus-player < 70)
	(stance-toward focus-player enemy)
	(or
		(and
			(unit-type-count militiaman-line > 3)
			(players-unit-type-count focus-player archer-line < 6)
		)
		(and
			(unit-type-count scout-cavalry > 3)
			(players-unit-type-count focus-player spearman-line < 3)
		)
	)
	(players-unit-type-count focus-player skirmisher-line > 10)
=>
	(up-modify-sn sn-military-level c:+ 1)
)
(defrule
	(strategic-number sn-maximum-town-size < 24)
	(or
		(population > fifty-percent-pop)
		(or
			(or
				(unit-type-count castle-arrow > 0)
				(unit-type-count castle-arrow-chemistry > 0)
			)
			(or
				(unit-type-count tc-arrow > 0)
				(unit-type-count tc-arrow-chemistry > 0)
			)
		)
	)
=>
	(set-strategic-number sn-maximum-town-size 24)
	(set-strategic-number sn-enemy-sighted-response-distance 30)
)
(defrule
	(strategic-number sn-maximum-town-size < 36)
	(population > eighty-five-percent-pop)
	(building-type-count-total castle > 0)
=>
	(set-strategic-number sn-maximum-town-size 36)
	(set-strategic-number sn-enemy-sighted-response-distance 30)
)
(defrule
	(players-building-type-count focus-player watch-tower > 3)
	(up-projectile-detected projectile-watch-tower c:< 5000)
	(goal forward-threat-goal 1)
	(goal strategy-goal flush)
	(current-age == feudal-age)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(strategic-number sn-infantry-threat < 1)
	(or 
		(or
			(or
				(players-unit-type-count any-enemy militiaman-line > 6)
				(players-unit-type-count any-enemy eagle-warrior-line > 6)
			)
			(or
				(players-unit-type-count any-enemy teutonic-knight-line > 4)
				(players-unit-type-count any-enemy berserk-line > 4)
			)
		)
		(or
			(or
				(players-unit-type-count any-enemy samurai-line > 4)
				(players-unit-type-count any-enemy woad-raider-line > 4)
			)
			(players-unit-type-count any-enemy jaguar-man-line > 4)
		)
	)
=>
	(set-strategic-number sn-infantry-threat 1)
)
(defrule
	(strategic-number sn-infantry-threat < 1)
	(or
		(players-unit-type-count any-enemy huskarl-line > 6)
		(players-unit-type-count any-enemy throwing-axeman-line > 6)
	)
=>
	(set-strategic-number sn-infantry-threat 1)
)
(defrule
	(strategic-number sn-infantry-threat < 2)
	(or 
		(or
			(or
				(players-unit-type-count any-enemy militiaman-line > 12)
				(players-unit-type-count any-enemy eagle-warrior-line > 12)
			)
			(or
				(players-unit-type-count any-enemy teutonic-knight-line > 10)
				(players-unit-type-count any-enemy berserk-line > 10)
			)
		)
		(or
			(or
				(players-unit-type-count any-enemy samurai-line > 10)
				(players-unit-type-count any-enemy woad-raider-line > 10)
			)
			(players-unit-type-count any-enemy jaguar-man-line > 12)
		)
	)
=>
	(set-strategic-number sn-infantry-threat 2)
)
(defrule
	(strategic-number sn-infantry-threat < 2)
	(or 
		(or
			(and
				(players-unit-type-count any-enemy spearman-line > 18)
				(unit-type-count cavalry-class > 20)
			)
			(players-unit-type-count any-enemy huskarl-line > 12)
		)
		(players-unit-type-count any-enemy throwing-axeman-line > 12)
	)
=>
	(set-strategic-number sn-infantry-threat 2)
)
(defrule
	(strategic-number sn-infantry-threat < 2)
	(or 
		(or
			(or
				(players-unit-type-count any-enemy militiaman-line > 20)
				(players-unit-type-count any-enemy eagle-warrior-line > 20)
			)
			(or
				(players-unit-type-count any-enemy teutonic-knight-line > 15)
				(players-unit-type-count any-enemy berserk-line > 15)
			)
		)
		(or
			(or
				(players-unit-type-count any-enemy samurai-line > 15)
				(players-unit-type-count any-enemy woad-raider-line > 15)
			)
			(players-unit-type-count any-enemy jaguar-man-line > 15)
		)
	)
=>
	(set-strategic-number sn-infantry-threat 3)
)
(defrule
	(strategic-number sn-infantry-threat < 3)
	(or 
		(or
			(and
				(players-unit-type-count any-enemy spearman-line > 25)
				(unit-type-count cavalry-class > 20)
			)
			(players-unit-type-count any-enemy huskarl-line > 16)
		)
		(players-unit-type-count any-enemy throwing-axeman-line > 15)
	)
=>
	(set-strategic-number sn-infantry-threat 3)
)
(defrule
	(strategic-number sn-infantry-threat < 4)
	(or 
		(or
			(or
				(players-unit-type-count any-enemy militiaman-line > 36)
				(players-unit-type-count any-enemy eagle-warrior-line > 36)
			)
			(or
				(players-unit-type-count any-enemy teutonic-knight-line > 24)
				(players-unit-type-count any-enemy berserk-line > 24)
			)
		)
		(or
			(or
				(players-unit-type-count any-enemy samurai-line > 24)
				(players-unit-type-count any-enemy woad-raider-line > 24)
			)
			(players-unit-type-count any-enemy jaguar-man-line > 24)
		)
	)
=>
	(set-strategic-number sn-infantry-threat 4)
)
(defrule
	(strategic-number sn-infantry-threat < 4)
	(or 
		(or
			(and
				(players-unit-type-count any-enemy spearman-line > 46)
				(unit-type-count cavalry-class > 20)
			)
			(players-unit-type-count any-enemy huskarl-line > 24)
		)
		(players-unit-type-count any-enemy throwing-axeman-line > 24)
	)
=>
	(set-strategic-number sn-infantry-threat 4)
)
(defrule
	(strategic-number sn-archer-threat < 1)
	(game-time < 1500)
	(players-building-type-count focus-player castle > 0); early enemy castle and ranged uu civ
	(or
		(or
			(players-civ focus-player briton)
			(players-civ focus-player chinese)
		)
		(or
			(or
				(players-civ focus-player mayan)
				(players-civ focus-player mongol)
			)
			(or
				(players-civ focus-player spanish)
				(players-civ focus-player turkish)
			)
		)
	)
=>
	(set-strategic-number sn-archer-threat 1)
)
(defrule
	(strategic-number sn-archer-threat < 1)
	(game-time < 1500)
	(players-building-type-count focus-player castle > 0); early enemy castle and ranged uu civ
	(or
		(players-civ focus-player korean)
		(players-civ focus-player italian)
	)
=>
	(set-strategic-number sn-archer-threat 1)
)
(defrule
	(strategic-number sn-archer-threat < 1)
	(game-time < 1500)
	(players-building-type-count focus-player archery-range > 1)
	(or
		(goal enemy-goal skirmisher); enemy probably going for xbow rush
		(or
			(goal enemy-goal rush)
			(goal enemy-goal flush)
		)
	)
=>
	(set-strategic-number sn-archer-threat 1)
)
(defrule
	(strategic-number sn-archer-threat < 1)
	(game-time < 1650)
	(or 
		(or
			(players-unit-type-count any-enemy genoese-crossbowman > 4)
			(or
				(players-unit-type-count focus-player chu-ko-nu-line > 4)
				(players-unit-type-count focus-player longbowman-line > 4)
			)
		)
		(or
			(or
				(players-unit-type-count focus-player plumed-archer-line > 4)
				(players-unit-type-count focus-player mangudai-line > 4)
			)
			(or
				(players-unit-type-count focus-player janissary-line > 4)
				(players-unit-type-count focus-player war-wagon-line > 4)
			)
		)
	)
=>
	(set-strategic-number sn-archer-threat 1)
)
(defrule
	(strategic-number sn-archer-threat < 1)
	(or
		(players-unit-type-count any-enemy hand-cannoneer > 6)
		(or
			(players-unit-type-count any-enemy conquistador-line > 4)
			(players-unit-type-count any-enemy janissary-line > 4)
		)
	)
=>
	(set-strategic-number sn-archer-threat 1)
)
(defrule
	(strategic-number sn-archer-threat < 2)
	(or 
		(or
			(players-unit-type-count any-enemy archer-line > 13)
			(or
				(players-unit-type-count any-enemy chu-ko-nu-line > 9)
				(players-unit-type-count any-enemy longbowman-line > 9)
			)
		)
		(or
			(or
				(players-unit-type-count any-enemy plumed-archer-line > 9)
				(players-unit-type-count any-enemy mangudai-line > 9)
			)
			(or
				(players-unit-type-count any-enemy cavalry-archer-line > 9)
				(players-unit-type-count any-enemy war-wagon-line > 9)
			)
		)
	)
=>
	(set-strategic-number sn-archer-threat 2)
)
(defrule
	(strategic-number sn-archer-threat < 2)
	(or
		(or
			(or
				(players-unit-type-count any-enemy elephant-archer > 9)
				(players-unit-type-count any-enemy genoese-crossbowman > 9)
			)
			(or
				(players-unit-type-count any-enemy hand-cannoneer > 13)
				(and
					(goal unit-goal archer)
					(players-unit-type-count focus-player skirmisher-line > 15)
				)
			)
		)
		(or
			(players-unit-type-count any-enemy conquistador-line > 9)
			(players-unit-type-count any-enemy janissary-line > 9)
		)
	)
=>
	(set-strategic-number sn-archer-threat 2)
)
(defrule
	(strategic-number sn-archer-threat < 3)
	(or 
		(or
			(players-unit-type-count any-enemy archer-line > 25)
			(or
				(players-unit-type-count any-enemy chu-ko-nu-line > 20)
				(players-unit-type-count any-enemy longbowman-line > 20)
			)
		)
		(or
			(or
				(players-unit-type-count any-enemy plumed-archer-line > 20)
				(players-unit-type-count any-enemy mangudai-line > 20)
			)
			(or
				(players-unit-type-count any-enemy cavalry-archer-line > 20)
				(players-unit-type-count any-enemy war-wagon-line > 20)
			)
		)
	)
=>
	(set-strategic-number sn-archer-threat 3)
)
(defrule
	(strategic-number sn-archer-threat < 3)
	(players-unit-type-count focus-player archer-line > 12)
	(players-unit-type-count focus-player skirmisher-line > 12)
=>
	(set-strategic-number sn-archer-threat 3)
)
(defrule
	(strategic-number sn-archer-threat < 4)
	(or 
		(and
			(strategic-number sn-archer-threat == 3)
			(players-unit-type-count any-enemy skirmisher-line > 25)
		)
		(players-unit-type-count any-enemy archer-line > 40)
	)
=>
	(set-strategic-number sn-archer-threat 4)
)
(defrule
	(strategic-number sn-archer-threat < 4)
	(or 
		(or
			(players-unit-type-count any-enemy genoese-crossbowman > 40)
			(or
				(players-unit-type-count any-enemy chu-ko-nu-line > 40)
				(players-unit-type-count any-enemy longbowman-line > 40)
			)
		)
		(or
			(or
				(players-unit-type-count any-enemy plumed-archer-line > 40)
				(players-unit-type-count any-enemy mangudai-line > 40)
			)
			(or
				(players-unit-type-count any-enemy cavalry-archer-line > 40)
				(players-unit-type-count any-enemy war-wagon-line > 40)
			)
		)
	)
=>
	(set-strategic-number sn-archer-threat 4)
)
(defrule
	(strategic-number sn-cavalry-threat < 1)
	(or
		(players-unit-type-count any-enemy camel-line > 4)
		(players-unit-type-count any-enemy cataphract-line > 4)
	)
=>
	(set-strategic-number sn-cavalry-threat 1)
)
(defrule
	(strategic-number sn-cavalry-threat < 1)
	(or
		(or
			(players-unit-type-count any-enemy magyar-huszar > 4)
			(players-unit-type-count any-enemy boyar > 4)
		)
		(or
			(or
				(players-unit-type-count any-enemy knight-line > 4)
				(players-unit-type-count any-enemy scout-cavalry-line > 4)
			)
			(or
				(players-unit-type-count any-enemy tarkan-line > 3)
				(players-unit-type-count any-enemy war-elephant-line > 2)
			)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 1)
)
(defrule
	(strategic-number sn-cavalry-threat < 2)
	(or
		(or
			(players-unit-type-count any-enemy magyar-huszar > 10)
			(players-unit-type-count any-enemy boyar > 10)
		)
		(or
			(or
				(players-unit-type-count any-enemy knight-line > 10)
				(players-unit-type-count any-enemy scout-cavalry-line > 10)
			)
			(or
				(players-unit-type-count any-enemy tarkan-line > 10)
				(players-unit-type-count any-enemy war-elephant-line > 6)
			)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 2)
)
(defrule
	(strategic-number sn-cavalry-threat < 2)
	(or
		(or
			(players-unit-type-count any-enemy camel-line > 10)
			(players-unit-type-count any-enemy cataphract-line > 10)
		)
		(and
			(players-unit-type-count any-enemy knight-line > 4)
			(players-unit-type-count any-enemy camel-line > 4)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 2)
)
(defrule
	(strategic-number sn-cavalry-threat < 3)
	(or
		(or
			(players-unit-type-count any-enemy magyar-huszar > 16)
			(players-unit-type-count any-enemy boyar > 16)
		)
		(or
			(or
				(players-unit-type-count any-enemy knight-line > 16)
				(players-unit-type-count any-enemy scout-cavalry-line > 16)
			)
			(or
				(players-unit-type-count any-enemy tarkan-line > 16)
				(players-unit-type-count any-enemy war-elephant-line > 10)
			)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 3)
)
(defrule
	(strategic-number sn-cavalry-threat < 3)
	(or
		(or
			(players-unit-type-count any-enemy camel-line > 16)
			(players-unit-type-count any-enemy cataphract-line > 16)
		)
		(and
			(players-unit-type-count any-enemy knight-line > 10)
			(players-unit-type-count any-enemy camel-line > 6)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 3)
)
(defrule
	(strategic-number sn-cavalry-threat < 4)
	(or
		(or
			(players-unit-type-count any-enemy magyar-huszar > 26)
			(players-unit-type-count any-enemy boyar > 22)
		)
		(or
			(or
				(players-unit-type-count any-enemy knight-line > 22)
				(players-unit-type-count any-enemy scout-cavalry-line > 26)
			)
			(or
				(players-unit-type-count any-enemy tarkan-line > 22)
				(players-unit-type-count any-enemy war-elephant-line > 15)
			)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 4)
)

(defrule
	(strategic-number sn-cavalry-threat < 4)
	(or
		(or
			(players-unit-type-count any-enemy camel-line > 22)
			(players-unit-type-count any-enemy cataphract-line > 22)
		)
		(and
			(players-unit-type-count any-enemy knight-line > 16)
			(players-unit-type-count any-enemy camel-line > 7)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 4)
)
(defrule
	(or
		(or
			(players-unit-type-count any-enemy camel-line > 9)
			(players-unit-type-count any-enemy spearman-line > 15)
		)
		(or
			(players-unit-type-count any-enemy mameluke-line > 9)
			(and
				(game-time < 1380)
				(players-unit-type-count focus-player spearman-line > 6)
			)
		)
	)
=>
	(set-goal anti-cavalry-threat-goal 1)
)
(defrule
	(or
		(players-unit-type-count focus-player scout-cavalry-line > 9)
		(or
			(players-unit-type-count any-enemy eagle-warrior-line > 9)
			(goal enemy-goal eagle-warrior)
		)
	)
=>
	(set-goal anti-monk-threat-goal 1)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-cavalry-threat == 0);defend vs scout flush, krush
	(game-time > 1000)
	(game-time < 1320)
	(players-building-type-count any-enemy stable > 0)
	(or
		(players-unit-type-count focus-player stable > 0)
		(and
			(players-building-count any-enemy == 0)
			(up-players-in-game ally == 4)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 1)
)
#end-if
(defrule
	(up-compare-goal second-enemy-goal != -1)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-modify-sn sn-focus-player-number g:= second-enemy-goal)
)
(defrule
	(or
		(players-unit-type-count focus-player monk > 5)
		(and
			(game-time < 1500)
			(players-building-type-count focus-player monastery > 1)
		)
	)
	(or
		(game-time < 1500)
		(players-unit-type-count focus-player monk > 11)
	)
	(or
		(game-time < 1800)
		(players-unit-type-count focus-player monk > 18)
	)
=>
	(set-goal monk-threat-goal 1)
)

; Following rules help to make up for poor scouting.

(defrule
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
	(strategic-number sn-cavalry-threat < 2)
	(game-time < 2700)
	(game-time > 1020)
	(or
		(and
			(and
				(game-time < 1500)
				(players-unit-type-count focus-player knight-line > 3)
			)
			(or
				(players-unit-type-count focus-player knight-line > 5)
				(game-time < 1320)
			)
		)
		(players-building-type-count focus-player stable > 1)
	)
=>
	(set-strategic-number sn-cavalry-threat 2)
)
(defrule
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
	(game-time > 960)
	(game-time < 2700)
	(strategic-number sn-cavalry-threat > 0)
	(or
		(players-unit-type-count focus-player archer-line > 7)
		(players-building-type-count focus-player archery-range > 1)
	)
=>
	(set-strategic-number sn-cavalry-threat 0)
	(set-strategic-number sn-archer-threat 2)
)
(defrule
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
	(game-time > 960)
	(game-time < 2700)
	(strategic-number sn-cavalry-threat > 0)
	(or
		(players-civ focus-player incan)
		(or
			(players-civ focus-player aztec)
			(players-civ focus-player mayan)
		)
	)
=>
	(set-strategic-number sn-cavalry-threat 0)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
	(game-time > 960)
	(game-time < 1800)
	(or
		(players-unit-type-count focus-player archer-line > 7)
		(players-building-type-count focus-player archery-range > 1)
	)
	(or
		(unit-type-count cavalry-class > 6)
		(unit-type-count scorpion-line > 3)
	)
=>
	(up-modify-sn sn-military-level c:+ 1)
)
#end-if

(defrule
	(true)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)

#load-if-defined DEATH-MATCH
(defrule
	(strategic-number sn-cavalry-threat < 1)
	(current-age >= castle-age)
	(game-time < 480)
	(nor
		(or
			(players-civ every-enemy gothic)
			(players-civ every-enemy korean)
		)
		(or
			(players-civ every-enemy viking)
			(players-civ every-enemy briton)
		)
	)
	(nor 
		(players-civ every-enemy japanese)
		(goal meso-enemy-goal 1)
	)
=>
	(set-strategic-number sn-cavalry-threat 1)
)
(defrule
	(strategic-number sn-infantry-threat < 1)
	(current-age >= castle-age)
	(game-time < 300)
	(or
		(or
			(players-civ every-enemy gothic)
			(players-civ every-enemy aztec)
		)
		(or
			(players-civ every-enemy japanese)
			(players-civ every-enemy viking)
		)
	)
=>
	(set-strategic-number sn-infantry-threat 1)
)

; Scouting

(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(players-building-count every-enemy == 0)
	(goal under-attack-goal 0)
	(warboat-count > 0)
=>
	(set-strategic-number sn-number-boat-explore-groups 5)
	(set-strategic-number sn-total-number-explorers 6)
)
(defrule
	(strategic-number sn-number-boat-explore-groups > 1)
	(players-building-type-count every-enemy dock > 0)
=>
	(set-strategic-number sn-number-boat-explore-groups 1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(timer-triggered one-minute-timer)		
		(game-time < 3)
	)
	(game-time < 720)
	(or
		(players-building-count every-enemy == 0)
		(player-in-game any-ally)
	)
=>
	(up-send-scout group-type-land-explore scouting-style)
)
#else
(defrule
	(players-building-type-count every-enemy dock == 0)
	(warboat-count > 0)
	(strategic-number sn-number-boat-explore-groups < 1)
	(nand
		(warboat-count < 12)
		(unit-type-count revealer > 0)
	)
=>
	(set-strategic-number sn-number-boat-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 2)
	(chat-local-to-self "search enemy docks")
)
(defrule
	(timer-triggered one-minute-timer)
	(up-compare-goal avoid-tc-goal < 1)
	(game-time > 1200)
	(players-building-count every-enemy > 0)
	(or
		(or
			(strategic-number sn-infantry-threat >= 1)
			(strategic-number sn-archer-threat >= 1)
		)
		(or
			(strategic-number sn-cavalry-threat >= 1)
			(goal monk-threat-goal 1)
		)
	)
	(strategic-number sn-number-explore-groups == 1)
=>
	(set-strategic-number sn-number-explore-groups 0)
	(set-strategic-number sn-total-number-explorers 0)
	(chat-local-to-self "disable scouting");disable scouting when found threats to not waste units
)
(defrule
	(game-time > 840)
	(timer-triggered one-minute-timer)
	(up-compare-goal avoid-tc-goal < 1)
	(up-compare-goal kill-villager-goal != 1)
	(players-building-count any-enemy == 0)
	(strategic-number sn-number-explore-groups == 0)
	(goal under-attack-goal 0)
=>
	(set-strategic-number sn-number-explore-groups 1); restart when enemy not found
	(set-strategic-number sn-total-number-explorers 1)
	(chat-local-to-self "restart scouting, not all enemies found")
)
(defrule
	(game-time > 840)
	(timer-triggered one-minute-timer)
	(up-compare-goal avoid-tc-goal < 1)
	(up-compare-goal kill-villager-goal != 1)
	(strategic-number sn-infantry-threat == 0)
	(strategic-number sn-archer-threat == 0)
	(strategic-number sn-cavalry-threat == 0)
	(goal monk-threat-goal 0)
	(goal under-attack-goal 0)
	(strategic-number sn-number-explore-groups == 0)
=>
	(set-strategic-number sn-number-explore-groups 1); restart scouting when no threats found
	(set-strategic-number sn-total-number-explorers 1)
	(up-send-scout group-type-land-explore position-target)
	(chat-local-to-self "restart scouting, no threats found")
)
(defrule
	(goal strategy-goal flush)
	(goal enemy-goal flush)
	(players-current-age focus-player == castle-age)
	(goal hostilities-goal 0)
	(up-compare-goal avoid-tc-goal < 1)
	(up-compare-goal kill-villager-goal != 1)
=>
	(set-strategic-number sn-number-explore-groups 1); restart scouting when no threats found
	(set-strategic-number sn-total-number-explorers 1)
	(up-send-scout group-type-land-explore position-target)
	(chat-local-to-self "enemy reached castle age, send scout")
	(disable-self)
)
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(up-compare-goal avoid-tc-goal < 1)
	(game-time < 1200)
	(players-unit-type-count focus-player skirmisher > 4)
	(up-compare-goal under-attack-goal > 0)
	(unit-type-count scout-unit == 1)
=>
	(set-strategic-number sn-number-explore-groups 0)
	(set-strategic-number sn-total-number-explorers 0)
	(up-modify-sn sn-target-player-number g:= threat-player-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(up-compare-goal avoid-tc-goal < 1)
	(game-time < 1200)
	(players-unit-type-count focus-player skirmisher > 4)
	(up-compare-goal under-attack-goal > 0)
	(up-find-local c: scout-unit c: 1)
=>
	(up-reset-scouts)
	(up-set-target-point position-self-goal)
	(up-filter-distance c: -1 c: 18)
	(up-find-remote c: skirmisher c: 1)
	(up-set-target-object search-remote c: 0)
	(up-target-objects 0 action-default -1 aggressive)
;	(chat-to-player my-player-number "use scout for defense")
	(set-strategic-number sn-allow-civilian-defense 3)
)
(defrule
	(up-compare-goal under-attack-goal > 0)
	(unit-type-count monk > 0)
=>
	(up-modify-sn sn-target-player-number g:= threat-player-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-set-target-point position-self-goal)
	(up-filter-distance c: -1 c: 18)
	(up-filter-include cmdid-monk -1 orderid-relic -1)
	(up-find-local c: -1 c: 200)
	(up-get-search-state local-total-goal)
	(up-filter-include -1 -1 -1 -1)
)
(defrule
	(up-compare-goal under-attack-goal > 0)
	(unit-type-count monk > 0)
	(up-compare-goal local-total-goal > 0)
	(up-find-remote c: -1 c: 1)
=>
	(up-target-objects 0 action-patrol -1 -1)
	(chat-to-player my-player-number "use relic monk for defense")
)
(defrule
	(timer-triggered five-seconds-timer)
	(game-time < 900)
	(players-military-population focus-player < 14)
	(players-current-age focus-player == feudal-age)
	(players-unit-type-count focus-player skirmisher > 3)
	(players-unit-type-count focus-player archer < 2)
=>
	(set-strategic-number sn-allow-civilian-defense 3)
)
(defrule
	(game-time < 600)
	(players-military-population any-enemy > 1)
	(players-current-age every-enemy == dark-age)
	(strategic-number sn-current-age == dark)
	(up-compare-goal enemy-goal != drush)
=>
	(set-goal enemy-goal drush)
	(set-strategic-number sn-allow-civilian-defense 3)
	(chat-to-player chat-player "24enemy drush")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age >= df-transit)
	(game-time < 840)
	(players-building-count any-enemy == 0)
	(goal avoid-tc-goal -1)
	(goal kill-villager-goal -1)
=>
	(up-send-scout group-type-land-explore scouting-style)
	(chat-to-player chat-player "search for enemy")
	(enable-timer scouting-timer 30)
	(set-strategic-number sn-number-explore-groups 1)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(timer-triggered scouting-timer)
	(game-time > 590)
	(game-time < 1200)
	(players-building-count any-enemy == 0)
	(strategic-number sn-current-age >= df-transit)
=>
	(up-send-scout group-type-land-explore scouting-style)
	(enable-timer scouting-timer 30)
)
#end-if; not MICHI
#end-if; not DM
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(player-in-game any-enemy)
	(or
		(players-civ every-enemy aztec)
		(or
			(players-civ every-enemy mayan)
			(players-civ every-enemy incan)
		)
	)
=>
	(set-goal meso-enemy-goal 1)
	(disable-self)
)
#end-if
(defrule
	(up-compare-goal closest-enemy-goal != -1)
	(or
		(up-player-distance focus-player < 70)
		(up-players-in-game any == 2)
	)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-modify-goal enemy-previous-score-goal g:= enemy-score-goal)
	(up-get-focus-fact current-score 0 enemy-score-goal)
	(up-modify-goal temp-goal8 g:= enemy-previous-score-goal)
	(up-modify-goal temp-goal8 g:- enemy-score-goal)
)
#load-if-not-defined POPULATION-CAP-25
#load-if-not-defined UP-MICHI-STYLE
#load-if-not-defined FORTRESS-MAP
#load-if-defined LOW-RESOURCES-START
(defrule
	(players-current-age focus-player == dark-age)
	(or
		(up-player-distance focus-player < 70)
		(up-players-in-game any == 2)
	)
	(players-civilian-population focus-player < 26)
	(up-compare-goal temp-goal8 > 30)
=>
	(set-goal enemy-goal flush)
	(chat-to-player chat-player "enemy clicked up and might be flushing")
	(disable-self)
)
(defrule
	(players-current-age focus-player == feudal-age)
	(players-military-population focus-player < 4)
	(or
		(up-player-distance focus-player < 70)
		(up-players-in-game any == 2)
	)
	(goal enemy-goal flush)
	(up-compare-goal temp-goal8 > 65)
	(current-age-time < 900)
=>
	(set-goal enemy-goal rush)
	(chat-local-to-self "enemy clicked up and is not flushing")
	(disable-self)
)
(defrule
	(or
		(up-player-distance focus-player < 70)
		(up-players-in-game any == 2)
	)
	(players-military-population focus-player > 4)
	(or
		(game-time < 900)
		(players-military-population focus-player > 7)
	)
	(or
		(game-time < 1000)
		(players-military-population focus-player > 11)
	)
	(players-current-age every-enemy == feudal-age)
=>
	(set-goal enemy-goal flush)
)
#end-if
#end-if
#end-if
#end-if
(defrule
	(up-compare-goal closest-enemy-goal != -1)
	(or
		(up-player-distance focus-player < 70)
		(up-players-in-game any == 2)
	)
	(players-current-age focus-player == feudal-age)
	(up-compare-goal temp-goal8 > 65)
=>
	(set-strategic-number sn-number-explore-groups 1); restart scouting when no threats found
	(set-strategic-number sn-total-number-explorers 1)
	(up-send-scout group-type-land-explore position-target)
	(chat-local-to-self "enemy clicked up, send scout")
)
#load-if-not-defined POST-IMPERIAL-AGE-START
#load-if-not-defined IMPERIAL-AGE-START
#load-if-not-defined CASTLE-AGE-START
#load-if-not-defined DEATH-MATCH
#load-if-not-defined HIGH-RESOURCES-START
(defrule
	(game-time < 1260)
	(players-current-age focus-player >= castle-age)
	(players-military-population focus-player > 5)
	(goal enemy-goal flush)
=>
	(set-goal enemy-goal skirmisher)
	(chat-to-player chat-player "enemy not flushing")
)
(defrule
	(game-time > 960)
	(game-time < 1500)
	(players-unit-type-count focus-player skirmisher-line > 8)
	(up-compare-goal enemy-goal != flush)
	(players-military-population focus-player >= 6)
=>
	(set-goal enemy-goal skirmisher)
	(chat-to-player chat-player "enemy trash strategy detected")
	(disable-self)
)
#end-if
#load-if-not-defined UP-1-PLAYER-TEAM
#load-if-not-defined UP-2-PLAYER-TEAM
(defrule
	(game-time < 2400)
	(up-compare-goal second-enemy-goal != -1)
	(players-current-age any-enemy == imperial-age)
=>
	(up-modify-sn sn-focus-player-number g:= second-enemy-goal)
)
(defrule
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
	(or
		(players-civ focus-player aztec)
		(or
			(players-civ focus-player mayan)
			(players-civ focus-player incan)
		)
	)
=>
	(set-goal meso-enemy-goal 1)
	(disable-self)
)
(defrule
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
	(game-time < 2400)
	(or
		(and
			(goal monk-threat-goal 0)
			(game-time < 2100)
		)
		(players-unit-type-count any-enemy eagle-warrior-line > 25)
	)
	(players-current-age focus-player == imperial-age)
	(up-compare-goal enemy-goal != eagle-warrior)
	(goal meso-enemy-goal 1)
=>
	(set-goal enemy-goal eagle-warrior)
	(chat-to-player chat-player "24EEW incoming")
	(disable-self)
)
#end-if
#end-if
(defrule
	(true)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)
(defrule
	(game-time < 1500)
	(players-unit-type-count focus-player eagle-warrior-line > 4)
	(players-current-age-time focus-player < 150)
=>
	(set-goal enemy-goal eagle-warrior)
	(chat-to-player chat-player "24EW rush detected")
	(disable-self)
)
#end-if
#end-if
#end-if
#end-if

;******************** STRATEGY SELECTION ********************

#load-if-not-defined POST-IMPERIAL-AGE-START
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(goal enemy-goal flush)
	(goal nr-map-goal -1)
	(up-compare-goal strategy-goal != flush)
	(up-compare-goal control-goal != shot)
	(up-compare-goal strategy-goal != sling)
	(up-compare-goal unit-goal != monk)
	(building-type-count gate == 0)
	(current-age == dark-age)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(chat-to-player chat-player "countering enemy flush with feudal war")
)
#end-if
#load-if-defined FEUDAL-AGE-END
(defrule
	(game-time > 60)
=>
	(set-goal enemy-goal flush)
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(disable-self)
)
#else
#load-if-not-defined DIFFICULTY-MODERATE
#load-if-not-defined DIFFICULTY-HARD
#load-if-not-defined DIFFICULTY-HARDEST
#load-if-not-defined DEATH-MATCH
(defrule
	(strategic-number sn-current-age == dark)
	(goal strategy-goal flush)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal default-unit)
	(chat-local-to-self "no flush on easy difficulties")
)
#end-if
#end-if
#end-if
#end-if
#end-if
#load-if-defined DEATH-MATCH ; treat this as flush and build an army in feudal age.
(defrule
	(strategic-number sn-current-age == df-transit)
=>
	(set-goal enemy-goal flush)
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(disable-self)
)
#else
#load-if-not-defined DIFFICULTY-MODERATE
#load-if-not-defined DIFFICULTY-HARD
#load-if-not-defined DIFFICULTY-HARDEST
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(nor
		(goal strategy-goal flush)
		(goal strategy-goal sling)
	)
	(or
		(building-type-count-total mining-camp == 0)
		(dropsite-min-distance gold < 0)
	)
	(strategic-number sn-current-age == feudal)
	(current-age-time < 10)
	(nor
		(town-under-attack)
		(goal unit-goal eagle-warrior)
	)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal no-gold-flush-unit)
	(set-strategic-number sn-maximum-gold-drop-distance 20)
)
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal unit-goal my-unique-unit-line)
	(strategic-number sn-current-age == feudal)
	(current-age-time < 10)
	(or
		(dropsite-min-distance stone < 0); no stone found
		(dropsite-min-distance stone > 33); so switch strategy
	)
=>
	(set-goal unit-goal default-ranged); should work for all civs
	(set-goal ranged-unit-type-goal default-ranged)
)
(defrule
	(or
		(and
			(player-in-game any-human-enemy)
			(or
				(map-type black-forest)
				(cc-players-unit-type-count 0 tree-class > heavy-wood)
			)
		)
		(building-type-count gate > 0)
	)
=>
	(set-goal nr-map-goal 1)
	(disable-self)
)
(defrule
	(taunt-detected any-ally 32)
	(or
		(goal strategy-goal sling)
		(goal control-goal shot)
	)
=>
	(acknowledge-taunt this-any-ally 32)
	(chat-to-allies "The strategy can't be changed")
)

; Change strategy if enemy flushes on NR-map/arena
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal strategy-goal boom)
	(or
		(goal unit-goal knight)
		(or
			(goal unit-goal eagle-warrior)
			(goal unit-goal cavalry-archer)
		)
	)
	(goal enemy-goal flush)
	(game-time < 1800)
	(strategic-number sn-current-age == fc-transit)
=>
	(set-goal strategy-goal rush)
)
(defrule
	(strategic-number sn-current-age == dark)
	(goal enemy-goal flush)
	(goal unit-goal gunpowder-class)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal knight)
	(set-goal ranged-unit-type-goal gunpowder-class)
)
#end-if

; *** STRATEGY DEPENDING ON CIV ***

#load-if-not-defined UP-MICHI-STYLE
#load-if-defined AZTEC-CIV
(defrule
	(or
		(goal position-goal pocket)
		(goal nr-map-goal 1)
	)
	(random-number <= 15)
	(game-time > 13)
	(game-time < 60)
	(not (players-civ any-enemy teutonic))
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal monk)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 13)
	(game-time < 60)
	(or
		(goal strategy-goal flush)
		(random-number <= 20)
	)
	(goal nr-map-goal 1)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
)
(defrule
	(goal nr-map-goal -1)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(random-number <= 40)
	(game-time > 13)
	(or
		(goal strategy-goal flush)
		(game-time < 60)
	)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(set-goal ranged-unit-type-goal monk)
)
(defrule
	(goal nr-map-goal -1)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 15)
	(game-time < 60)
	(up-compare-goal strategy-goal != flush)
	(up-compare-goal control-goal != shot)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(set-goal control-goal 0)
	(disable-self)
)
#end-if
(defrule
	(strategic-number sn-current-age == castle)
	(or
		(and
			(goal strategy-goal boom)
			(or
				(goal unit-goal archer)
				(goal unit-goal eagle-warrior)
			)
		)
		(goal unit-goal skirmisher)
	)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count monastery > 0)
	(or
		(building-type-count-total town-center >= tc-level-four)
		(goal unit-goal eagle-warrior)
	)
=>
	(set-goal strategy-goal fast-imp)
	(set-goal ranged-unit-type-goal 0)
	(set-goal nr-map-goal 1)
)
(defrule
	(or
		(or
			(players-unit-type-count any-enemy huskarl-line > 15)
			(players-unit-type-count any-enemy samurai-line > 15)
		)
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	)
=>
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
(defrule
	(goal nr-map-goal 1)
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
=>
	(set-goal strategy-goal boom)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(or
		(or
			(goal strategy-goal boom)
			(goal strategy-goal fast-imp)
		)
		(and
			(strategic-number sn-archer-threat > 2)
			(and
				(goal ranged-unit-type-goal archer)
				(population >= max-civ-pop)
			)
		)
	)
	(up-compare-goal unit-goal != monk)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal eagle-warrior)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-current-age < ci-transit)
	(goal unit-goal eagle-warrior)
	(goal strategy-goal rush)
	(military-population > 40)
=>
	(set-goal unit-goal eagle-warrior)
	(set-goal strategy-goal fast-imp)
	(set-goal ranged-unit-type-goal 0)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(goal unit-goal eagle-warrior)
	(goal strategy-goal rush)
	(or 
		(players-building-type-count every-enemy castle > 0)
		(or
			(players-unit-type-count any-enemy teutonic-knight-line > 0)
			(players-unit-type-count any-enemy cataphract-line > 0)
		)
	)
	(game-time < 1500)
=>
	(set-goal unit-goal eagle-warrior)
	(set-goal strategy-goal fast-imp)
	(set-goal ranged-unit-type-goal 0)
)
(defrule
	(or
		(goal unit-goal archer)
		(goal unit-goal skirmisher)
	)
	(unit-type-count villager > fourty-percent-pop)
	(goal enemy-goal eagle-warrior)
	(building-type-count-total castle == 0)
	(up-compare-goal unit-goal != militiaman-line)
=>
	(set-goal unit-goal militiaman-line)
	(chat-to-player chat-player "research m@a and long-swordsman")
)
(defrule
	(food-amount > 150)
	(or
		(gold-amount < 50)
		(food-amount > 300)
	)
	(unit-type-count villager > thirty-percent-pop)
	(goal unit-goal eagle-warrior)
	(goal enemy-goal eagle-warrior)
	(building-type-count-total castle == 0)
=>
	(set-goal control-goal militiaman-line)
	(disable-self)
)
(defrule
	(or
		(and
			(strategic-number sn-current-age >= ci-transit)
			(and
				(goal control-goal militiaman-line)
				(research-completed ri-long-swordsman)
			)
		)
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	)
	(nand
		(unit-type-count-total eagle-warrior-line > 20); go for eew instead as some enemy still may have ranged units
		(strategic-number sn-archer-threat > 0)
	)
=>
	(set-goal unit-goal militiaman-line)
	(disable-self)
)
(defrule
	(up-compare-goal control-goal != shot)
	(strategic-number sn-infantry-threat >= 3)
	(players-unit-type-count every-enemy samurai-line < 20)
	(nand
		(players-unit-type-count any-enemy eagle-warrior-line > 20)
		(goal control-goal militiaman-line)
	)
	(dropsite-min-distance stone < 5)
	(building-type-count-total town-center >= tc-level-two)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(building-type-count castle > 1)
	(goal control-goal my-unique-unit-line)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 32)
	(goal strategy-goal boom)
=>
	(acknowledge-taunt this-any-ally 32)
	(set-goal control-goal 0)
	(set-goal strategy-goal rush)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(chat-to-allies "xbow rush")
)
#end-if ; AZTEC CIV STRATEGY
#load-if-defined BRITON-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(or
		(regicide-game)
		(goal nr-map-goal 1)
	)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(random-number > 50)
	(goal nr-map-goal -1)
	(game-time > 15)
	(game-time < 60)
=>
	(set-goal unit-goal knight)
	(set-goal strategy-goal rush)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#end-if
(defrule
	(up-compare-goal control-goal != my-unique-unit-line)
	(up-compare-goal unit-goal != my-unique-unit-line)
	(dropsite-min-distance stone < 5)
	(civilian-population > fourty-percent-pop)
	(nand
		(or
			(players-unit-type-count any-enemy eagle-warrior-line > 14)
			(players-unit-type-count any-enemy huskarl-line > 20)
		)
		(up-research-status c: ri-champion < research-pending)
	)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "prepare uu")
)
(defrule
	(building-type-count castle > 1)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal uu)
)
(defrule
	(up-research-status c: ri-champion < research-pending)
	(or
		(or
			(players-unit-type-count any-enemy eagle-warrior-line > 11)
			(players-unit-type-count any-enemy huskarl-line > 14)
		)
		(and
			(building-type-count castle > 0)
			(goal enemy-goal eagle-warrior)
		)
	)
=>
	(set-goal unit-goal militiaman-line)
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 30)
	(game-time < 300)
	(taunt-detected any-ally 33)
	(or
		(goal strategy-goal flush)
		(goal strategy-goal rush)
	)
=>
	(acknowledge-taunt this-any-ally 33)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(set-goal unit-goal my-unique-unit-line)
	(chat-to-allies "strategy changed to booming with UU.")
)
#end-if ; BRITON STRATEGY
#load-if-defined BYZANTINE-CIV
(defrule
	(building-type-count castle > 0)
	(up-compare-goal unit-goal != my-unique-unit-line)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "switch to unique-units")
	(disable-self)
)
(defrule
	(or
		(or
			(building-type-count castle > 0)
			(and
				(building-type-count gate > 0)
				(goal position-goal pocket)
			)
		)
		(and
			(goal nr-map-goal 1)
			(goal unit-goal skirmisher)
		)
	)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal -1)
	(goal strategy-goal rush)
=>
	(set-goal unit-goal knight)
	(disable-self)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal knight)
	(strategic-number sn-cavalry-threat < 3); make camels
	(strategic-number sn-infantry-threat < 3)
	(players-unit-type-count every-enemy eagle-warrior-line < 15)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
(defrule
	(goal unit-goal knight)
	(or
		(and
			(strategic-number sn-infantry-threat > 2)
			(strategic-number sn-current-age >= ci-transit)
		)
		(or
			(or
				(players-unit-type-count any-enemy eagle-warrior-line >= 15)
				(players-civ focus-player gothic)
			)
			(goal enemy-goal eagle-warrior)
		)
	)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal gunpowder-class)
	(disable-self)
)
(defrule
	(dropsite-min-distance stone < 5)
	(or
		(current-age == imperial-age)
		(players-unit-type-count any-enemy huskarl-line > 6)
	)
	(building-type-count-total town-center >= tc-level-three)
	(up-compare-goal unit-goal != my-unique-unit-line)
	(up-compare-goal control-goal != my-unique-unit-line)
	(up-compare-goal unit-goal != knight)
=>
	(set-goal control-goal my-unique-unit-line)
	(chat-to-player chat-player "prepare for unique-units")
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 30)
	(game-time < 300)
	(taunt-detected any-ally 32)
	(up-compare-goal strategy-goal != flush)
	(goal unit-goal skirmisher)
=>
	(acknowledge-taunt this-any-ally 32)
	(set-goal strategy-goal rush)
	(set-goal control-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal archer)
	(set-goal unit-goal archer)
	(chat-to-allies-using-id 22155)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age == dark)
	(goal nr-map-goal -1)
	(game-time > 15)
	(game-time < 60)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(dropsite-min-distance stone < 5)
	(building-type-count-total town-center >= tc-level-three)
	(up-compare-goal unit-goal != my-unique-unit-line)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-goal control-goal my-unique-unit-line)
	(chat-to-player chat-player "prepare for unique-units")
	(disable-self)
)
#end-if
#end-if
#load-if-defined CELTIC-CIV
#load-if-defined UP-POCKET-POSITION
(defrule
	(goal nr-map-goal -1)
	(current-age == dark-age)
	(or
		(random-number > 60)
		(map-type arabia)
	)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal unit-goal knight)
	(set-goal strategy-goal rush)
	(set-goal ranged-unit-type-goal scorpion)
	(chat-local-to-self "knight rush")
	(disable-self)
)
#else
(defrule
	(current-age == dark-age)
	(game-time > 13)
	(game-time < 60)
	(goal strategy-goal flush)
	(random-number < 40)
	(goal nr-map-goal -1)
=>
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
#end-if
(defrule
	(current-age == castle-age)
	(or
		(goal unit-goal archer)
		(goal unit-goal skirmisher)
	)
	(building-type-count siege-workshop > 0)
=>
	(set-goal ranged-unit-type-goal scorpion)
)
(defrule
	(goal enemy-goal eagle-warrior)
	(building-type-count-total castle == 0)
=>
	(set-goal unit-goal militiaman-line)
	(set-goal control-goal militiaman-line)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal -1)
)
(defrule
	(goal unit-goal knight)
	(nor
		(unit-type-count knight-line > 12)
		(up-research-status c: ri-paladin >= research-pending)
	)
	(strategic-number sn-current-age >= ci-transit)
	(up-compare-goal unit-goal != my-unique-unit-line)
	(up-compare-goal control-goal != my-unique-unit-line)
	(up-research-status c: ri-paladin < research-pending)
=>
	(set-goal unit-goal scorpion)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal -1)
)
(defrule
	(goal enemy-goal eagle-warrior)
	(building-type-count-total castle < 2)
	(up-compare-goal unit-goal != my-unique-unit-line)
=>
	(set-goal unit-goal militiaman-line)
	(set-goal control-goal militiaman-line)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal -1)
	(disable-self)
)
(defrule
	(or
		(or
			(goal unit-goal skirmisher)
			(goal unit-goal archer)
		)
		(or
			(goal unit-goal scorpion)
			(and
				(goal unit-goal knight)
				(nor
					(unit-type-count knight-line > 12)
					(up-research-status c: ri-paladin >= research-pending)
				)
			)
		)
	)
	(building-type-count-total castle > 0)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal control-goal 0)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(and
			(goal nr-map-goal 1)
			(random-number <= 80)
		)
		(building-type-count wall-class > 0)
	)
	(game-time > 25)
	(game-time < 60)
=>
	(set-goal unit-goal scorpion)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal strategy-goal boom)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 30)
	(game-time < 300)
	(goal unit-goal scorpion)
	(taunt-detected any-ally 32)
=>
	(acknowledge-taunt this-any-ally 32)
	(set-goal strategy-goal rush)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal unit-goal knight)
	(chat-to-allies-using-id 22155)
)
#end-if
#load-if-defined CHINESE-CIV
(defrule
	(nor
		(goal unit-goal knight)
		(goal unit-goal my-unique-unit-line)
	)
	(goal strategy-goal flush)
	(strategic-number sn-current-age == fc-transit)
	(military-population < 20)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "switch to knight")
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
	(building-type-count castle == 0)
=>
	(set-goal unit-goal knight)
	(set-goal strategy-goal rush)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#else
(defrule
	(current-age == dark-age)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
#end-if
(defrule
	(goal unit-goal my-unique-unit-line)
	(players-unit-type-count focus-player eagle-warrior-line > 20)
	(civilian-population > 80)
	(building-type-count castle > 0)
=>
	(set-goal unit-goal militiaman-line)
	(disable-self)
)
(defrule
	(up-compare-goal control-goal != shot)
	(up-compare-goal control-goal != my-unique-unit-line)
	(or
		(and
			(building-type-count-total archery-range > 0)
			(building-type-count-total town-center >= tc-level-three)
		)
		(unit-type-count villager > fourty-percent-pop)
	)
	(dropsite-min-distance stone < 5)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "prepare uu")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(goal nr-map-goal 1)
		(building-type-count castle > 0)
	)
	(game-time > 17)
	(game-time < 60)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined FRANKISH-CIV
(defrule
	(goal unit-goal knight)
	(or
		(players-unit-type-count focus-player camel-line > 10)
		(players-civ focus-player gothic)
	)
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(dropsite-min-distance stone < 5)
	(game-time >= 1800)
	(research-available ri-man-at-arms)
	(or
		(strategic-number sn-current-age == ci-transit)
		(up-compare-goal position-goal != pocket)
	)
	(up-compare-goal enemy-goal != eagle-warrior)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(goal control-goal my-unique-unit-line)
	(or
		(or
			(players-unit-type-count any-enemy camel-line > 15)
			(players-unit-type-count any-enemy spearman-line > 30)
		)
		(and
			(players-civ focus-player gothic)
			(strategic-number sn-focus-player-number g:== closest-enemy-goal)
		)
	)
	(building-type-count-total castle > 1)
	(research-available ri-man-at-arms)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(goal strategy-goal rush)
	(building-type-count gate > 0)
=>
	(set-goal unit-goal knight)
	(set-goal strategy-goal boom)
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal 1)
=>
	(set-goal control-goal 0)
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
(defrule
	(game-time > 16)
	(game-time < 60)	
	(goal unit-goal knight)
	(goal strategy-goal rush)
=>
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
#end-if
#end-if ;french
#load-if-defined GOTHIC-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-archer-threat >= 1)
	(not (research-completed ri-champion))
	(can-research my-unique-research)
=>
	(research my-unique-research)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "switch to huskies")
)
(defrule
	(up-compare-goal control-goal != my-unique-unit-line)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(civilian-population > fifty-percent-pop)
	)
	(goal unit-goal knight)
	(strategic-number sn-archer-threat > 1)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(building-type-count-total town-center >= tc-level-three)
	(or
		(or
			(players-civ focus-player briton)
			(players-civ focus-player hun)
		)
		(or
			(or
				(players-civ focus-player mongol)
				(players-civ focus-player mayan)
			)
			(or
				(players-civ focus-player chinese)
				(players-unit-type-count focus-player archer-line > 15)
			)
		)
	)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(building-type-count-total town-center >= tc-level-three)
	(strategic-number sn-current-age >= ci-transit)
	(or
		(or
			(players-civ any-enemy briton)
			(players-civ any-enemy chinese)
		)
		(or
			(or
				(players-civ any-enemy korean)
				(players-civ any-enemy mayan)
			)
			(players-civ any-enemy mongol)
		)
	)
	(up-compare-goal enemy-goal != eagle-warrior)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(up-compare-goal control-goal != militiaman-line)
	(up-compare-goal control-goal != my-unique-unit-line)
	(or
		(or
			(goal enemy-goal eagle-warrior)
			(strategic-number sn-current-age >= ci-transit)
		)
		(and
			(and
				(food-amount > 500)
				(gold-amount < 100)
			)
			(and
				(goal unit-goal knight)
				(civilian-population > fifty-percent-pop)
			)
		)
	)
=>
	(set-goal control-goal militiaman-line)
)
(defrule
	(goal unit-goal knight)
	(research-completed ri-long-swordsman)
=>
	(set-goal unit-goal militiaman-line)
	(set-goal control-goal 0)
)
(defrule
	(strategic-number sn-current-age >= castle)
	(civilian-population >= 100)
	(goal ranged-unit-type-goal archer)
=>
	(set-goal ranged-unit-type-goal 0)
)
(defrule
	(or
		(goal nr-map-goal 1)
		(building-type-count castle > 0)
	)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 13)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(building-type-count gate > 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 15)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(goal strategy-goal flush)
	(research-available ri-man-at-arms)
	(or 
		(up-player-distance focus-player < 70)
		(up-players-in-game any == 2)
	)
	(or
		(players-civ focus-player mayan)
		(players-civ focus-player briton)
	)
=>
	(set-goal unit-goal skirmisher)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal strategy-goal flush)
	(strategic-number sn-current-age == fc-transit)
	(military-population < 15)
	(up-compare-goal unit-goal != knight)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "switch to knight")
)
#end-if
#end-if
#load-if-defined HUN-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal 1)
=>
	(set-goal control-goal 0)
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(current-age == dark-age)
	(random-number >= 60)
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(current-age == dark-age)
	(random-number < 60)
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(disable-self)
)
#end-if
#end-if
#load-if-defined JAPANESE-CIV
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total castle == 0)
	(goal enemy-goal eagle-warrior)
=>
	(set-goal unit-goal militiaman-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
(defrule
	(or
		(and
			(strategic-number sn-current-age >= ci-transit)
			(or
				(goal strategy-goal boom)
				(goal unit-goal knight)
			)
		)
		(and
			(goal unit-goal skirmisher)
			(building-type-count-total castle >= 1)
		)
	)
	(up-compare-goal enemy-goal != eagle-warrior)
=>
	(set-goal unit-goal archer)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal archer)
	(set-goal strategy-goal rush)	
)
(defrule
	(strategic-number sn-current-age == castle)
	(up-research-status c: ri-long-swordsman < research-pending)
	(unit-type-count-total militiaman-line < 10)
	(goal strategy-goal flush)
=>
	(set-goal unit-goal skirmisher)
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(goal strategy-goal flush)
	(goal nr-map-goal 1)
=>
	(set-goal unit-goal archer)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal archer)
	(set-goal strategy-goal boom)
	(disable-self)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(or
		(up-allied-goal any-ally ranged-unit-type-goal == uu)
		(up-allied-goal any-ally unit-goal == archer)
	)
	(game-time < 60)
	(game-time > 16)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal knight)
)
#load-if-not-defined ARABIA-MAP
(defrule
	(goal meso-enemy-goal -1)
	(nor 
		(players-civ any-enemy teutonic)
		(up-allied-goal any-ally unit-goal == monk)
	)
	(random-number < 10)
	(game-time > 15)
	(game-time < 60)
=>
	(set-goal strategy-goal fast-imp)
	(set-goal unit-goal monk)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#end-if
#end-if
#end-if
#load-if-defined KOREAN-CIV
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(set-goal control-goal 0)
	(disable-self)
)
#else
#load-if-not-defined ARABIA-MAP
(defrule
	(game-time > 13)
	(random-number > 50)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal uu)
	(disable-self)
)
#end-if
#end-if
(defrule
	(goal unit-goal knight)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
(defrule
	(dropsite-min-distance stone < 5)
	(or
		(or
			(civilian-population > fourty-percent-pop)
			(strategic-number sn-current-age >= ci-transit)
		)
		(goal strategy-goal flush)
	)
	(building-type-count-total town-center >= tc-level-three)
	(up-compare-goal unit-goal != my-unique-unit-line)
=>
	(set-goal control-goal my-unique-unit-line)
	(chat-to-player chat-player "prepare for unique-units")
	(disable-self)
)
(defrule
	(building-type-count castle > 1)
	(goal unit-goal militiaman-line)
	(goal control-goal my-unique-unit-line)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal militiaman-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(building-type-count castle > 1)
	(nor 
		(goal unit-goal militiaman-line)
		(goal unit-goal my-unique-unit-line)
	)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(goal enemy-goal eagle-warrior)
	(building-type-count-total barracks > 0)
=>
	(set-goal unit-goal militiaman-line)
	(chat-to-player chat-player "research m@a and long-swordsman")
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(or
		(goal nr-map-goal 1)
		(building-type-count castle > 0)
	)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 30)
	(game-time < 300)
	(taunt-detected any-ally 33)
=>
	(acknowledge-taunt this-any-ally 33)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(set-goal unit-goal my-unique-unit-line)
	(chat-to-allies-using-id 22156)
)
#end-if
#load-if-defined MAYAN-CIV
(defrule
	(or
		(and
			(or
				(research-completed ri-heresy)
				(strategic-number sn-current-age >= ci-transit)
			)
			(or
				(goal strategy-goal boom)
				(goal strategy-goal fast-imp)
			)
		)
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal eagle-warrior)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal strategy-goal boom)
	(goal unit-goal eagle-warrior)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count monastery > 0)
=>
	(set-goal strategy-goal fast-imp)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(goal unit-goal eagle-warrior)
	(or
		(players-civ every-enemy gothic)
		(players-civ every-enemy japanese)
	)
	(up-compare-goal control-goal != shot)
	(up-compare-goal position-goal != pocket)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal nr-map-goal 1)
	(random-number < 67)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(dropsite-min-distance stone < 5)
	(building-type-count-total town-center >= tc-level-three)
	(up-compare-goal unit-goal != my-unique-unit-line)
	(strategic-number sn-current-age >= ci-transit)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "prepare for unique-units")
	(disable-self)
)
(defrule
	(or
		(building-type-count castle > 1)
		(regicide-game)
	)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "switch to unique-units")
	(set-goal control-goal 0)
	(disable-self)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(random-number > 66)
	(game-time > 15)
	(game-time < 60)
=>
	(set-goal unit-goal eagle-warrior)
	(set-goal strategy-goal boom)
	(set-goal nr-map-goal 1)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 32)
	(goal strategy-goal boom)
=>
	(acknowledge-taunt this-any-ally 32)
	(set-goal control-goal 0)
	(set-goal strategy-goal rush)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(chat-to-allies "xbow rush")
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(current-age == dark-age)
	(goal nr-map-goal -1)
	(up-compare-goal position-goal != pocket)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(disable-self)
)
#end-if
(defrule
	(or
		(and
			(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
			(current-age < castle-age)
		)
		(and
			(current-age == castle-age)
			(unit-type-count villager-stone == 0)
		)
	)
	(goal unit-goal my-unique-unit-line)
	(building-type-count-total castle == 0)
=>
	(set-goal unit-goal eagle-warrior)
)
(defrule
	(population >= max-civ-pop)
	(strategic-number sn-current-age >= ci-transit)
	(or
		(goal unit-goal archer)
		(goal unit-goal skirmisher)
	)
	(or
		(players-unit-type-count focus-player scorpion-line > 10)
		(strategic-number sn-archer-threat > 2)
	)
=>
	(set-goal unit-goal eagle-warrior)
	(set-goal strategy-goal rush)
	(disable-self)
)
(defrule
	(research-completed ri-elite-eagle-warrior)
	(research-completed my-unique-unit-upgrade)
	(research-available ri-arbalest)
	(unit-type-count archer-line < 5)
=>
	(set-goal unit-goal eagle-warrior)
	(set-goal strategy-goal rush)
	(set-goal ranged-unit-type-goal uu)
	(disable-self)
)
#end-if
#load-if-defined MONGOL-CIV
(defrule
	(current-age == dark-age)
	(goal strategy-goal flush)
=>
	(set-goal unit-goal default-flush-unit); debug
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
(defrule
	(current-age == dark-age)
	(goal nr-map-goal -1)
	(random-number < 67)
=>
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
(defrule
	(or
		(goal unit-goal cavalry-archer)
		(and
			(goal position-goal pocket)
			(strategic-number sn-current-age >= ci-transit)
		)
	)
	(up-compare-goal control-goal != my-unique-unit-line)
	(unit-type-count villager > fourty-percent-pop)
	(dropsite-min-distance stone < 5)
=>
	(set-goal unit-goal knight)
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "prepare uu")
)
#load-if-not-defined UP-1-PLAYER-TEAM
(defrule
	(up-compare-goal control-goal != my-unique-unit-line)
	(building-type-count-total town-center >= tc-level-three)
	(goal strategy-goal flush)
	(unit-type-count villager > fourty-percent-pop)
	(dropsite-min-distance stone < 5)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "prepare uu")
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal strategy-goal flush)
	(strategic-number sn-current-age == fc-transit)
	(military-population < 15)
	(up-compare-goal unit-goal != knight)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "switch to knight")
)
(defrule
	(up-compare-goal unit-goal != militiaman-line)
	(goal control-goal my-unique-unit-line)
	(or
		(and
			(players-unit-type-count focus-player spearman-line > 20)
			(unit-type-count trade-cart == 0)
		)
		(or
			(players-unit-type-count focus-player camel-line > 12)
			(goal enemy-goal eagle-warrior)
		)
	)
	(unit-type-count villager > fifty-percent-pop)
	(building-type-count castle > 0)
=>
	(set-goal unit-goal militiaman-line)
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal enemy-goal eagle-warrior)
	(unit-type-count villager > fifty-percent-pop)
	(nor
		(goal unit-goal militiaman-line)			
		(goal control-goal militiaman-line)
	)
=>
	(set-goal unit-goal militiaman-line)
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
(defrule
	(building-type-count-total castle > 1)
	(goal unit-goal knight)
=>
	(set-goal unit-goal my-unique-unit-line)
	(disable-self)
)
(defrule
	(building-type-count castle > 2)
	(or
		(goal unit-goal militiaman-line)
		(goal control-goal militiaman-line)
	)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal militiaman-line)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal nr-map-goal 1)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if; mongol
#load-if-defined PERSIAN-CIV
(defrule
	(or
		(and
			(strategic-number sn-current-age >= fc-transit)
			(military-population < 20)
		)
		(strategic-number sn-current-age >= ci-transit)
	)
	(goal strategy-goal flush)
	(up-compare-goal unit-goal != scorpion)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "switch to knights")
	(disable-self)
)
(defrule
	(goal strategy-goal flush)
	(up-compare-goal unit-goal != knight)
	(building-type-count-total town-center >= tc-level-three)
	(unit-type-count villager > fourty-percent-pop)
	(dropsite-min-distance stone < 5)
	(strategic-number sn-archer-threat > 2)
=>
	(set-goal unit-goal scorpion)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-current-age == dark)
	(goal nr-map-goal -1)
	(game-time > 15)
	(game-time < 60)
	(up-compare-goal control-goal != shot)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal archer)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age == dark)
	(goal nr-map-goal -1)
	(game-time > 15)
	(game-time < 60)
	(random-number > 50)
	(up-compare-goal control-goal != shot)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(disable-self)
)
#end-if
(defrule
	(or
		(goal nr-map-goal 1)
		(building-type-count gate > 0)
	)
	(random-number >= 40)
	(goal strategy-goal rush)
	(goal unit-goal knight)
	(game-time > 60)
=>
	(set-goal strategy-goal boom)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#load-if-not-defined ARABIA-MAP
(defrule
	(or
		(and
			(random-number < 15)
			(goal position-goal pocket)
		)
		(or
			(goal nr-map-goal 1)
			(building-type-count gate > 0)
		)
	)
	(random-number < 40)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal unit-goal scorpion)
	(set-goal strategy-goal boom)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#end-if
(defrule
	(goal unit-goal scorpion)
	(strategic-number sn-current-age >= ci-transit)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal scorpion)
)
(defrule
	(or
		(and
			(goal unit-goal scorpion)
			(goal control-goal my-unique-unit-line)
		)
		(goal unit-goal skirmisher)
	)
	(building-type-count-total castle > 1)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal scorpion)
)
#end-if ; persian
#load-if-defined SARACEN-CIV
(defrule
	(current-age == dark-age)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal unit-goal archer)
	(set-goal strategy-goal flush)
	(disable-self)
)
(defrule
	(game-time > 30)
	(game-time < 60)
	(goal strategy-goal rush)
	(goal unit-goal archer)
	(up-allied-goal every-ally unit-goal != knight)
=>
	(set-goal unit-goal knight)
)
#load-if-defined UP-POCKET-POSITION
#load-if-not-defined ARABIA-MAP
(defrule
	(current-age == dark-age)
	(random-number <= 10)
	(goal meso-enemy-goal -1)
	(nor 
		(players-civ any-enemy teutonic)
		(up-allied-goal any-ally unit-goal == monk)
	)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal strategy-goal fast-imp)
	(set-goal unit-goal monk)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#end-if
(defrule
	(current-age == dark-age)
	(random-number > 50)
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal unit-goal knight)
	(set-goal strategy-goal rush)
	(disable-self)
)
#end-if
(defrule
	(goal nr-map-goal 1)
	(or
		(and
			(goal strategy-goal rush)
			(goal unit-goal archer)
		)
		(goal strategy-goal flush)
	)
	(game-time > 60)
	(game-time < 70)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
)
(defrule
	(current-age == dark-age)
	(random-number > 70)
	(building-type-count gate > 0)	
	(game-time < 60)
=>
	(set-goal unit-goal monk)
	(set-goal strategy-goal fast-imp)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
(defrule
	(or
		(players-unit-type-count any-enemy eagle-warrior > 15)
		(goal enemy-goal eagle-warrior)
	)
	(strategic-number sn-current-age == ci-transit)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal unit-goal knight)
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal gunpowder-class)
)
(defrule
	(goal unit-goal knight)
	(or
		(goal control-goal shot)
		(goal control-goal my-unique-unit-line)
	)
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total castle > 0)
=>
	(set-goal unit-goal my-unique-unit-line)
)
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age >= ci-transit)
	(or
		(up-compare-goal control-goal != my-unique-unit-line)
		(strategic-number sn-cavalry-threat < 2)
	)
	(up-compare-goal enemy-goal != eagle-warrior)
=>
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
)
(defrule
	(strategic-number sn-cavalry-threat > 2)
	(civilian-population > fourty-percent-pop)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal 0)
)
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-current-age == feudal)
	(players-unit-type-count any-enemy spearman-line > 6)
	(up-compare-goal unit-goal != skirmisher)
=>
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
)
(defrule
	(or
		(and
			(goal nr-map-goal 1)
			(building-type-count wall-class < 15)
		)
		(and
			(and
				(random-number >= 70)
				(regicide-game)
			)
			(goal unit-goal monk)
		)
	)
	(game-time > 15)
	(game-time < 60)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal monk)
	(set-goal control-goal 0)
)
#end-if
#load-if-defined SPANISH-CIV
(defrule
	(goal control-goal 0)
	(building-type-count castle > 0)
	(civilian-population >= 100)
	(goal unit-goal monk)
=>
	(set-goal unit-goal my-unique-unit-line)
	(chat-to-player chat-player "switch to uu")
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 50)
	(goal nr-map-goal 1)
	(game-time > 15)
	(game-time < 60)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
=>
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 30)
	(game-time < 300)
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal knight)
	(building-type-count gate > 0)
=>
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
)
#load-if-defined UP-POCKET-POSITION
#load-if-not-defined ARABIA-MAP
(defrule
	(random-number >= 75)
	(game-time > 15)
	(game-time < 60)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
=>
	(set-goal strategy-goal rush)
	(set-goal uu-up-goal 1)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(random-number > 25)
	(random-number < 35)
	(game-time > 13)
	(game-time < 60)
	(goal meso-enemy-goal -1)
	(nor 
		(players-civ any-enemy teutonic)
		(up-allied-goal any-ally unit-goal == monk)
	)
=>
	(set-goal unit-goal monk)
	(set-goal control-goal 0)
	(set-goal strategy-goal fast-imp)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#end-if
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
#end-if
#end-if; spanish
#load-if-defined TEUTONIC-CIV
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(random-number >= 80)
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal 1)
=>
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(goal nr-map-goal 1)
	(random-number <= 40)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal strategy-goal boom)
	(disable-self)
)
#end-if
(defrule
	(or
		(goal enemy-goal eagle-warrior)
		(or
			(players-civ any-enemy gothic)
			(strategic-number sn-cavalry-threat > 3)
		)
	)
	(unit-type-count teutonic-knight-line > 5)
=>
	(set-goal uu-up-goal 1)
)
(defrule
	(or
		(and
			(strategic-number sn-current-age >= fc-transit)
			(military-population < 20)
		)
		(strategic-number sn-current-age >= ci-transit)
	)
	(goal strategy-goal flush)
=>
	(set-goal unit-goal knight)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(research-completed ri-paladin)
	(goal anti-cavalry-threat-goal 1)
	(or
		(strategic-number sn-archer-threat >= 3)
		(strategic-number sn-cavalry-threat < 3)
	)
	(up-compare-goal ranged-unit-type-goal != scorpion)
	(up-compare-goal ranged-unit-type-goal != monk)
	(unit-type-count trade-cart > 4)
=>
	(set-goal ranged-unit-type-goal scorpion)
)
(defrule
	(goal strategy-goal rush)
	(building-type-count gate > 0)
=>
	(set-goal unit-goal knight)
	(set-goal strategy-goal boom)
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#end-if;teutonic
#load-if-defined TURKISH-CIV
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(current-age == dark-age)
	(goal nr-map-goal -1)
	(game-time > 13)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal scout-cavalry)
	(disable-self)
)
(defrule
	(current-age == dark-age)
	(goal strategy-goal flush)
	(random-number < 50)
	(game-time > 13)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal archer)
	(disable-self)
)
#end-if
(defrule
	(or
		(goal ffa-game-goal 1)
		(goal nr-map-goal 1)
	)
	(or
		(random-number >= 50)
		(regicide-game)
	)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal 0)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(goal unit-goal knight)
	(unit-type-count villager > fourty-percent-pop)
	(dropsite-min-distance stone < 6)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(building-type-count-total castle > 1)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal uu)
	(set-goal control-goal 0)
	(set-goal uu-up-goal 1)
)
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal flush)
	(goal unit-goal default-ranged)
=>
	(set-goal unit-goal knight)
	(set-goal ranged-unit-type-goal archer)
)
(defrule
	(goal unit-goal knight)
	(goal control-goal militiaman-line)
	(players-unit-type-count any-enemy spearman-line > 30)
	(strategic-number sn-archer-threat < 2)
	(research-completed ri-champion)
	(building-type-count-total barracks >= 3)
=>
	(set-goal unit-goal militiaman-line)
	(chat-to-player chat-player "switch to militiaman-line")
)
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(building-type-count gate > 0)
		(and
			(goal nr-map-goal 1)
			(goal position-goal pocket)
		)
	)
	(game-time > 13)
	(random-number < 50)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal gunpowder-class)
	(set-goal ranged-unit-type-goal gunpowder-class)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(goal unit-goal gunpowder-class)
	(goal strategy-goal boom)
	(building-type-count-total town-center > tc-level-two)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
=>
	(set-goal strategy-goal fast-imp)
	(disable-self)
)
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(current-age >= castle-age)
	(or
		(game-time > 2700)
		(death-match-game)
	)
	(nand
		(goal strategy-goal fast-imp)
		(current-age-time < 900)
	)
	(goal control-goal 0)
=>
	(set-goal control-goal militiaman-line)
)
#end-if
#end-if
#load-if-defined VIKING-CIV
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal knight)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(or
		(and
			(goal strategy-goal flush); bug fix for maps that were previously falsely detected as water maps
			(goal position-goal pocket)
		)
		(goal nr-map-goal 1); on arena
	)
	(game-time > 60)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal archer)
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(current-age == dark-age)
	(up-compare-goal strategy-goal != flush)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal militiaman-line)
	(chat-to-player chat-player "switch to militia")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(set-goal control-goal 0)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal strategy-goal flush)
	(strategic-number sn-current-age == fc-transit)
	(military-population < 15)
	(up-compare-goal unit-goal != knight)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "switch to knight")
)
#else
(defrule
	(random-number > 75)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal archer)
	(disable-self)
)
#end-if
#load-if-defined UP-GAME-AGE2-X2
(defrule
	(goal control-goal 0)
	(or
		(goal strategy-goal boom)
		(goal position-goal pocket)
	)
	(civilian-population > 90)
	(strategic-number sn-cavalry-threat > 2)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
#end-if
(defrule
	(goal control-goal 0)
	(or
		(and
			(research-completed ri-arbalest)
			(research-completed ri-ring-archer-armor)
		)
		(goal enemy-goal eagle-warrior)
	)
=>
	(set-goal control-goal militiaman-line)
)
(defrule
	(civilian-population >= 60)
	(research-completed ri-long-swordsman)
	(not (goal unit-goal militiaman-line))
	(goal enemy-goal eagle-warrior)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal militiaman-line)
	(chat-to-player chat-player "switch to militiaman-line")
)
(defrule
	(game-time > 25)
	(game-time < 60)
	(building-type-count castle > 0)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal archer)
	(set-goal strategy-goal boom)
	(disable-self)
)
#end-if; viking-civ
#load-if-defined INCAN-CIV
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal strategy-goal fast-imp)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal eagle-warrior)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(random-number < 40)
	(goal nr-map-goal 1)
	(game-time > 13)
	(or
		(goal position-goal pocket)
		(goal nr-map-goal 1)
	)
=>
	(set-goal unit-goal archer)
	(set-goal control-goal 0)
	(set-goal strategy-goal boom)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(random-number >= 40)
	(or
		(goal position-goal pocket)
		(goal nr-map-goal 1)
	)
	(game-time > 13)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal strategy-goal boom)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
(defrule
	(current-age == castle-age)
	(goal strategy-goal boom)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count monastery > 0)
=>
	(set-goal strategy-goal fast-imp)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(strategic-number sn-cavalry-threat == 0)
	(strategic-number sn-infantry-threat > 0)
	(not (research-completed ri-arbalest))
=>
	(set-goal ranged-unit-type-goal slinger)
	(disable-self)
)
(defrule
	(dropsite-min-distance stone < 5)
	(building-type-count-total town-center >= tc-level-three)
	(nor
		(goal unit-goal my-unique-unit-line)
		(goal control-goal shot)
	)
	(or
		(or
			(strategic-number sn-cavalry-threat > 2)
			(strategic-number sn-infantry-threat > 2)
		)
		(strategic-number sn-current-age >= ci-transit)
	)
	(up-compare-goal unit-goal != my-unique-unit-line)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "prepare for unique-units")
	(disable-self)
)
(defrule
	(building-type-count castle > 1)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal control-goal 0)
	(chat-to-player chat-player "switch to unique-units")
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 13)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal archer)
	(set-goal ranged-unit-type-goal archer)
	(set-goal control-goal 0)
	(disable-self)
)
#end-if
#end-if; INCAN-CIV
#load-if-defined MAGYAR-CIV
(defrule
	(goal strategy-goal flush)
=>
	(set-goal unit-goal scout-cavalry)
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal scout-cavalry)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
#end-if
(defrule
	(or
		(and
			(strategic-number sn-current-age >= fc-transit)
			(military-population < 20)
		)
		(strategic-number sn-current-age >= ci-transit)
	)
	(goal strategy-goal flush)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "switch to knights")
	(disable-self)
)
(defrule
	(or
		(strategic-number sn-current-age >= ci-transit)
		(building-type-count-total town-center >= tc-level-four)
	)
	(dropsite-min-distance stone < 5)
=>
	(set-goal uu-up-goal 1)
	(set-goal control-goal my-unique-unit-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(building-type-count wall-class > 15)
	(game-time > 25)
=>
	(set-goal control-goal 0)
	(set-goal strategy-goal boom)
	(disable-self)
)
#end-if ; MAGYAR-CIV
#load-if-defined INDIAN-CIV
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(current-age == dark-age)
	(goal nr-map-goal -1)
	(up-compare-goal position-goal != pocket)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal default-flush-unit)
	(disable-self)
)
#else
(defrule
	(game-time > 13)
	(game-time < 60)
	(or
		(or
			(players-civ any-enemy hun)
			(players-civ any-enemy mongol)
		)
		(or
			(or
				(players-civ any-enemy persian)
				(players-civ any-enemy teutonic)
			)
			(or
				(players-civ any-enemy frankish)
				(players-civ any-enemy magyar)
			)
		)
	)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal knight)
	(disable-self)
)
#end-if
(defrule
	(or
		(goal unit-goal skirmisher)
		(goal unit-goal archer)
	)
	(building-type-count stable > 1)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "enable camels")
)
(defrule
	(goal unit-goal skirmisher)
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone < 6)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if ; INDIAN-CIV
#load-if-defined SLAVIC-CIV
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 15)
	(game-time < 60)
	(goal nr-map-goal -1)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal skirmisher)
	(set-goal ranged-unit-type-goal archer)
	(set-goal control-goal 0)
	(disable-self)
)
#else
(defrule
	(strategic-number sn-current-age == dark)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(random-number > 90)
	(game-time < 60)
	(game-time > 13)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal strategy-goal boom)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(random-number < 15)
	(game-time < 60)
	(game-time > 13)
	(goal meso-enemy-goal -1)
	(nor 
		(players-civ any-enemy teutonic)
		(up-allied-goal any-ally unit-goal == monk)
	)
=>
	(set-goal unit-goal monk)
	(set-goal control-goal 0)
	(set-goal strategy-goal fast-imp)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#end-if
(defrule
	(or
		(and
			(goal unit-goal knight)
			(game-time > 1800)
		)
		(and
			(goal unit-goal scorpion)
			(building-type-count-total town-center >= tc-level-four)
		)
	)
	(dropsite-min-distance stone < 6)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(or
		(goal unit-goal knight)
		(goal unit-goal scorpion)
	)
	(building-type-count castle > 0)
	(or
		(building-type-count castle > 1)
		(goal unit-goal knight)
	)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal control-goal 0)
)
(defrule
	(food-amount > 150)
	(or
		(gold-amount < 50)
		(food-amount > 300)
	)
	(or
		(goal unit-goal monk)
		(goal strategy-goal flush)
	)
	(goal enemy-goal eagle-warrior)
=>
	(set-goal unit-goal militiaman-line)
	(chat-to-player chat-player "long-swordsman to counter EW rush")
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(goal nr-map-goal 1)
		(goal position-goal pocket)
	)
	(random-number < 80)
	(random-number > 60)
	(game-time > 13)
	(game-time < 60)
=>
	(set-goal unit-goal scorpion)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal strategy-goal boom)
	(disable-self)
)
#end-if; SLAVIC-CIV
#load-if-defined ITALIAN-CIV
(defrule
	(game-time < 60)
	(game-time > 13)
	(goal nr-map-goal 1)		
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal nr-map-goal -1)
	(game-time > 15)
=>
	(set-goal strategy-goal flush)
	(set-goal unit-goal archer)
	(disable-self)
)
#else
(defrule
	(random-number < 5)
	(game-time > 20)
	(goal meso-enemy-goal -1)
	(nor 
		(players-civ any-enemy teutonic)
		(up-allied-goal any-ally unit-goal == monk)
	)
=>
	(set-goal unit-goal monk)
	(set-goal control-goal 0)
	(set-goal strategy-goal fast-imp)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#end-if
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(or
		(goal unit-goal archer)
		(goal unit-goal skirmisher)
	)
	(strategic-number sn-cavalry-threat > 1)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
)
(defrule
	(goal control-goal my-unique-unit-line)
	(building-type-count castle > 0)
	(not (goal unit-goal gunpowder-class))
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(strategic-number sn-infantry-threat > 2)
=>
	(set-goal ranged-unit-type-goal gunpowder-class)
)
#end-if ;ITALIAN-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 33)
	(goal strategy-goal rush)
	(goal unit-goal knight)
=>
	(acknowledge-taunt this-any-ally 33)
	(set-goal control-goal 0)
	(set-goal strategy-goal boom)
	(chat-to-allies "one stable knights")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 33)
	(goal strategy-goal boom)
	(goal unit-goal knight)
=>
	(acknowledge-taunt this-any-ally 33)
	(set-goal nr-map-goal 1)
	(chat-to-allies "full boom")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 33)
	(goal strategy-goal rush)
	(goal unit-goal archer)
=>
	(acknowledge-taunt this-any-ally 33)
	(set-goal control-goal 0)
	(set-goal strategy-goal boom)
	(chat-to-allies "xbow boom")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 32)
	(goal strategy-goal boom)
	(goal unit-goal knight)
=>
	(acknowledge-taunt this-any-ally 32)
	(set-goal control-goal 0)
	(set-goal strategy-goal rush)
	(chat-to-allies "two stables knights")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 32)
	(goal strategy-goal boom)
	(goal unit-goal archer)
=>
	(acknowledge-taunt this-any-ally 32)
	(set-goal control-goal 0)
	(set-goal strategy-goal rush)
	(chat-to-allies "xbow rush")
)
#load-if-defined UP-POCKET-POSITION
#load-if-not-defined BRITON-CIV
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined TURKISH-CIV
#load-if-not-defined VIKING-CIV
#load-if-not-defined ITALIAN-CIV
#load-if-not-defined SLAVIC-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 33)
	(goal strategy-goal boom)
	(goal unit-goal knight)
	(goal nr-map-goal -1)
=>
	(acknowledge-taunt this-any-ally 33)
	(set-goal nr-map-goal 1)
	(chat-to-allies "I boom into pala")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(game-time > 60)
	(game-time < 600)
	(taunt-detected any-ally 32)
	(goal strategy-goal boom)
	(goal unit-goal knight)
	(goal nr-map-goal 1)
=>
	(acknowledge-taunt this-any-ally 32)
	(set-goal nr-map-goal -1)
	(chat-to-allies "one stable knights")
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(building-type-count town-center > tc-level-two)
	(strategic-number sn-current-age == castle)
	(unit-type-count cavalry-class > 12)
	(goal unit-goal knight)
	(or
		(players-building-type-count focus-player castle > 0)
		(players-population focus-player < 35)
	)
=>
	(set-strategic-number sn-resource-control 2)
	(set-goal strategy-goal fast-imp)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(building-type-count town-center > tc-level-two)
	(or
		(building-type-count-total town-center > tc-level-three)
		(and
			(stone-amount < 100)
			(strategic-number sn-stone-gatherer-percentage == 0)
		)
	)
	(building-type-count barracks == 0)
	(strategic-number sn-current-age == castle)
	(goal strategy-goal boom)
	(goal unit-goal knight)
=>
	(set-goal strategy-goal fast-imp)
)
#load-if-not-defined GOTHIC-CIV; go champs instead
(defrule
	(up-research-status c: ri-chemistry < research-pending)
	(building-type-count-total town-center >= tc-level-three)
	(goal enemy-goal eagle-warrior)
	(goal unit-goal knight)
	(up-compare-const gunpowder == 1)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal strategy-goal fast-imp)
	(set-goal ranged-unit-type-goal gunpowder-class)
)
#end-if
#else
#load-if-not-defined BRITON-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined KOREAN-CIV
(defrule
	(goal unit-goal my-unique-unit-line)
	(goal train-civ-goal 1)
	(strategic-number sn-resource-control == 0)
	(unit-type-count-total my-unique-unit-line < 3)
	(building-type-count castle > 0)
	(building-type-count wall-class < 15)
	(current-age-time < 300)
	(current-age == castle-age)
	(food-amount < unique-unit-food)
=>
	(set-goal train-civ-goal -1)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if ; NOT POCKET
#end-if ; NOT MICHI
#end-if ; NOT DM
#load-if-defined DEATH-MATCH
#load-if-defined KOREAN-CIV
(defrule
	(starting-age < imperial-age)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal militiaman-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined TURKISH-CIV
(defrule
	(strategic-number sn-infantry-threat > 1)
	(research-completed ri-chemistry)
=>
	(set-goal ranged-unit-type-goal gunpowder-class)
	(disable-self)
)
#end-if
#end-if
#load-if-not-defined BRITON-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined FRANKISH-CIV
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined PERSIAN-CIV
#load-if-not-defined VIKING-CIV
#load-if-not-defined MAGYAR-CIV
#load-if-not-defined INCAN-CIV
#load-if-not-defined INDIAN-CIV
#load-if-defined UP-MICHI-STYLE
(defrule
	(or
		(taunt-detected any-computer 230)
		(taunt-detected any-human 230)
	)
	(current-age == dark-age)
	(game-time > 65)
	(game-time < 300)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal monk)
	(set-goal ranged-unit-type-goal monk)
	(chat-to-player this-any-computer "30 WOLOLO")
	(chat-to-player this-any-human "30 WOLOLO")
	(acknowledge-taunt this-any-computer 230)
	(acknowledge-taunt this-any-human 230)
)
#else
(defrule
	(or
		(taunt-detected any-computer 230)
		(taunt-detected any-human 230)
	)
	(current-age == dark-age)
	(game-time > 65)
	(game-time < 300)
=>
	(set-goal strategy-goal rush)
	(set-goal unit-goal monk)
	(set-goal ranged-unit-type-goal monk)
	(chat-to-player this-any-computer "30 WOLOLO")
	(chat-to-player this-any-human "30 WOLOLO")
	(acknowledge-taunt this-any-computer 230)
	(acknowledge-taunt this-any-human 230)
)
(defrule
	(current-age == dark-age)
	(game-time > 16)
	(goal strategy-goal rush)
	(goal unit-goal monk)
	(goal position-goal pocket)
	(building-type-count gate > 0)
=>
	(set-goal strategy-goal boom)
)
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(goal strategy-goal boom)
	(goal unit-goal monk)
	(goal position-goal pocket)
=>
	(set-goal strategy-goal rush)
)
(defrule
	(goal strategy-goal flush)
	(goal ranged-unit-type-goal monk)
=>
	(set-goal ranged-unit-type-goal default-ranged)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal unit-goal monk)
	(strategic-number sn-archer-threat > 1)
	(or
		(and
			(building-type-count-total monastery < 3)
			(game-time < 1500)
		)
		(or
			(or
				(civ-selected japanese)
				(civ-selected chinese)
			)
			(or
				(civ-selected slavic)
				(civ-selected teutonic)
			)
		)
	)
=>
	(set-goal unit-goal scorpion)
	(set-goal ranged-unit-type-goal monk)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(or
		(strategic-number sn-archer-threat > 1)
		(and
			(players-building-type-count any-enemy archery-range > 1)
			(goal enemy-goal skirmisher)
		)
	)
	(strategic-number sn-infantry-threat == 0)
=>
	(set-goal unit-goal skirmisher)
	(chat-to-player chat-player "switch to skirmisher")
)
#load-if-not-defined AZTEC-CIV
#load-if-not-defined VIKING-CIV
#load-if-not-defined MAYAN-CIV
(defrule
	(goal strategy-goal fast-imp)
	(up-compare-goal unit-goal != gunpowder-class)
	(up-compare-goal unit-goal != knight)
	(strategic-number sn-current-age >= ci-transit)
=>
	(set-goal strategy-goal rush)
)
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal archer)
	(or
		(players-unit-type-count focus-player skirmisher-line > 4)
		(strategic-number sn-archer-threat > 1)
	)
	(players-unit-type-count focus-player militiaman-line < 2)
=>
	(set-goal unit-goal skirmisher)
	(chat-to-player chat-player "switch to skirmisher")
)
(defrule
	(game-time < 1320)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal archer)
	(or
		(players-civ focus-player byzantine)
		(players-building-type-count focus-player archery-range	> 1)
	)
	(up-player-distance focus-player < 70)
=>
	(set-goal unit-goal skirmisher)
	(chat-to-player chat-player "switch to skirmisher")
	(disable-self)
)
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined AZTEC-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined GOTHIC-CIV
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(or
		(goal unit-goal skirmisher)
		(and
			(players-unit-type-count focus-player skirmisher-line > 16)
			(goal unit-goal archer)
		)
	)
	(or
		(or
			(civ-selected mayan)
			(civ-selected mongol)
		)
		(or
			(civ-selected chinese)
			(civ-selected korean)
		)
	)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#load-if-not-defined TURKISH-CIV
(defrule
	(goal strategy-goal flush)
	(goal unit-goal skirmisher)
	(strategic-number sn-current-age == fc-transit)
	(or
		(players-unit-type-count any-enemy skirmisher-line > 10)
		(strategic-number sn-archer-threat > 2)
	)	
=>
	(up-jump-rule 1)
)
#end-if
(defrule
	(goal strategy-goal flush)
	(or
		(goal unit-goal scout-cavalry)
		(goal unit-goal skirmisher)
	)
	(strategic-number sn-current-age == fc-transit)
=>
	(set-goal unit-goal default-ranged)
	(set-goal ranged-unit-type-goal default-ranged)
	(chat-local-to-self "switch back to default ranged")
)
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-current-age == fc-transit)
	(goal strategy-goal flush)
	(goal unit-goal default-ranged)
	(or
		(or
			(or
				(civ-selected frankish)
				(civ-selected persian)
			)
			(or
				(civ-selected gothic)
				(civ-selected celtic)
			)
		)
		(or
			(civ-selected spanish)
			(civ-selected teutonic)
		)
	)
=>
	(set-goal unit-goal knight)
	(chat-to-player chat-player "switch to knights")
)
#end-if
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined CELTIC-CIV
(defrule
	(goal unit-goal knight)
	(goal ranged-unit-type-goal scorpion)
	(or
		(goal enemy-goal eagle-warrior)
		(players-unit-type-count focus-player huskarl-line > 9)
	)
=>
	(set-goal ranged-unit-type-goal gunpowder-class)
)
#end-if
#end-if
#else ; not PIDM
(defrule
	(or
		(research-completed ri-bloodlines)
		(civ-selected frankish)
	)
	(research-completed ri-paladin)
=>
	(set-goal unit-goal knight)
)
(defrule
	(nand
		(or
			(research-completed ri-bloodlines)
			(civ-selected frankish)
		)
		(research-completed ri-paladin)
	)
=>
	(set-goal unit-goal my-unique-unit-line)
)
(defrule
	(game-time >= 480)
	(research-completed ri-heavy-scorpion)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#end-if ;PIDM
#load-if-defined UP-MICHI-STYLE
#load-if-defined AZTEC-CIV
(defrule
	(random-number < 40)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal monk)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
(defrule
	(random-number >= 40)
	(random-number < 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal eagle-warrior)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
(defrule
	(random-number >= 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal eagle-warrior)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal archer)
	(disable-self)
)
#end-if
#load-if-defined BRITON-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined BYZANTINE-CIV
(defrule
	(random-number < 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
(defrule
	(random-number >= 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(can-research-with-escrow my-unique-research)
	(research-completed my-unique-unit-upgrade)
=>
	(release-escrow food)(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined CELTIC-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined CHINESE-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined FRANKISH-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
(defrule
	(strategic-number sn-archer-threat > 1)
=>
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#end-if
#load-if-defined GOTHIC-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined HUN-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal cavalry-archer)
	(disable-self)
)
#end-if
#load-if-defined JAPANESE-CIV
(defrule
	(random-number < 40)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal archer)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 40)
	(random-number < 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined KOREAN-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined MAYAN-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal eagle-warrior)
	(set-goal control-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined MONGOL-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined PERSIAN-CIV
(defrule
	(random-number < 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#end-if
#load-if-defined SARACEN-CIV
(defrule
	(random-number < 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal archer)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 80)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal monk)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
#end-if
#load-if-defined SPANISH-CIV
(defrule
	(random-number < 20)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 20)
	(random-number < 60)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 60)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#end-if
#load-if-defined TEUTONIC-CIV
(defrule
	(random-number < 20)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 20)
	(random-number < 60)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(random-number >= 60)
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#end-if
#load-if-defined TURKISH-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(research-completed my-unique-unit-upgrade)
=>
	(set-goal unit-goal gunpowder-class)
	(set-goal control-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#end-if
#load-if-defined VIKING-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined INCAN-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal eagle-warrior)
	(set-goal control-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal archer)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined INDIAN-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal 0)
	(disable-self)
)
#end-if
#load-if-defined ITALIAN-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal gunpowder-class)
	(set-goal control-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined MAGYAR-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal knight)
	(set-goal control-goal my-unique-unit-line)
	(set-goal ranged-unit-type-goal cavalry-archer)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-defined SLAVIC-CIV
(defrule
	(strategic-number sn-current-age == dark)
=>
	(set-goal strategy-goal boom)
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
	(set-goal uu-up-goal 1)
	(disable-self)
)
#end-if
#load-if-not-defined INDIAN-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined CELTIC-CIV
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal knight)
	(or
		(and
			(not (research-completed ri-cavalier))
			(or
				(food-amount < 1300)
				(gold-amount < 750)
			)
		)
		(research-available ri-paladin)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = ri-paladin")
)
#end-if
#end-if
#end-if
#else
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal knight)
	(unit-type-count camel-line > 7)
	(or
		(and
			(up-research-status c: ri-heavy-camel < research-complete)
			(or
				(food-amount < 1200)
				(gold-amount < 600)
			)
		)
		(research-available ri-imperial-camel)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = ri-imperial-camel")
)
#end-if
(defrule
	(nand
		(civ-selected mayan)
		(population < max-civ-pop)
	)
	(building-type-count castle > 0)
	(research-available my-unique-unit-upgrade)	
	(or
		(goal unit-goal my-unique-unit-line)
		(goal control-goal my-unique-unit-line)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = my-unique-unit-upgrade")
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal strategy-goal boom)
=>
	(set-goal strategy-goal rush)
	(set-strategic-number sn-free-siege-targeting 1)
)
#end-if ; MICHI MAP

; civ specific research
#load-if-defined UP-GAME-AGE2-X2
(defrule
	(true)
=>
	(set-goal aofe-game-goal 1)
	(disable-self)
)
(defrule
	(gold-amount < 50)
	(unit-type-count fishing-ship > 10)
	(research-completed ri-hand-cart)
	(can-research ri-gillnets)
=>
	(research ri-gillnets)
)
(defrule
	(or
		(or
			(unit-type-count eagle-warrior > 12)
			(goal unit-goal eagle-warrior)
		)
		(strategic-number sn-resource-control == ri-elite-eagle-warrior)
	)
	(can-research-with-escrow ri-eagle-warrior)
=>
	(up-release-escrow)
	(research ri-eagle-warrior)
)
(defrule
	(or
		(or
			(unit-type-count genoese-crossbowman > 14)
			(unit-type-count kamayuk > 14)
		)
		(or
			(unit-type-count magyar-huszar > 14)
			(or
				(unit-type-count elephant-archer > 14)
				(unit-type-count boyar > 14)
			)
		)
	)
	(can-research-with-escrow my-unique-unit-upgrade);fix for the fact that new uu don't count as my-uu-line
=>
	(up-release-escrow)
	(research my-unique-unit-upgrade)
	(set-goal uu-up-goal 0)
)
#else
(defrule
	(true)
=>
	(up-jump-rule 1); jump aofe research
)
#end-if
#load-if-defined AZTEC-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count skirmisher-line > 15)
	(or
		(unit-type-count battering-ram-line > 3)
		(unit-type-count trebuchet-set > 0)
	)
	(can-research-with-escrow ri-atlatl)
=>
	(up-release-escrow)
	(research ri-atlatl)
)
(defrule
	(building-type-count castle > 0)
	(research-available my-unique-research)
	(research-completed ri-blast-furnace)
	(research-completed ri-plate-mail)
	(nand
		(or
			(unit-type-count eagle-warrior-line > 15)
			(unit-type-count heavy-eagle-warrior > 15); aofe
		)
		(research-available ri-elite-eagle-warrior)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(research-completed ri-blast-furnace)
	(research-completed ri-plate-mail)
	(can-research-with-escrow my-unique-research)
	(nand		
		(or
			(unit-type-count eagle-warrior-line > 15)
			(unit-type-count heavy-eagle-warrior > 15)
		)
		(research-available ri-elite-eagle-warrior)
	)
=>
	(release-escrow food)(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined BRITON-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count trebuchet-set > 3)
	(research-completed my-unique-research)
	(can-research ri-war-wolf)
=>
	(research ri-war-wolf)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(building-type-count castle > 0)
	(research-available my-unique-research)
	(population >= max-civ-pop)
=>
	(set-strategic-number sn-resource-control 2)
)
#end-if
#load-if-defined BYZANTINE-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count fire-ship-line > 3)
	(can-research-with-escrow ri-greek-fire)
=>
	(up-release-escrow)
	(research ri-greek-fire)
)
(defrule
	(up-compare-goal ranged-unit-type-goal != archer)
	(research-completed ri-halberdier)
	(food-amount < 300)
	(gold-amount > 700)
	(wood-amount > 400)
=>
	(set-goal ranged-unit-type-goal archer)
)
#end-if
#load-if-defined CELTIC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(population > max-civ-pop)
	(can-research ri-stronghold)
=>
	(research ri-stronghold)
)
#end-if
#load-if-defined CHINESE-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(building-type-count bombard-tower > 1)
	(can-research ri-great-wall)
=>
	(research ri-great-wall)
)
#end-if
#load-if-defined FRANKISH-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(population > del-civ-pop)
		(death-match-game)
	)
	(can-research ri-chivalry)
=>
	(research ri-chivalry)
)
(defrule
	(unit-type-count trade-cart > 20)
	(goal anti-cavalry-threat-goal 1)
	(goal unit-goal militiaman-line)
	(research-completed ri-cavalier)
=>
	(set-goal unit-goal knight)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
)
(defrule
	(unit-type-count trade-cart > 20)
	(goal anti-cavalry-threat-goal 1)
	(goal control-goal militiaman-line)
=>
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
)
#end-if
#load-if-defined GOTHIC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed my-unique-unit-upgrade)
	(can-research ri-anarchy)
=>
	(research ri-anarchy)
)
#end-if
#load-if-defined HUN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(goal unit-goal my-unique-unit-line)
	(can-research ri-marauders)
=>
	(research ri-marauders)
)
#end-if
#load-if-defined JAPANESE-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(building-type-count watch-tower > 3)
	(or
		(research-completed ri-masonry)
		(goal under-attack-goal 1)
	)
	(can-research ri-yasama)
=>
	(research ri-yasama)
)
(defrule
	(research-completed my-unique-unit-upgrade)
	(unit-type-count trebuchet-set >= 4)
	(can-research-with-escrow my-unique-research)
=>
	(release-escrow wood)(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined KOREAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count turtle-ship-line > 3)
	(can-research ri-panokseon)
=>
	(research ri-panokseon)
)
#end-if
#load-if-defined MAYAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count archer-line > 14)
	(goal team-coordination-goal 1)
	(can-research ri-obsidian-arrows)
=>
	(research ri-obsidian-arrows)
)
#end-if
#load-if-defined MONGOL-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(wood-amount > 600)
	(can-research ri-nomads)
=>
	(research ri-nomads)
)
(defrule
	(building-type-count house > 0)
	(research-completed ri-nomads)
=>
	(up-delete-objects c: house c: 32767)
)
#end-if
#load-if-defined PERSIAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-murder-holes)
	(can-research ri-boiling-oil)
=>
	(research ri-boiling-oil)
)
(defrule
	(can-research-with-escrow my-unique-research)
	(or
		(research-completed my-unique-unit-upgrade)
		(unit-type-count my-unique-unit-line > 4)
	)
=>
	(release-escrow food)(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined SARACEN-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count monk > 12)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(gold-amount < 500)
	)
	(can-research ri-madrasah)
=>
	(research ri-madrasah)
)
(defrule
	(can-research-with-escrow my-unique-research)
	(or
		(research-completed my-unique-unit-upgrade)
		(or
			(unit-type-count my-unique-unit-line > 4)
			(unit-type-count camel-line > 10)
		)
	)
=>
	(release-escrow food)(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined SPANISH-CIV
(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-inquisition)
	)
	(unit-type-count monk > 7)
	(strategic-number sn-current-age >= ci-transit)
	(can-research-with-escrow ri-inquisition)
=>
	(up-release-escrow)
	(research ri-inquisition)
)
(defrule	
	(random-number < 90)
	(goal strategy-goal boom)
	(goal unit-goal my-unique-unit-line)
	(current-age == castle-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-focus-player-number g:= closest-enemy-goal)
	(or
		(or
			(players-civ focus-player gothic)
			(players-civ focus-player indian)
		)
		(players-civ focus-player persian)
	)
=>
	(set-goal ranged-unit-type-goal monk)
	(disable-self)
)
(defrule
	(population > del-civ-pop)
	(current-age-time > 300)
	(can-research my-unique-research)
=>
	(research my-unique-research)
	(set-strategic-number sn-allow-civilian-defense 3)
)
#end-if
#load-if-defined TEUTONIC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-current-age >= ci-transit)
	(can-research ri-ironclad)
=>
	(research ri-ironclad)
)
(defrule
	(goal unit-goal knight)
	(goal control-goal 0)
	(strategic-number sn-archer-threat >= 2)
	(goal anti-cavalry-threat-goal 1)
	(up-research-status c: my-unique-unit-upgrade < research-pending)
=>
	(set-goal control-goal 0)
	(set-goal uu-up-goal 0)
	(set-goal ranged-unit-type-goal scorpion)
	(disable-self)
)
#end-if
#load-if-defined TURKISH-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count cavalry-archer-class > high-number-upgrade)
	(can-research ri-sipahi)
=>
	(research ri-sipahi)
)
#end-if
#load-if-defined VIKING-CIV
(defrule
	(strategic-number sn-resource-control < 3)
	(unit-type-count berserk-line > mid-number-upgrade)
	(research-completed my-unique-unit-upgrade)
	(strategic-number sn-cavalry-threat > 1)
	(can-research ri-chieftains)
=>
	(research ri-chieftains)
)
(defrule
	(can-research-with-escrow my-unique-research)
	(research-completed my-unique-unit-upgrade)
=>
	(release-escrow food)(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined MAGYAR-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(game-time > 3000)
	)
	(can-research-with-escrow ri-mercenaries)
=>
	(up-release-escrow)
	(research ri-mercenaries)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count cavalry-archer-class > high-number-upgrade)
	(can-research ri-recurve-bow)
=>
	(research ri-recurve-bow)
)
#end-if
#load-if-defined INCAN-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(strategic-number sn-resource-control < 1)
		(goal hostilities-goal 1)
	)
	(or
		(unit-type-count skirmisher-line > 15)
		(unit-type-count slinger > 9)
	)
	(can-research ri-andean-sling)
=>
	(research ri-andean-sling)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-conscription)
	(can-research my-unique-research)
=>
	(research my-unique-research)
)
(defrule
	(dropsite-min-distance stone < 5)
	(or
		(up-compare-goal control-goal != shot)
		(research-completed ri-elite-eagle-warrior)
	)
	(strategic-number sn-cavalry-threat > 1)
=>
	(set-goal control-goal my-unique-unit-line)
	(set-goal uu-up-goal 1)
	(chat-to-player chat-player "prepare for unique-units")
	(disable-self)
)
#end-if
#load-if-defined SLAVIC-CIV
(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-orthodoxy)
	)
	(unit-type-count monk > 7)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(goal hostilities-goal 1)
	)
	(can-research-with-escrow ri-orthodoxy)
=>
	(up-release-escrow)
	(research ri-orthodoxy)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(research-completed ri-halberdier)
		(research-completed ri-champion)
	)
	(unit-type-count infantry-class > 20)
	(can-research my-unique-research)
=>
	(research my-unique-research)
)
#end-if
#load-if-defined INDIAN-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count camel-line > 7)
	(can-research-with-escrow ri-imperial-camel)
=>
	(up-release-escrow)
	(research ri-imperial-camel)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(gold-amount < 100)
		(strategic-number sn-current-age >= ci-transit)
	)
	(can-research ri-sultans)
=>
	(research ri-sultans)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count hand-cannoneer > 7)
	(can-research ri-shatagni)
=>
	(research ri-shatagni)
)
#end-if
#load-if-defined ITALIAN-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(goal hostilities-goal 1)
		(strategic-number sn-resource-control < 1)
	)
	(or
		(unit-type-count genoese-crossbowman > 10)
		(research-completed my-unique-unit-upgrade)
	)
	(can-research ri-pavise)
=>
	(research ri-pavise)
)
#end-if
#load-if-defined AZTEC-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(game-time > 1800)
	(goal unit-goal monk)
	(wood-amount > 400)
	(food-amount > 800)
	(gold-amount < 300)
=>
	(set-goal unit-goal skirmisher)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
)
#else
(defrule
	(strategic-number sn-current-age == castle)
	(game-time > 1800)
	(goal unit-goal monk)
	(wood-amount > 400)
	(food-amount > 800)
	(gold-amount < 300)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal control-goal 0)
	(set-goal ranged-unit-type-goal monk)
)
#end-if
(defrule
	(game-time > 3000)
	(goal ranged-unit-type-goal monk)
	(unit-type-count trade-cart < 6)
	(dropsite-min-distance gold > 10)
=>
	(set-goal ranged-unit-type-goal 0)
)
#load-if-not-defined UP-MICHI-STYLE
#load-if-not-defined DEATH-MATCH
#load-if-not-defined DEFEND-WONDER
#load-if-defined DIFFICULTY-HARD
#load-if-not-defined HIGH-RESOURCES-START
#load-if-not-defined FORTRESS-MAP
#load-if-defined UP-POCKET-POSITION
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time >= 90)
	(game-time < 120)
	(goal nr-map-goal -1)
	(random-number > sling-number)
	(up-allied-goal any-ally control-goal == accept-slinging)
=>
	(chat-to-player this-any-ally "237")
	(chat-local-to-self "offer slinging")
	(disable-self)
)
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time >= 90)
	(game-time < 120)
	(goal nr-map-goal -1)
	(random-number > sling-number)
	(taunt-detected any-ally 38)
	(up-allied-goal any-ally id-goal == horde)
=>
	(chat-to-player my-player-number "237")
	(chat-local-to-self "request accepted")
	(disable-self)
)
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time >= 15)
	(game-time < 300)
	(random-number > sling-number)
	(up-allied-goal any-ally id-goal == barbarian)
	(taunt-detected any-ally 124)
=>
	(chat-to-player this-any-ally "1")
	(chat-to-player my-player-number "237")
	(chat-local-to-self "request accepted")
	(disable-self)
)
#end-if
(defrule
	(current-age == dark-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time >= 15)
	(game-time < 300)
	(taunt-detected any-human-ally 38)
=>
	(chat-to-player this-any-human-ally "1")
	(chat-to-player my-player-number "237")
	(chat-local-to-self "request accepted")
	(disable-self)
)
#load-if-not-defined TURKISH-CIV
(defrule
	(taunt-detected my-player-number 237)
=>
	(set-goal unit-goal my-unique-unit-line)
	(set-goal strategy-goal sling)
	(set-goal ranged-unit-type-goal 0)
	(set-goal control-goal 0)
	(acknowledge-taunt my-player-number 237)
	(chat-to-allies "I sling")
)
#else
(defrule
	(taunt-detected my-player-number 237)
=>
	(set-goal unit-goal gunpowder-class)
	(set-goal strategy-goal sling)
	(set-goal ranged-unit-type-goal 0)
	(set-goal control-goal 0)
	(acknowledge-taunt my-player-number 237)
	(chat-to-allies "I sling")
)
#end-if
(defrule
	(or
		(and
			(or
				(game-time > 3000)
				(food-amount > 500)
			)
			(game-time > 1500)
		)
		(or	
			(taunt-detected any-ally 97)
			(taunt-detected any-human-ally 33)
		)
	)
	(goal strategy-goal sling)
=>
	(set-goal strategy-goal fast-imp); don't train a large castle age army after a long time of slinging
	(chat-to-allies "96I stop slinging now")
)
(defrule
	(or
		(game-time > 3000)
		(food-amount > 500)
	)
	(game-time > 1500)
	(unit-type-count villager < 80)
	(population-cap >= 200)
	(goal strategy-goal sling)
=>
	(set-goal strategy-goal boom)
)
(defrule
	(goal control-goal shot)
	(or
		(and
			(population < 20); debug, as computer may tribute resigned allies
			(game-time > 900)
		)
		(and
			(current-age == imperial-age)
			(or
				(goal train-civ-goal -1); delete excess villager mode, so no resources needed anymore
				(and
					(research-completed ri-elite-eagle-warrior)
					(population > del-civ-pop)
				)
			)
		)
	)
=>
	(set-goal control-goal 0)
	(chat-to-allies "97")
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal position-goal flank)
	(goal control-goal accept-slinging)
	(taunt-detected any-ally 237)
	(up-allied-goal every-ally control-goal != shot); sling/shot code doesn't work for two shots
=>
	(acknowledge-taunt this-any-ally 237)
	(chat-to-player this-any-ally "38")
	(set-goal control-goal shot)
	(chat-to-allies "I am shot")
)
#else
(defrule
	(up-compare-goal control-goal != shot)
	(up-allied-goal every-ally control-goal != shot)
	(taunt-detected any-ally 237)
	(unit-type-count eagle-warrior-line == 1)
=>
	(acknowledge-taunt this-any-ally 237)
	(chat-to-player this-any-ally "38")
	(set-goal strategy-goal boom)
	(set-goal unit-goal eagle-warrior)
	(set-goal control-goal shot)
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(chat-to-allies "I am shot")
)
#end-if
#end-if ; no fortress
#end-if ; no high-res start
#end-if ; hard
#end-if ; not wonder defend
#end-if ; no DM
#else
(defrule
	(strategic-number sn-resource-control <= 1)
	(building-type-count-total town-center > 0)
	(strategic-number sn-current-age >= fc-transit)
	(or
		(can-research ri-double-bit-axe)
		(can-research ri-horse-collar)
	)
=>
	(research ri-double-bit-axe)
	(research ri-horse-collar)
)
(defrule
	(or
		(and
			(game-time > 2400)
			(up-compare-const get-lightcav == 1)
		)
		(goal monk-threat-goal 1)
	)
	(can-research ri-light-cavalry)
=>
	(research ri-light-cavalry)
)
#end-if

; end strategy section

;******************** RESOURCE MANAGEMENT AND AGE UP ********************
(defrule
	(players-unit-type-count any-enemy monk > 10)
	(or
		(players-unit-type-count any-enemy monk > 20); changed don't trigger if enemy doesn't do really lots of monks
		(game-time < 1800)
	)
	(research-available ri-heresy)
	(building-type-count monastery > 0)
=>
	(set-strategic-number sn-resource-control ri-heresy)
;	(chat-local-to-self "save-resources = ri-heresy")
	(up-set-offense-priority c: monastery-class c: 11)
)
(defrule
	(or
		(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
		(building-type-count-total town-center == 0)
	)
=>
	(up-jump-rule 30)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(or
		(and
			(unit-available camel-line)
			(building-type-count stable > 0)
		)
		(and
			(goal unit-goal knight)
			(strategic-number sn-cavalry-threat < 2)
		)
	)
=>
	(up-jump-rule 1)
)
#end-if
(defrule
	(goal train-civ-goal 1)
	(strategic-number sn-cavalry-threat >= 1)
	(strategic-number sn-resource-control <= 2)
	(building-type-count barracks > 0)
	(nand
		(goal unit-goal knight)
		(unit-type-count villager < 60)
	)
	(research-available ri-pikeman)
=>
	(set-strategic-number sn-resource-control ri-pikeman)
	(set-goal train-civ-goal ri-pikeman)
;	(chat-to-player chat-player "save-resources = ri-pikeman")
)
(defrule
	(goal train-civ-goal 1)
	(strategic-number sn-cavalry-threat >= 2)
	(strategic-number sn-resource-control <= 2)
	(military-population > 10)
	(building-type-count barracks > 0)
	(civilian-population >= fifty-percent-pop)
	(gold-amount < 500)
	(research-available ri-pikeman)
	(or
		(civ-selected japanese)
		(civ-selected gothic)
	)
=>
	(set-strategic-number sn-resource-control ri-pikeman)
	(set-goal train-civ-goal ri-pikeman)
;	(chat-to-player chat-player "save-resources = ri-pikeman")
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(research-available ri-chain-mail)
=>
	(set-goal train-civ-goal ri-chain-mail); stop villager training only EWs
;	(chat-to-player chat-player "save-resources = ri-chain-mail")
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal ri-chain-mail)
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(research-completed ri-pikeman)
	(research-available ri-chain-mail)	
=>
	(set-strategic-number sn-resource-control ri-chain-mail);stop all units; vils remain stopped
;	(chat-to-player chat-player "save-resources = ri-chain-mail")
)
(defrule
	(current-age >= castle-age)
	(unit-type-count villager > villager-siege-workshop)
	(building-type-count-total siege-workshop < 1)
	(goal forward-threat-goal 1)
	(nand
		(unit-type-count ranged-unit-class < 5)
		(strategic-number sn-military-level < 0)
	)
	(wood-amount < 260); some buffer
=>
	(set-goal increase-town-size-goal 2);block buildings
	(set-goal save-wood-goal 1)
	(set-strategic-number sn-resource-control battering-ram)
;	(chat-local-to-self "save-resources = siege-workshop")
)
(defrule
	(current-age >= castle-age)
	(unit-type-count villager > villager-siege-workshop)
	(building-type-count-total siege-workshop < 1)
	(goal forward-threat-goal 1); don't check wood to block other buildings if enemy forwarded us
	(nand
		(unit-type-count ranged-unit-class < 5)
		(strategic-number sn-military-level < 0)
	)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(current-age >= castle-age)
	(building-type-count-total siege-workshop < 1)
	(strategic-number sn-archer-threat > 1)
	(goal strategy-goal flush)
	(current-age-time < 15)
	(players-current-age-time every-enemy > 30)
	(wood-amount >= 200)
	(or
		(strategic-number sn-military-level < -1)
		(up-players-in-game ally < 2)
	)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(building-type-count-total town-center >= tc-level-two)
	(goal unit-goal my-unique-unit-line)
	(or
		(and
			(goal monk-threat-goal 1)
			(map-type arena)
		)
		(or
			(players-unit-type-count any-enemy mangonel-line > 1)
			(players-unit-type-count any-enemy scorpion-line > 4)
		)
	)
	(wood-amount >= 200)
	(current-age >= castle-age)
	(building-type-count-total siege-workshop < 1)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(or
		(goal enemy-goal flush)
		(and
			(up-players-in-game any == 2)
			(strategic-number sn-archer-threat > 1)
		)
	)
	(or
		(goal unit-goal my-unique-unit-line)
		(goal unit-goal archer)
	)
	(goal strategy-goal boom)
	(wood-amount >= 200)
	(current-age == castle-age)
	(building-type-count-total siege-workshop < 1)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(wood-amount >= 200)
	(current-age >= castle-age)
	(strategic-number sn-resource-control < 2)
	(building-type-count-total town-center >= tc-level-four)	
	(or
		(goal unit-goal knight)
		(civ-selected teutonic)
	)
	(building-type-count-total siege-workshop < 1)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(or
		(strategic-number sn-archer-threat > 0)
		(building-type-count-total wall-class > 10)
	)
	(wood-amount >= 200)
	(current-age >= castle-age)
	(strategic-number sn-resource-control < 2)
	(building-type-count-total town-center >= tc-level-three)	
	(goal unit-goal knight)
	(goal strategy-goal boom)
	(building-type-count-total siege-workshop < 1)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-archer-threat > 2)
	(building-type-count archery-range > 0)
	(players-building-type-count focus-player siege-workshop < 2)
	(up-compare-goal position-goal != pocket)
=>
	(up-jump-rule 2)
)
(defrule
	(current-age == castle-age)
	(or
		(strategic-number sn-archer-threat > 2)
		(players-building-type-count focus-player siege-workshop > 1)
	)
	(or
		(goal unit-goal knight)
		(goal ranged-unit-type-goal monk)
	)
	(goal strategy-goal rush)
	(building-type-count-total siege-workshop < 1)
	(wood-amount < 200)
	(game-time < 1500)
	(up-compare-goal position-goal != pocket)
=>
	(set-goal save-wood-goal siege-workshop)
;	(chat-local-to-self "save-resources = siege-workshop")
)
(defrule
	(game-time < 1500)
	(can-build siege-workshop)
	(or
		(strategic-number sn-archer-threat > 2)
		(players-building-type-count focus-player siege-workshop > 1)
	)
	(or
		(goal unit-goal knight)
		(goal ranged-unit-type-goal monk)
	)
	(goal strategy-goal rush)
	(building-type-count-total siege-workshop < 1)
	(up-compare-goal position-goal != pocket)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
	(build siege-workshop)
)
(defrule
	(or
		(strategic-number sn-military-level > 1)
		(goal team-coordination-goal 1)
	)
	(or
		(players-building-type-count focus-player castle > 0)
		(strategic-number sn-military-level > 2)
	)
	(goal under-attack-goal 0)
	(building-type-count-total siege-workshop < 1)
	(can-build siege-workshop)
	(up-compare-goal position-goal != pocket)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(can-build siege-workshop)
	(military-population > 8)
	(goal unit-goal monk)
	(building-type-count-total siege-workshop < 1)
	(goal position-goal pocket)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
	(build siege-workshop)
)
(defrule
	(current-age >= castle-age)
	(wood-amount >= 200)
	(goal save-wood-goal 0)
	(goal unit-goal cavalry-archer)
	(building-type-count-total town-center == 1)
	(building-type-count-total stable == 1)
	(building-type-count-total archery-range > 3)
	(building-type-count-total siege-workshop < 1)
	(or
		(up-compare-goal position-goal != pocket)
		(or
			(players-building-type-count any-enemy watch-tower > 1)
			(strategic-number sn-archer-threat > 2)
		)
	)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal strategy-goal flush)
	(goal save-wood-goal 0)
	(strategic-number sn-archer-threat > 2)
	(or
		(military-population < 30)
		(or
			(building-type-count-total town-center > 1)
			(stone-amount < 100)
		)
	)
	(building-type-count town-center > 0)
	(building-type-count-total siege-workshop < 1)
	(stone-amount < 100)
	(can-build siege-workshop)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal control-goal shot)
	(building-type-count-total town-center < tc-level-three)
	(stone-amount >= 100)
=>
	(up-jump-rule 3)
)
(defrule
	(current-age >= castle-age)
	(wood-amount >= 200)
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(or
		(or
			(strategic-number sn-archer-threat > 1)
			(players-unit-type-count any-enemy scorpion-line > 9)
		)
		(or
			(players-building-type-count focus-player castle > 1)
			(players-building-type-count focus-player stone-wall-line > 15)
		)
	)
	(building-type-count-total siege-workshop < 1)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal increase-town-size-goal 0)
	(not (research-available imperial-age))
	(building-type-count-total siege-workshop < 1)
	(wood-amount < 250);buffer
	(food-amount > 500)
	(gold-amount > 400)
=>
	(set-goal save-wood-goal 1)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal increase-town-size-goal 0)
	(not (research-available imperial-age))
	(building-type-count-total siege-workshop < 1)
	(food-amount > 500)
	(gold-amount > 400)
	(building-type-count-total siege-workshop < 1)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
	(up-assign-builders c: siege-workshop c: 2)
)
(defrule
	(current-age >= castle-age)
	(goal increase-town-size-goal 0)
	(or
		(goal strategy-goal fast-imp)
		(or
			(goal control-goal shot)
			(goal unit-goal monk)
		)
	)
	(building-type-count-total siege-workshop < 1)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal increase-town-size-goal 0)
	(nor
		(research-available imperial-age)
		(goal control-goal my-unique-unit-line)
	)
	(building-type-count-total siege-workshop < 1)
	(wood-amount < 250);buffer
	(food-amount > 700)
	(gold-amount > 600)
=>
	(set-goal save-wood-goal 1)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal increase-town-size-goal 0)
	(nor
		(research-available imperial-age)
		(goal control-goal my-unique-unit-line)
	)
	(building-type-count-total siege-workshop < 1)
	(food-amount > 500)
	(gold-amount > 400)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal forward-threat-goal 1)
	(building-type-count siege-workshop > 0)
	(or
		(unit-type-count-total battering-ram-line == 0)
		(players-building-type-count every-enemy watch-tower > 3)
	)
	(unit-type-count-total battering-ram-line < 3)
	(nand
		(strategic-number sn-military-level < 0)
		(or
			(goal under-attack-goal 1)
			(goal unit-goal knight)
		)
	)
	(civilian-population > 22)
=>
	(set-strategic-number sn-resource-control battering-ram)
;	(chat-to-player chat-player "save-resources = battering-ram")
)
(defrule
	(goal forward-threat-goal 1)
	(building-type-count siege-workshop > 0)
	(players-building-type-count every-enemy watch-tower > 5)
	(unit-type-count-total battering-ram-line < 8)
	(unit-type-count-total trebuchet-set == 0)
	(nand
		(strategic-number sn-military-level < 0)
		(wood-amount > 400)
	)
=>
	(set-strategic-number sn-resource-control battering-ram)
;	(chat-to-player chat-player "save-resources = battering-ram")
)
(defrule
	(goal increase-town-size-goal siege-workshop)
	(or 
		(or
			(goal unit-goal skirmisher)
			(goal strategy-goal boom)
		)
		(goal unit-goal my-unique-unit-line)
	)
	(up-compare-goal forward-threat-goal != 1); don't build an aggressive siege-workshop with these strategies
	(building-type-count-total town-center < tc-level-two)
	(up-compare-goal unit-goal != scorpion)
=>
	(set-goal increase-town-size-goal 0)
	(set-goal save-wood-goal 0)
)
(defrule
	(current-age >= castle-age)
	(goal increase-town-size-goal 0)
	(goal save-wood-goal 0)
	(building-type-count-total town-center >= tc-level-four)
	(or
		(goal ranged-unit-type-goal scorpion)
		(research-completed ri-heavy-scorpion)
	)
	(building-type-count-total siege-workshop < 3)
	(wood-amount >= 400)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
(defrule
	(strategic-number sn-target-player-number g:== threat-player-goal)
	(strategic-number sn-maximum-town-size >= 60)
	(goal increase-town-size-goal 0)
	(goal save-wood-goal 0)
	(building-type-count-total town-center >= tc-level-four)
	(wood-amount > 1400)
	(research-completed ri-siege-ram)
	(building-type-count-total siege-workshop < 3)
	(can-build siege-workshop)
=>
	(build-forward siege-workshop)
)
(defrule
	(population >= del-civ-pop)
	(goal increase-town-size-goal 0)
	(goal save-wood-goal 0)
	(building-type-count-total town-center >= tc-level-four)
	(wood-amount > 400)
	(research-completed ri-capped-ram)
	(building-type-count-total siege-workshop < 2)
	(can-build siege-workshop)
=>
	(build siege-workshop)
)
#end-if
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(goal save-wood-goal 0)
	(building-type-count-total town-center >= tc-level-four)
	(building-type-count-total siege-workshop < 1)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(or
		(or
			(goal forward-threat-goal 1)
			(goal strategy-goal boom)
		)
		(strategic-number sn-military-level < 0)
	)
	(goal increase-town-size-goal siege-workshop)
	(nor 
		(up-pending-placement c: siege-workshop)
		(up-pending-objects c: siege-workshop > 0)
	)
	(can-build siege-workshop)
=>
	(up-set-placement-data my-player-number -1 c: -8)
	(up-build place-control 0 c: siege-workshop)
)
#load-if-not-defined OASIS-MAP
(defrule
	(goal increase-town-size-goal siege-workshop)
	(nor 
		(up-pending-placement c: siege-workshop)
		(up-pending-objects c: siege-workshop > 0)
	)
	(can-build siege-workshop)
=>
	(up-set-placement-data my-player-number -1 c: 8)
	(up-build place-control 0 c: siege-workshop)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 1)
)
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(goal strategy-goal rush)
	(goal unit-goal scorpion)
	(building-type-count-total siege-workshop < 3)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-archer-threat > 0)
	(strategic-number sn-military-level < 0)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total siege-workshop < 1)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(goal unit-goal scorpion)
	(building-type-count-total town-center >= 1)
	(building-type-count-total siege-workshop < 1)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined CELTIC-CIV
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(current-age >= castle-age)
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center >= tc-level-three)
	(goal save-wood-goal 0)
	(or
		(building-type-count-total town-center >= tc-level-four)
		(building-type-count-total siege-workshop == 0)
	)
	(building-type-count-total siege-workshop < 3)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#else
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center >= tc-level-five)
	(or
		(building-type-count-total siege-workshop < 3)
		(research-completed ri-heavy-scorpion)
	)
	(building-type-count-total siege-workshop < 5)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
#load-if-defined SLAVIC-CIV
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(goal save-wood-goal 0)
	(current-age >= castle-age)
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center >= tc-level-two)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(building-type-count-total siege-workshop == 0)
	)
	(or
		(building-type-count-total town-center >= tc-level-four)
		(building-type-count-total siege-workshop == 1)
	)
	(building-type-count-total siege-workshop < 3)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(building-type-count siege-workshop > 0)
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-current-age >= ci-transit)
	(unit-type-count scorpion-line > 9)
	(up-research-status c: ri-heavy-scorpion < research-pending)
	(goal unit-goal scorpion)
=>
	(set-strategic-number sn-resource-control 2)
)
#else
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center >= tc-level-five)
	(building-type-count-total siege-workshop < 3)
	(wood-amount >= 200)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
#load-if-defined DEATH-MATCH
(defrule
	(game-time > 120)
	(up-compare-goal increase-town-size-goal != siege-workshop)
	(building-type-count-total siege-workshop == 0)
	(wood-amount >= 200)
	(current-age == castle-age)
=>
	(set-goal increase-town-size-goal siege-workshop)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(goal increase-town-size-goal siege-workshop)
	(up-pending-objects c: siege-workshop > 0)
=>
	(set-goal increase-town-size-goal 0)
)
(defrule
	(goal increase-town-size-goal siege-workshop)
	(strategic-number sn-maximum-town-size >= 40)
=>
	(up-set-placement-data my-player-number -1 c: 16)
	(up-build place-control 0 c: siege-workshop)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
)
(defrule
	(goal increase-town-size-goal siege-workshop)
	(can-build siege-workshop)
=>
	(build siege-workshop)
	(set-goal increase-town-size-goal 2)
)
#end-if ; NOT WONDER RACE
#load-if-defined DEATH-MATCH
(defrule
	(game-time > 20)
	(research-available ri-loom)
=>
	(research ri-loom)
	(set-goal need-loom-goal 1)
)
#end-if
(defrule
	(or
		(or
			(players-military-population any-enemy > 3); loom fast when enemy looks dangerous
			(current-age >= castle-age); loom anyways at some point
		)
		(or
			(or
				(goal need-loom-goal 1); loom needed before hunting
				(housing-headroom == 0)
			)
			(and
				(up-pending-objects c: villager == 0)
				(food-amount < 50)
			)
		)
	)
	(can-research ri-loom)
=>
	(research ri-loom)
)
(defrule
	(building-type-count-total town-center > 0)
	(or 
		(housing-headroom < 3)
		(and
			(population > 11)
			(housing-headroom < 5)
		)
	)
	(population-headroom > 0)
=>
	(set-goal housing-goal 1)
)
#load-if-not-defined WONDER-RACE
(defrule
	(housing-headroom < 10)
	(or
		(goal strategy-goal flush)
		(building-type-count-total blacksmith > 0)
	)
	(nand
		(goal unit-goal my-unique-unit-line)
		(strategic-number sn-current-age == fc-transit); build additonal houses in castle age to get builder for the castle
	)
	(strategic-number sn-current-age >= feudal)
	(population < eighty-five-percent-pop)
	(housing-headroom < 25)
	(population-headroom > 0)
=>
	(set-goal housing-goal 1)
)
(defrule
	(or
		(population > 60)
		(and
			(strategic-number sn-current-age >= castle)
			(or
				(goal unit-goal eagle-warrior)
				(goal unit-goal archer)
			)
		)
	)
	(or
		(housing-headroom < 25)
		(population > seventy-five-percent-pop)
	)
	(population-headroom > 0)
	(nand
		(goal strategy-goal boom)
		(building-type-count-total town-center < 2)
	)
	(housing-headroom < 40)
=>
	(set-goal housing-goal 1)
)
#end-if
(defrule
	(strategic-number sn-current-age == dark)
	(building-type-count town-center == 0)
	(or
		(or
			(civ-selected hun); on LN, collect more wood for houses or be huns
			(civ-selected chinese)
		)
		(or
			(building-type-count-total house > 0)
			(wood-amount > 300)
		)
	)
=>
	(up-assign-builders c: 621 c: 6)
	(set-goal housing-goal 0)
)
#load-if-defined DEATH-MATCH
(defrule
	(or
		(strategic-number sn-current-age == dark)
		(game-time < 30)
	)
	(or
		(or
			(building-type-count-total house > 1)
			(and
				(building-type-count-total house > 0)
				(up-pending-placement c: house)
			)
		)
		(civ-selected saracen)
	)
=>
	(set-goal housing-goal 0)
)
(defrule
	(population-headroom > 0)
	(housing-headroom < 40)
	(or
		(up-pending-objects c: house == 0)
		(and
			(strategic-number sn-current-age >= df-transit)
			(and
				(game-time >= 80)
				(or
					(housing-headroom < 20)
					(game-time > 120)
				)
			)
		)
	)
=>
	(set-goal housing-goal 1)
)
#load-if-defined DARK-AGE-START
(defrule
	(building-type-count-total house > 0)
	(building-type-count-total lumber-camp == 0)
	(building-type-count-total mill == 0)
	(unit-type-count villager == 3)
=>
	(set-goal housing-goal 0)
)
#end-if
#load-if-defined GOTHIC-CIV
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(population-headroom > 0)
	(building-type-count-total barracks > 1)
	(housing-headroom < 40)
	(or
		(housing-headroom < 20)		
		(building-type-count-total barracks > 2)
	)
=>
	(set-goal housing-goal 1)
)
#end-if
#end-if
#load-if-not-defined UP-MULTIPLAYER-GAME
(defrule
	(game-time < 120)
=>
	(up-get-fact unit-type-count villager-builder temp-goal5); Don't start more buildings than we have villagers that can build or we mess up. This idea is taken from Barbie.
	(set-strategic-number sn-placement-fail-delta 0)
	(set-strategic-number sn-placement-zone-size 5)
)
(defrule
	(game-time < 120)
	(goal housing-goal 1)
	(can-build house)
	(unit-type-count villager g:> temp-goal5)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: house)
	(set-goal housing-goal 0)
	(up-modify-goal temp-goal5 c:+ 1)
)
#end-if
(defrule
	(true)
=>
	(set-strategic-number sn-maximum-hunt-drop-distance hunt-distance)
	(set-strategic-number sn-maximum-food-drop-distance food-distance)
	(set-strategic-number sn-maximum-wood-drop-distance wood-distance)
	(disable-self)
)
#else
#load-if-not-defined HUN-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined SPANISH-CIV ; they have speed bonus on buildings
(defrule
	(building-type-count-total house == 0)
	(goal housing-goal 1)
	(or
		(or
			(civ-selected aztec)
			(civ-selected mayan)
		)
		(not (map-type scandanavia)); go for early loom
	)
=>
	(up-assign-builders c: house c: 2)
	(disable-self)
)
#else
(defrule
	(building-type-count-total house < 3)
	(goal housing-goal 1)
	(can-build house)
=>
	(up-set-placement-data my-player-number villager-class c: 1)
	(up-build place-control 0 c: house)
	(set-goal housing-goal 0)
)
#end-if
(defrule
	(building-type-count-total house < 2)
	(goal housing-goal 1)
	(can-build house)
=>
	(up-set-placement-data my-player-number villager-class c: 1)
	(up-build place-control 0 c: house)
	(set-goal housing-goal 0)
)
#else
(defrule
	(not (death-match-game))
	(building-type-count town-center > 0)
=>
	(enable-timer civ-explore-timer 75)
	(set-goal civ-explore-goal food)
	(chat-local-to-self "search for food as chinese")
	(disable-self)
)
#end-if
#end-if
(defrule
	(or
		(civ-selected mayan)
		(civ-selected hun)
	)
	(game-time > 0)
	(game-time < 60)
	(or
		(building-type-count-total town-center > 0)
		(building-type-count-total lumber-camp > 0)
	)
=>
	(set-strategic-number sn-maximum-food-drop-distance food-distance)
	(set-strategic-number sn-maximum-wood-drop-distance 7)
	(set-strategic-number sn-cap-civilian-explorers 0)
	(set-strategic-number sn-percent-civilian-explorers 0)
	(set-strategic-number sn-minimum-civilian-explorers 0)
	(disable-self)
)
(defrule
	(dropsite-min-distance food == -1)
	(building-type-count-total house > 1)
=>
	(set-strategic-number sn-maximum-food-drop-distance food-distance)
	(set-strategic-number sn-maximum-wood-drop-distance 7)
	(set-strategic-number sn-cap-civilian-explorers 0)
	(set-strategic-number sn-percent-civilian-explorers 0)
	(set-strategic-number sn-minimum-civilian-explorers 0)
	(disable-self)
)
(defrule
	(building-type-count town-center > 0)
	(game-time > 1)
	(game-time < 300)
	(or
		(up-compare-goal sheep-count-goal > 0)
		(resource-found food)
	)
=>
	(set-strategic-number sn-maximum-wood-drop-distance 0); up-drop-resources does not work
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: male-lumberjack c: 100)
	(up-find-local c: female-lumberjack c: 100)
	(up-modify-sn sn-focus-player-number g:= my-player-number-goal)
	(up-find-remote c: town-center c: 1)
	(up-target-objects 0 action-garrison -1 -1)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(disable-self)
)
(defrule
	(or
		(unit-type-count villager-builder > 2); prevent missing assignment due to lag in MP
		(game-time > 13);
	)
=>
	(up-assign-builders c: house c: 1)
	(set-strategic-number sn-maximum-hunt-drop-distance hunt-distance)
	(set-strategic-number sn-maximum-food-drop-distance food-distance)
	(disable-self)
)
#end-if
(defrule
	(goal housing-goal 1)
	(or
		(up-pending-objects c: house < 1)
		(and
			(and
				(population > 90)
				(housing-headroom < 5)
			)
			(or
				(up-pending-objects c: house < 2)
				(population > pop-house)
			)
		)
	)
	(up-pending-objects c: house < 3)
	(can-build house)
=>
	(build house)
	(set-goal housing-goal 0)
	(set-goal increase-town-size-goal house)
)
#load-if-defined DEATH-MATCH
(defrule
	(goal housing-goal 1)
	(or
		(up-pending-objects c: house < 1)
		(unit-type-count villager > 3)
	)
	(up-pending-objects c: house < 2)
	(can-build house)
=>
	(build house)
	(set-goal housing-goal 0)
	(set-goal increase-town-size-goal house)
)
(defrule
	(goal housing-goal 1)
	(or
		(building-type-count-total barracks > 0)
		(building-type-count-total stable > 0)
	)
	(current-age >= feudal-age)
	(or
		(up-pending-objects c: house < 3)
		(starting-age == post-imperial-age)
	)
	(up-pending-objects c: house < 5)
	(can-build house)
=>
	(build house)
	(set-goal housing-goal 0)
	(set-goal increase-town-size-goal house)
)
#end-if
#load-if-defined UP-MICHI-STYLE
(defrule
	(goal housing-goal 1)
	(building-type-count-total town-center > 4)
	(civilian-population < max-civ)
	(up-pending-objects c: house < 5)
	(can-build house)
=>
	(build house)
	(set-goal housing-goal 0)
	(set-goal increase-town-size-goal house)
)
#end-if
(defrule
	(goal housing-goal 1)
	(population > 150)
	(population-cap >= 225)
	(up-pending-objects c: house < 5)
	(can-build house)
=>
	(build house)
	(set-goal housing-goal 0)
	(set-goal increase-town-size-goal house)
)
#load-if-not-defined WONDER-RACE
(defrule
	(current-age == dark-age)
	(building-type-count town-center > 1); double arabia
	(unit-type-count villager < 40)
	(food-amount < 430)
	(can-train villager)
=>
	(train villager)
	(set-strategic-number sn-total-number-explorers 2)
	(set-strategic-number sn-number-explore-groups 2)
)
#end-if
#load-if-defined DEATH-MATCH
(defrule
	(can-research feudal-age)
=>
	(set-strategic-number sn-current-age df-transit)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-town-size 12)
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(set-strategic-number sn-maximum-wood-drop-distance 15)
	(chat-to-allies "41up")
	(research feudal-age)
)
#else
(defrule
	(unit-type-count-total villager >= villager-feudal)
	(or
		(or
			(unit-type-count fishing-ship > 3)
			(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
		)
		(goal dark-build-order-goal 2)
	)
	(can-research feudal-age)
=>
	(set-strategic-number sn-current-age df-transit)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-town-size 10)
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(chat-to-allies "41up")
	(research feudal-age)
)
(defrule
	(civilian-population >= villager-flush)
	(or
		(goal strategy-goal flush)
		(goal strategy-goal sling)
	)
	(can-research feudal-age)
=>
	(set-strategic-number sn-current-age df-transit)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-town-size 10)
	(chat-to-allies "41up")
	(research feudal-age)
)
(defrule
	(unit-type-count villager >= dark-age-villager)
	(up-compare-goal strategy-goal != fast-imp)
	(can-research feudal-age)
=>
	(set-strategic-number sn-current-age df-transit)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-town-size 10)
	(chat-to-allies "41up")
	(research feudal-age)
)
(defrule
	(food-amount >= 700)
	(unit-type-count villager >= 28)
	(or
		(food-amount >= 725)
		(unit-type-count villager >= 29)
	)
	(up-compare-goal strategy-goal != fast-imp)
	(can-research feudal-age)
=>
	(set-strategic-number sn-current-age df-transit)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-town-size 10)
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(chat-to-allies "41up")
	(research feudal-age)
)
(defrule
	(unit-type-count villager >= villager-wonder-dark)
	(goal strategy-goal fast-imp)
	(can-research feudal-age)
=>
	(set-strategic-number sn-current-age df-transit)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-town-size 10)
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
	(chat-to-allies "41up")
	(research feudal-age)
)
(defrule
	(or
		(or
			(game-type == 8)
			(difficulty != hard)
		)
		(or
			(population-cap == 25)
			(starting-resources != low-resources)
		)
	)
	(unit-type-count-total villager >= villager-feudal)
	(can-research feudal-age)
=>
	(set-strategic-number sn-current-age df-transit)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(set-strategic-number sn-maximum-wood-drop-distance 15)
	(chat-to-allies "41up")
	(research feudal-age)
)
#end-if
(defrule
	(current-age == feudal-age)
	(unit-type-count villager > 16); pretend dark age if feudal age start
=>
	(enable-timer lumber-timer 60)
	(set-strategic-number sn-maximum-town-size 10)
	(set-strategic-number sn-current-age feudal)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-minimum-number-hunters 0)
	(disable-self)
)
#load-if-not-defined SPANISH-CIV
(defrule
	(strategic-number sn-current-age == feudal)
	(current-age-time < 10)
	(food-amount > 730)

=>
	(up-assign-builders c: archery-range c: 2)
	(up-assign-builders c: stable c: 2)
	(up-assign-builders c: market c: 2)
	(disable-self)
)
#end-if
(defrule
	(strategic-number sn-current-age == fc-transit)
=>
	(up-assign-builders c: bombard-tower c: 3)
	(up-assign-builders c: castle c: 3)
	(up-assign-builders c: archery-range c: 1)
	(up-assign-builders c: stable c: 1)
	(up-assign-builders c: market c: 1)
	(up-assign-builders c: blacksmith c: 1)
	(up-assign-builders c: mining-camp c: 1)
	(disable-self)
)
#load-if-not-defined WONDER-RACE
#load-if-defined LOW-RESOURCES-START
(defrule
	(current-age == feudal-age)
	(or
		(and
			(gold-amount < 200)
			(strategic-number sn-current-age == feudal)
		)
		(or
			(goal unit-goal eagle-warrior)
			(goal unit-goal monk)
		)
	)
	(food-amount >= 700)
	(goal strategy-goal rush)
	(can-sell-commodity stone)
=>
	(sell-commodity stone)
)
#end-if
#load-if-defined SARACEN-CIV
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(game-time < 1800); don't do this late game
	(nor 
		(goal strategy-goal sling)
		(and
			(goal unit-goal my-unique-unit-line)
			(building-type-count castle == 0)
		)
	)
	(commodity-selling-price stone > 95)
	(or
		(wood-amount < 300); prevent sell/buy for TC loop
		(stone-amount >= 200)
	)
	(can-sell-commodity stone)
=>
	(sell-commodity stone)
)
#end-if
#end-if
(defrule
	(current-age >= imperial-age)
	(game-time > 1800)
	(current-age-time > 180)
	(population > max-civ-pop)
	(gold-amount > 1600)
	(stone-amount < 650)
	(goal under-attack-goal 0)
	(building-type-count-total castle < 4)
	(or
		(research-completed my-unique-unit-upgrade)
		(building-type-count-total castle == 0)
	)
	(can-buy-commodity stone)
=>
	(buy-commodity stone)
	(chat-local-to-self "6 buy stone for a castle")
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(nor 
		(or
			(goal strategy-goal sling)
			(goal strategy-goal boom)
		)
		(goal unit-goal my-unique-unit-line)
	)
	(strategic-number sn-current-age == feudal)
	(stone-amount >= 200)
	(food-amount < 670)
	(gold-amount > 180)
	(building-type-count market >= 1)
=>
	(sell-commodity stone)
	(buy-commodity food)
	(chat-to-player chat-player "sell stone and buy food for age up")
)
#end-if
(defrule
	(strategic-number sn-resource-control != ri-heresy)
	(game-time < 1800)
	(goal monk-threat-goal 1)
	(or
		(and
			(research-available ri-light-cavalry)
			(gold-amount >= 200)
		)
		(unit-type-count scout-cavalry-line < 6)
	)
	(goal enemy-goal rush)
	(food-amount < 80)
	(building-type-count stable > 0)
	(can-buy-commodity food)
=>
	(buy-commodity food)
)
(defrule
	(or
		(dropsite-min-distance wood > 20)
		(cc-players-unit-type-count 0 tree-class < 20)
	)
	(wood-amount < 200)
	(or
		(gold-amount > 200)
		(wood-amount < 60)
	)
	(can-buy-commodity wood)
=>
	(buy-commodity wood)
	(chat-to-player chat-player "buy wood")
)
(defrule
	(game-time < 1500)
	(wood-amount < 120)
	(gold-amount > 350)
	(or
		(unit-type-count-total 219 >= 1)
		(unit-type-count-total 228 >= 1); dead lumberjacks
	)
	(can-buy-commodity wood)
=>
	(buy-commodity wood)
	(chat-to-player chat-player "buy wood")
)

#load-if-not-defined AZTEC-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined INCAN-CIV
(defrule
	(strategic-number sn-resource-control < 2)
	(goal monk-threat-goal 1)
	(building-type-count stable > 0)
	(building-type-count market > 0)
	(or
		(unit-type-count scout-cavalry-line < 6)
		(research-available ri-light-cavalry)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
#end-if
#end-if
#end-if
#load-if-defined SARACEN-CIV
(defrule
	(strategic-number sn-current-age == feudal)
	(or
		(food-amount < 650)
		(gold-amount > 320)
	)
	(food-amount < 800)
	(commodity-buying-price food < 120)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(chat-local-to-self "3 Special Saracen Market Rule")
)
(defrule
	(or
		(game-time < 2100)
		(research-available ri-horse-collar)
	)
	(or
		(food-amount < 120)
		(and
			(up-compare-goal unit-goal != monk)
			(and
				(food-amount < 200)
				(gold-amount > 275)
			)
		)
	)
	(gold-amount > 200)
	(commodity-buying-price food < 140)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(chat-local-to-self "3 Special Saracen Market Rule")
)
(defrule
	(game-time < 1500)
	(building-type-count-total stable < 2)
	(building-type-count-total archery-range < 2)
	(wood-amount < 200)
	(or
		(wood-amount < 120)
		(gold-amount > 500)
	)
	(gold-amount > 200)
	(or
		(unit-type-count-total 219 >= 1)
		(unit-type-count-total 228 >= 1); dead lumberjacks
	)
	(commodity-buying-price wood < 120)
	(can-buy-commodity wood)
=>
	(buy-commodity wood)
	(chat-local-to-self "4 Special Saracen Market Rule")
)
#end-if
(defrule
	(goal train-civ-goal 1)
	(building-type-count-total town-center >= tc-level-two)
	(gold-amount > 250)
	(food-amount < 50)
	(civilian-population < max-civ)
	(civilian-population < 85)
	(commodity-buying-price food < 150)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(chat-local-to-self "3 buy food for villagers")
)
(defrule
	(strategic-number sn-resource-control != ri-heresy)
	(gold-amount > 500)
	(food-amount < 200)
	(unit-type-count trade-cart > 15)
	(commodity-buying-price food < 70)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(chat-local-to-self "3 buy cheap food with trade cart gold")
)
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(can-sell-commodity stone)
	(stone-amount > 750)
	(food-amount > 2000)
	(wood-amount > 2000)
	(gold-amount < 1000)
	(commodity-selling-price stone > 40)
	(building-type-count-total castle > 1)
=>
	(sell-commodity stone)
)
#end-if
#end-if ; NOT WONDER-RACE
#load-if-defined DEATH-MATCH
#load-if-not-defined ISLANDS-MAP
#load-if-not-defined TEAM-ISLANDS-MAP
(defrule
	(can-sell-commodity wood)
	(commodity-selling-price wood > 40)
=>
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)(disable-self)
)
(defrule
	(can-sell-commodity wood)
	(commodity-selling-price wood > 30)
=>
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)
	(sell-commodity wood)(disable-self)
)
#end-if
#end-if
#load-if-not-defined BRITON-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined MAYAN-CIV
(defrule
	(can-sell-commodity stone)
	(commodity-selling-price stone > 70)
=>
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)(disable-self))
#end-if
(defrule
	(can-sell-commodity stone)
	(commodity-selling-price stone > 70)
=>
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)
	(sell-commodity stone)(disable-self))
#end-if
#end-if
#end-if
#end-if
#end-if

(defrule
	(research-available castle-age)
	(strategic-number sn-current-age >= fc-transit)
=>
	(set-strategic-number sn-current-age feudal)
	(chat-to-player chat-player "castle-age research failed!"); castle research failed, set our age-tracking back
)
(defrule
	(can-research castle-age)
=>
	(set-strategic-number sn-current-age fc-transit)
	(set-strategic-number sn-maximum-town-size 10)
	(set-strategic-number sn-allow-civilian-defense 0)
	(research castle-age)
	(chat-to-allies "41up")
)
(defrule
	(current-age == castle-age)
	(unit-type-count villager > 16)
=>
	(set-strategic-number sn-maximum-fish-boat-drop-distance -1)
	(set-strategic-number sn-maximum-town-size 12)
	(set-strategic-number sn-maximum-hunt-drop-distance 18)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-wood-dropsite-distance 15)
	(set-strategic-number sn-camp-max-distance 35)
	(set-strategic-number sn-current-age castle)
	(set-strategic-number sn-defer-dropsite-update 1)
	(enable-timer scouting-timer 120)
	(disable-self)
)
(defrule
	(or	
		(strategic-number sn-gold-dropsite-distance >= 40); reset distances after forced camp
		(strategic-number sn-stone-dropsite-distance >= 40)
	)
=>
	(set-strategic-number sn-gold-dropsite-distance 5)
	(set-strategic-number sn-stone-dropsite-distance 5)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
)
#load-if-not-defined DEATH-MATCH
(defrule
	(or
		(or
			(stone-amount == 0)
			(unit-type-count villager-gold == 0)
		)
		(wood-amount > 420)
	)
	(gold-amount < 200)
	(food-amount > 750)
	(research-available castle-age)
	(strategic-number sn-current-age == feudal)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
	(chat-local-to-self "sell feudal wood")
)
(defrule
	(stone-amount < 100)
	(gold-amount < 200)
	(food-amount > 900)
	(research-available castle-age)
	(strategic-number sn-current-age == feudal)
	(commodity-selling-price food > 40)
	(can-sell-commodity food)
=>
	(sell-commodity food)
	(chat-local-to-self "sell feudal food")
)
#end-if
(defrule
	(unit-type-count villager-gold == 0); debug
	(gold-amount < 200)
	(food-amount > 900)
	(research-available castle-age)
	(strategic-number sn-current-age == feudal)
	(commodity-selling-price food > 40)
	(can-sell-commodity food)
=>
	(sell-commodity food)
	(chat-local-to-self "sell excess food")
)
(defrule
	(wood-amount < 1300)
	(unit-type-count-total trade-cart > 0)
	(unit-type-count-total trade-cart < max-trade-cart)
	(unit-type-count-total trade-cart < max-trade-pop)
=>
	(up-jump-rule 2)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands); don't sell too much on water maps, we can't gatherer on other islands
	(cc-players-unit-type-count 0 tree-class > 100)
	(food-amount < 2000); sell food first
	(wood-amount > 1200)
	(dropsite-min-distance wood < 7)
	(gold-amount < 800)
	(or
		(starting-age < post-imperial-age)
		(or
			(commodity-selling-price wood > 40)
			(food-amount < 800)
		)
	)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
	(chat-local-to-self "sell excess wood")
)
(defrule
	(starting-age < post-imperial-age)
	(or
		(research-available imperial-age)
		(current-age >= imperial-age)
	)
	(wood-amount > 430)
	(commodity-selling-price wood > 40)
	(or
		(building-type-count town-center >= tc-level-four)
		(and
			(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
			(stone-amount < 50)
		)
	)
	(cc-players-unit-type-count 0 tree-class > 100)
	(gold-amount < 800)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
	(chat-local-to-self "sell excess wood")
)
#load-if-defined POST-IMPERIAL-AGE-START
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined PERSIAN-CIV
(defrule
	(food-amount > 6000)
	(can-sell-commodity food)
	(commodity-selling-price food > 50)
=>
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
	(sell-commodity food)
)
#end-if
#end-if
#end-if
(defrule
	(game-time > 1200)
	(current-age >= imperial-age)
	(food-amount > 2000)
	(gold-amount < 1250)
	(population > eighty-five-percent-pop)
	(can-sell-commodity food)
	(commodity-buying-price wood < 120); don't run into a buy wood, farm and sell food cycle, it's not worth
=>
	(sell-commodity food)
	(chat-local-to-self "sell excess food")
)
(defrule
	(game-time > 1200)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(food-amount < 2000)
	(wood-amount > 2000)
	(cc-players-unit-type-count 0 tree-class > 100)
	(dropsite-min-distance wood < 7)
	(gold-amount < 1200)
	(population > eighty-five-percent-pop)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
	(chat-local-to-self "sell excess wood")
)
(defrule
	(game-time > 1200)
	(current-age >= imperial-age)
	(food-amount > 600)
	(gold-amount < 800)
	(or
		(and
			(gold-amount < 150)
			(or
				(food-amount > 1100)
				(commodity-selling-price food > 40)
			)
		)
		(food-amount > 1650)
	)
	(commodity-buying-price wood < 135)
	(can-sell-commodity food)
=>
	(sell-commodity food)
	(chat-local-to-self "sell excess food")
)

(defrule
	(game-time > 1200)
	(food-amount > 400)
	(gold-amount < 50)
	(wood-amount > 400)
	(unit-type-count trade-cart > 0)
	(unit-type-count trade-cart < max-trade-cart)
	(unit-type-count trade-cart < max-trade-pop)
	(unit-type-count trade-cart < 20)
	(can-sell-commodity food)
=>
	(sell-commodity food)
	(chat-local-to-self "sell food")
)
(defrule
	(strategic-number sn-resource-control == ri-heresy)
	(wood-amount > 220)
	(gold-amount < 1100)
	(commodity-selling-price wood > 20)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
	(chat-local-to-self "emergency sell wood - need ri-heresy")
)
(defrule
	(strategic-number sn-resource-control == ri-heresy)
	(food-amount > 300)
	(gold-amount < 1100)
	(commodity-selling-price food > 20)
	(not (research-completed ri-light-cavalry))
	(can-sell-commodity food)
=>
	(sell-commodity food)
	(chat-local-to-self "emergency sell food - need ri-heresy")
)
(defrule
	(research-available imperial-age)
	(food-amount > 1100)
	(gold-amount < 800)
	(or
		(unit-type-count villager >= fourty-percent-pop)
		(goal strategy-goal fast-imp)
	)
	(can-sell-commodity food)
=>
	(sell-commodity food)
	(chat-local-to-self "5 Sell Food - need gold for imperial-age")
	(set-goal tribute-goal 5)
)
(defrule
	(research-available imperial-age)
	(wood-amount > 400)
	(food-amount > 800)
	(gold-amount < 700)
	(dropsite-min-distance wood < 7)
	(nand
		(gold-amount < 500)
		(population < max-civ-pop)
	)
	(can-sell-commodity wood)
=>
	(sell-commodity wood)
	(chat-local-to-self "5 Sell Wood - need gold for imperial-age")
)
(defrule
	(research-available imperial-age)
	(civilian-population >= max-civ)
	(players-unit-type-count any-ally villager > fourty-percent-pop)
	(food-amount < 500)
	(gold-amount >= 800)
=>
	(set-goal tribute-goal 3)
)
(defrule
	(food-amount < 800)
	(research-available castle-age)
	(gold-amount > 330)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(chat-local-to-self "3 buy food for age up")
)
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control != ri-heresy)
	(food-amount < 1000)
	(research-available imperial-age)
	(gold-amount > 950)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(chat-local-to-self "3 buy food for age up")
)
#end-if
(defrule
	(strategic-number sn-resource-control != ri-heresy)
	(goal under-attack-goal 1)
	(civilian-population > 30)
	(civilian-population < fifty-percent-pop)
	(food-amount < 100)
	(gold-amount > 350)
	(commodity-buying-price food < 170)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(set-goal tribute-goal 3)
)
(defrule
	(wood-amount >= 275)
	(stone-amount < 100)
	(building-type-count-total town-center == 0)
	(can-buy-commodity stone)
	(unit-type-count villager > 0)
=>
	(buy-commodity stone)
)
(defrule
	(wood-amount > 375)
	(stone-amount < 100)
	(gold-amount < 300)
	(unit-type-count villager > 0)
	(building-type-count-total town-center == 0)
	(building-type-count market > 0)
=>
	(release-escrow wood)
	(sell-commodity wood)
)
(defrule
	(wood-amount >= 275)
	(stone-amount < 100)
	(building-type-count-total town-center == 0)
	(unit-type-count villager > 0)
=>
	(set-goal tribute-goal 6)
)
(defrule
	(wood-amount < 275)
	(stone-amount >= 100)
	(building-type-count-total town-center == 0)
	(unit-type-count villager > 0)
=>
	(set-goal tribute-goal 4)
)
(defrule
	(strategic-number sn-resource-control < 2)
	(wood-amount >= 330)
	(stone-amount < 100)
	(civilian-population < 80)
	(or
		(current-age-time > 300)
		(gold-amount > 400)
	)
	(current-age >= castle-age)
	(building-type-count-total town-center < 3)
	(up-compare-goal strategy-goal != fast-imp)
	(up-pending-objects c: town-center == 0)
	(can-buy-commodity stone)
=>
	(buy-commodity stone)
	(chat-local-to-self "6")
)
(defrule
	(stone-amount < 650)
	(civilian-population < 80)
	(current-age >= imperial-age)
	(building-type-count-total castle == 0)
	(gold-amount > 400)
	(unit-type-count trade-cart > 4)
	(or
		(dropsite-min-distance stone > 20)
		(dropsite-min-distance stone < 0)
	)
	(can-buy-commodity stone)
=>
	(buy-commodity stone)
	(chat-local-to-self "6")
)
(defrule
	(research-available imperial-age)
	(strategic-number sn-current-age >= ci-transit)
=>
	(set-strategic-number sn-current-age castle)
	(chat-to-player chat-player "imperial-age research failed!"); imperial research failed, set our age-tracking back
)
#load-if-not-defined CASTLE-AGE-END
(defrule
	(strategic-number sn-current-age == castle)
	(civilian-population >= fifty-percent-pop)
	(food-amount > 1000)(gold-amount < 400)
=>
	(set-escrow-percentage gold 60)
)
(defrule
	(strategic-number sn-current-age == castle)
	(civilian-population >= fifty-percent-pop)
	(game-time >= 2100)
	(population >= sixty-percent-pop)
=>
	(set-escrow-percentage food 10)(set-escrow-percentage gold 20)
)
#end-if
(defrule
	(can-research-with-escrow imperial-age)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(set-strategic-number sn-current-age ci-transit)
	(chat-to-allies "41up")
	(research imperial-age)
)
(defrule
	(civilian-population < fourty-percent-pop)
	(goal under-attack-goal 1)
	(strategic-number sn-military-level < 0)
=>
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(release-escrow food)(release-escrow gold)
)
(defrule
	(current-age == imperial-age)
=>
	(set-strategic-number sn-current-age imperial)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)(disable-self)
)
#load-if-not-defined WONDER-RACE
(defrule
	(building-type-count dock > 0)
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(research-available ri-war-galley)
	(goal enemy-boats-goal 1)
	(or
		(and
			(unit-type-count fire-ship-line > 14)
			(research-completed ri-bow-saw)
		)
		(or
			(civ-selected viking)
			(unit-type-count galley-line > 6)
		)
	)
	(food-amount < 230)
=>
	(set-goal train-civ-goal navy)
	(set-strategic-number sn-resource-control navy)
	(chat-local-to-self "save-resources for navy upgrades")
)
(defrule
	(gold-amount > 300)
	(food-amount < 200)
	(strategic-number sn-resource-control == navy)
	(or
		(research-available ri-war-galley)
		(research-available ri-careening)
	)
	(can-buy-commodity food)
=>
	(buy-commodity food)
	(chat-local-to-self "3 buy food for navy-upgrades")
)
(defrule
	(or
		(strategic-number sn-resource-control == ri-long-swordsman)
		(or
			(goal train-civ-goal ri-chain-mail)
			(goal train-civ-goal ri-pikeman)
		)
	)
	(gold-amount > 300)
	(food-amount < 150)
	(can-buy-commodity food)
=>
	(buy-commodity food)
)
#load-if-defined UP-POCKET-POSITION
#load-if-not-defined BRITON-CIV
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined SARACEN-CIV
(defrule
	(strategic-number sn-archer-threat < 4)
	(or
		(goal unit-goal knight)
		(or
			(goal unit-goal eagle-warrior)
			(goal unit-goal scorpion)
		)
	)
=>
	(set-strategic-number sn-resource-control 0)
	(set-goal save-wood-goal 0)
	(up-jump-rule 5)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-archer-threat < 4)
	(goal position-goal pocket)
	(game-time < 1560)
	(nor
		(goal unit-goal skirmisher)
		(civ-selected byzantine)
	)
=>
	(up-jump-rule 5)
)
(defrule
	(strategic-number sn-archer-threat < 4)
	(or
		(goal strategy-goal fast-imp)
		(and
			(goal position-goal pocket)
			(unit-type-count cavalry-class > 10)
		)
	)
	(goal unit-goal knight)
=>
	(up-jump-rule 4)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count skirmisher-line > 7)
	(research-available ri-elite-skirmisher)
	(nor
		(and
			(wood-amount < 150)
			(and
				(food-amount > 250); get eskirms over 2nd TC or monastery
				(gold-amount > 250); but don't slow down all other researches
			)
		)
		(and
			(wood-amount > 400); better spend the wood on buildings as gold is low.
			(gold-amount < 75)
		)
	)
	(building-type-count archery-range > 0)
=>
	(set-goal save-wood-goal 1)
	(set-strategic-number sn-resource-control ri-elite-skirmisher)
;	(chat-to-player chat-player "save-resources = ri-elite-skirmisher")
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(research-available ri-elite-skirmisher)	
	(or
		(building-type-count-total town-center >= tc-level-three)
		(strategic-number sn-archer-threat > 0)
	)
	(current-age-time > 120); don't immediately block everything
	(unit-type-count skirmisher-line > 7)
	(building-type-count archery-range > 0)
=>
	(set-goal save-wood-goal 1)
	(set-strategic-number sn-resource-control ri-elite-skirmisher)
;	(chat-to-player chat-player "save-resources = ri-elite-skirmisher")
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-archer-threat > 1)
	(research-available ri-elite-skirmisher)
	(nand
		(building-type-count siege-workshop > 0)
		(unit-type-count-total mangonel-line == 0)
	)
	(building-type-count-total town-center >= tc-level-three)
	(military-population > 14)
	(building-type-count archery-range > 0)
=>
	(set-goal save-wood-goal 1)
	(set-strategic-number sn-resource-control ri-elite-skirmisher)
;	(chat-to-player chat-player "save-resources = ri-elite-skirmisher")
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-archer-threat > 1)
	(research-available ri-elite-skirmisher)
	(or
		(goal unit-goal archer)
		(goal unit-goal cavalry-archer)
	)
	(building-type-count-total siege-workshop == 0)
	(military-population > 19)
	(building-type-count archery-range > 0)
=>
	(set-goal save-wood-goal 1)
	(set-strategic-number sn-resource-control ri-elite-skirmisher)
;	(chat-to-player chat-player "save-resources = ri-elite-skirmisher")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(or
		(goal unit-goal archer)
		(and
			(and
				(goal unit-goal knight)
				(goal ranged-unit-type-goal archer)
			)
			(goal anti-cavalry-threat-goal 1)
		)
	)
	(research-available ri-crossbow)
	(building-type-count archery-range >= 1)
=>
	(set-goal train-civ-goal ri-crossbow)
	(set-strategic-number sn-resource-control ri-crossbow)
;	(chat-to-player chat-player "save-resources = ri-crossbow")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(goal ranged-unit-type-goal archer)
	(or
		(and
			(wood-amount > 150)
			(food-amount < 70)
		)
		(goal enemy-goal eagle-warrior)
	)
	(gold-amount > 250)
	(research-available ri-crossbow)
	(building-type-count archery-range >= 1)
=>
	(set-goal train-civ-goal ri-crossbow)
	(set-strategic-number sn-resource-control ri-crossbow)
;	(chat-to-player chat-player "save-resources = ri-crossbow")
)
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(unit-type-count archer-line > 7)
	(goal ranged-unit-type-goal archer)
	(research-available ri-crossbow)
	(building-type-count archery-range >= 1)
=>
	(set-goal train-civ-goal ri-crossbow)
	(set-strategic-number sn-resource-control ri-crossbow)
;	(chat-to-player chat-player "save-resources = ri-crossbow")
)
(defrule
	(goal train-civ-goal 1)
	(strategic-number sn-cavalry-threat >= 1)
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-military-level >= 0)
	(military-population > 10)
	(building-type-count barracks > 0)
	(research-available ri-pikeman)
=>
	(up-jump-rule 3)
)
(defrule
	(strategic-number sn-military-level >= 0)
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-elite-skirmisher)
	)
	(goal train-civ-goal 1)
	(unit-type-count ranged-unit-class >= mid-number-upgrade)
	(goal strategy-goal flush)
	(not (research-available ri-long-swordsman))
	(research-available ri-bodkin-arrow)
=>
	(set-goal train-civ-goal ri-bodkin-arrow)
	(set-strategic-number sn-resource-control ri-bodkin-arrow)
;	(chat-to-player chat-player "save-resources = ri-bodkin-arrow")
)
(defrule
	(or
		(strategic-number sn-military-level >= 0)
		(goal hostilities-goal 1)
	)
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(goal unit-goal archer)
	(research-available ri-bodkin-arrow)
	(unit-type-count ranged-unit-class >= mid-number-upgrade)
	(nor 
		(research-available ri-crossbow)
		(or
			(goal strategy-goal boom)
			(goal strategy-goal fast-imp)
		)
	)
=>
	(set-goal train-civ-goal ri-bodkin-arrow)
	(set-strategic-number sn-resource-control ri-bodkin-arrow)
;	(chat-to-player chat-player "save-resources = ri-bodkin-arrow")
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(unit-type-count villager > 50)
	(research-available ri-bodkin-arrow)
	(unit-type-count ranged-unit-class >= mid-number-upgrade)
	(goal hostilities-goal 1)
=>
	(set-goal train-civ-goal ri-bodkin-arrow)
	(set-strategic-number sn-resource-control ri-bodkin-arrow)
;	(chat-to-player chat-player "save-resources = ri-bodkin-arrow")
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(or
		(unit-type-count-total war-galley > 0)
		(and
			(building-type-count-total stable < 2)
			(unit-type-count cavalry-archer-class >= high-number-upgrade)
		)
	)
	(research-available ri-bodkin-arrow)
=>
	(set-goal train-civ-goal ri-bodkin-arrow)
	(set-strategic-number sn-resource-control ri-bodkin-arrow)
;	(chat-to-player chat-player "save-resources = ri-bodkin-arrow")
)
(defrule
	(food-amount > 200)
	(goal train-civ-goal ri-bodkin-arrow)
=>
	(set-goal train-civ-goal 1)
)
#load-if-defined BRITON-CIV
(defrule
	(population > eighty-five-percent-pop)
	(research-available my-unique-research)
	(or
		(unit-type-count my-unique-unit-line > 9)
		(unit-type-count scorpion-line > 9)
	)

	(building-type-count castle > 0)
	(unit-type-count trade-cart > 20)
=>
	(set-escrow-percentage wood 20)(set-escrow-percentage gold 20)
	(set-goal escrow-purpose-goal my-unique-research)
)
(defrule
	(can-research-with-escrow my-unique-research)
=>
	(up-release-escrow)
	(set-escrow-percentage wood 0)(set-escrow-percentage gold 0)
	(research my-unique-research)
	(set-goal escrow-purpose-goal 0)
)
(defrule
	(goal escrow-purpose-goal my-unique-research)
	(building-type-count castle == 0)
=>
	(up-release-escrow)
	(set-escrow-percentage wood 0)(set-escrow-percentage gold 0)
	(set-goal escrow-purpose-goal 0)
)
#end-if
#load-if-defined CHINESE-CIV
(defrule
	(population > eighty-five-percent-pop)
	(research-available my-unique-research)
	(or
		(unit-type-count my-unique-unit-line > 9)
		(unit-type-count scorpion-line > 9)
	)

	(building-type-count castle > 0)
	(unit-type-count trade-cart > 20)
=>
	(set-escrow-percentage wood 20)(set-escrow-percentage gold 20)
	(set-goal escrow-purpose-goal my-unique-research)
)
(defrule
	(can-research-with-escrow my-unique-research)
=>
	(up-release-escrow)
	(set-escrow-percentage wood 0)(set-escrow-percentage gold 0)
	(research my-unique-research)
	(set-goal escrow-purpose-goal 0)
)
(defrule
	(goal escrow-purpose-goal my-unique-research)
	(building-type-count castle == 0)
=>
	(up-release-escrow)
	(set-escrow-percentage wood 0)(set-escrow-percentage gold 0)
	(set-goal escrow-purpose-goal 0)
)
#end-if
#load-if-defined FRANKISH-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count-total my-unique-unit-line > 10)
	(can-research my-unique-research)
=>
	(research my-unique-research)
)
#end-if
#load-if-defined GOTHIC-CIV
(defrule
	(can-research-with-escrow ri-perfusion)
=>
	(release-escrow wood)(release-escrow gold)
	(research ri-perfusion)
)
#end-if
#load-if-defined MAYAN-CIV
(defrule
	(not (research-available ri-elite-eagle-warrior))
	(can-research my-unique-research)
=>
	(research my-unique-research)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-current-age >= ci-transit)
	(strategic-number sn-military-level >= 0)
	(research-available ri-eagle-warrior)
	(goal unit-goal eagle-warrior)
=>
	(set-strategic-number sn-resource-control ri-elite-eagle-warrior)
;	(chat-to-player chat-player "save-resources = ri-eagle-warrior")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(or
		(and
			(and
				(strategic-number sn-current-age == ci-transit)
				(strategic-number sn-military-level >= 0)
			)
			(nand
				(food-amount > 800)
				(gold-amount > 500)
			)
		)
		(research-available ri-elite-eagle-warrior)
	)
	(goal unit-goal eagle-warrior)
=>
	(set-strategic-number sn-resource-control ri-elite-eagle-warrior)
;	(chat-to-player chat-player "save-resources = ri-elite-eagle-warrior")
)
(defrule
	(strategic-number sn-resource-control == ri-elite-eagle-warrior)
	(food-amount < 800)
	(gold-amount > 600)
	(research-available ri-elite-eagle-warrior)
=>
	(set-goal train-civ-goal ri-elite-eagle-warrior)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(up-research-status c: ri-bracer != research-available)
	(research-available ri-arbalest)
	(or
		(and
			(food-amount > 350)
			(gold-amount < 300)
		)
		(food-amount < 350)
	)
	(goal unit-goal archer)
=>
	(set-strategic-number sn-resource-control ri-arbalest)
;	(chat-to-player chat-player "save-resources = ri-arbalest")
)
#load-if-not-defined TURKISH-CIV; they get chemistry for free
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(research-available ri-chemistry)
		(and
			(strategic-number sn-current-age == ci-transit)
			(or
				(food-amount < 300)
				(gold-amount < 200)
			)
		)
	)
	(building-type-count university > 0)
	(or
		(goal unit-goal gunpowder-class)
		(goal ranged-unit-type-goal gunpowder-class)
	)
=>
	(set-strategic-number sn-resource-control ri-chemistry)
;	(chat-to-player chat-player "save-resources = ri-chemistry")
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(strategic-number sn-resource-control < 2)
	(or
		(soldier-count >= 45)
		(game-time < 3600)
	)
	(soldier-count > 15)
	(nand
		(building-type-count dock == 0); we lost the naval battle so train more soldiers to defend
		(and
			(current-age >= castle-age)
			(game-time > 1800)
		)
	)
	(population-cap <= 250)
=>
	(set-strategic-number sn-resource-control 1); train navy only
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(warboat-count > 20)
	(current-age >= castle-age)
	(or
		(and
			(stone-amount >= 100)
			(building-type-count-total town-center < tc-level-three)
		)
		(and
			(current-age == castle-age)
			(building-type-count-total siege-workshop == 0)
		)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(population >= del-civ-pop)
	(or
		(civilian-population > 70)
		(game-time > 5400)
	)
	(players-unit-type-count every-enemy fire-ship-line < 12)
	(players-unit-type-count every-enemy galley-line < 12)
	(warboat-count > 40)
	(or
		(and
			(soldier-count > 19)
			(unit-type-count transport-ship < 2)
		)
		(soldier-count < 21)
	)
	(population-cap <= 250)
=>
	(set-goal train-civ-goal -1)
	(delete-unit villager)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(population >= del-civ-pop)
	(current-age == imperial-age)
	(or
		(warboat-count < 45)
		(and
			(unit-type-count transport-ship > 1)
			(or
				(dropsite-min-distance wood > 20)
				(cc-players-unit-type-count 0 tree-class < 20)
			)
		)
	)
	(building-type-count dock > 0)
=>
	(set-goal train-civ-goal -1)
	(delete-unit villager)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal unit-goal knight)
	(research-available ri-chain-barding)
	(unit-type-count cavalry-class > 11)
	(or
		(strategic-number sn-archer-threat >= 2)
		(or
			(up-compare-goal patrol-goal >= 0)
			(goal team-coordination-goal 1)
		)
	)
=>
	(set-strategic-number sn-resource-control ri-chain-barding)
;	(chat-to-player chat-player "save-resources = ri-chain-barding")
)
(defrule
	(strategic-number sn-resource-control == ri-chain-barding)
	(goal train-civ-goal 1)
	(goal unit-goal knight)
	(research-available ri-chain-barding)
	(gold-amount >= 150)
	(food-amount < 250)
	(unit-type-count cavalry-class > 11)
	(or
		(up-compare-goal patrol-goal >= 0)
		(goal team-coordination-goal 1)
	)
=>
	(set-goal train-civ-goal ri-chain-barding)
	(set-strategic-number sn-resource-control ri-chain-barding)
;	(chat-to-player chat-player "save-resources = ri-chain-barding")
)
#end-if
; Place TC upgrades always before villager training or they won't fire unless you reached max villager
(defrule
	(strategic-number sn-resource-control == 3)
	(or
		(not (research-available ri-hand-cart))
		(unit-type-count-total villager < 60)
	)
	(research-completed ri-wheel-barrow)
=>
	(release-escrow wood)(set-escrow-percentage wood 0)
	(set-strategic-number sn-resource-control 0)
	(set-goal train-civ-goal 1)
)
(defrule
	(goal train-civ-goal 1)
	(unit-type-count villager >= 40)
	(can-train villager)
	(research-available ri-wheel-barrow)
	(can-buy-commodity food)
	(food-amount >= 75)
	(food-amount < 175)
	(wood-amount >= 50); enough wood for it
	(up-compare-goal strategy-goal != sling)
=>
	(buy-commodity food)
	(chat-to-player chat-player "Attempting to research ri-wheel-barrow: Buy Food")
	(set-goal train-civ-goal 2)
	(set-strategic-number sn-resource-control 3)
)
(defrule
	(goal train-civ-goal 1)
	(unit-type-count villager >= 50)
	(goal unit-goal knight)
	(research-available ri-wheel-barrow)
	(food-amount < 175)
	(wood-amount >= 50); enough wood for it
=>
	(set-strategic-number sn-resource-control 3)
	(set-strategic-number sn-enable-training-queue 0); disable to allow research over queuing additional villagers
)
(defrule
	(goal train-civ-goal 1)
	(food-amount > 100)
	(unit-type-count-total villager >= 35)
	(research-available ri-wheel-barrow)
	(strategic-number sn-military-level >= 0)
=>
	(set-strategic-number sn-enable-training-queue 0)
)
(defrule
	(goal train-civ-goal 1)
	(unit-type-count villager >= 60)
	(research-available ri-wheel-barrow)
	(strategic-number sn-military-level >= 0)
=>
	(set-goal train-civ-goal 2)
	(set-strategic-number sn-resource-control 3)
;	(chat-to-player chat-player "save-resources = ri-wheel-barrow")	
)
(defrule
	(strategic-number sn-resource-control == 3)
	(players-building-type-count any-ally market > 0)
	(unit-type-count villager >= 60)
	(research-available ri-hand-cart)
	(food-amount > 300)
	(wood-amount < 150)
=>
	(set-goal tribute-goal 4)
)
(defrule
	(nor
		(town-under-attack)
		(and
			(building-type-count-total town-center == 1)
			(up-research-status c: imperial-age == research-pending)
		)
	)
	(goal train-civ-goal 1)
	(strategic-number sn-resource-control < 4)
	(unit-type-count villager >= 60)
	(research-available ri-hand-cart)
	(can-buy-commodity food)
	(food-amount < 300)
=>
	(set-escrow-percentage wood 100)
	(buy-commodity food)
	(chat-to-player chat-player "Attempting to research ri-hand-cart: Buy Food")
	(set-strategic-number sn-resource-control 3)
)
(defrule
	(nor
		(town-under-attack)
		(and
			(building-type-count-total town-center == 1)
			(up-research-status c: imperial-age == research-pending)
		)
	)
	(strategic-number sn-resource-control < 4)
	(unit-type-count villager >= 60)
	(research-available ri-hand-cart)
	(can-buy-commodity wood)
	(food-amount >= 300); enough food for it
	(wood-amount >= 100)
	(wood-amount < 200)
=>
	(set-escrow-percentage wood 100)
	(buy-commodity wood)
	(chat-to-player chat-player "Attempting to research ri-hand-cart: Buy Wood")
	(set-strategic-number sn-resource-control 3)
)
(defrule
	(strategic-number sn-resource-control == 3)
	(research-available ri-hand-cart)
	(escrow-amount wood >= 200)
=>
	(set-escrow-percentage wood 0)
)
(defrule
	(strategic-number sn-resource-control < 4)
	(or
		(unit-type-count villager >= 35)
		(unit-type-count villager >= max-civ)
	)
	(nand
		(and
			(up-compare-goal strategy-goal != sling)
			(up-compare-goal strategy-goal != flush)
		)
		(current-age == feudal-age)
	)
	(can-research ri-wheel-barrow)
=>
	(research ri-wheel-barrow)
	(set-goal train-civ-goal 1)
	(set-strategic-number sn-resource-control 0)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(unit-type-count villager >= 28)
	(strategic-number sn-resource-control < 4)
	(or
		(current-age >= castle-age)
		(and
			(current-age-time < 20)
			(food-amount > 720)
		)
	)
	(can-research ri-wheel-barrow)
=>
	(research ri-wheel-barrow)
	(set-goal train-civ-goal 1)
	(set-strategic-number sn-resource-control 0)
)
#end-if
(defrule
	(or
		(unit-type-count-total villager >= 60)
		(civilian-population >= max-civ)
	)
	(can-research-with-escrow ri-hand-cart)
=>
	(release-escrow wood)(set-escrow-percentage wood 0)
	(research ri-hand-cart)
	(set-goal train-civ-goal 1)
	(set-strategic-number sn-resource-control 0)
)
#end-if ; not WONDER-RACE
(defrule
	(or
		(or
			(research-available ri-wheel-barrow)
			(civ-selected viking)
		)
		(and
			(gold-amount > 220)
			(food-amount < 730)
		)
	)
	(current-age >= feudal-age)
	(can-research ri-loom)
=>
	(research ri-loom)
)
#load-if-not-defined ALL-TECHS-ENABLED
(defrule
	(can-research ri-loom)
	(or
		(civ-selected mayan)
		(civ-selected chinese)
	)
=>
	(research ri-loom)
)
#end-if
#load-if-defined DEATH-MATCH
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined DIFFICULTY-MODERATE
#load-if-defined DARK-AGE-START
(defrule
	(up-pending-placement c: mill)
	(can-build lumber-camp)
	(building-type-count-total lumber-camp == 0)
=>
	(up-reset-placement c: mill)
	(build lumber-camp)
)
#end-if
(defrule
	(or
		(and
			(current-age == dark-age)
			(strategic-number sn-current-age == dark)
		)
		(unit-type-count villager >= 15)
	)
	(building-type-count-total mill == 0)
	(game-time > 0)
	(can-build mill)
=>
	(build mill)
	(set-strategic-number sn-maximum-food-drop-distance 10)
	(set-strategic-number sn-cap-civilian-explorers 0)
	(set-strategic-number sn-percent-civilian-explorers 0)
	(set-strategic-number sn-minimum-civilian-explorers 0)
	(set-strategic-number sn-mill-max-distance 12)
)
#end-if
#end-if
#end-if
#end-if
; try to push to castle age
#load-if-not-defined FEUDAL-AGE-END
(defrule
	(goal strategy-goal flush)
	(or
		(or
			(goal enemy-goal flush)
			(goal enemy-goal eagle-warrior)
		)
		(current-age-time >= 600)
	)
	(research-available castle-age)
	(strategic-number sn-resource-control == 0)
	(or
		(strategic-number sn-military-level >= 0)
		(military-population > 20)
	)
	(goal under-attack-goal 0)
	(game-time >= 1200)
=>
	(set-strategic-number sn-resource-control 2)
	(chat-to-player chat-player "save-resources = castle-age")
)
(defrule
	(goal strategy-goal flush)
	(research-available castle-age)
	(strategic-number sn-resource-control == 2)
	(military-population < 15)
	(goal under-attack-goal 1)
=>
	(set-strategic-number sn-resource-control 0)
)
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-current-age == feudal)
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(wood-amount < 200)
	(food-amount > 700)
	(building-type-count-total blacksmith == 0)
=>
	(set-strategic-number sn-resource-control 2)
	(chat-to-player chat-player "save-resources = castle-age")
)
(defrule
	(players-military-population every-enemy < 5)
	(goal strategy-goal flush)
	(goal forward-threat-goal 1)
	(strategic-number sn-current-age == feudal)
	(strategic-number sn-resource-control == 0)
=>
	(set-strategic-number sn-resource-control 2)
	(chat-to-player chat-player "save-resources = castle-age")
)
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-current-age == feudal)
	(strategic-number sn-resource-control == 0)
	(goal under-attack-goal 0)
	(game-time >= 1300)
	(up-compare-sn sn-focus-player-number g:== closest-enemy-goal)
	(up-player-distance focus-player < 70); assume the nearest enemy is found
	(or
		(players-building-type-count focus-player watch-tower > 1)
		(players-building-type-count focus-player stone-wall > 5)
	)
=>
	(set-strategic-number sn-resource-control 2)
	(chat-to-player chat-player "save-resources = castle-age")
)
#end-if
#load-if-not-defined CASTLE-AGE-END
; try to push to imperial age
(defrule
	(research-available imperial-age)
	(goal strategy-goal fast-imp)
	(goal unit-goal monk)
	(food-amount > 750)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(current-age-time > 600)
	(strategic-number sn-current-age == castle)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(food-amount > 500)
	(gold-amount > 400)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(up-compare-goal under-attack-goal != 1)
	(building-type-count town-center >= tc-level-three)
	(strategic-number sn-current-age == castle)
	(military-population >= thirty-percent-pop)
	(population-cap >= 100)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(up-compare-goal under-attack-goal != 1)
	(current-age-time > 300)
	(building-type-count town-center >= tc-level-three)
	(strategic-number sn-current-age == castle)
	(up-research-status c: imperial-age != research-available); we can't advance yet
	(military-population >= thirty-percent-pop)
	(population-cap >= 100)
	(wood-amount < 250)
	(food-amount > 500)
	(gold-amount > 400)
=>
	(set-goal save-wood-goal 1); save up for siege work shop
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(building-type-count town-center >= tc-level-two)
	(population > sixty-percent-pop)
	(strategic-number sn-military-level >= 0)
	(up-compare-goal under-attack-goal != 1)
	(nand
		(players-current-age every-enemy == imperial-age)
		(and
			(strategic-number sn-military-level >= 2)
			(up-players-in-game any == 2)
		)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(research-available imperial-age)
	(or
		(goal position-goal pocket)
		(strategic-number sn-military-level >= 0)
	)
	(building-type-count gate > 0)
	(goal under-attack-goal 0)
	(up-compare-goal unit-goal g:!= ranged-unit-type-goal)
	(up-compare-goal unit-goal != my-unique-unit-line)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(players-building-type-count focus-player stone-wall > 5)
	(players-military-population focus-player < 5)
	(military-population > 7)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(or
		(population >= 140)
		(unit-type-count-total eagle-warrior-line > 40)
	)
	(or
		(strategic-number sn-military-level >= -1)
		(unit-type-count cavalry-class > 14)
	)
	(goal under-attack-goal 0)
	(or
		(goal strategy-goal fast-imp)
		(and
			(goal strategy-goal boom)
			(goal unit-goal knight)
		)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(game-time > 1800)
	(civilian-population < fourty-percent-pop)
	(strategic-number sn-military-level < -2)
	(up-compare-goal under-attack-goal != -1)
=>
	(set-goal strategy-goal boom)
	(set-strategic-number sn-resource-control 2)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(building-type-count town-center > tc-level-two)
	(goal unit-goal knight)
	(unit-type-count cavalry-class > 14)
	(or
		(civ-selected korean)
		(civ-selected saracen)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(research-available imperial-age)
	(goal under-attack-goal 0)
	(or
		(goal strategy-goal boom)
		(unit-type-count cavalry-class > 17)
	)
	(goal unit-goal knight)
	(unit-type-count cavalry-class > 11)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(research-available imperial-age)
	(goal hostilities-goal 0)
	(goal control-goal my-unique-unit-line)
	(building-type-count-total castle > 0)
	(military-population > 15)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
#end-if
#load-if-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-current-age == castle)
	(or
		(building-type-count monastery > 0)
		(building-type-count siege-workshop > 0)
	)
=>
	(set-strategic-number sn-resource-control 2); block resources and don't train monks or scorps before imperial age up
)
#end-if
(defrule
	(strategic-number sn-resource-control == 0)
	(or
		(and
			(research-available ri-cavalier)
			(or
				(goal unit-goal knight)
				(unit-type-count knight-line > 19)
			)
		)
		(and	
			(research-available ri-hussar)
			(nor 
				(goal unit-goal militiaman-line)
				(civ-selected magyar)
			)
		)
	)
	(not (research-completed ri-champion))
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(strategic-number sn-resource-control == 0)
	(or
		(and	
			(or
				(research-available ri-two-handed-swordsman)
				(research-available ri-champion)
			)
			(or
				(goal unit-goal militiaman-line)
				(goal control-goal militiaman-line)
			)
		)
		(and
			(goal ranged-unit-type-goal archer)
			(research-available ri-arbalest)
		)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-current-age >= ci-transit)
	(or
		(or
			(research-available ri-blast-furnace)
			(research-available ri-plate-mail)
		)
		(research-available ri-chain-mail)
	)
	(unit-type-count infantry-class > 12)
	(or
		(food-amount < 350)
		(gold-amount < 225)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
#load-if-not-defined AZTEC-CIV
#load-if-not-defined MAYAN-CIV
(defrule
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-current-age >= ci-transit)
	(or
		(or
			(research-available ri-blast-furnace)
			(research-available ri-plate-barding)
		)
		(research-available ri-chain-barding)
	)
	(unit-type-count cavalry-class > 12)
	(or
		(food-amount < 350)
		(gold-amount < 225)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-current-age >= fc-transit)
	(game-time < 1800)
	(building-type-count gate > 0)
	(goal unit-goal skirmisher)
	(building-type-count town-center < 3)
=>
	(set-strategic-number sn-resource-control 2)
)
#end-if
(defrule
	(strategic-number sn-resource-control == 0)
	(current-age == castle-age)
	(civilian-population > 90)
	(goal unit-goal knight)
	(or
		(strategic-number sn-military-level >= 0)
		(population >= eighty-five-percent-pop)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
#load-if-defined PERSIAN-CIV
(defrule
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal my-unique-unit-line)
	(building-type-count castle > 0)
	(or
		(and
			(up-research-status c: my-unique-unit-upgrade < research-pending); ri-elite-war-elephant
			(or
				(food-amount < 1600)
				(gold-amount < 1200)
			)
		)
		(research-available my-unique-unit-upgrade)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = ewe")
)
#end-if
#load-if-not-defined INDIAN-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined TURKISH-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined BRITON-CIV
#load-if-not-defined KOREAN-CIV
(defrule
	(strategic-number sn-resource-control == 0)
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal knight)
	(unit-type-count knight-line > 16)
	(or
		(and
			(not (research-completed ri-cavalier))
			(or
				(food-amount < 1300)
				(gold-amount < 750)
			)
		)
		(research-available ri-paladin)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = ri-paladin")
)
(defrule
	(strategic-number sn-resource-control == 0)
	(unit-type-count trade-cart > 2)
	(goal unit-goal knight)
	(research-available ri-paladin)
=>
	(set-escrow-percentage food 30)(set-escrow-percentage gold 20)
	(set-goal escrow-purpose-goal ri-paladin)
)
(defrule
	(goal escrow-purpose-goal ri-paladin)
	(or
		(up-compare-goal unit-goal != knight)
		(up-research-status c: ri-paladin != research-available)
	)
=>
	(up-release-escrow)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(set-goal escrow-purpose-goal 0)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#else
(defrule
	(strategic-number sn-resource-control == 0)
	(unit-type-count camel-line > 7)
	(or
		(research-available ri-heavy-camel)
		(research-available ri-imperial-camel)
	)
	(population > eighty-five-percent-pop)
=>
	(set-strategic-number sn-resource-control 2)
)
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-resource-control == 0)
	(or
		(research-available ri-bracer)
		(research-available ri-ring-archer-armor)
	)
	(or
		(food-amount < 300)
		(gold-amount < 250)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(goal train-civ-goal ri-crossbow)
	(strategic-number sn-resource-control == ri-crossbow)
	(up-research-status c: ri-elite-skirmisher == research-pending)
	(building-type-count archery-range == 1)
=>
	(set-goal train-civ-goal 1)
	(set-strategic-number sn-resource-control 0)
)

(defrule
	(strategic-number sn-resource-control == 0)
	(population > max-civ-pop)
	(unit-type-count scorpion-line > 11)
	(or
		(or
			(research-available ri-heavy-scorpion)
			(research-available ri-siege-engineers)
		)
		(and
			(civ-selected celtic)
			(and
				(building-type-count castle > 0)
				(research-available my-unique-research)
			)
		)
	)
	(building-type-count siege-workshop > 0)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(goal uu-up-goal 1)
	(population > eighty-five-percent-pop)
	(research-available my-unique-unit-upgrade)
	(building-type-count castle > 0)
	(or
		(or
			(unit-type-count female-gold-miner > 0); don't start escrowing when gold income is low
			(unit-type-count male-gold-miner > 0)
		)
		(unit-type-count trade-cart > 5)
	)
=>
	(set-escrow-percentage food 30)(set-escrow-percentage gold 30)
	(set-goal escrow-purpose-goal my-unique-unit-line)
)
(defrule
	(goal escrow-purpose-goal my-unique-unit-line)
	(unit-type-count female-gold-miner == 0)
	(unit-type-count male-gold-miner == 0)
	(unit-type-count trade-cart == 0)
=>
	(release-escrow food)
	(set-escrow-percentage food 0); pile up relic/market gold first, as this will take long
)
(defrule
	(or
		(and
			(goal escrow-purpose-goal my-unique-unit-line)
			(or
				(escrow-amount food >= max-food-escrow)
				(population < max-civ-pop)
			)
		)
		(and
			(research-available imperial-age)
			(escrow-amount food >= imp-food)
		)
	)
=>
	(set-escrow-percentage food 0)
)
(defrule
	(or
		(and
			(goal escrow-purpose-goal my-unique-unit-line)
			(escrow-amount gold >= max-gold-escrow)
		)
		(and
			(research-available imperial-age)
			(escrow-amount gold >= imp-gold)
		)
	)
=>
	(set-escrow-percentage food 30)
	(set-escrow-percentage gold 0)
)
(defrule
	(goal escrow-purpose-goal my-unique-unit-line)
	(or
		(up-research-status c: my-unique-unit-upgrade != research-available)
		(building-type-count castle == 0); not included in check above
	)
=>
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(release-escrow food)(release-escrow gold)
	(set-goal escrow-purpose-goal 0)
)
#load-if-defined SPANISH-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count monastery-class > 12)
	(research-completed ri-theocracy)
	(research-available ri-inquisition)
	(building-type-count castle > 0)
=>
	(set-strategic-number sn-resource-control ri-inquisition)
;	(chat-to-player chat-player "save-resources = ri-inquisition")
)
#end-if
#load-if-defined SLAVIC-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count monastery-class > 12)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(goal hostilities-goal 1)
	)
	(building-type-count castle > 0)
	(research-available ri-orthodoxy)
=>
	(set-strategic-number sn-resource-control ri-orthodoxy)
;	(chat-to-player chat-player "save-resources = ri-orthodoxy")
)
#end-if
(defrule
	(strategic-number sn-resource-control == 0)
	(or
		(or
			(goal unit-goal monk)
			(goal ranged-unit-type-goal monk)
		)
		(players-unit-type-count any-enemy war-elephant-line > 12)
	)
	(unit-type-count-total monk > 5)
	(or
		(or
			(research-available ri-theocracy)
			(research-available ri-illumination)
		)
		(research-available ri-block-printing)
	)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(current-age == imperial-age)
	(unit-type-count monastery-class > 20)
	(strategic-number sn-resource-control == 0)
	(goal unit-goal monk)
	(gold-amount < 300)
	(unit-type-count-total trebuchet-set == 0)
	(building-type-count castle > 0)
=>
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(current-age == imperial-age)
	(unit-type-count monastery-class > 20)
	(goal unit-goal monk)
	(wood-amount < 200)
	(unit-type-count-total trebuchet-set == 0)
	(building-type-count castle > 0)
=>
	(set-goal save-wood-goal 1)
)
(defrule
	(research-available imperial-age)
	(strategic-number sn-resource-control == 0)
	(goal unit-goal monk)
	(unit-type-count-total monk > 20)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
(defrule
	(food-amount >= 175)
	(or
		(goal train-civ-goal ri-crossbow)
		(and
			(food-amount >= 265)
			(goal train-civ-goal ri-pikeman)
		)
	)
=>
	(set-goal train-civ-goal 1)
)
(defrule
	(strategic-number sn-resource-control < 2)
	(population >= del-civ-pop)
	(unit-type-count trebuchet-set == 0)
	(unit-type-count battering-ram-line < 4)
=>
	(set-strategic-number sn-resource-control 2)
)
#load-if-defined DEATH-MATCH
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(strategic-number sn-resource-control < 2)
	(civilian-population < fourty-percent-pop)
	(population >= max-civ-pop)
	(game-time < 900)
=>
	(set-strategic-number sn-resource-control villager)
)
#end-if
#load-if-defined DARK-AGE-START
(defrule
	(strategic-number sn-resource-control > 2)
	(food-amount > 2000)
	(gold-amount > 2000)
	(wood-amount > 2000)
=>
	(set-strategic-number sn-resource-control 0); resource control not needed in DM
	(set-goal save-wood-goal 0)
)
#end-if
#end-if
#end-if ; not CASTLE-AGE-END
#load-if-not-defined WONDER-RACE
(defrule
	(research-completed ri-coinage)
	(food-amount > 170)
	(goal strategy-goal sling)
	(taunt-detected any-human-ally 38)
=>
	(tribute-to-player this-any-human food 100)
)
(defrule
	(research-completed ri-coinage)
	(food-amount > 170)
	(goal strategy-goal sling)
	(up-allied-goal any-ally control-goal == shot)
=>
	(tribute-to-player this-any-ally food 100)
)
(defrule
	(research-completed ri-coinage)
	(food-amount > 180)
	(goal strategy-goal sling)
	(taunt-detected any-computer-ally 124)	
=>
	(tribute-to-player this-any-computer-ally food 100)
)
(defrule
	(research-completed ri-coinage)
	(wood-amount > 170)
	(goal strategy-goal sling)
	(taunt-detected any-human-ally 38)
=>
	(tribute-to-player this-any-human wood 100)
)
(defrule
	(research-completed ri-coinage)
	(wood-amount > 170)
	(goal strategy-goal sling)
	(up-allied-goal any-ally control-goal == shot)
=>
	(tribute-to-player this-any-ally wood 100)
)
(defrule
	(research-completed ri-coinage)
	(wood-amount > 170)
	(goal strategy-goal sling)
	(taunt-detected any-computer-ally 124)	
=>
	(tribute-to-player this-any-computer-ally wood 100)
)
(defrule
	(research-completed ri-coinage)
	(gold-amount > 170)
	(goal strategy-goal sling)
	(taunt-detected any-human-ally 38)
=>
	(tribute-to-player this-any-human gold 100)
)
(defrule
	(research-completed ri-coinage)
	(gold-amount > 170)
	(goal strategy-goal sling)
	(up-allied-goal any-ally control-goal == shot)
=>
	(tribute-to-player this-any-ally gold 100)
)
(defrule
	(research-completed ri-coinage)
	(gold-amount > 170)
	(goal strategy-goal sling)
	(taunt-detected any-computer-ally 124)	
=>
	(tribute-to-player this-any-computer-ally gold 100)
)
(defrule
	(research-completed ri-coinage)
	(stone-amount > 170)
	(goal strategy-goal sling)
	(taunt-detected any-human-ally 38)
=>
	(tribute-to-player this-any-human stone 100)
)
(defrule
	(research-completed ri-coinage)
	(stone-amount > 170)
	(goal strategy-goal sling)
	(up-allied-goal any-ally control-goal == shot)
=>
	(tribute-to-player this-any-ally stone 100)
)
(defrule
	(research-completed ri-coinage)
	(stone-amount > 170)
	(goal strategy-goal sling)
	(taunt-detected any-computer-ally 124)	
=>
	(tribute-to-player this-any-computer-ally stone 100)
)
(defrule
	(or
		(strategic-number sn-resource-control == 2)
		(and
			(goal under-attack-goal 1)
			(population < 190)
		)
	)
	(food-amount < 800)
	(or
		(food-amount < 400)
		(research-available imperial-age)
	)
	(building-type-count town-center > 0)
	(up-allied-resource-amount any-computer-ally food > 1600)
	(up-allied-resource-percent any-computer-ally amount-tribute-inefficiency < 30)
	(up-compare-goal strategy-goal != sling)
=>
	(set-goal tribute-goal 3)
)
(defrule
	(or
		(goal under-attack-goal 1)
		(and
			(strategic-number sn-resource-control == 2); wood for siege
			(gold-amount >= 200)
		)
	)
	(wood-amount < 200)
	(food-amount > 300)
	(up-allied-resource-amount any-computer-ally wood > 1200)
	(up-allied-resource-percent any-computer-ally amount-tribute-inefficiency < 30)
	(up-compare-goal strategy-goal != sling)
=>
	(set-goal tribute-goal 4)
)
(defrule
	(or
		(goal under-attack-goal 1)
		(food-amount > 800)
	)
	(population > sixty-percent-pop)
	(gold-amount < 200)
	(food-amount > 400)
	(up-allied-resource-amount any-computer-ally gold > 2000)
	(up-allied-resource-percent any-computer-ally amount-tribute-inefficiency < 30)
	(up-compare-goal strategy-goal != sling)
=>
	(set-goal tribute-goal 5)
)
(defrule
	(strategic-number sn-resource-control == 2)
	(or
		(gold-amount < 300)
		(goal escrow-purpose-goal ri-halberdier)
	)
	(or
		(gold-amount < 600)
		(research-available imperial-age)
	)
	(food-amount > 300)
	(gold-amount < 800)
	(up-allied-resource-amount any-computer-ally gold > 2000)
	(up-allied-resource-percent any-computer-ally amount-tribute-inefficiency < 30)
	(up-compare-goal strategy-goal != sling)
=>
	(set-goal tribute-goal 5)
)
(defrule
	(goal under-attack-goal 1)
	(population < 190)
	(wood-amount < 200)
	(food-amount > 300)
	(up-allied-resource-amount any-computer-ally wood > 1100)
	(up-allied-resource-percent any-computer-ally amount-tribute-inefficiency < 30)
	(up-compare-goal strategy-goal != sling)
=>
	(set-goal tribute-goal 4)
)
(defrule
	(goal under-attack-goal 1)
	(population < 190)
	(population > 120)
	(gold-amount < 200)
	(food-amount > 400)
	(up-allied-resource-amount any-computer-ally gold > 2000)
	(up-allied-resource-percent any-computer-ally amount-tribute-inefficiency < 30)
	(up-compare-goal strategy-goal != sling)
=>
	(set-goal tribute-goal 5)
)
(defrule
	(strategic-number sn-resource-control == 2)
	(or
		(gold-amount < 300)
		(goal escrow-purpose-goal ri-halberdier)
	)
	(or
		(gold-amount < 600)
		(research-available imperial-age)
	)
	(food-amount > 300)
	(gold-amount < 800)
	(up-allied-resource-amount any-computer-ally gold > 900)
	(up-allied-resource-percent any-computer-ally amount-tribute-inefficiency < 30)
	(up-compare-goal strategy-goal != sling)
=>
	(set-goal tribute-goal 5)
)
#load-if-defined DEATH-MATCH
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(timer-triggered five-seconds-timer)
	(food-amount > 2000)
	(wood-amount > 2000)
	(gold-amount < 200)
	(up-allied-resource-amount any-computer-ally gold > 1000)
	(players-building-type-count any-computer-ally market > 0)
=>
	(chat-to-player this-any-computer-ally "5")
)
#end-if
#end-if
(defrule
	(players-tribute-memory any-computer-ally food > 0)
	(players-population any-computer-ally >= del-civ-pop)
	(up-allied-resource-amount any-computer-ally food > 1200)
=>
	(clear-tribute-memory every-computer food)
	(enable-timer tribute-timer 1); some scripts send only 100 of one res, even if they have tons of it, so keep annoying them.
)
(defrule
	(players-tribute-memory any-computer-ally gold > 0)
	(players-population any-computer-ally >= del-civ-pop)
	(up-allied-resource-amount any-computer-ally gold > 1000)
=>
	(clear-tribute-memory every-computer gold)
	(enable-timer tribute-timer 1); some scripts send only 100 of one res, even if they have tons of it, so keep annoying them.
)
(defrule
	(game-time > 1200)
	(building-type-count town-center > 0)
	(or
		(unit-type-count villager < 15)
		(and
			(players-population any-ally > sixty-percent-pop)
			(or
				(unit-type-count villager < twenty-five-percent-pop)
				(players-population any-ally > eighty-five-percent-pop)
			)				
		)
	)
	(unit-type-count villager < fourty-percent-pop)
	(food-amount < 250)
	(goal under-attack-goal 0)
=>
	(set-goal tribute-goal 3)
)
(defrule
	(building-type-count town-center >= tc-level-two)
	(players-building-type-count any-ally market > 0)
	(game-time > 1800)
	(or
		(unit-type-count villager < 90)
		(strategic-number sn-resource-control == 2)
	)
	(food-amount < 100)
	(gold-amount > 200)
=>
	(set-goal tribute-goal 3)
)
(defrule
	(players-building-type-count any-ally market > 0)
	(up-allied-resource-percent any-ally amount-tribute-inefficiency < 10)
	(up-allied-resource-amount any-ally food > 1200)
	(players-population any-ally >= max-civ-pop)
	(food-amount < 200)
	(building-type-count town-center > 0)
=>
	(set-goal tribute-goal 3)
)
(defrule
	(building-type-count town-center >= tc-level-two)
	(players-building-type-count any-ally market > 0)
	(or
		(strategic-number sn-resource-control == 2)
		(up-allied-resource-amount any-ally gold > 1500)
	)
	(gold-amount < 200)
=>
	(set-goal tribute-goal 5)
)
(defrule
	(players-building-type-count any-ally market > 0)
	(food-amount > 500)
	(gold-amount > 400)
	(wood-amount < 150)
=>
	(set-goal tribute-goal 4)
)
(defrule
	(taunt-detected any-ally 3)
	(food-amount > 1000)
	(strategic-number sn-resource-control < 1)
	(research-completed ri-banking)
	(building-type-count market > 0)
	(current-age == imperial-age)
=>
	(acknowledge-taunt this-any-ally 3)
	(tribute-to-player this-any-ally food 400)
	(chat-to-player-using-id this-any-ally 22077)
	(chat-local-to-self "tribute food")
)
(defrule
	(taunt-detected any-ally 3)
	(food-amount > 300)
	(or
		(research-completed ri-banking)
		(food-amount > 1200)
	)
	(strategic-number sn-resource-control < 1)
	(building-type-count market > 0)
=>
	(acknowledge-taunt this-any-ally 3)
	(tribute-to-player this-any-ally food 100)
	(chat-to-player-using-id this-any-ally 22077)
	(chat-local-to-self "tribute food")
)
(defrule
	(taunt-detected any-ally 4)
	(wood-amount > 900)
	(goal save-wood-goal 0)
	(research-completed ri-banking)
	(building-type-count market > 0)
=>
	(acknowledge-taunt this-any-ally 4)
	(tribute-to-player this-any-ally wood 400)
	(chat-to-player-using-id this-any-ally 22076)
	(chat-local-to-self "tribute wood")
)
(defrule
	(taunt-detected any-ally 4)
	(wood-amount > 300)
	(goal save-wood-goal 0)
	(building-type-count market > 0)
=>
	(acknowledge-taunt this-any-ally 4)
	(tribute-to-player this-any-ally wood 100)
	(chat-to-player-using-id this-any-ally 22076)
	(chat-local-to-self "tribute wood")
)
(defrule
	(taunt-detected any-ally 5)
	(gold-amount > 1500)
	(research-completed ri-banking)
	(building-type-count market > 0)
=>
	(acknowledge-taunt this-any-ally 5)
	(tribute-to-player this-any-ally gold 400)
	(chat-to-player-using-id this-any-ally 22078)
	(chat-local-to-self "tribute gold")
)
(defrule
	(taunt-detected any-ally 5)
	(strategic-number sn-resource-control < 1)
	(gold-amount > 300)
	(research-completed ri-banking)
	(building-type-count market > 0)
=>
	(acknowledge-taunt this-any-ally 5)
	(tribute-to-player this-any-ally gold 100)
	(chat-to-player-using-id this-any-ally 22078)
	(chat-local-to-self "tribute gold")
)
(defrule
	(taunt-detected any-ally 6)
	(building-type-count-total market > 0)
	(or
		(stone-amount > 750)
		(and
			(players-building-type-count any-ally town-center == 0)
			(stone-amount >= 130)
		)
	)
	(building-type-count market > 0)
=>
	(acknowledge-taunt this-any-ally 6)
	(tribute-to-player this-any-ally stone 100)
	(chat-to-player-using-id this-any-ally 22079)
	(chat-local-to-self "tribute stone")
)
(defrule
	(or
		(or
			(taunt-detected any-ally 3)
			(taunt-detected any-ally 4)
		)
		(or
			(taunt-detected any-ally 5)
			(taunt-detected any-ally 6)
		)
	)
	(building-type-count market == 0)
=>
	(chat-to-player-using-id this-any-ally 22164)
	(acknowledge-taunt this-any-ally 3)
	(acknowledge-taunt this-any-ally 4)
	(acknowledge-taunt this-any-ally 5)
	(acknowledge-taunt this-any-ally 6)
)
(defrule
	(or
		(or
			(taunt-detected any-ally 3)
			(taunt-detected any-ally 4)
		)
		(or
			(taunt-detected any-ally 5)
			(taunt-detected any-ally 6)
		)
	)
=>
	(acknowledge-taunt this-any-ally 3)
	(acknowledge-taunt this-any-ally 4)
	(acknowledge-taunt this-any-ally 5)
	(acknowledge-taunt this-any-ally 6)
)
(defrule
	(up-compare-goal strategy-goal != sling)
	(taunt-detected any-ally 130); cooperate with barbie's share resources
	(wood-amount < 400)
	(gold-amount > 800)
	(food-amount > 1000)
=>
	(chat-to-allies-using-id 22123)
	(acknowledge-taunt this-any-ally 130)
)
(defrule
	(up-compare-goal strategy-goal != sling)
	(nand	
		(goal under-attack-goal 1)
		(population < 45)
	)
	(taunt-detected any-ally 130)
	(or
		(food-amount < 1000)
		(or	
			(wood-amount < 800)
			(gold-amount < 700)
		)
	)
	(building-type-count-total town-center > 0)
=>
	(chat-to-player this-any-ally "1")
	(chat-to-allies "Yes, I do.")
	(acknowledge-taunt this-any-ally 130)
	(acknowledge-taunt my-player-number 1)
)
(defrule
	(taunt-detected any-ally 130); if we need them, the other rules above would've triggered
=>
	(chat-to-player this-any-ally "2")
	(chat-to-allies "No, thanks.")
	(acknowledge-taunt this-any-ally 130)
	(acknowledge-taunt my-player-number 2)
)
(defrule
	(up-timer-status tribute-timer != timer-running)
	(goal tribute-goal 3)
	(unit-type-count villager < 15)
	(goal hostilities-goal 0)
	(not (up-compare-goal threat-time-goal < 30000))
=>
	(enable-timer tribute-timer 30)
	(chat-to-allies-using-id 22122)
)
(defrule
	(up-compare-goal strategy-goal != sling)
	(up-timer-status tribute-timer != timer-running)
	(goal tribute-goal 3)
	(players-building-type-count any-ally market > 0)
	(or
		(up-allied-resource-percent any-ally amount-tribute-inefficiency < 10)
		(and
			(up-allied-resource-amount any-ally food > 1200)
			(up-allied-resource-percent any-ally amount-tribute-inefficiency < 30)
		)
	)
	(up-allied-resource-amount any-ally food > 400)
=>
	(enable-timer tribute-timer 30)
	(chat-to-allies-using-id 22122)
)
(defrule
	(up-timer-status tribute-timer != timer-running)
	(goal tribute-goal 4)
	(players-building-type-count any-ally market > 0)
	(or
		(or
			(building-type-count-total town-center == 0)
			(up-allied-resource-percent any-ally amount-tribute-inefficiency < 10)
		)
		(and
			(up-allied-resource-amount any-ally wood > 600)
			(up-allied-resource-percent any-ally amount-tribute-inefficiency < 30)
		)
	)
	(up-allied-resource-amount any-ally wood > 400)
=>
	(enable-timer tribute-timer 30)
	(chat-to-allies-using-id 22123)
)
(defrule
	(up-timer-status tribute-timer != timer-running)
	(goal tribute-goal 5)
	(players-building-type-count any-ally market > 0)
	(or
		(up-allied-resource-percent any-ally amount-tribute-inefficiency < 10)
		(and
			(up-allied-resource-amount any-ally gold > 1300)
			(up-allied-resource-percent any-ally amount-tribute-inefficiency < 30)
		)
	)
	(up-allied-resource-amount any-ally gold > 400)
=>
	(enable-timer tribute-timer 30)
	(chat-to-allies-using-id 22124)
)
(defrule
	(up-timer-status tribute-timer != timer-running)
	(goal tribute-goal 6)
	(players-building-type-count any-ally market > 0)
	(or
		(up-allied-resource-percent any-ally amount-tribute-inefficiency < 10)
		(building-type-count town-center == 0)
	)
	(up-allied-resource-amount any-ally stone > 100)
=>
	(enable-timer tribute-timer 30)
	(chat-to-allies-using-id 22125)
)
#end-if ; NOT WONDER-RACE

;********************BASICS ********************

(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(resource-found wood)
	(unit-type-count-total villager > 9)
	(building-type-count-total lumber-camp < 1)
	(or
		(dropsite-min-distance deer-hunting < 30)
		(dropsite-min-distance boar-hunting < 30)
	)
	(up-compare-goal sheep-count-goal >= 5)
	(building-type-count-total mill == 0)
	(can-build lumber-camp)
=>
	(build lumber-camp)
	(set-goal dark-build-order-goal 1)
	(set-strategic-number sn-allow-adjacent-dropsites 1); delay mill after dock
)
(defrule
	(resource-found wood)
	(unit-type-count-total villager > 12)
	(building-type-count-total lumber-camp < 1)
	(dropsite-min-distance boar-hunting < 30)
	(up-compare-goal sheep-count-goal >= 5)
	(can-build lumber-camp)
=>
	(build lumber-camp)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-current-age == dark)
	(building-type-count town-center > 0)
	(building-type-count-total mill == 0)
	(or
		(unit-type-count villager-shepherd > 9)
		(and
			(building-type-count-total lumber-camp > 0)
			(unit-type-count-total villager >= 16)
		)
	)
	(game-time < 360)
	(resource-found food)	
	(can-build mill)
=>
	(build mill)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
	(set-strategic-number sn-maximum-food-drop-distance 8)
)
(defrule
	(strategic-number sn-current-age == dark)
	(building-type-count town-center > 0)
	(building-type-count-total mill == 0)
	(resource-found food)
	(or
		(or 
			(unit-type-count male-forager > 0)
			(unit-type-count female-forager > 0)
		)
		(or
			(unit-type-count-total villager >= 18)
			(unit-type-count-total villager >= villager-feudal)
		)
	)
	(can-build mill)
=>
	(build mill)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
	(set-strategic-number sn-maximum-food-drop-distance 8)
)
(defrule
	(current-age == dark-age)
	(building-type-count town-center > 0)
	(building-type-count-total mill == 0); emergency mill for no berries
	(game-time > 180)
	(unit-type-count-total villager >= 20); some other foood sources available
	(up-compare-goal dark-build-order-goal != 1)
	(or
		(wood-amount > 200)
		(building-type-count lumber-camp > 0)
	)
	(can-build mill)
=>
	(build mill)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(current-age == dark-age)
	(building-type-count town-center > 0)
	(building-type-count-total mill == 0); emergency mill for no berries
	(unit-type-count villager-hunter == 0)
	(unit-type-count sheep == 0)
	(unit-type-count scout-unit == 0)
	(building-type-count lumber-camp > 0)
	(can-build mill)
=>
	(build mill)
	(set-strategic-number sn-allow-adjacent-dropsites 0)
)
#load-if-defined SCANDANAVIA-MAP
(defrule
	(building-type-count town-center > 0)
	(strategic-number sn-current-age == dark)
	(dropsite-min-distance deer-hunting > 8)
	(dropsite-min-distance deer-hunting < 38)
	(can-build mill)
	(building-type-count-total mill < 1)
=>
	(set-strategic-number sn-preferred-mill-placement 1); mill deer
	(build mill)
)
#end-if
#load-if-not-defined DEATH-MATCH
(defrule
	(building-type-count town-center >= 1); this idea from ER rarely fails
	(game-time >= 15); increase lumber camp distance when we still found no wood after 15s
	(or ; so the scout has some time to find a forest
		(not (resource-found wood))
		(building-type-count-total stone-wall > 15); also increase in walled maps, as we may find the forest with the wall
	)
	(strategic-number sn-mill-max-distance < 30)
=>
	(set-strategic-number sn-camp-max-distance 18)
	(disable-self)
)
(defrule
	(building-type-count town-center >= 1)
	(game-time >= 18); and even further when still no wood after 18s
	(not (resource-found wood))
	(strategic-number sn-mill-max-distance < 30)
=>
	(set-strategic-number sn-camp-max-distance 21)
	(disable-self)
)
(defrule
	(building-type-count-total lumber-camp > 0)
	(building-type-count-total stone-wall < 15)
	(or
		(dropsite-min-distance gold > 12)
		(building-type-count-total mining-camp > 0)
	)
=>
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(set-strategic-number sn-camp-max-distance 30)
	(set-strategic-number sn-wood-dropsite-distance 7); move wood gatherers from far stragglers to the camp
	(disable-self)
)
(defrule
	(building-type-count-total lumber-camp > 1)
	(building-type-count-total mining-camp == 0)
	(dropsite-min-distance gold > 3)
	(dropsite-min-distance gold < 12); close gold like in gold rush maps
=>
	(set-strategic-number sn-camp-max-distance 12); so only a small camp max distance is required, else the gold will be skipped.
	(set-strategic-number sn-wood-dropsite-distance 4)
	(disable-self)
)
(defrule
	(current-age == dark-age); don't trigger this later when initial gold is gone.
	(building-type-count town-center >= 1); mainly a fix for nomad map where gold may be farther away
	(building-type-count-total lumber-camp > 0); as the computer doesn't care about resources when placing the initial TC.
	(unit-type-count villager >= 24)
	(dropsite-min-distance gold > 24)
	(dropsite-min-distance gold < 64)
=>
	(set-strategic-number sn-camp-max-distance 60)
	(disable-self)
)
(defrule
	(game-time > lumber-delay)
	(resource-found wood)
	(or
		(unit-type-count-total villager > 13)
		(starting-resources > low-resources)
	)
	(building-type-count-total mill > 0)
	(building-type-count-total lumber-camp == 0)
	(can-build lumber-camp)
=>
	(build lumber-camp)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or 
		(up-compare-goal strategy-goal != flush)
		(or
			(strategic-number sn-gold-gatherer-percentage > 0)
			(building-type-count-total blacksmith > 0)
		)
	)
	(resource-found gold)
	(strategic-number sn-current-age >= df-transit)
	(can-build mining-camp)
	(building-type-count-total mining-camp == 0)
=>
	(build mining-camp)
	(set-strategic-number sn-allow-adjacent-dropsites 0)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(goal strategy-goal flush)
	(resource-found gold)
	(strategic-number sn-current-age >= df-transit)
	(can-build mining-camp)
	(building-type-count-total mining-camp == 0)
=>
	(build mining-camp)
	(set-strategic-number sn-allow-adjacent-dropsites 0)
)
#end-if ; no DM
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
(defrule
	(resource-found wood)
	(unit-type-count villager >= 20)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(dropsite-min-distance wood < 4)
	(wood-amount < 160)
	(strategic-number sn-current-age == dark)
=>
	(up-jump-rule 1)
)
(defrule ;build additional lumber camps
	(resource-found wood)
	(unit-type-count villager >= 20)
	(or
		(building-type-count-total lumber-camp < 2)
		(unit-type-count villager >= 50)
	)
	(or
		(building-type-count-total lumber-camp < 3)
		(unit-type-count villager >= 70)
	)
	(building-type-count-total lumber-camp < 4)
	(can-build lumber-camp)
=>
	(build lumber-camp)
	(set-strategic-number sn-dropsite-separation-distance 3)
	(set-strategic-number sn-maximum-wood-drop-distance 30)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
#end-if
#end-if
; Lumber Camp Replacement when either fast when 1st lumber-camp placed really bad or in feudal-age when flushing or after castle-age is available
#load-if-not-defined POST-IMPERIAL-AGE-START
(defrule
	(or
		(goal increase-town-size-goal 0)
		(current-age <= feudal-age)
	)
	(building-type-count town-center > 0)
	(or
		(and
			(building-type-count lumber-camp > 0)
			(dropsite-min-distance wood > 3)
		)
		(and
			(unit-type-count-total villager >= villager-feudal)
			(building-type-count-total lumber-camp == 0); debug for no placement
		)
	)
	(strategic-number sn-camp-max-distance < 35)
	(can-build lumber-camp)
=>
	(set-strategic-number sn-camp-max-distance 35)
	(set-strategic-number sn-dropsite-separation-distance 3)
	(chat-local-to-self "bad lumber camp placement!")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(resource-found wood)
	(unit-type-count 979 > 0); lumberjack
	(building-type-count-total lumber-camp < 1)
	(or
		(unit-type-count villager > 14)
		(strategic-number sn-current-age > dark)
	)
	(wood-amount > 200)
	(can-build lumber-camp)
=>
	(build lumber-camp)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(strategic-number sn-current-age >= df-transit)
	(resource-found gold)
	(goal strategy-goal flush)
	(or
		(and
			(or
				(building-type-count-total stable > 0)
				(building-type-count-total archery-range > 1)
			)
			(building-type-count blacksmith > 0)
		)
		(and
			(goal unit-goal militiaman-line)
			(building-type-count-total barracks > 0)
		)	
	)
	(can-build mining-camp)
	(building-type-count-total mining-camp == 0)
=>
	(build mining-camp)
	(set-strategic-number sn-allow-adjacent-dropsites 0)
)
(defrule
	(strategic-number sn-current-age >= feudal)
	(resource-found gold)
	(strategic-number sn-minimum-water-body-size-for-dock > water-mixed)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total market > 0)
	(can-build mining-camp)
	(building-type-count-total mining-camp == 0)
=>
	(build mining-camp)
	(set-strategic-number sn-allow-adjacent-dropsites 0)
)
#load-if-not-defined WONDER-RACE
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(resource-found gold)
	(strategic-number sn-military-level < -2)
	(gold-amount < 75)
	(or
		(wood-amount > 400)
		(food-amount > 400)
	)
	(game-time > 1200)
	(game-time < 1800)
	(unit-type-count-total villager > 25)
	(can-build mining-camp)
	(up-compare-goal control-goal != shot)
	(building-type-count-total mining-camp < 3)
=>
	(build mining-camp)
	(set-strategic-number sn-dropsite-separation-distance 10)
)
#end-if
(defrule
	(building-type-count-total mining-camp == 1)
	(goal under-attack-goal 1)
	(can-build mining-camp)
=>
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: mining-camp c: 1)
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 8)
	(up-find-remote c: -1 c: 100)
	(up-get-search-state local-total-goal)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(building-type-count-total mining-camp == 1)
	(goal under-attack-goal 1)
	(up-compare-goal remote-total-goal > 5)
	(can-build mining-camp)
=>
	(build mining-camp)
	(set-strategic-number sn-maximum-stone-drop-distance 40)
	(set-strategic-number sn-stone-dropsite-distance 40)
	(set-strategic-number sn-maximum-gold-drop-distance 3)
	(set-strategic-number sn-gold-dropsite-distance 3)
)
(defrule
	(resource-found gold)
	(or
		(strategic-number sn-gold-gatherer-percentage > 30)
		(or
			(unit-type-count-total 229 >= 1); dead miners detected
			(unit-type-count-total 221 >= 1)
		)
	)
	(unit-type-count-total villager > 33)
	(can-build mining-camp)
	(building-type-count-total mining-camp < 3)
	(up-pending-objects c: town-center < 1)
=>
	(build mining-camp)
	(set-strategic-number sn-maximum-stone-drop-distance 40)
	(set-strategic-number sn-stone-dropsite-distance 40)
	(set-strategic-number sn-maximum-gold-drop-distance 3)
	(set-strategic-number sn-gold-dropsite-distance 3)
	(set-strategic-number sn-allow-adjacent-dropsites 0)
)
#end-if
(defrule
	(or
		(unit-type-count fishing-ship > 1)
		(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	)
	(game-time > 360)
	(or
		(goal dark-build-order-goal 1)
		(goal dark-build-order-goal 3)
	)
=>
	(set-goal dark-build-order-goal 0)
	(disable-self)
)
(defrule
	(game-time < 900)
	(unit-type-count villager-gold > 1)
	(building-type-count-total mining-camp == 0)
=>
	(up-assign-builders c: mining-camp c: 2)
)
#end-if ; NOT PIDM
(defrule
	(resource-found gold)
	(dropsite-min-distance gold > 5);rebuild bad camps
	(dropsite-min-distance gold < 255)
	(building-type-count mining-camp > 0)
	(can-build mining-camp)
	(or
		(building-type-count mining-camp < 5)
		(and
			(strategic-number sn-camp-max-distance > 45)
			(dropsite-min-distance gold > 13); don't spam camps if it's in an unreachable spot
		)
	)
	(building-type-count-total mining-camp < 15)
	(up-pending-objects c: town-center < 1)
	(up-pending-objects c: mining-camp < 1)
=>
	(build mining-camp)
	(set-strategic-number sn-defer-dropsite-update 1)
	(set-strategic-number sn-dropsite-separation-distance 6)
)
(defrule
	(resource-found stone)
	(dropsite-min-distance stone > 5)
	(dropsite-min-distance stone < 255)
	(building-type-count mining-camp > 1)
	(or
		(building-type-count mining-camp < 4)
		(and
			(strategic-number sn-camp-max-distance > 45)
			(dropsite-min-distance stone > 13)
		)
	)
	(can-build mining-camp)
	(strategic-number sn-stone-gatherer-percentage > 0)
	(building-type-count-total mining-camp < 15)
	(up-pending-objects c: mining-camp < 1)
	(up-pending-objects c: town-center < 1)
=>
	(build mining-camp)
	(set-strategic-number sn-dropsite-separation-distance 6)
)
(defrule
	(wood-amount > 200)
	(dropsite-min-distance gold > 1); build subsequent mining camps to avoid crowd
	(dropsite-min-distance gold < 255); check for gold still on the map
	(civilian-population > 80)
	(game-time < 2400)
	(can-build mining-camp)
	(building-type-count-total mining-camp < 5)
	(up-pending-objects c: mining-camp < 1)
=>
	(set-strategic-number sn-camp-max-distance 60)
	(set-strategic-number sn-dropsite-separation-distance 12)
	(build mining-camp)
)
#load-if-not-defined WONDER-RACE
(defrule
	(strategic-number sn-resource-control != battering-ram)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(or
		(or
			(research-completed ri-bow-saw)
			(building-type-count town-center >= tc-level-three)
		)
		(or
			(strategic-number sn-stone-gatherer-percentage > 0)
			(wood-amount > 210)
		)
	)
	(up-pending-objects c: town-center < 1)
	(stone-amount < 100)
	(current-age-time > 90)
	(building-type-count-total mining-camp < 4)
	(dropsite-min-distance stone > 4)
	(can-build mining-camp)
=>
	(build mining-camp)
)
#end-if
#load-if-defined SARACEN-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith > 0)
	(commodity-selling-price stone > 100)
	(building-type-count-total mining-camp < 4)
	(or
		(and
			(building-type-count-total mining-camp < 2)
			(dropsite-min-distance stone > 5)
		)
		(and
			(current-age == castle-age)
			(building-type-count stable > 1)
		)
	)
	(can-build mining-camp)
=>
	(build mining-camp)
)
#end-if
#load-if-defined UP-MICHI-STYLE
(defrule
	(building-type-count-total blacksmith > 0)
	(building-type-count-total mining-camp == 1)
	(dropsite-min-distance stone > 3)
	(can-build mining-camp)
=>
	(build mining-camp)
)
#end-if
(defrule
	(or
		(goal unit-goal my-unique-unit-line)
		(goal unit-goal wonder)
	)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total mining-camp == 1)
	(dropsite-min-distance stone > 3)
	(can-build mining-camp)
=>
	(build mining-camp)
)
#load-if-not-defined DEATH-MATCH
(defrule
	(or
		(game-time > 2100)
		(dropsite-min-distance wood > 15)
	)
	(strategic-number sn-camp-max-distance < 60)
=>
	(set-strategic-number sn-camp-max-distance 60)
	(set-strategic-number sn-defer-dropsite-update 1)
)
(defrule
	(game-time > 2700)
	(goal ffa-game-goal -1)
	(goal increase-town-size-goal 0)
	(strategic-number sn-camp-max-distance < 90)
=>
	(set-strategic-number sn-camp-max-distance 90)
	(set-strategic-number sn-defer-dropsite-update 1)
)
#end-if
#load-if-defined GOLD-RUSH-MAP
(defrule
	(game-time > 1200)
	(up-pending-placement c: mining-camp)
	(goal threat-target-goal villager-class)
=>
	(up-reset-placement c: mining-camp)
	(chat-local-to-self "reset mining camp")
)
#end-if
(defrule
	(game-time > 1800)
	(strategic-number sn-dropsite-separation-distance != 3)
	(or
		(cc-players-unit-type-count 0 tree-class >= heavy-wood)
		(map-type oasis)
	)
=>
	(set-strategic-number sn-dropsite-separation-distance 3)
)
(defrule
	(cc-players-unit-type-count 0 tree-class < heavy-wood)	
	(or
		(game-time > 3600); 60min
		(death-match-game)
	)
	(or
		(goal ffa-game-goal -1)
		(game-time > 5400); 90 min
	)
	(game-time > 900)
	(goal increase-town-size-goal 0)
	(strategic-number sn-camp-max-distance < 144)
=>
	(set-strategic-number sn-camp-max-distance 144)
)
#load-if-not-defined DIFFICULTY-MODERATE
#load-if-not-defined DIFFICULTY-HARD
#load-if-not-defined DIFFICULTY-HARDEST
(defrule ;build additional lumber camps
	(resource-found wood)
	(unit-type-count villager >= 15)
	(building-type-count-total lumber-camp < 2)
	(building-type-count-total mill > 0)
	(can-build lumber-camp)
=>
	(build lumber-camp)
	(set-strategic-number sn-dropsite-separation-distance 3)
	(set-strategic-number sn-maximum-wood-drop-distance 30)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(dropsite-min-distance wood < 255)
	(dropsite-min-distance wood > 10); on low difficulty only rebuild really bad camps
	(timer-triggered lumber-timer)
	(can-build lumber-camp)
	(up-pending-objects c: lumber-camp < 1)
	(cc-players-unit-type-count 0 tree-class > 25)
=>
	(build lumber-camp)
	(enable-timer lumber-timer 120)
)
#end-if
#end-if
#end-if
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
(defrule
	(building-type-count town-center > 0)
	(dropsite-min-distance wood < 255)
	(dropsite-min-distance wood > 4)
	(timer-triggered lumber-timer)
	(can-build lumber-camp)
	(up-pending-objects c: lumber-camp < 1)
	(cc-players-unit-type-count 0 tree-class > 25)
=>
	(build lumber-camp)
	(enable-timer lumber-timer 120)
	(set-strategic-number sn-dropsite-separation-distance 4)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(unit-type-count villager >= 75)
	(can-build lumber-camp)
	(dropsite-min-distance wood > 1)
	(dropsite-min-distance wood < 255)
	(timer-triggered lumber-timer)
	(up-pending-objects c: lumber-camp < 1)
	(cc-players-unit-type-count 0 tree-class > 25)
=>
	(build lumber-camp)
	(enable-timer lumber-timer 120)
	(set-strategic-number sn-dropsite-separation-distance 3)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
	(set-strategic-number sn-defer-dropsite-update 1)
)
(defrule
	(or
		(unit-type-count-total 219 >= 1)
		(unit-type-count-total 228 >= 1)
	)
	(can-build lumber-camp)
	(dropsite-min-distance wood > 1)
	(dropsite-min-distance wood < 255)
	(timer-triggered lumber-timer)
	(up-pending-objects c: lumber-camp < 1)
	(cc-players-unit-type-count 0 tree-class > 25)
=>
	(build lumber-camp)
	(enable-timer lumber-timer 120)
	(set-strategic-number sn-dropsite-separation-distance 12)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
	(set-strategic-number sn-defer-dropsite-update 1)
)
(defrule
	(building-type-count-total town-center == 1)
	(or
		(goal strategy-goal flush)
		(building-type-count farm > 18)
	)
	(building-type-count farm > 15)
	(building-type-count-total mill < 2)
	(goal save-wood-goal 0)
	(building-type-count blacksmith > 0)
	(can-build mill)
=>
	(set-strategic-number sn-mill-max-distance 20)
	(set-strategic-number sn-dropsite-separation-distance 10)
	(set-strategic-number sn-preferred-mill-placement 1)
	(build mill)
)
(defrule
	(building-type-count farm > 25)
	(or
		(building-type-count farm > 37)
		(building-type-count-total town-center == 1)
	)
	(building-type-count-total mill < 3)
	(goal save-wood-goal 0)
	(can-build mill)
=>
	(set-strategic-number sn-mill-max-distance 23)
	(set-strategic-number sn-dropsite-separation-distance 10)
	(set-strategic-number sn-preferred-mill-placement 0)
	(build mill)
)
(defrule
	(resource-found wood)
	(or
		(unit-type-count-total 219 >= 1)
		(unit-type-count-total 228 >= 1)
	)
	(or
		(building-type-count-total blacksmith > 0)
		(strategic-number sn-current-age >= fc-transit)
	)
	(game-time < 1800)
	(can-build lumber-camp)
	(building-type-count-total lumber-camp < 5)
	(strategic-number sn-camp-max-distance <= 35)
	(up-pending-objects c: lumber-camp < 1)
=>
	(build lumber-camp)
	(set-strategic-number sn-camp-max-distance 35)
	(set-strategic-number sn-dropsite-separation-distance 10)
	(chat-to-player chat-player "Dead Lumberjacks found, relocate camp!")
)
#end-if
#end-if
#load-if-defined YUCATAN-MAP
(defrule
	(building-type-count town-center > 0)
	(building-type-count-total blacksmith > 0)
	(dropsite-min-distance deer-hunting > 8)
	(dropsite-min-distance deer-hunting < 38)
	(can-build mill)
	(building-type-count-total mill == 3)
=>
	(set-strategic-number sn-preferred-mill-placement 1); mill deer
	(set-strategic-number sn-dropsite-separation-distance 10)
	(set-strategic-number sn-mill-max-distance 29)
	(build mill)
)
#end-if
#load-if-not-defined DEATH-MATCH
(defrule
	(strategic-number sn-current-age == dark)
	(goal strategy-goal flush)
	(or
		(idle-farm-count > 0)
		(wood-amount < 130)
	)	
	(building-type-count-total lumber-camp > 0)
=>
	(up-jump-rule 1)
)
(defrule
	(strategic-number sn-current-age == dark)
	(or
		(unit-type-count villager > 13)	
		(wood-amount > 130)
	)	
	(or
		(unit-type-count villager < 20)
		(building-type-count-total lumber-camp > 1)
	)
	(or
		(unit-type-count villager < 30)
		(wood-amount > 130)
	)
	(building-type-count-total lumber-camp > 0)
=>
	(set-goal farming-goal 1)
)
(defrule
	(strategic-number sn-current-age == df-transit)
	(or
		(wood-amount >= 330)
		(unit-type-count villager == 30); bad dark age for some reason
	)
	(idle-farm-count < 1)
	(building-type-count-total lumber-camp > 0)
=>
	(set-goal farming-goal 1)
)
#end-if ; NOT DM
#load-if-not-defined WONDER-RACE
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-current-age == feudal)
	(or
		(and
			(building-type-count-total blacksmith > 0)
			(building-type-count-total farm < 8)
		)
		(goal strategy-goal sling)
	)
	(idle-farm-count < 2)
=>
	(set-goal farming-goal 1)
)
(defrule
	(unit-type-count villager < villager-feudal); rebuild rule
	(building-type-count farm < 10)
	(building-type-count-total dock == 0); not when water available
	(strategic-number sn-current-age >= fc-transit)
	(idle-farm-count < 1)
=>
	(set-goal farming-goal 1)
)
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-current-age == feudal)
	(or
		(or
			(building-type-count-total archery-range > 1)
			(goal unit-goal militiaman-line)
		)
		(building-type-count-total stable > 0)
	)
	(building-type-count-total archery-range > 0)
	(idle-farm-count < 1)
=>
	(set-goal farming-goal 1)
)
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-current-age == feudal)
	(or
		(building-type-count-total dock > 1)
		(and
			(building-type-count-total dock > 0)
			(goal enemy-boats-goal 0)
		)
	)
	(building-type-count-total blacksmith > 0)
	(idle-farm-count < 1)
=>
	(set-goal farming-goal 1)
)
(defrule
	(goal strategy-goal rush)
	(strategic-number sn-current-age == fc-transit)
	(building-type-count-total archery-range > 1)
	(idle-farm-count < 2)
	(wood-amount > 120)
=>
	(set-goal farming-goal 1)
)
(defrule
	(strategic-number sn-current-age == fc-transit)
	(up-research-status c: ri-horse-collar >= research-pending)
	(or
		(or
			(building-type-count-total archery-range > 2)
			(building-type-count-total stable > 1)
		)
		(and
			(goal unit-goal eagle-warrior)
			(building-type-count-total barracks > 0)
		)
	)
	(idle-farm-count < 2)
=>
	(set-goal farming-goal 1)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(up-compare-goal strategy-goal != sling)
	(up-compare-goal strategy-goal != flush)
	(building-type-count-total blacksmith > 0)
	(idle-farm-count < 1)
	(or
		(wood-amount > 120)
		(food-amount < 750)
	)
=>
	(set-goal farming-goal 1)
)
(defrule
	(strategic-number sn-current-age == fc-transit)
	(building-type-count-total blacksmith > 0)
	(or
		(goal unit-goal skirmisher)
		(and
			(goal strategy-goal boom)
			(up-research-status c: ri-horse-collar > research-available)
		)
	)
	(building-type-count-total farm < 14)
=>
	(set-goal farming-goal 1)
)
(defrule
	(building-type-count town-center >= 1)
	(strategic-number sn-current-age >= castle)
	(or
		(idle-farm-count < 2)
		(or
			(building-type-count-total town-center >= tc-level-three)
			(and
				(building-type-count-total town-center >= tc-level-two)
				(or
					(wood-amount > 335); 275 for 3rd TC + 60
					(goal strategy-goal boom); build as many farms as possible after 2nd TC
				)
			)
		)
	)
=>
	(set-goal farming-goal 1)
)
#end-if ;no WONDER-RACE
(defrule
	(strategic-number sn-current-age == castle)
	(goal strategy-goal fast-imp)
	(not (research-available imperial-age))
	(or
		(wood-amount < 260)
		(goal save-wood-goal 1)
	)
	(food-amount > 400)
	(gold-amount > 400)
=>
	(set-goal farming-goal 0)
)
#load-if-not-defined TURKISH-CIV
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal gunpowder-class)
	(building-type-count-total university == 0)
	(not (research-completed ri-chemistry))
	(wood-amount < 200)
=>
	(set-goal farming-goal 0)
)
#end-if
(defrule
	(building-type-count-total town-center == 1)
	(goal strategy-goal flush)
	(wood-amount < 130)
	(building-type-count-total farm > 17)
	(building-type-count-total mill < 2)
	(goal farming-goal 1)
=>
	(set-goal farming-goal 0)
)
(defrule
	(or
		(goal strategy-goal flush)
		(food-amount < 400)
	)
	(strategic-number sn-current-age == feudal)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(idle-farm-count < 1)
	(wood-amount > 150)
=>
	(set-goal strategy-goal flush)
	(set-goal farming-goal 1)
)
(defrule
	(current-age == castle-age)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(wood-amount < 335)
	(food-amount > 400)
	(unit-type-count fishing-ship > 3)
	(current-age-time < 600)
=>
	(set-goal farming-goal 0)
)
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(game-time < 480)
	(or
		(death-match-game)
		(game-time < 300)
	)
=>
	(set-goal farming-goal 0)
)
#end-if
#load-if-not-defined UP-GROUPED-BY-TEAM
#load-if-not-defined DIFFICULTY-EASIEST; no boar hunting so farming is required
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined DIFFICULTY-MODERATE
#load-if-not-defined SALT-MARSH-MAP ; has many berries but all at the same spot
(defrule
	(game-time > 170)
	(game-time < 180)
	(up-gaia-type-count c: forage-bush > 8)
=>
	(set-strategic-number sn-focus-player-number 0)
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 18)
	(disable-self)
)
(defrule
	(game-time > 170)
	(game-time < 180)
	(up-gaia-type-count c: forage-bush > 8)
	(up-find-remote c: forage-bush c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: 8 c: 24)
	(up-find-remote c: forage-bush c: 240)	
	(up-get-search-state local-total-goal)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(disable-self)
)
(defrule
	(game-time > 170)
	(game-time < 180)
	(up-gaia-type-count c: forage-bush > 8)
	(up-compare-goal remote-total-goal > 3)
=>
	(set-goal dark-build-order-goal 2)
	(chat-local-to-self "many berries found")
	(disable-self)
)
(defrule
	(strategic-number sn-current-age == dark)
	(goal dark-build-order-goal 2)
	(can-build mill)
	(building-type-count-total mill >= 1)
	(building-type-count-total lumber-camp >= 1)
	(nand
		(building-type-count-total dock == 0)
		(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	)
=>
	(set-strategic-number sn-preferred-mill-placement 0); mill 2nd berries
	(build mill)
	(disable-self)
)
(defrule
	(current-age >= feudal-age)
	(goal dark-build-order-goal 2)
	(goal farming-goal 1)
	(or
		(goal strategy-goal flush)
		(goal strategy-goal sling)
	)
	(building-type-count lumber-camp > 1)
	(up-research-status c: ri-horse-collar >= research-pending)
	(building-type-count blacksmith > 0)
	(idle-farm-count == 0)
=>
	(set-goal dark-build-order-goal -1)
)
(defrule
	(current-age == dark-age)
	(goal dark-build-order-goal 2)
	(goal farming-goal 1)
	(building-type-count lumber-camp > 1)
	(or
		(and
			(building-type-count mill > 1)
			(building-type-count-total farm < 4)
		)
		(and
			(wood-amount > 200)
			(building-type-count-total mill == 1)
		)
	)
=>
	(up-jump-rule 1)
)
(defrule
	(or
		(and
			(and
				(research-completed ri-horse-collar)
				(or
					(idle-farm-count > 0)
					(food-amount > 125)
				)
			)
			(up-research-status c: ri-heavy-plow < research-pending)
		)
		(up-research-status c: ri-horse-collar < research-pending)
	)
	(goal dark-build-order-goal 2)
	(goal farming-goal 1)
	(unit-type-count villager-food > 10)
=>
	(set-goal farming-goal 0)
)
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-current-age == dark)
	(or
		(or
			(goal dark-build-order-goal 2); map with more than standard berry amount, e.g. yucatan
			(and
				(building-type-count-total farm > 5)
				(idle-farm-count > 0)
			)
		)
		(unit-type-count-total villager > 22)
	)
	(wood-amount < 120); sometimes there was still no hunting
=>
	(set-goal farming-goal 0)
)
(defrule
	(or
		(and
			(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
			(and
				(current-age == dark-age)
				(wood-amount < 210); place farms only if still enough wood left for dock
			)
		)
		(wood-amount < farm-wood)
	)
	(goal farming-goal 1)
=>
	(set-goal farming-goal 0)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(strategic-number sn-current-age == dark)
	(wood-amount > 250); high wood because dock was delayed for long
	(goal farming-goal 1)
=>
	(set-goal farming-goal 2); allow placing many farms, probably failed dock placement
)
(defrule
	(strategic-number sn-current-age == df-transit)
	(unit-type-count villager > dark-age-villager)
	(building-type-count-total farm < 12)
	(building-type-count-total mining-camp > 0)
	(or
		(goal strategy-goal rush)
		(goal strategy-goal boom)
	)
	(can-build farm)
=>
	(build farm); build one additional farm, then rax
	(disable-self)
)
(defrule
	(strategic-number sn-current-age == df-transit)
	(unit-type-count villager >= 30)
	(unit-type-count villager > dark-age-villager)
	(building-type-count-total farm < 12)
	(or
		(building-type-count-total barracks > 0)
		(goal unit-goal monk)
	)
	(or
		(goal strategy-goal rush)
		(goal strategy-goal boom)
	)
	(idle-farm-count == 0)
	(food-amount < 600)
=>
	(set-goal farming-goal 1)
)
(defrule
	(goal farming-goal 1)
=>
	(up-get-fact unit-type-count villager temp-goal)
	(up-modify-goal temp-goal s:* sn-food-gatherer-percentage)
	(up-modify-goal temp-goal c:/ 100)
	(up-get-fact building-type-count farm temp-goal2)
)
(defrule
	(goal farming-goal 1)
	(up-compare-goal temp-goal2 g:> temp-goal)
	(nand
		(and
			(unit-type-count male-gold-miner == 0)
			(unit-type-count female-gold-miner == 0)
		)
		(idle-farm-count < 3)
	)	
=>
	(set-goal farming-goal 0)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-current-age == castle)
	(goal farming-goal 0)
	(building-type-count-total town-center >= tc-level-five)
	(building-type-count-total farm < 40); build some farms anyways we'll need them later more than some early monastery or so
	(current-age-time < 480)
	(research-completed ri-heavy-plow)
=>
	(set-goal farming-goal 2)
)
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-five)
	(food-amount > imp-f2)
	(gold-amount > imp-g2)
	(building-type-count-total siege-workshop == 0)
=>
	(set-goal farming-goal 0)
)
#end-if
(defrule
	(goal farming-goal 1)
	(or
		(and
			(building-type-count-total monastery > 0)
			(building-type-count-total town-center >= tc-level-three)
		)
		(and
			(wood-amount > 120)
			(building-type-count-total stable > 1)
		)
	)
=>
	(set-goal farming-goal 2)
)
(defrule
	(or
		(strategic-number sn-current-age == dark)
		(and
			(and
				(strategic-number sn-current-age == df-transit)
				(wood-amount >= 385);175stable+150bs+60farm
			)
			(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
		)
	)
	(wood-amount >= 130)
	(or
		(and
			(unit-type-count villager > 20)
			(building-type-count-total lumber-camp > 1)
		)
		(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed); fix if no docks could be placed
	)
	(can-build farm)
	(goal farming-goal 1)
=>
	(set-goal farming-goal 2)
)
(defrule
	(wood-amount > 120)
	(building-type-count-total blacksmith > 0)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(current-age-time > 120); don't spend all wood into farms at the start of the age
	(can-build farm)
	(goal farming-goal 1)
=>
	(set-goal farming-goal 2)
)
#load-if-not-defined WONDER-RACE
#load-if-not-defined POPULATION-CAP-25
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(goal save-wood-goal 0)
	(goal enemy-boats-goal 1)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total dock > 0)
	(building-type-count-total dock < 5)
	(or
		(building-type-count-total dock < 2)
		(civilian-population > 60)
	)
	(or
		(building-type-count-total dock < 4)
		(civilian-population > 80)
	)
	(can-build dock)
=>
	(build dock)
)
(defrule
	(goal save-wood-goal 0)
	(goal strategy-goal flush)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(building-type-count-total lumber-camp > 0); fish boom instead of farming is better with UP
	(or
		(building-type-count-total mill > 0)
		(goal dark-build-order-goal 1)
	)
	(building-type-count-total dock < 2)
	(or
		(building-type-count-total dock < 1)
		(goal enemy-boats-goal 1)
	)
	(can-build dock)
=>
	(build dock)
)
(defrule
	(goal save-wood-goal 0)
	(strategic-number sn-current-age == dark)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(building-type-count-total lumber-camp > 0); fish boom instead of farming is better with UP
	(building-type-count-total dock < 1)
	(can-build dock)
=>
	(build dock)
)
(defrule
	(strategic-number sn-current-age < fc-transit)
	(strategic-number sn-current-age >= df-transit)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(goal save-wood-goal 0)
	(goal enemy-boats-goal 1)
	(wood-amount > 400)
	(food-amount < 550); less food to even place a blacksmith, mainly on poor maps with bugged fishing near mill.
	(can-build dock)
	(building-type-count-total dock < 2)
=>
	(build dock)
)
(defrule
	(or
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
		(goal enemy-boats-goal 1)
	)
	(goal save-wood-goal 0)
	(building-type-count-total dock < 3)
	(building-type-count-total town-center >= tc-level-three)
	(can-build dock)
=>
	(build dock)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(building-type-count-total blacksmith > 0)
	(or
		(building-type-count-total dock < 2)
		(or
			(up-research-status c: ri-bow-saw >= research-pending)
			(unit-type-count villager >= 37)
		)
	)
	(or
		(building-type-count-total dock < 3)
		(building-type-count-total town-center >= tc-level-two)
	)
	(building-type-count-total dock < 4)	
	(can-build dock)
=>
	(build dock)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(building-type-count-total dock < 8)
	(or
		(civilian-population > 80)
		(death-match-game)
	)
	(can-build dock)
=>
	(build dock)
)
(defrule	
	(or
		(goal unit-goal knight)
		(goal unit-goal skirmisher)
	)
=>
	(up-jump-rule 1)
)
(defrule
	(game-time < 1800)
	(current-age == castle-age)
	(building-type-count monastery == 1)
	(strategic-number sn-resource-control <= 2)
	(unit-type-count-total monk == 0)
	(unit-type-count-total 134 == 0); dead monk
	(unit-type-count-total 286 == 0); monk with relic
	(building-type-count-total market == 0)
	(cc-players-unit-type-count 0 relic > 0)
	(gold-amount < 100)
=>
	(set-strategic-number sn-resource-control monk)
;	(chat-to-player chat-player "save-resources = monk")
)
#load-if-defined UP-1-PLAYER-TEAM
(defrule
	(game-time < 1800)
	(building-type-count monastery == 1)
	(strategic-number sn-resource-control <= 2)
	(unit-type-count-total monk == 0)
	(unit-type-count-total 134 == 0); dead monk
	(goal unit-goal knight)
	(or
		(strategic-number sn-cavalry-threat > 0); still get a monk if enemy is krushing, as they're good vs cavalry
		(goal enemy-goal skirmisher); enemy spam trash so it's not likely we're going to attack quick
	)
	(cc-players-unit-type-count 0 relic > 0)
	(gold-amount < 100)
=>
	(set-strategic-number sn-resource-control monk)
;	(chat-to-player chat-player "save-resources = monk")
)
#end-if
(defrule
	(strategic-number sn-resource-control == monk)
	(or
		(and
			(unit-type-count female-gold-miner == 0)
			(unit-type-count male-gold-miner == 0)
		)
		(and
			(or
				(town-under-attack)
				(strategic-number sn-military-level < 0)
			)
			(strategic-number sn-military-level < 1)
		)
	)
=>
	(set-strategic-number sn-resource-control 0)
)

; ********** RESEARCH **********

(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-chain-barding)
	)
	(or
		(or
			(unit-type-count cavalry-class > mid-number-upgrade)
			(goal control-goal shot)
		)
		(and
			(unit-type-count cavalry-class > min-number-upgrade)
			(goal unit-goal knight)
		)
	)
	(can-research-with-escrow ri-chain-barding)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-chain-barding)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(goal nr-map-goal -1)
	(or
		(goal unit-goal archer)
		(goal unit-goal cavalry-archer)
	)
	(goal strategy-goal rush)
	(up-research-status c: ri-bodkin-arrow < research-pending)
=>	
	(up-jump-rule 4)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(goal unit-goal my-unique-unit-line)
	(goal nr-map-goal -1)
	(or
		(or
			(civ-selected gothic); uu that cost food
			(civ-selected saracen); we need some uu early
		)
		(or
			(civ-selected turkish)
			(civ-selected spanish)
		)
	)
	(current-age-time < 600)
=>
	(up-jump-rule 3)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(can-research ri-bow-saw)
	(goal train-civ-goal 1)
	(or
		(or
			(current-age-time > 120)
			(or
				(gold-amount < 30)
				(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
			)
		)
		(or
			(goal unit-goal my-unique-unit-line)
			(goal unit-goal skirmisher)
		)
	)
	(up-compare-goal strategy-goal != fast-imp)
=>
	(research ri-bow-saw)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(can-research ri-bow-saw)
	(goal train-civ-goal 1)
	(or
		(and
			(goal strategy-goal fast-imp)
			(or
				(strategic-number sn-current-age >= ci-transit)
				(unit-type-count villager > 45)
			)
		)
		(or
			(goal strategy-goal boom)
			(goal strategy-goal flush)
		)
	)
=>
	(research ri-bow-saw)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal train-civ-goal 1)
	(current-age-time > 120)
	(nand
		(goal unit-goal default-ranged)
		(research-available ri-bodkin-arrow)
	)
	(can-research ri-heavy-plow)
=>
	(research ri-heavy-plow)
)
(defrule
	(nand
		(research-available ri-war-galley)
		(civ-selected viking)
	)
	(warboat-count > 12)
	(goal enemy-boats-goal 1)
	(or
		(strategic-number sn-resource-control < 2)
		(players-unit-type-count any-enemy galley-line > 0)
	)
	(can-research ri-careening)
=>
	(research ri-careening)
)
(defrule
	(or
		(research-completed ri-bow-saw)
		(or
			(civ-selected viking)
			(unit-type-count galley-line > 4)
		)
	)
	(can-research-with-escrow ri-war-galley)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-war-galley)
)
(defrule
	(can-research-with-escrow ri-fast-fire-ship)
=>
	(release-escrow wood)(release-escrow gold)
	(set-escrow-percentage wood 0)(set-escrow-percentage gold 0)
	(research ri-fast-fire-ship)
)
(defrule
	(can-research-with-escrow ri-galleon)
=>
	(release-escrow wood)(release-escrow food)
	(set-escrow-percentage wood 0)(set-escrow-percentage food 0)
	(research ri-galleon)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(research-available ri-galleon)
	(building-type-count dock > 0)
=>
	(set-escrow-percentage wood 20)(set-escrow-percentage food 20)
	(set-goal escrow-purpose-goal navy)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(research-available ri-fast-fire-ship)
	(building-type-count dock > 0)
=>
	(set-escrow-percentage wood 20)(set-escrow-percentage gold 20)
	(set-goal escrow-purpose-goal navy)
)
(defrule
	(strategic-number sn-resource-control < 2)
	(nor
		(research-available ri-fast-fire-ship)
		(research-available ri-galleon)
	)
	(research-available ri-cannon-galleon)
	(unit-type-count warship-class > 30)
	(strategic-number sn-military-level >= 0)
	(players-unit-type-count every-enemy galley-line < 20)
	(players-unit-type-count every-enemy fire-ship-line < 20)
	(players-building-type-count any-enemy castle > 0)
	(building-type-count dock > 0)
=>
	(set-escrow-percentage wood 20)(set-escrow-percentage food 20)
	(set-goal escrow-purpose-goal navy)
	(set-strategic-number sn-resource-control navy)
	(up-jump-rule 1)
)
(defrule
	(goal escrow-purpose-goal navy)
	(or
		(nor
			(research-available ri-fast-fire-ship)
			(research-available ri-galleon)
		)
		(building-type-count dock == 0)
	)
=>
	(set-goal escrow-purpose-goal 0)
	(release-escrow wood)(release-escrow food)(release-escrow gold)
	(set-escrow-percentage wood 0)(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
)
(defrule
	(nor 
		(research-available ri-galleon)
		(research-available ri-fast-fire-ship)
	)
	(can-research-with-escrow ri-cannon-galleon)
=>
	(release-escrow food)(release-escrow wood)
	(research ri-cannon-galleon)
)
(defrule
	(nor 
		(research-available ri-galleon)
		(research-available ri-fast-fire-ship)
	)
	(or
		(can-research-with-escrow ri-shipwright)
		(or
			(can-research-with-escrow ri-dry-dock)
			(can-research-with-escrow ri-deck-guns)
		)
	)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-shipwright)
	(research ri-dry-dock)
	(research ri-deck-guns)
)
(defrule
	(strategic-number sn-resource-control <= 1)
	(building-type-count-total town-center > 0)
	(or
		(and
			(goal strategy-goal sling)
			(game-time > 960)
		)
		(or 
			(strategic-number sn-current-age >= fc-transit)
			(goal strategy-goal flush)
		)
	)
	(nand
		(goal unit-goal my-unique-unit-line)
		(research-available ri-stone-mining)
	)
	(can-research ri-double-bit-axe)
=>
	(research ri-double-bit-axe)
)
(defrule
	(strategic-number sn-resource-control <= 1)
	(building-type-count-total town-center > 0)
	(strategic-number sn-current-age >= fc-transit)
	(nand
		(goal unit-goal my-unique-unit-line)
		(research-available ri-stone-mining)
	)
	(or
		(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
		(research-completed ri-bow-saw)
	)
	(can-research ri-horse-collar)
=>
	(research ri-horse-collar)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(building-type-count-total town-center > 0)
	(or
		(and
			(goal strategy-goal flush)
			(building-type-count blacksmith > 0)
			)
		(and
			(goal strategy-goal sling)
			(game-time > 1200)
		)
	)
	(building-type-count-total mining-camp > 0)
	(can-research ri-horse-collar)
=>
	(research ri-horse-collar)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(civilian-population >= 42)
	(goal strategy-goal flush)
	(food-amount > 600)
	(gold-amount < 75)
	(research-completed ri-wheel-barrow)
	(can-research ri-gold-mining)
=>
	(research ri-gold-mining)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-current-age >= fc-transit)
	(or
		(goal unit-goal eagle-warrior)
		(goal unit-goal monk)
	)
	(up-compare-goal strategy-goal != boom)
	(can-research ri-gold-mining)
=>
	(research ri-gold-mining)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(strategic-number sn-gold-gatherer-percentage >= 20)
		(research-completed ri-wheel-barrow)
	)
	(or
		(civilian-population >= 50)
		(strategic-number sn-gold-gatherer-percentage >= 30)
	)
	(strategic-number sn-current-age >= fc-transit)
	(can-research ri-gold-mining)
=>
	(research ri-gold-mining)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(or
			(or
				(unit-type-count cavalry-class > min-number-upgrade)
				(unit-type-count cavalry-archer-class > min-number-upgrade)
			)
			(strategic-number sn-current-age >= ci-transit)
		)
		(or
			(unit-type-count scout-cavalry > mid-number-upgrade);scouts do not count as cavalry-class(!)
			(unit-type-count conquistador-line > mid-number-upgrade)
		)
	)
	(can-research ri-bloodlines)
=>
	(research ri-bloodlines)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count infantry-class > mid-number-upgrade)
		(and
			(goal unit-goal eagle-warrior)
			(building-type-count-total barracks > 0); don't research too early on islands
		)
	)
	(or
		(and
			(strategic-number sn-current-age >= fc-transit)
			(research-completed ri-gold-mining)
		)
		(or
			(goal hostilities-goal 1)
			(death-match-game)
		)
	)
	(can-research ri-scale-mail)
=>
	(research ri-scale-mail)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(or
			(unit-type-count cavalry-class > min-number-upgrade)
			(unit-type-count scout-cavalry > mid-number-upgrade)
		)
		(and
			(building-type-count-total stable > 1)
			(food-amount > 230)
		)
	)
	(strategic-number sn-current-age >= fc-transit)
	(can-research ri-scale-barding)
=>
	(research ri-scale-barding)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count scout-cavalry > min-number-upgrade)
	(strategic-number sn-archer-threat > 1)
	(can-research ri-scale-barding)
=>
	(research ri-scale-barding)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-archer-threat < 2)
	(unit-type-count trade-cart > 0)
=>
	(up-jump-rule 2)
)
#end-if
(defrule
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-archer-threat < 4)
	(or
		(goal strategy-goal fast-imp)
		(and
			(goal position-goal pocket)
			(unit-type-count trade-cart < 20)
		)
	)
	(or
		(goal unit-goal knight)
		(goal ranged-unit-type-goal scorpion)
	)
=>
	(up-jump-rule 1)
)
(defrule
	(or
		(and
			(strategic-number sn-resource-control <= 2)
			(or
				(or 
					(and
						(building-type-count-total siege-workshop > 0)
						(nand
							(goal enemy-goal eagle-warrior)
							(strategic-number sn-archer-threat == 0)
						)
					)
					(strategic-number sn-archer-threat >= 1)
				)
				(unit-type-count-total skirmisher-line > mid-number-upgrade)
			)
		)
		(strategic-number sn-resource-control == ri-elite-skirmisher)
	)
	(can-research ri-elite-skirmisher)
=>
	(research ri-elite-skirmisher)
)
(defrule
	(or
		(or
			(unit-type-count eagle-warrior-line > 11)
			(and
				(not (civ-selected aztec))
				(current-age-time >= 300)
			)
		)
		(goal unit-goal eagle-warrior)
	)
	(can-research-with-escrow ri-elite-eagle-warrior)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(research ri-elite-eagle-warrior)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count monk > 4)
		(and
			(goal enemy-goal flush)
			(goal ranged-unit-type-goal monk)
		)
	)
=>
	(research ri-sanctity)
	(research ri-theocracy)
	(research ri-illumination)
	(research ri-block-printing)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(up-research-status c: ri-sanctity >= research-pending)
	(unit-type-count monk > 4)
	(or
		(civ-selected aztec); bugged, movement speed bonus only counts for monks with relics in 1.0c
		(goal aofe-game-goal 1); bug is fixed in AoFE
	)
	(can-research ri-fervor)
=>
	(research ri-fervor)
)
#load-if-defined AZTEC-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(up-research-status c: ri-sanctity >= research-pending)
	(unit-type-count monk > 4)
	(can-research ri-fervor)
=>
	(research ri-fervor)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(building-type-count-total monastery > 2)
	(unit-type-count monk > 19)
	(current-age == imperial-age)
	(population >= max-pop)
	(can-research ri-herbal-medicine)
=>
	(research ri-herbal-medicine)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(goal ranged-unit-type-goal monk); redemption will lure monks into death so avoid it unless enemy tries to counter monks with siege
	(or
		(players-unit-type-count any-enemy scorpion-line > 2)
		(or
			(players-unit-type-count any-enemy bombard-cannon > 0)
			(players-unit-type-count any-enemy mangonel-line > 0)
		)
	)
	(nand
		(current-age == castle-age)
		(goal strategy-goal boom)
	)
	(can-research ri-redemption)
=>
	(research ri-redemption)
)
(defrule
	(or
		(strategic-number sn-resource-control < 1)
		(strategic-number sn-resource-control == ri-heresy)
	)
	(or
		(and
			(and
				(unit-type-count monk > 12)
				(strategic-number sn-current-age >= ci-transit)
			)
			(unit-type-count monk > 5)
		)
		(and
			(players-unit-type-count any-enemy monk > 3)
			(goal ranged-unit-type-goal monk)
		)
	)
	(can-research ri-atonement)
=>
	(research ri-atonement)
)
#load-if-defined CELTIC-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(can-research-with-escrow ri-heavy-scorpion)
		(can-research-with-escrow my-unique-research)
	)
=>
	(release-escrow wood)(release-escrow food)(release-escrow gold)
	(research ri-heavy-scorpion)
	(research my-unique-research)
)
#else
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count scorpion-line > min-number-upgrade)
		(goal ranged-unit-type-goal scorpion)
	)
	(can-research ri-heavy-scorpion)
=>
	(research ri-heavy-scorpion)
)
(defrule
	(or
		(civ-selected japanese)
		(civ-selected viking)
	)
	(strategic-number sn-resource-control < 1)
	(or
		(players-unit-type-count any-enemy skirmisher-line > 20)
		(strategic-number sn-archer-threat > 2)
	)
	(unit-type-count trade-cart > 5)
	(can-research ri-heavy-scorpion)
=>
	(research ri-heavy-scorpion)
)
#end-if
(defrule
	(or
		(unit-type-count-total my-unique-unit-line >= high-number-upgrade)
		(goal uu-up-goal 1)
	)
	(can-research-with-escrow my-unique-unit-upgrade)
=>
	(release-escrow wood)(release-escrow food)(release-escrow gold)
	(research my-unique-unit-upgrade)
	(set-goal uu-up-goal 0)
)
(defrule
	(population >= del-civ-pop)
	(current-age-time > 300)
	(game-time > 1800); not in DM
	(strategic-number sn-resource-control < 1)
	(or (or (civ-selected saracen)(civ-selected turkish))(civ-selected japanese))
	(can-research-with-escrow my-unique-unit-upgrade)
=>
	(release-escrow food)(release-escrow gold)
	(research my-unique-unit-upgrade)
	(set-goal uu-up-goal 0)
)
#load-if-defined BYZANTINE-CIV
(defrule
	(population >= del-civ-pop)
	(current-age-time > 300)
	(gold-amount > 2000)
	(can-research-with-escrow my-unique-research)
=>
	(release-escrow food)(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined TURKISH-CIV
(defrule
	(population >= del-civ-pop)
	(unit-type-count bombard-tower > 4)
	(can-research-with-escrow my-unique-research)
=>
	(release-escrow gold)
	(research my-unique-research)
)
#end-if
#load-if-defined GOTHIC-CIV
(defrule
	(strategic-number sn-resource-control != ri-long-swordsman)
	(goal unit-goal knight)
	(building-type-count town-center < tc-level-three)
	(goal control-goal militiaman-line)
=>
	(up-jump-rule 4)
)
#end-if
(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-long-swordsman)
	)
	(or
		(goal unit-goal militiaman-line)
		(goal control-goal militiaman-line)
	)
	(or
		(goal strategy-goal flush)
		(strategic-number sn-current-age >= fc-transit)
	)
	(can-research ri-man-at-arms)
=>
	(research ri-man-at-arms)
)
(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-long-swordsman)
	)
	(or
		(goal unit-goal militiaman-line)
		(goal control-goal militiaman-line)
	)
	(can-research ri-long-swordsman)
=>
	(research ri-long-swordsman)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(goal unit-goal militiaman-line)
		(goal control-goal militiaman-line)
	)
	(can-research ri-two-handed-swordsman)
=>
	(research ri-two-handed-swordsman)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(goal unit-goal militiaman-line)
		(goal control-goal militiaman-line)
	)
	(can-research ri-champion)
=>
	(research ri-champion)
)
(defrule
	(or
		(and
			(strategic-number sn-resource-control <= 2)
			(or
				(goal strategy-goal flush)
				(current-age-time > 240)
			)
		)
		(strategic-number sn-resource-control == ri-crossbow)
	)
	(or
		(or
			(goal ranged-unit-type-goal archer)
			(strategic-number sn-resource-control == ri-crossbow)
		)
		(and
			(unit-type-count archer > 10)
			(goal hostilities-goal 1)
		)
	)
	(can-research ri-crossbow)
=>
	(research ri-crossbow)
)
(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-arbalest)
	)
	(or
		(goal ranged-unit-type-goal archer)
		(and
			(unit-type-count archer-line > 15)
			(goal hostilities-goal 1)
		)
	)
	(can-research ri-arbalest)
=>
	(research ri-arbalest)
)
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control <= 2)	
	(or
		(unit-type-count knight-line > 14)
		(goal unit-goal knight)
	)
	(can-research ri-cavalier)
=>
	(research ri-cavalier)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(research-completed my-unique-unit-upgrade)
		(research-completed ri-heavy-cavalry-archer)
	)
	(or
		(civ-selected hun)
		(or
			(civ-selected spanish)
			(civ-selected frankish)
		)
	)
	(gold-amount > 1000)
	(goal anti-cavalry-threat-goal 0)
	(can-research ri-cavalier)
=>
	(research ri-cavalier)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed my-unique-unit-upgrade)
	(up-allied-goal every-ally unit-goal != knight)
	(wood-amount < 100)
	(food-amount > 600)
	(gold-amount > 600)
	(goal anti-cavalry-threat-goal 0)
	(or
		(civ-selected chinese)
		(civ-selected mongol)
	)
	(can-research ri-cavalier)
=>
	(research ri-cavalier)
)
#else
#load-if-not-defined CHINESE-CIV
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined TURKISH-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal unit-goal knight)
	(or 
		(research-completed ri-bloodlines)
		(civ-selected frankish)
	)
	(nand
		(up-compare-goal unit-goal != knight)
		(civ-selected persian)
	)
	(can-research ri-cavalier)
=>
	(research ri-cavalier)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-resource-control <= 2)
	(or 
		(research-completed ri-bloodlines)
		(civ-selected frankish)
	)
	(can-research-with-escrow ri-paladin)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-paladin)
)
#load-if-defined CELTIC-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(and
			(goal anti-cavalry-threat-goal 0)
			(gold-amount > 2000)
		)
		(goal unit-goal knight)
	)
	(can-research ri-paladin)
=>
	(research ri-paladin)
)
#end-if
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count ranged-unit-class > mid-number-upgrade)
		(unit-type-count cavalry-archer-class > mid-number-upgrade)
	)
	(or
		(or
			(gold-amount < 200)
			(strategic-number sn-current-age >= ci-transit)
		)
		(goal hostilities-goal 1)
	)
	(can-research-with-escrow ri-thumb-ring)
=>
	(release-escrow wood)(release-escrow food)
	(research ri-thumb-ring)
)
#load-if-not-defined UP-MICHI-STYLE
#load-if-not-defined GOTHIC-CIV
#load-if-defined UP-POCKET-POSITION
#load-if-defined PERSIAN-CIV
(defrule
	(or
		(goal unit-goal my-unique-unit-line)
		(goal unit-goal scorpion)
	)
	(goal monk-threat-goal 0)
	(game-time < 2700)
	(not (research-completed my-unique-unit-upgrade))
=>
	(up-jump-rule 3)
)
#end-if
(defrule
	(goal nr-map-goal 1)
	(goal unit-goal knight)
	(goal monk-threat-goal 0)
	(game-time < 2400)
	(not (research-completed ri-paladin))
=>
	(up-jump-rule 2)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 2)
	(up-research-status c: ri-bloodlines >= research-available)
	(or
		(and
			(and
				(food-amount > 400)
				(current-age-time > 600)
			)
			(or 
				(gold-amount < 100)
				(strategic-number sn-current-age >= ci-transit)
			)
		)
		(unit-type-count scout-cavalry > 6)
	)
	(can-research ri-light-cavalry)
=>
	(research ri-light-cavalry)
)
#end-if
(defrule
	(or
		(goal monk-threat-goal 1)
		(and
			(strategic-number sn-resource-control < 2)
			(and
				(and
					(food-amount > 400)
					(gold-amount < 100)
				)
				(and
					(or
						(civ-selected frankish)
						(civ-selected briton)
					)
					(current-age-time > 300)
				)
			)
		)
	)
	(can-research ri-light-cavalry)
=>
	(research ri-light-cavalry)
)
(defrule
	(game-time < 1500)
	(or
		(players-building-type-count every-enemy monastery > 1)
		(and
			(players-building-type-count every-enemy castle > 0)
			(players-unit-type-count every-enemy monk > 3)
		)
	)
	(can-research ri-light-cavalry)
=>
	(research ri-light-cavalry)
)
#end-if
(defrule
	(goal monk-threat-goal 0)
	(unit-type-count-total trade-cart > 1)
	(goal unit-goal knight)
	(or
		(or
			(civ-selected hun)
			(civ-selected spanish)
		)
		(or
			(civ-selected magyar)
			(civ-selected persian)
		)
	)
	(up-research-status c: ri-paladin < research-pending)
=>
	(up-jump-rule 1)
)
(defrule
	(current-age-time > 10)
	(strategic-number sn-resource-control <= 2)
	(up-compare-goal escrow-purpose-goal != ri-halberdier)
	(can-research ri-hussar)
=>
	(research ri-hussar)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(goal unit-goal cavalry-archer)
		(and
			(civ-selected hun)
			(unit-type-count trade-cart > 5)
		)
	)
	(unit-type-count cavalry-archer-line >= mid-number-upgrade)
	(can-research ri-heavy-cavalry-archer)
=>
	(research ri-heavy-cavalry-archer)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(or
			(research-completed ri-crossbow)
			(unit-type-count cavalry-archer-class > mid-number-upgrade)
		)
		(or
			(or
				(unit-type-count ranged-unit-class > mid-number-upgrade)
				(goal strategy-goal flush)
			)
			(or
				(unit-type-count galley-line > 4)
				(research-completed ri-chemistry)
			)
		)
	)
	(can-research ri-fletching)
=>
	(research ri-fletching)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-fletching)
	)
	(strategic-number sn-current-age >= fc-transit)
	(or
		(or 
			(goal unit-goal cavalry-archer)
			(goal unit-goal archer)
		)
		(goal unit-goal skirmisher)
	)
	(can-research ri-fletching)
=>
	(research ri-fletching)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-fletching)
	)
	(current-age >= castle-age)
	(building-type-count castle > 0)
	(or
		(unit-type-count ranged-unit-class > 0)
		(unit-type-count cavalry-archer-class > 0)
	)
	(can-research ri-fletching)
=>
	(research ri-fletching)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(or
			(unit-type-count ranged-unit-class > mid-number-upgrade)
			(unit-type-count cavalry-archer-class > mid-number-upgrade)
		)
		(or
			(unit-type-count galley-line > 0)
			(and
				(goal unit-goal skirmisher)
				(current-age-time > 60)
			)
		)
	)
	(can-research ri-bodkin-arrow)
=>
	(research ri-bodkin-arrow)
)
(defrule
	(or 
		(and
			(goal strategy-goal rush)
			(or
				(goal unit-goal cavalry-archer)
				(goal unit-goal archer)
			)
		)
		(or
			(and
				(building-type-count castle > 1)
				(population >= max-civ-pop)
			)
			(strategic-number sn-resource-control == ri-bodkin-arrow)
		)
	)
	(can-research ri-bodkin-arrow)
=>
	(research ri-bodkin-arrow)
)
(defrule
	(or
		(and
			(unit-type-count infantry-class > mid-number-upgrade)
			(strategic-number sn-resource-control <= 2)
		)
		(goal train-civ-goal ri-chain-mail)
	)
	(can-research-with-escrow ri-chain-mail)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-chain-mail)
)

(defrule
	(strategic-number sn-resource-control <= 2)
	(current-age-time > 60)
	(can-research ri-conscription)
=>
	(research ri-conscription)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(population > sixty-percent-pop)
	(unit-type-count cavalry-class > high-number-upgrade)
	(research-available ri-plate-barding)
=>
	(set-escrow-percentage food 20)(set-escrow-percentage gold 20)
	(set-goal escrow-purpose-goal blacksmith)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count cavalry-class > mid-number-upgrade)
	(can-research-with-escrow ri-plate-barding)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(set-goal escrow-purpose-goal 0)
	(research ri-plate-barding)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(population > sixty-percent-pop)
	(not (can-research-with-escrow ri-bracer))
	(research-available ri-bracer)
=>
	(set-escrow-percentage food 20)(set-escrow-percentage gold 20)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(can-research-with-escrow ri-bracer)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(research ri-bracer)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count infantry-class < min-number-upgrade)
		(research-completed ri-scale-mail)
	)
	(research-completed ri-chain-barding)
	(unit-type-count cavalry-class > min-number-upgrade)
	(strategic-number sn-current-age >= fc-transit)
	(research-completed ri-wheel-barrow)
	(can-research ri-forging)
=>
	(research ri-forging)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(nand
		(goal unit-goal eagle-warrior)
		(research-available ri-chain-mail)
	)
	(or 
		(unit-type-count cavalry-class < min-number-upgrade)
		(research-completed ri-scale-barding)
	)
	(unit-type-count infantry-class > min-number-upgrade)
	(research-completed ri-scale-mail)
	(nand
		(goal strategy-goal boom)
		(goal hostilities-goal 0)
	)
	(research-completed ri-wheel-barrow)
	(strategic-number sn-current-age >= fc-transit)
	(can-research ri-forging)
=>
	(research ri-forging)
)
(defrule
	(strategic-number sn-resource-control <= 0)
	(or
		(research-completed ri-chain-barding)
		(research-completed ri-chain-mail)
	)
	(or
		(or
			(strategic-number sn-current-age >= ci-transit)
			(goal hostilities-goal 1)
		)
		(and
			(goal unit-goal eagle-warrior)
			(up-compare-goal strategy-goal != fast-imp)
		)
	)
	(can-research-with-escrow ri-iron-casting)
=>

	(release-escrow food)(release-escrow gold)
	(research ri-iron-casting)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(population > sixty-percent-pop)
	(or 
		(unit-type-count infantry-class > high-number-upgrade)
		(unit-type-count cavalry-class > high-number-upgrade)
	)
	(not (can-research-with-escrow ri-blast-furnace))
	(research-available ri-blast-furnace)
=>
	(set-escrow-percentage food 20)(set-escrow-percentage gold 20)
	(set-goal escrow-purpose-goal blacksmith)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(nand
		(research-available ri-plate-mail)
		(unit-type-count infantry-class > mid-number-upgrade)
	)
	(nand
		(research-available ri-plate-barding)
		(unit-type-count cavalry-class > mid-number-upgrade)
	)
	(can-research-with-escrow ri-blast-furnace)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(research ri-blast-furnace)
	(set-goal escrow-purpose-goal 0)
)
(defrule
	(goal unit-goal knight)
	(unit-type-count knight-line > 12)
	(game-time < 2700)
	(not (up-research-status c: ri-paladin >= research-pending))	
=>
	(up-jump-rule 2)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(population > sixty-percent-pop)
	(building-type-count university > 0)
	(not (can-research-with-escrow ri-chemistry))
	(research-available ri-chemistry)
=>
	(set-escrow-percentage food 20)(set-escrow-percentage gold 20)
)
(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == ri-chemistry)
	)
	(or
		(current-age-time > 120)
		(or
			(or
				(goal unit-goal gunpowder-class)
				(goal ranged-unit-type-goal gunpowder-class)
			)
			(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
		)
	)
	(can-research-with-escrow ri-chemistry)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(research ri-chemistry)
)
#load-if-not-defined AZTEC-CIV; enable hc against EEW flood
#load-if-not-defined BRITON-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined GOTHIC-CIV; go champs instead
#load-if-not-defined HUN-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined VIKING-CIV
(defrule
	(goal enemy-goal eagle-warrior)
	(can-research-with-escrow ri-chemistry)
	(nand 
		(civ-selected byzantine)
		(building-type-count castle > 0)
	)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-chemistry)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#load-if-not-defined PERSIAN-CIV
#load-if-not-defined SPANISH-CIV
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count cavalry-archer-class > mid-number-upgrade)
	(can-research ri-parthian-tactics)
=>
	(research ri-parthian-tactics)
)
#end-if
#end-if
(defrule
	(strategic-number sn-resource-control <= 2)
	(population > sixty-percent-pop)
	(unit-type-count infantry-class > high-number-upgrade)
	(research-available ri-plate-mail)
=>
	(set-escrow-percentage food 20)(set-escrow-percentage gold 20)
	(set-goal escrow-purpose-goal blacksmith)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(unit-type-count infantry-class > mid-number-upgrade)
	(can-research-with-escrow ri-plate-mail)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(set-goal escrow-purpose-goal 0)
	(research ri-plate-mail)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(research-completed ri-fletching)
	(or 
		(unit-type-count ranged-unit-class > mid-number-upgrade)
		(unit-type-count cavalry-archer-class > mid-number-upgrade)
	)
	(or
		(food-amount >= 150)
		(goal hostilities-goal 1)
	)
	(can-research ri-padded-archer-armor)
=>
	(research ri-padded-archer-armor)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count gunpowder-class > min-number-upgrade)
		(unit-type-count conquistador-line > min-number-upgrade)
	)
	(can-research ri-padded-archer-armor)
=>
	(research ri-padded-archer-armor)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count ranged-unit-class > min-number-upgrade)
		(unit-type-count cavalry-archer-class > min-number-upgrade)
	)
	(research-completed ri-bodkin-arrow)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(goal hostilities-goal 1)
	)
	(can-research-with-escrow ri-leather-archer-armor)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-leather-archer-armor)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count gunpowder-class > min-number-upgrade)
		(unit-type-count conquistador-line > min-number-upgrade)
	)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(goal hostilities-goal 1)
	)
	(can-research-with-escrow ri-leather-archer-armor)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-leather-archer-armor)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(not (research-available ri-bracer))
	(or
		(or 
			(unit-type-count ranged-unit-class > min-number-upgrade)
			(unit-type-count cavalry-archer-class > min-number-upgrade)
		)
		(or
			(unit-type-count gunpowder-class > min-number-upgrade)
			(unit-type-count conquistador-line > min-number-upgrade)
		)
	)
	(can-research-with-escrow ri-ring-archer-armor)
=>
	(release-escrow food)(release-escrow gold)	
	(research ri-ring-archer-armor)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(strategic-number sn-current-age >= ci-transit)
		(and
			(gold-amount < 200)
			(or
				(food-amount > 500)
				(goal hostilities-goal 1)
			)
		)
	)
	(unit-type-count infantry-class > mid-number-upgrade)
	(can-research ri-squires)
=>
	(research ri-squires)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(or
			(gold-amount < 200)
			(strategic-number sn-current-age >= ci-transit)
		)
		(goal hostilities-goal 1)
	)
	(or
		(unit-type-count cavalry-class > mid-number-upgrade)
		(or
			(unit-type-count cavalry-archer-class > mid-number-upgrade)
			(unit-type-count conquistador-line > mid-number-upgrade)
		)
	)
	(can-research ri-husbandry)
=>
	(research ri-husbandry)
)
(defrule
	(strategic-number sn-resource-control != ri-pikeman)
	(goal position-goal pocket)
	(or
		(and
			(goal unit-goal knight)
			(civilian-population < fifty-percent-pop)
		)
		(and
			(building-type-count stable > 0)
			(unit-available camel-line)
		)
	)
=>
	(up-jump-rule 2)
)
(defrule
	(or
		(unit-type-count spearman-line > 7)
		(strategic-number sn-cavalry-threat >= 1)
	)
	(nand
		(and
			(strategic-number sn-resource-control != ri-pikeman)
			(strategic-number sn-current-age == castle)
		)
		(goal position-goal pocket)
	)
	(can-research ri-pikeman)
=>
	(research ri-pikeman)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-resource-control < 3)
	(food-amount > 400)
	(gold-amount < 400)
	(strategic-number sn-cavalry-threat > 1)
	(or
		(food-amount > 800)
		(gold-amount < 100)
	)
	(not (unit-available camel-line))
	(civilian-population >= fifty-percent-pop)
	(can-research ri-pikeman)
=>
	(research ri-pikeman)
)
#end-if
(defrule
	(unit-type-count spearman-line > high-number-upgrade)
	(research-available ri-halberdier)
=>
	(set-escrow-percentage gold 67)
	(set-goal escrow-purpose-goal ri-halberdier)
)
(defrule
	(or
		(up-compare-goal escrow-purpose-goal !=  ri-halberdier)
		(escrow-amount gold >= 600)
	)
	(escrow-amount gold >= 225)
	(up-compare-goal escrow-purpose-goal != ri-paladin)
	(up-compare-goal escrow-purpose-goal != my-unique-unit-line)
	(current-age == imperial-age)
=>
	(set-escrow-percentage food 30)
	(set-escrow-percentage gold 0); stop escrowing gold when we have enough
)
(defrule
	(or
		(unit-type-count spearman-line > mid-number-upgrade)
		(strategic-number sn-minimum-water-body-size-for-dock > water-mixed)
	)
	(can-research-with-escrow ri-halberdier)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(research ri-halberdier)
	(set-goal escrow-purpose-goal 0)
)
(defrule
	(or
		(and
			(unit-type-count spearman-line < 10)
			(goal escrow-purpose-goal ri-halberdier)
		)
		(and
			(and
				(unit-type-count infantry-class < 10)
				(unit-type-count cavalry-class < 10)
			)
			(goal escrow-purpose-goal blacksmith)
		)
	)
=>
	(release-escrow food)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage gold 0)
	(set-goal escrow-purpose-goal 0)
)
(defrule
	(research-completed ri-elite-eagle-warrior); try to press advatange with as many EEW as possivle in early imp
	(current-age-time < 600)
	(population < del-civ-pop)
=>
	(release-escrow food)(release-escrow wood)(release-escrow gold)
	(set-escrow-percentage food 0)(set-escrow-percentage wood 0)(set-escrow-percentage gold 0)
	(set-goal escrow-purpose-goal 0)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(current-age-time > 60)
	(strategic-number sn-resource-control <= 2)
	(can-research ri-capped-ram)
	(unit-type-count-total battering-ram-line > 0)
=>
	(research ri-capped-ram)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(population > max-civ-pop)
	(strategic-number sn-resource-control <= 1)
	(can-research ri-siege-ram)
	(unit-type-count-total battering-ram-line > 3)
=>
	(research ri-siege-ram)
)
(defrule
	(goal save-wood-goal 0)
	(strategic-number sn-resource-control <= 1)
	(or 
		(strategic-number sn-current-age >= ci-transit)
		(or
			(building-type-count-total monastery > 2); mush
			(and
				(food-amount > 400)
				(gold-amount < 200)
			)
		)
	)
	(can-research ri-gold-shaft-mining)
=>
	(research ri-gold-shaft-mining)
)
(defrule
	(or
		(strategic-number sn-stone-gatherer-percentage > 9)
		(strategic-number sn-current-age >= ci-transit)
	)
	(strategic-number sn-stone-gatherer-percentage > 3)
	(strategic-number sn-current-age >= fc-transit)
	(or
		(goal unit-goal my-unique-unit-line)
		(strategic-number sn-current-age >= ci-transit)
	)
	(can-research ri-stone-mining)
=>
	(research ri-stone-mining)
)
(defrule
	(or
		(can-research ri-crop-rotation)
		(can-research ri-two-man-saw)
	)
=>
	(research ri-crop-rotation)
	(research ri-two-man-saw)
)
;********** SIEGE UNITS **********

#load-if-not-defined POPULATION-CAP-25
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal enemy-fortifications-goal 1)
	(building-type-count castle > 0)
	(current-age == imperial-age)
	(unit-type-count-total trebuchet-set < 1)
	(population >= max-civ-pop)
	(wood-amount < 200)
=>
	(set-goal save-wood-goal 1)
	(set-strategic-number sn-resource-control 2)
	(set-goal train-civ-goal -1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(unit-type-count-total battering-ram-line < 3)
		(or
			(players-building-type-count any-enemy castle > 1)
			(players-building-type-count any-enemy stone-wall-line > 10)
		)
	)
	(unit-type-count trebuchet-set == 0)
	(building-type-count castle > 0)
	(up-research-status c: ri-conscription != research-pending)
	(current-age == imperial-age)
	(unit-type-count trebuchet-set < 1)
	(population >= max-civ-pop)
	(wood-amount < 300)
=>
	(set-goal save-wood-goal 1)
	(set-strategic-number sn-resource-control 2)
	(set-goal train-civ-goal -1)
)
(defrule
	(population >= del-civ-pop)
	(population-cap <= 250)
	(building-type-count castle > 0)
	(unit-type-count-total battering-ram-line < 4)			
	(unit-type-count trebuchet-set < 1)
	(current-age == imperial-age)
	(current-age-time > 180)
	(wood-amount > 200)
	(gold-amount > 200)
	(unit-type-count villager > 6)
=>
	(set-goal train-civ-goal -1)
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(population >= max-pop)
	(population-cap <= 250)
	(building-type-count castle > 0)
	(unit-type-count-total battering-ram-line < 4)			
	(unit-type-count trebuchet-set < 1)
	(current-age == imperial-age)
	(current-age-time > 180)
	(wood-amount > 200)
	(gold-amount > 200)
	(unit-type-count villager > 6)
=>
	(delete-unit villager)
	(chat-local-to-self "delete villager to make room for treb")
)
(defrule
	(population >= del-civ-pop)
	(population-cap <= 250)
	(building-type-count siege-workshop > 0)
	(unit-type-count-total battering-ram-line < 8)
	(or
		(unit-type-count trebuchet-set < 1)
		(unit-type-count-total battering-ram-line < 4)
	)
	(current-age == imperial-age)
	(current-age-time > 180)
	(wood-amount > 175)
	(gold-amount > 75)
	(unit-type-count villager > 6)
=>
	(set-goal train-civ-goal -1)
	(set-strategic-number sn-resource-control 2)
)
(defrule
	(population >= max-pop)
	(population-cap <= 250)
	(building-type-count siege-workshop > 0)
	(unit-type-count-total battering-ram-line < 8)
	(or
		(unit-type-count trebuchet-set < 1)
		(unit-type-count-total battering-ram-line < 4)
	)
	(current-age == imperial-age)
	(current-age-time > 180)
	(wood-amount > 175)
	(gold-amount > 75)
	(unit-type-count villager > 6)
=>
	(delete-unit villager)
	(chat-local-to-self "delete villager to make room for rams")
)
#load-if-not-defined HIGHLAND-MAP
#load-if-not-defined YUCATAN-MAP
(defrule
	(population-cap <= 250)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(building-type-count-total dock > 0)
	(or
		(warboat-count < 25)
		(and
			(civilian-population > 50)
			(and
				(can-afford-unit cannon-galleon-line)
				(unit-type-count cannon-galleon-line < 6)
			)
		)
	)
	(population >= max-pop)
=>
	(delete-unit villager)
	(set-goal train-civ-goal -1)
	(set-strategic-number sn-resource-control 1)
	(chat-to-player chat-player "execute villagers too make room for a fleet")
)
#end-if
#end-if
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(building-type-count-total dock > 0)
	(or
		(and
			(unit-type-count villager >= max-civ)
			(current-age == castle-age)
		)
		(research-completed ri-cannon-galleon)
	)
	(unit-type-count villager >= 6)
	(population >= max-pop)
=>
	(delete-unit villager)
	(set-goal train-civ-goal -1)
	(set-strategic-number sn-resource-control 1)
	(chat-to-player chat-player "execute villagers too make room for a fleet")
)
#end-if
(defrule
	(unit-type-count fishing-ship > 0)
	(population >= del-civ-pop)
	(or
		(and
			(food-amount > 500)
			(goal train-civ-goal -1)
		)
		(and
			(cc-players-unit-type-count 0 sea-fish == 0)
			(cc-players-unit-type-count 0 shore-fish == 0)
		)
	)	
=>
	(delete-unit fishing-ship)
)	
(defrule
	(unit-type-count fishing-ship > 0)
	(timer-triggered one-minute-timer); run once every minute only
	(population >= del-civ-pop)
	(up-idle-unit-count 2 > 0); idle fishing ships
=>
	(up-delete-idle-units 2)
)	
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands); not on water maps
	(dropsite-min-distance wood > 0)
	(dropsite-min-distance wood < 20)
	(goal train-civ-goal -1)
	(strategic-number sn-resource-control < 1); no siege required from rules above
	(civilian-population <= fourty-percent-pop); already deleted lots of villagers
=>
	(set-goal train-civ-goal 1); restart villager training
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(strategic-number sn-resource-control == 1)
	(warboat-count >= 25)
	(or
		(not (research-completed ri-cannon-galleon))
		(unit-type-count cannon-galleon-line > 7)
	)
=>
	(set-strategic-number sn-resource-control 0)
)
; order of rules does matter as 1st rule that will be executed 1st, so next rules may not spend the resources
; so put siege training above building

#load-if-not-defined AZTEC-CIV
#load-if-not-defined INCAN-CIV
#load-if-not-defined MAYAN-CIV
(defrule
	(strategic-number sn-resource-control < 2)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(up-research-status c: ri-cannon-galleon < research-pending)
	(warboat-count > 30)
	(civilian-population > 100)
	(strategic-number sn-military-level >= 0)
	(players-building-type-count any-enemy castle > 0)
	(nand
		(up-compare-goal aofe-game-goal != 1)
		(civ-selected briton)
	)
	(nand
		(up-compare-goal aofe-game-goal == 1)
		(civ-selected hun)
	)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = cannon-galleon")
)
#end-if
#end-if
#end-if
(defrule
	(wood-amount < 200)
	(gold-amount > 500)
	(unit-type-count trade-cart > 4)
	(or
		(or
			(strategic-number sn-resource-control == 2)
			(gold-amount > 1500)
		)
		(and
			(commodity-buying-price wood < 30)
			(gold-amount > 800)
		)
	)
	(commodity-buying-price wood < 80)
=>
	(buy-commodity wood)
	(chat-local-to-self "4 Buy cheap wood with trade cart gold")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(warboat-count >= 17)
	(or
		(up-projectile-detected projectile-castle c:< 2000)
		(up-projectile-detected projectile-bombard-tower c:< 2000)
	)
	(unit-type-count cannon-galleon-line < 4)
	(can-train-with-escrow cannon-galleon-line)
=>
	(release-escrow wood)(release-escrow gold)
	(train cannon-galleon-line)
)
#load-if-defined DEFEND-WONDER
(defrule
	(building-type-count wonder == 0)
	(unit-type-count-total trebuchet-set < 1)
	(can-train trebuchet)
=>
	(train trebuchet)
)
#end-if
(defrule
	(population >= del-civ-pop)
	(unit-type-count-total trebuchet-set < 1)
	(can-train-with-escrow trebuchet)
=>
	(release-escrow wood)(release-escrow gold)
	(train trebuchet)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(or
		(current-age < imperial-age)
		(current-age-time < 900)
	)
=>
	(up-jump-rule 11)	
)
(defrule
	(population >= del-civ-pop)
	(current-age-time >= 10)
	(or
		(or
			(unit-type-count trebuchet-set < 2)
			(and
				(game-time < 1200); start in DM
				(population >= eighty-five-percent-pop)
			)
		)
		(or
			(goal enemy-fortifications-goal 1)
			(players-building-type-count any-enemy castle > 1)
		)
	)
	(unit-type-count trebuchet-set < 4)
	(can-train trebuchet)
=>
	(train trebuchet)
)
(defrule
	(strategic-number sn-resource-control == 0)
	(or
		(strategic-number sn-military-level > 1)
		(and
			(military-population > 25)
			(players-military-population focus-player < 10)
		)
	)
	(current-age == castle-age)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(and
			(up-players-in-game ally > 1)
			(up-compare-goal position-goal != pocket)
		)
	)
	(building-type-count siege-workshop > 0)
	(unit-type-count-total battering-ram-line == 0)
=>
	(set-strategic-number sn-resource-control battering-ram)
;	(chat-to-player chat-player "save-resources = battering-ram")
)
(defrule
	(research-completed ri-onager)
	(strategic-number sn-resource-control < 3)
	(unit-type-count mangonel-line < 3)
	(unit-type-count cavalry-class < 20)
	(or
		(strategic-number sn-archer-threat > 3)
		(players-unit-type-count any-human-enemy mameluke-line > 25)
	)
	(or
		(unit-type-count villager >= 25)
		(strategic-number sn-resource-control < 1)
	)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(unit-type-count-total battering-ram-line < number-rams)
	(population >= seventy-five-percent-pop)
	(can-train battering-ram-line)
=>
	(train battering-ram-line)
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control < 3)
	(unit-type-count-total mangonel-line < 2)
	(or
		(unit-type-count ranged-unit-class > high-number-upgrade)
		(unit-type-count cavalry-archer-class > mid-number-upgrade)
	)
	(nand
		(goal team-coordination-goal 1)
		(unit-type-count-total battering-ram-line == 0)
	)
	(or
		(players-unit-type-count focus-player skirmisher-line > 12)
		(or
			(players-unit-type-count any-enemy mangonel-line > 1)
			(players-unit-type-count any-enemy scorpion-line > 4)
		)
	)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control < 3)
	(unit-type-count mangonel-line < 2)
	(building-type-count-total town-center >= tc-level-two)
	(goal unit-goal my-unique-unit-line)
	(players-current-age focus-player == feudal-age)
	(building-type-count watch-tower > 0)
	(wood-amount >= 200)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(or
		(unit-type-count cavalry-class > 10)
		(goal unit-goal eagle-warrior)
	)
	(strategic-number sn-archer-threat < 4)
=>
	(up-jump-rule 2)
)
#else
(defrule
	(strategic-number sn-current-age == castle)
	(unit-type-count mangonel-line < 3)
	(building-type-count gate > 0)
	(strategic-number sn-military-level < -1)
	(or
		(goal enemy-goal flush)
		(strategic-number sn-archer-threat > 1)
	)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
(defrule
	(building-type-count gate > 0)
	(strategic-number sn-military-level < -1)
	(or
		(goal enemy-goal flush)
		(strategic-number sn-archer-threat > 1)
	)
	(current-age == castle-age)
	(unit-type-count-total mangonel-line < 3)
	(building-type-count siege-workshop >= 1)
	(not (can-afford-unit mangonel-line))
=>
	(set-strategic-number sn-resource-control mangonel)
;	(chat-to-player chat-player "save-resources = mangonel")
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-resource-control != battering-ram)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-archer-threat > 1)
	(current-age == castle-age)
	(unit-type-count-total mangonel-line < 1)
	(building-type-count siege-workshop >= 1)
	(nor 
		(can-afford-unit mangonel-line)
		(research-completed ri-elite-skirmisher)
	)
=>
	(set-strategic-number sn-resource-control mangonel)
;	(chat-to-player chat-player "save-resources = mangonel")
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(strategic-number sn-archer-threat > 1)
		(players-unit-type-count any-enemy scorpion-line > 5)
	)
	(or
		(current-age == castle-age)
		(or
			(strategic-number sn-archer-threat > 2)
			(players-unit-type-count any-enemy scorpion-line > 11)
		)
	)
	(unit-type-count mangonel-line < 1)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
(defrule
	(strategic-number sn-resource-control < 3)
	(goal unit-goal monk)
	(current-age == imperial-age)
	(building-type-count siege-workshop > 0)
	(unit-type-count-total battering-ram-line == 0)
=>
	(set-strategic-number sn-resource-control battering-ram)
;	(chat-to-player chat-player "save-resources = battering-ram")
)
(defrule
	(strategic-number sn-resource-control < 3)
	(or
		(or
			(strategic-number sn-archer-threat > 3)
			(players-unit-type-count any-enemy skirmisher-line > 40)
		)
		(players-unit-type-count any-enemy scorpion-line > 15)
	)
	(unit-type-count trade-cart > 15)
	(or
		(unit-type-count ranged-unit-class > 20)
		(unit-type-count cavalry-archer-class > 20)
	)
	(unit-type-count-total mangonel-line < 1)
	(research-completed ri-onager)
	(building-type-count siege-workshop >= 1)
=>
	(set-strategic-number sn-resource-control mangonel)
;	(chat-to-player chat-player "save-resources = onager")
)
(defrule
	(or
		(strategic-number sn-resource-control == battering-ram)
		(strategic-number sn-resource-control == mangonel)
	)
	(wood-amount < 200)
=>
	(set-goal save-wood-goal 1)
)
(defrule
	(or
		(and
			(goal forward-threat-goal 1)
			(unit-type-count-total battering-ram-line < 2)
		)
		(strategic-number sn-resource-control == battering-ram)
	)
	(can-train-with-escrow battering-ram-line)
=>
	(release-escrow wood)(release-escrow gold)
	(train battering-ram-line)
	(set-goal save-wood-goal 0)
	(set-strategic-number sn-resource-control 0)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control < 1)
=>
	(research ri-onager)
	(research ri-siege-onager)
	(research 445); ri-shinkichon
)
(defrule
	(or
		(and
			(strategic-number sn-current-age >= ci-transit)
			(unit-type-count mangonel-line < 1)
		)
		(and
			(strategic-number sn-resource-control < 1)
			(and
				(unit-type-count-total mangonel-line < 12)
				(research-completed ri-onager)
			)
		)
	)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
#else
#load-if-defined UP-ALLY-IN-GAME
(defrule
	(or
		(or
			(strategic-number sn-archer-threat > 3)
			(players-unit-type-count any-enemy skirmisher-line > 40)
		)
		(players-unit-type-count any-enemy scorpion-line > 15)
	)
	(or
		(unit-type-count ranged-unit-class > 20)
		(unit-type-count cavalry-archer-class > 20)
	)
	(up-compare-goal unit-goal != knight)
	(up-research-status c: ri-elite-eagle-warrior < research-pending)
	(can-research-with-escrow ri-onager)
=>
	(up-release-escrow)
	(research ri-onager)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-siege-onager)
	(or
		(unit-type-count mangonel-line < 6)
		(and
			(civ-selected korean)
			(game-time < 900)
		)
	)
	(unit-type-count mangonel-line < 12)
	(nand
		(unit-type-count cavalry-class > 20)
		(strategic-number sn-archer-threat < 3)
	)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
#end-if
(defrule
	(or
		(strategic-number sn-resource-control < 1)
		(civ-selected mongol)
	)
	(or
		(or
			(strategic-number sn-archer-threat > 3)
			(players-unit-type-count any-enemy scorpion-line > 15)
		)
		(and
			(or
				(strategic-number sn-archer-threat > 2)
				(players-unit-type-count any-enemy scorpion-line > 8)
			)
			(unit-type-count trade-cart > 15)
		)
	)
=>
	(research ri-onager)
	(research ri-siege-onager)
	(research 445); ri-shinkichon
)
(defrule
	(strategic-number sn-resource-control < 1)
	(players-unit-type-count any-enemy galley-line > 20)
	(or
		(warboat-count < 15)
		(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	)
=>
	(research ri-onager)
	(research ri-siege-onager)
	(research 445); ri-shinkichon
)
(defrule
	(or
		(and
			(strategic-number sn-resource-control < 1)
			(or
				(strategic-number sn-archer-threat > 2)
				(or
					(players-unit-type-count any-enemy scorpion-line > 8)
					(players-unit-type-count any-enemy galley-line > 15)
				)
			)
		)
		(strategic-number sn-resource-control == mangonel)
	)
	(research-completed ri-onager)
	(unit-type-count mangonel-line < 4)
	(can-train mangonel-line)
=>
	(train mangonel-line)
)
#load-if-not-defined POPULATION-CAP-25
#load-if-defined DEATH-MATCH
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(military-population > 20)
	(unit-type-count battering-ram-line < 4)
	(or
		(strategic-number sn-military-level >= 0)
		(population > sixty-percent-pop)
	)
	(can-train battering-ram-line)
=>
	(train battering-ram-line)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(goal unit-goal monk)
	(nand
		(unit-available trebuchet)
		(unit-type-count trebuchet-set < 1)
	)
	(military-population > 20)
	(unit-type-count-total battering-ram-line < 2)
	(strategic-number sn-military-level > 0)
	(can-train battering-ram-line)
=>
	(train battering-ram-line)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(population > max-civ-pop)
	(or
		(unit-type-count battering-ram-line < 6)
		(up-research-status c: ri-siege-ram >= research-available)
	)
	(or
		(goal enemy-fortifications-goal 1)
		(unit-type-count battering-ram-line < 6)
	)
	(unit-type-count battering-ram-line < 12)
	(can-train battering-ram-line)
=>
	(train battering-ram-line)
)
; Relic Monks
#end-if
(defrule
	(or
		(strategic-number sn-resource-control < 1)
		(strategic-number sn-resource-control == monk)
	)
	(unit-type-count-total monastery-class == 0)
	(unit-type-count-total 134 == 0); 134 = dead monk; dont make monks when they're dying while trying to collect relics behind enemy bases.
	(can-train monk)
=>
	(train monk)
)
(defrule
	(strategic-number sn-resource-control == 2)
	(unit-type-count-total monastery-class == 0)
	(unit-type-count-total 134 == 0)
	(unit-type-count cavalry-class > 12)
	(goal hostilities-goal 0)
	(up-compare-goal threat-time-goal > 20000)
	(can-train monk)
=>
	(train monk)
)

; Trade
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count-total trade-cart > 9)
		(unit-type-count-total trade-cog > 9)
	)
	(or
		(research-available ri-cartography)
		(research-available ri-caravan)
	)
=> 
	(set-strategic-number sn-enable-training-queue 0); disable trade cart queue, if we need to upgrade them, or else the researches are never available.
)
(defrule
	(goal strategy-goal sling)
	(can-research ri-coinage)
=> 
	(research ri-coinage)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-current-age >= ci-transit)
	(unit-type-count villager >= max-civ)
	(players-building-type-count any-ally market > 0)
	(can-research ri-coinage)
=> 
	(research ri-coinage)
)
(defrule
	(or
		(strategic-number sn-resource-control <= 2)
		(strategic-number sn-resource-control == sling)
	)
	(players-building-type-count any-ally market > 0)
	(can-research ri-banking)
=> 
	(research ri-banking)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count-total trade-cart > 9)
		(unit-type-count-total trade-cog > 9)
	)
	(can-research-with-escrow ri-cartography)
=> 
	(release-escrow food)(release-escrow gold)
	(research ri-cartography)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(unit-type-count-total trade-cart > 9)
		(unit-type-count-total trade-cog > 9)
	)
	(can-research-with-escrow ri-caravan)
=> 
	(release-escrow food)(release-escrow gold)
	(research ri-caravan)
)
#load-if-not-defined DEFEND-WONDER
#load-if-not-defined POPULATION-CAP-25
#load-if-not-defined POPULATION-CAP-50
#load-if-defined ITALIAN-CIV
(defrule
	(unit-type-count trade-cart < 12)
	(player-in-game any-ally)
	(can-research ri-silk-road)
=>
	(research ri-silk-road)
)
#end-if
#load-if-not-defined SMALL-MAP
#load-if-not-defined TINY-MAP
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count town-center >= tc-level-three)
	(game-time > market-time)
	(up-research-status c: imperial-age >= research-available)
	(or
		(players-building-type-count any-ally market > 0)
		(building-type-count-total market == 0)
	)
	(building-type-count-total market < number-markets)
	(can-build market)
=>
	(set-strategic-number sn-placement-to-center 1)
	(up-set-placement-data focus-player -1 c: -120)
	(up-build place-control 0 c: market)
)
#end-if
#end-if
#load-if-defined DEATH-MATCH
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > market-time)
	(building-type-count town-center > tc-level-three)
	(building-type-count-total market < 1)
	(can-build market)
=>
	(build market)
)
#load-if-not-defined UP-1-PLAYER-TEAM
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > market-time)
	(building-type-count town-center > tc-level-three)
	(building-type-count-total market < 3)
	(or
		(building-type-count-total market < 1)
		(unit-type-count-total trade-cart > 0)
	)
	(can-build market)
=>
	(set-strategic-number sn-placement-to-center 1)
	(up-set-placement-data focus-player -1 c: -120)
	(up-build place-control 0 c: market)
)
#end-if
#end-if
(defrule
	(game-time > market-time)
	(timer-triggered one-minute-timer)
	(up-pending-placement c: market)
	(building-type-count-total market == 0)
=>
	(up-reset-placement c: market); unbug
	(build market)
)
(defrule
	(or
		(or
			(not(player-in-game any-ally))
			(or
				(players-building-type-count every-ally market == 0)
				(building-type-count town-center == 0)
			)
		)
		(or
			(unit-type-count-total trade-cart >= max-trade-pop); depending on popcap and difficulty
			(unit-type-count-total trade-cart >= max-trade-cart); depending on map size
		)
	)
=>
	(up-jump-rule 7)
)
(defrule
	(game-time > 1800)
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(set-strategic-number sn-focus-player-number 1)
	(set-goal remote-last-goal 0)
)
(defrule
	(game-time > 1800)
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
	(strategic-number sn-focus-player-number != my-player-number)
	(stance-toward focus-player ally)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include -1 -1 -1 1); search same island only
	(up-find-remote c: market c: 1)
	(up-get-search-state local-total-goal)
)
(defrule
	(game-time > 1800)
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
	(goal remote-last-goal 0)
	(player-valid focus-player)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-jump-rule -2)
)
(defrule
	(game-time > 1800)
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
	(up-compare-goal remote-last-goal >= 1); at least one allied market on same island found
	(can-train trade-cart)
=>
	(train trade-cart)
)
(defrule
	(game-time > 1800)
	(strategic-number sn-resource-control <= 2)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(research-available imperial-age)
		(strategic-number sn-current-age >= ci-transit)
	)
	(strategic-number sn-minimum-water-body-size-for-dock <= water-mixed)
	(or
		(game-time > 1800);30 min	
		(death-match-game)
	)
	(can-train trade-cart)
=>
	(train trade-cart)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal strategy-goal sling)
	(goal under-attack-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock <= water-mixed)
	(game-time > 1800)
	(can-train trade-cart)
=>
	(train trade-cart)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control <= 2)
	(player-in-game any-ally)
	(players-building-type-count any-ally market > 0)
	(building-type-count-total town-center >= tc-level-five)
	(or
		(research-available imperial-age)
		(strategic-number sn-current-age >= ci-transit)
	)
	(can-train trade-cart)
	(unit-type-count trade-cart < max-trade-pop); depending on popcap and difficulty
	(unit-type-count trade-cart < max-trade-cart); depending on map size
=>
	(train trade-cart)
)
#end-if
(defrule
	(or
		(unit-type-count 178 > 0);dead trade cart (empty/loaded)
		(unit-type-count 205 > 0)
	)
=>
	(set-strategic-number sn-preferred-trade-distance trade-distance)
)
(defrule	
	(unit-type-count 178 == 0)
	(unit-type-count 205 == 0)
=>
	(set-strategic-number sn-preferred-trade-distance 200); try farest market
)
#end-if
#end-if
#end-if
#end-if ; NO WONDER-RACE

; VILLAGERS

(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count-total villager >= villager-feudal)
	(or
		(or
			(difficulty != hard)
			(game-type == 8)
		)
		(or
			(starting-resources != low-resources)
			(or
				(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
				(map-type yucatan)
			)
		)
	)
=>
	(set-strategic-number sn-enable-training-queue 0)
)
#load-if-defined LOW-RESOURCES-START
#load-if-defined DIFFICULTY-HARD
(defrule
	(strategic-number sn-current-age == dark)
	(or
		(and
			(unit-type-count-total villager >= villager-flush)
			(goal strategy-goal flush)
		)
		(unit-type-count-total villager > 26)
	)
=>
	(set-strategic-number sn-enable-training-queue 0)
)
#end-if
#end-if
(defrule
	(or
		(and
			(current-age == feudal-age)
			(or
				(food-amount > 400)
				(strategic-number sn-current-age == fc-transit)
			)
		)
		(or
			(and
				(goal need-loom-goal 1)
				(research-available ri-loom)
			)
			(and
				(current-age < imperial-age)
				(death-match-game)
			)
		)
	)
=>
	(set-strategic-number sn-enable-training-queue 0)
)
(defrule
	(current-age >= castle-age)
	(or
		(up-compare-goal strategy-goal != boom)
		(or
			(strategic-number sn-resource-control > 2)
			(and
				(strategic-number sn-current-age == castle)
				(and
					(food-amount >= imp-f2)
					(gold-amount >= imp-g2)
				)
			)
		)
	)
=>
	(set-strategic-number sn-enable-training-queue 0)
)
(defrule
	(or
		(and
			(strategic-number sn-current-age >= ci-transit)
			(strategic-number sn-resource-control >= 1)
		)
		(or
			(population >= max-civ-pop)
			(and
				(goal strategy-goal flush)
				(food-amount < 150)
			)
		)
	)
	(current-age >= feudal-age)
=>
	(set-strategic-number sn-enable-training-queue 0)
)
#load-if-defined LOW-RESOURCES-START
#load-if-defined DIFFICULTY-HARD
#load-if-defined FORTRESS-MAP
(defrule
	(goal train-civ-goal 1)
	(nand
		(food-amount > 650)
		(research-available castle-age)
	)
	(or
		(and
			(current-age == feudal-age)
			(gold-amount < 190)
		)
		(and
			(unit-type-count villager < 31)
			(current-age == dark-age)
		)
	)
	(can-train villager)
=>
	(train villager)
)
#end-if
#end-if
#end-if
#load-if-not-defined WONDER-RACE
(defrule
	(goal train-civ-goal 1)
	(current-age == feudal-age)
	(or
		(goal strategy-goal rush)
		(goal strategy-goal boom)
	)
	(or
		(or
			(current-age-time < 40)
			(food-amount < 760)
		)
		(starting-age == feudal-age)
	)
	(can-train villager)
=>
	(train villager)
)
#load-if-defined DARK-AGE-END
(defrule
	(goal train-civ-goal 1)
	(unit-type-count villager < 60)
	(unit-type-count villager < max-civ)
	(can-train villager)
=>
	(train villager)
)
#end-if
#end-if
#load-if-not-defined DEATH-MATCH
#load-if-not-defined DIFFICULTY-HARDEST
#load-if-not-defined HIGH-RESOURCES-START
#load-if-not-defined MEDIUM-RESOURCES-START
(defrule
	(or
		(and
			(current-age == dark-age)
			(unit-type-count villager < 33)
		)
		(current-age == feudal-age)
	)
	(up-compare-goal unit-goal != wonder)
	(unit-type-count villager < 35)
	(goal strategy-goal fast-imp)
	(goal train-civ-goal 1)
	(can-train villager)
=>
	(train villager)
)
#load-if-defined WONDER-RACE
(defrule
	(or
		(or
			(and
				(current-age == dark-age)
				(unit-type-count villager < villager-wonder-dark)
			)
			(and
				(current-age >= feudal-age)
				(unit-type-count villager < villager-wonder-feudal)
			)
		)
		(current-age == imperial-age)
	)
	(goal train-civ-goal 1)
	(can-train villager)
=>
	(train villager)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#load-if-not-defined WONDER-RACE
(defrule
	(or
		(or
			(goal strategy-goal flush)
			(goal strategy-goal sling)
		)
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	)
	(nor
		(unit-type-count villager < villager-feudal)
		(food-amount < 430)
	)
=>
	(up-jump-rule 1)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager < 32)
	(or
		(unit-type-count villager < villager-feudal)
		(or
			(or
				(unit-type-count villager < 30)
				(food-amount < 620)
			)
			(food-amount < 430)
		)
	)
	(goal train-civ-goal 1)
	(can-train villager)
=>
	(train villager)
)
(defrule
	(goal train-civ-goal 1)
	(current-age == feudal-age)
	(or
		(goal strategy-goal flush)
		(goal strategy-goal sling)
	)
	(or
		(up-research-status c: castle-age < research-available)
		(or
			(food-amount < 750)
			(and
				(gold-amount < 160)
				(goal under-attack-goal 0)
			)
		)
	)
	(unit-type-count-total villager < max-civ)
	(population < max-civ-pop)
	(can-train villager)
=>
	(train villager)
)
(defrule
	(goal train-civ-goal 1)
	(current-age >= castle-age)
	(civilian-population < max-civ)
	(or
		(population < max-civ-pop)
		(game-time < 1800); keep training villagers in DM
	)
	(can-train villager)
=>
	(train villager)
)

;********** BUILDINGS **********

#load-if-defined KING-OF-THE-HILL
(defrule
	(game-time > 1800)
	(players-building-type-count any-ally 826 > 0)
	(can-build castle)
=>
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
	(up-set-placement-data this-any-ally 826 c: 2)
	(up-build place-control 0 c: castle)
)
(defrule
	(game-time > 1800)
	(building-type-count 826 > 0)
	(can-build castle)
=>
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
	(up-set-placement-data my-player-number 826 c: 2)
	(up-build place-control 0 c: castle)
)
#else
#load-if-defined GOLD-RUSH-MAP ; place point the castle at the center. requires to do that after all target point actions are done.
(defrule
	(can-build castle)
	(up-pending-objects c: castle < 2)
	(game-time > 480)
	(nand
		(building-type-count-total castle == 0)
		(goal unit-goal my-unique-unit-line)
	)
	(or
		(strategic-number sn-military-level > 0)
		(goal team-coordination-goal 1)
	)
=>
	(up-jump-rule 4); so skip other castle buildings rules.
)
#end-if
#end-if
(defrule
	(strategic-number sn-maximum-town-size >= 60)
	(or
		(up-compare-sn sn-target-player-number g:== closest-enemy-goal)
		(strategic-number sn-target-player-number g:== threat-player-goal); we actually fight the player we are going to forward
	)
	(building-type-count castle > 0)
	(or
		(up-compare-const allow-forward-castle == 1)
		(building-type-count-total castle > 2)
	)
	(can-build castle)
=>
	(build-forward castle)
	(up-assign-builders c: castle c: 4)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(can-build castle)
	(building-type-count-total castle == 0)
=>
	(up-set-placement-data my-player-number -1 c: 16)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
	(up-build place-control 0 c: castle)
	(up-jump-rule 2)
)
#end-if
#load-if-defined DEATH-MATCH
#load-if-defined MAYAN-CIV
(defrule
	(unit-type-count villager g:> temp-goal5)
	(game-time < 10)
	(building-type-count-total market == 0)
	(strategic-number sn-cavalry-threat == 0)
	(can-build market)
=>
	(up-set-placement-data my-player-number villager-class c: 1)
	(up-build place-control 0 c: market)
	(up-modify-goal temp-goal5 c:+ 1)
)
#end-if
#load-if-defined SARACEN-CIV
(defrule
	(unit-type-count villager g:> temp-goal5)
	(game-time < 10)
	(building-type-count-total market == 0)
	(nor
		(players-civ any-enemy hun)
		(players-civ any-enemy gothic)
	)
	(can-build market)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: market)
	(up-modify-goal temp-goal5 c:+ 1)
)
#end-if
#load-if-defined HUN-CIV
(defrule
	(game-time > 120)
	(game-time < 300)
	(building-type-count-total market == 0)
	(can-build market)
=>
	(build market)
)
#end-if
#load-if-defined TURKISH-CIV
(defrule
	(game-time > 120)
	(game-time < 300)
	(building-type-count-total market == 0)
	(can-build market)
=>
	(build market)
)
#end-if
#load-if-not-defined UP-MULTIPLAYER-GAME
(defrule
	(unit-type-count villager g:> temp-goal5)
	(unit-type-count villager >= villager-castle)
	(building-type-count-total castle == 0)
	(can-build castle)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: castle)
	(up-modify-goal temp-goal5 c:+ 1)
)
#else
(defrule
	(can-build castle)
	(up-pending-objects c: castle < 2)
	(unit-type-count-total villager >= villager-castle)
	(building-type-count-total castle < 2)
=>
	(build castle)
	(up-jump-rule 2)
)
#end-if
#end-if
(defrule
	(game-time > 20)
	(can-build castle)
	(or
		(up-pending-objects c: castle < 2)
		(game-time > 300)
	)
	(unit-type-count-total villager >= villager-castle)
	(or
		(game-time > 180)
		(up-compare-const villager-castle == 3)
	)
=>
	(up-set-placement-data my-player-number -1 c: 8)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 1)
	(up-build place-control 0 c: castle)
)
(defrule
	(can-build castle)
	(up-pending-objects c: castle < 2)
	(building-type-count-total wonder > 0)
	(unit-type-count-total villager >= villager-castle)
=>
	(up-set-placement-data my-player-number wonder c: 2)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 1)
	(up-build place-control 0 c: castle)
)
(defrule
	(game-time > 600)
	(can-build-with-escrow town-center)
	(building-type-count-total town-center == 0)
	(players-civilian-population any-ally > 70)
=>
	(set-strategic-number sn-maximum-town-size 255)
	(up-assign-builders c: 621 c: 100)
	(release-escrow wood)
	(set-strategic-number sn-town-center-placement 0)
	(set-strategic-number sn-placement-zone-size 50)
	(set-strategic-number sn-placement-fail-delta 1)
	(up-set-placement-data this-any-ally -1 c: -30)
	(up-build place-control 0 c: town-center)
	(up-delete-objects c: mill c: 32767)
	(chat-to-allies "Trying to rebuild TC next to ally")
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal save-wood-goal 0)
	(current-age >= castle-age)
	(can-build-with-escrow town-center)
	(building-type-count-total town-center < 5)
	(strategic-number sn-military-level < -2)
	(military-population < 12)
	(players-civilian-population any-ally > 70)
=>
	(release-escrow wood)
	(set-strategic-number sn-town-center-placement 0)
	(set-strategic-number sn-placement-zone-size 50)
	(set-strategic-number sn-placement-fail-delta 1)
	(up-set-placement-data this-any-ally -1 c: -20); don't build too close or we're stuck between his buildings
	(up-build place-control 0 c: town-center)
	(chat-to-allies "I build a TC next to an ally")
)
#end-if
(defrule
	(timer-triggered five-seconds-timer)
	(building-type-count-total town-center == 0)
	(up-pending-placement c: town-center)
=>
	(up-reset-placement c: town-center)
)
(defrule
	(timer-triggered five-seconds-timer)
	(building-type-count-total lumber-camp == 0)
	(building-type-count-total town-center == 0)
	(up-pending-placement c: lumber-camp)
=>
	(up-reset-placement c: lumber-camp)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(research-completed ri-bombard-tower)
	(or
		(building-type-count-total bombard-tower < 10)
		(gold-amount > 2500)
	)
	(unit-type-count trade-cart > 14)
	(unit-type-count unpacked-trebuchet > 0)
	(population > del-civ-pop); high pop
	(can-buy-commodity stone)
	(gold-amount >= 900)
	(stone-amount < 100)
	(commodity-buying-price stone < 400)
=>
	(buy-commodity stone)
	(chat-local-to-self "6 buy stone to push with bbt")
)
#load-if-defined TURKISH-CIV
(defrule
	(building-type-count bombard-tower > 4)	
	(research-available my-unique-research)
=>
	(up-jump-rule 3)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(game-time > 300)
	(building-type-count bombard-tower < 10)
	(or
		(game-time < 900); build them defensively early in DM
		(building-type-count bombard-tower < 4)
	)
	(up-pending-objects c: bombard-tower == 0)
	(can-build bombard-tower)
=>
	(up-set-placement-data my-player-number dock c: 2)
	(up-build place-control 0 c: bombard-tower)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-maximum-town-size >= 40)
	(unit-type-count bombard-cannon > 0)
	(unit-type-count 368 > 0); bombard cannon bullet
	(can-build bombard-tower)
=>
	(up-set-placement-data my-player-number 42 c: 1)
	(up-build place-control 0 c: bombard-tower)
	(up-set-offense-priority c: siege-unit-class c: 8); kill rams first as biggest danger to bbt
)
(defrule
	(building-type-count town-center > 0)
	(goal increase-town-size-goal 0)
	(goal save-wood-goal 0)
	(or
		(goal unit-goal monk)
		(and
			(goal ranged-unit-type-goal monk)
			(building-type-count-total monastery < 4)
		)
	)
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total monastery < 6)
	(can-build monastery)
=>
	(build monastery)
	(set-goal increase-town-size-goal 2)
)
#else
(defrule
	(goal strategy-goal boom)
	(goal unit-goal monk)
	(or
		(building-type-count-total siege-workshop < 1)
		(building-type-count-total town-center < tc-level-three)
	)
	(building-type-count-total monastery > 0)
=>
	(up-jump-rule 2)
)
(defrule
	(or
		(or
			(strategic-number sn-archer-threat > 1)
			(goal strategy-goal fast-imp)
		)
		(players-unit-type-count focus-player scorpion-line > 9)
	)
	(goal unit-goal monk)
	(building-type-count-total siege-workshop < 1)
	(building-type-count-total monastery > 0)
=>
	(up-jump-rule 1)
)
(defrule
	(building-type-count town-center > 0)
	(goal increase-town-size-goal 0)
	(goal unit-goal monk)
	(or
		(and
			(building-type-count-total monastery < 3)
			(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
		)
		(and
			(current-age == imperial-age)
			(current-age-time > 180)
		)
	)
	(goal save-wood-goal 0)
	(building-type-count-total monastery < 5)
	(can-build monastery)
=>
	(build monastery)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(building-type-count town-center > 0)
	(goal increase-town-size-goal 0)
	(goal ranged-unit-type-goal monk)
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total monastery < 3)
	(wood-amount > 300)
	(goal save-wood-goal 0)
	(can-build monastery)
=>
	(build monastery)
	(set-goal increase-town-size-goal 2)
)
#end-if
#load-if-defined AZTEC-CIV
#load-if-not-defined UP-MICHI-STYLE
#load-if-not-defined POST-IMPERIAL-AGE-START
(defrule
	(goal increase-town-size-goal 0)
	(goal save-wood-goal 0)
	(building-type-count-total siege-workshop > 0)
	(building-type-count-total town-center >= tc-level-three)
	(or
		(building-type-count-total monastery < 2)
		(strategic-number sn-current-age >= ci-transit)
	)
	(building-type-count-total monastery < 3)
	(nor 
		(goal enemy-goal eagle-warrior)
		(or
			(goal unit-goal archer)
			(goal unit-goal eagle-warrior)
		)
	)
	(can-build monastery)
=>
	(build monastery)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal save-wood-goal 0)
	(building-type-count-total town-center >= tc-level-two)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(building-type-count-total monastery < 2)
	)
	(or
		(building-type-count-total town-center >= tc-level-four)
		(building-type-count-total monastery < 3)
	)
	(building-type-count-total monastery < 5)
	(goal ranged-unit-type-goal monk)
	(can-build monastery)
=>
	(build monastery)
)
#else
(defrule
	(unit-type-count villager > villager-monastery)
	(or
		(or
			(players-civ every-enemy indian)
			(players-civ every-enemy slavic)
		)
		(or
			(or
				(players-civ every-enemy gothic)
				(players-civ every-enemy persian)
			)
			(or
				(players-civ every-enemy korean)
				(players-civ every-enemy chinese)
			)
		)
	)
	(building-type-count-total monastery < 5)
	(can-build monastery)
=>
	(build monastery)
)
#end-if
#end-if
#end-if
#load-if-not-defined DEATH-MATCH
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal save-wood-goal 0)
	(current-age >= castle-age)
	(can-build-with-escrow town-center)
	(building-type-count-total town-center < 3)
	(player-in-game any-ally)
	(goal under-attack-goal 1)
	(strategic-number sn-military-level < -2)
	(military-population < 5)
=>
	(release-escrow wood)
	(set-strategic-number sn-town-center-placement lumber-camp)
	(set-strategic-number sn-placement-zone-size 5)
	(set-strategic-number sn-placement-fail-delta 1)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(up-set-placement-data focus-player -1 c: -30)
	(up-build place-control 0 c: town-center)
)
(defrule
	(or
		(and
			(goal strategy-goal fast-imp)
			(and
				(current-age < imperial-age)
				(wood-amount < 400)
			)
		)
		(and
			(goal strategy-goal shot)
			(building-type-count-total siege-workshop == 0)
		)
	)
=>
	(up-jump-rule 2)
)
(defrule
	(goal unit-goal my-unique-unit-line)
	(building-type-count-total castle == 0)
	(building-type-count watch-tower != 2);  2 watch-tower = anti flush defense for uu strat
	(building-type-count gate == 0)
	(goal nr-map-goal -1)
	(unit-type-count villager-stone > 0)
=>
	(up-jump-rule 1)
)
(defrule
	(goal save-wood-goal 0)
	(current-age >= castle-age)
	(can-afford-building town-center)
	(building-type-count-total town-center < 5)
	(goal increase-town-size-goal 0)
=>
	(enable-timer increase-town-size-timer 6)
	(set-goal increase-town-size-goal town-center)
	(set-strategic-number sn-camp-max-distance camp-distance-tc); don't build TC too far waway.
)
(defrule
	(goal increase-town-size-goal town-center)
	(strategic-number sn-current-age == castle)
	(building-type-count town-center >= 1)
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal monk)
	(not (research-available imperial-age))
=>
	(disable-timer increase-town-size-timer)
	(set-goal increase-town-size-goal 0)
)
(defrule
	(up-compare-goal increase-town-size-goal != town-center); don't mess with TC placement
	(game-time > 1500)
	(strategic-number sn-camp-max-distance < camp-distance2)
=>
	(set-strategic-number sn-camp-max-distance camp-distance2)
)
(defrule
	(game-time < 1800)
	(current-age >= castle-age)
	(building-type-count-total town-center >= tc-level-two)	
=>
	(set-strategic-number sn-town-center-placement lumber-camp)
	(set-strategic-number sn-dropsite-separation-distance 10)
)
(defrule
	(building-type-count town-center > 0)	
	(current-age >= castle-age)
	(strategic-number sn-town-center-placement != mining-camp)
	(or
		(and
			(building-type-count-total town-center == 1)
			(dropsite-min-distance stone > 5)
		)
		(and
			(building-type-count-total castle == 0)
			(goal unit-goal my-unique-unit-line)
		)
	)
=>
	(set-strategic-number sn-town-center-placement mining-camp)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(building-type-count town-center > 0)
	(current-age >= castle-age)
	(goal increase-town-size-goal town-center)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal unit-goal archer)
	(goal strategy-goal rush)
	(players-building-type-count any-enemy watch-tower > 0)
	(building-type-count-total siege-workshop < 1)
=>
	(set-goal increase-town-size-goal 0)
)
#end-if
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(building-type-count town-center > 0)
	(goal save-wood-goal 0)
	(goal increase-town-size-goal town-center)
	(building-type-count-total town-center < 4)
	(can-build town-center)
=>
	(up-assign-builders c: 621 c: 4)
	(build town-center)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(wood-amount > 450)
	(building-type-count-total town-center == 4)
	(goal save-wood-goal 0)
	(goal increase-town-size-goal town-center)
	(can-build town-center)
=>
	(up-assign-builders c: 621 c: 1)
	(build town-center)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
#else
(defrule
	(current-age >= castle-age)
	(building-type-count town-center > 0)
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center < 5)
	(can-afford-building town-center)
=>
	(up-assign-builders c: 621 c: 2)
	(set-goal increase-town-size-goal town-center)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(building-type-count town-center > 0)
	(goal increase-town-size-goal town-center)
	(building-type-count-total town-center < 5)
	(can-build town-center)
=>
	(build town-center)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
#end-if
(defrule
	(can-afford-building town-center)
	(timer-triggered increase-town-size-timer)
	(goal increase-town-size-goal town-center)
	(up-pending-objects c: town-center == 0)
	(strategic-number sn-camp-max-distance >= camp-distance-tc)
	(strategic-number sn-camp-max-distance < 35)
=>
	(enable-timer increase-town-size-timer 1)
	(up-modify-sn sn-camp-max-distance c:+ 4)
)
(defrule
	(can-afford-building town-center)
	(timer-triggered increase-town-size-timer)
	(goal increase-town-size-goal town-center)
	(or
		(and
			(up-pending-objects c: town-center == 0)
			(strategic-number sn-camp-max-distance > 35)
		)
		(game-time > 1800)
	)
=>
	(set-strategic-number sn-town-center-placement 0)
)
(defrule
	(or
		(unit-type-count 213 > 0)
		(unit-type-count 225 > 0)
	)
	(up-get-fact 25 213 temp-goal)
	(up-get-fact 25 225 temp-goal2)
=>
	(up-modify-goal temp-goal g:+ temp-goal2); add dead builders
)
(defrule
	(or
		(unit-type-count 213 > 0)
		(unit-type-count 225 > 0)
	)
	(up-compare-goal temp-goal > 3)
	(strategic-number sn-placement-zone-size != 8)
=>
	(set-strategic-number sn-placement-zone-size 8)
	(set-strategic-number sn-placement-fail-delta 0)
	(up-assign-builders c: 621 c: 2)
	(up-assign-builders c: castle c: 2)
	(up-assign-builders c: bombard-tower c: 1)
	(set-strategic-number sn-town-center-placement 0)
	(set-strategic-number sn-percent-building-cancellation 100)
)
(defrule
	(or
		(unit-type-count 213 > 0)
		(unit-type-count 225 > 0)
	)
	(up-compare-goal temp-goal < 3)
	(strategic-number sn-placement-zone-size == 8)
=>
	(set-strategic-number sn-placement-fail-delta 1)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-percent-building-cancellation 70)
	(up-assign-builders c: 621 c: 4)
	(up-assign-builders c: castle c: 4)
)
#else; => DM
#load-if-not-defined POST-IMPERIAL-AGE-START
(defrule
	(goal save-wood-goal 0)
	(building-type-count-total monastery > 0)
	(building-type-count-total siege-workshop > 0)
	(building-type-count-total town-center < 5)
	(goal increase-town-size-goal 0)
	(can-build town-center)
=>
	(build town-center)
)
(defrule
	(unit-type-count villager < 15)
	(goal increase-town-size-goal town-center)
=>
	(set-goal increase-town-size-goal 0)
)
#end-if
#end-if
#end-if; NOT WONDER-RACE
#load-if-not-defined POST-IMPERIAL-AGE-START
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-archer-threat > 1)
	(building-type-count-total siege-workshop < 1)
=>
	(up-jump-rule 2)
)
(defrule
	(up-compare-goal strategy-goal != boom)
	(up-compare-goal strategy-goal != fast-imp)
	(up-compare-goal unit-goal != my-unique-unit-line)
	(up-compare-goal unit-goal != eagle-warrior)
	(building-type-count-total town-center < tc-level-three)
	(current-age-time < 300)
=>
	(up-jump-rule 1)
)
(defrule
	(goal save-wood-goal 0)
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(building-type-count-total town-center > 0)
	(building-type-count-total monastery == 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal monastery)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal save-wood-goal 0)
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(building-type-count-total town-center > 0)
	(goal unit-goal wonder)
	(building-type-count-total monastery == 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal monastery)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal save-wood-goal 0)
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(building-type-count-total town-center > 0)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(building-type-count-total monastery == 0)
	(wood-amount >= 175)
	(food-amount > 500)
	(gold-amount > 400)
=>
	(set-goal increase-town-size-goal monastery)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined DEATH-MATCH
(defrule
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(up-compare-goal increase-town-size-goal != monastery)
	(building-type-count-total siege-workshop > 0)
	(building-type-count-total monastery == 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal monastery)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#else
(defrule
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(building-type-count-total town-center >= tc-level-five)
	(building-type-count-total monastery == 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal monastery)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-not-defined WONDER-RACE
(defrule
	(building-type-count-total town-center == 0); rebuild tc and no stone => get a market and buy some.
	(stone-amount < 100)
	(building-type-count-total market == 0)
	(dropsite-min-distance wood < 10)
	(wood-amount >= 350)
=>
	(set-goal increase-town-size-goal market)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(goal increase-town-size-goal 0)
	(game-time > 1800)
	(building-type-count-total town-center >= tc-level-three)
	(current-age-time > 120)
	(goal save-wood-goal 0)
	(building-type-count-total market == 0)
	(building-type-count-total siege-workshop > 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal market)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(strategic-number sn-current-age == feudal)
	(game-time > 720)
	(current-age-time > 30)
	(building-type-count-total market == 0)
	(or
		(unit-type-count villager-gold == 0)
		(wood-amount > 800)
	)
	(wood-amount >= 300)
	(up-compare-goal unit-goal != skirmisher)
	(up-pending-objects c: barracks == 0)
=>
	(set-goal increase-town-size-goal market)
	(enable-timer increase-town-size-timer 3); unbug
)
(defrule
	(goal increase-town-size-goal 0)
	(or
		(up-research-status c: imperial-age >= research-available)
		(strategic-number sn-resource-control == sling)
	)
	(goal save-wood-goal 0)
	(building-type-count-total market == 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal market)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(wood-amount > 500)		
	(stone-amount < 100)
	(unit-type-count villager-stone == 0)
	(current-age-time > 120)
	(goal save-wood-goal 0)
	(building-type-count-total market == 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal market)
	(enable-timer increase-town-size-timer 3)
)
#load-if-not-defined AZTEC-CIV
#load-if-not-defined INCAN-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined KOREAN-CIV
(defrule
	(goal increase-town-size-goal 0)
	(goal strategy-goal flush)
	(up-players-in-game any < 5)
	(military-population < 12)
	(strategic-number sn-current-age == fc-transit)
	(game-time < 1560); don't build them too late e.g. after slinging
	(building-type-count-total stable < 1)
	(wood-amount >= 245)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(or
		(goal unit-goal scout-cavalry)
		(goal monk-threat-goal 1)
	)
	(building-type-count-total stable < 1)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined BRITON-CIV
(defrule
	(building-type-count-total university > 0)
	(goal increase-town-size-goal 0)
	(research-available ri-man-at-arms)
	(players-unit-type-count every-enemy skirmisher-line > 24)
	(or
		(building-type-count-total stable < 1)
		(unit-type-count-total villager > 90)
	)
	(building-type-count-total stable < 2)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-defined CHINESE-CIV
(defrule
	(goal increase-town-size-goal 0)
	(goal unit-goal my-unique-unit-line)
	(building-type-count-total town-center >= tc-level-four)
	(unit-type-count-total villager > 90)
	(building-type-count-total stable < 2)
	(wood-amount >= 175)
	(food-amount > 600)
	(gold-amount < 75)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined KOREAN-CIV
(defrule
	(goal increase-town-size-goal 0)
	(or
		(or
			(or
				(unit-type-count-total cavalry-class > 11)
				(unit-type-count-total cavalry-archer-class > 11)
			)
			(or
				(unit-type-count-total conquistador-line > 11)
				(unit-type-count-total mameluke-line > 11)
			)
		)
		(strategic-number sn-current-age >= ci-transit)
	)
	(wood-amount >= 175)
	(building-type-count-total stable < 1); bloodlines required
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined KOREAN-CIV
(defrule
	(goal increase-town-size-goal 0)
	(research-completed ri-hussar)
	(food-amount > 1200)
	(wood-amount > 600)
	(building-type-count-total stable < 6)
	(nand
		(research-available ri-heavy-scorpion)
		(unit-type-count scorpion-line > 5)
	)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total university > 0)
	(research-completed ri-light-cavalry)
	(or
		(building-type-count-total stable < 3)
		(and
			(wood-amount > 350)
			(strategic-number sn-current-age >= ci-transit)
		)
	)
	(building-type-count-total stable < 4)
	(wood-amount >= 225)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
#end-if
#end-if
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined VIKING-CIV
(defrule
	(goal increase-town-size-goal 0)
	(goal unit-goal archer)
	(building-type-count-total archery-range > 3)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total stable < 2)
	(research-available ri-man-at-arms)
	(strategic-number sn-current-age >= ci-transit)		
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(goal unit-goal gunpowder-class)
	(building-type-count-total stable < 1)
	(building-type-count-total archery-range > 1)
	(or
		(and
			(civ-selected turkish)
			(strategic-number sn-cavalry-threat > 1); get camels vs cavalry with turks
		)
		(building-type-count-total university > 0)
	)
	(wood-amount >= 175)
	(building-type-count-total archery-range > 4)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(or
		(building-type-count-total town-center >= tc-level-four)
		(or
			(goal monk-threat-goal 1)
			(and
				(goal meso-enemy-goal 1)
				(or
					(goal enemy-goal rush)
					(goal enemy-goal eagle-warrior)
				)
			)
		)
	)
	(building-type-count-total stable < 1)
	(current-age >= castle-age)
	(goal unit-goal skirmisher)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined INDIAN-CIV
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count-total stable < 3)
	(up-research-status c: imperial-age >= research-available)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total stable < 1)
	(or
		(goal unit-goal knight)
		(and
			(goal strategy-goal boom)
			(goal monk-threat-goal 1)
		)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total stable < 3)
	(building-type-count-total blacksmith > 0)
	(goal strategy-goal boom)
	(building-type-count-total town-center > tc-level-three)
	(or
		(goal nr-map-goal -1)
		(strategic-number sn-military-level < -1)
	)
	(goal unit-goal knight)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total stable < 2)
	(building-type-count-total blacksmith > 0)
	(up-compare-goal strategy-goal != boom)
	(or
		(nand
			(goal strategy-goal flush)
			(building-type-count-total town-center < 3)
		)
		(building-type-count-total stable < 1)
	)
	(goal unit-goal knight)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total stable < 1)
	(goal unit-goal cavalry-archer)
	(or
		(building-type-count-total monastery > 0)
		(building-type-count-total town-center >= tc-level-two)
	)
	(building-type-count-total archery-range > 2)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#load-if-not-defined KOREAN-CIV
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined VIKING-CIV
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total university > 0)
	(building-type-count town-center >= tc-level-three)
	(goal unit-goal knight)
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total stable < 6)
	(or
		(wood-amount >= 225)
		(goal strategy-goal fast-imp)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal unit-goal knight)
	(up-compare-goal control-goal != my-unique-unit-line)
	(building-type-count town-center >= tc-level-three)
	(building-type-count-total stable < 2)
	(strategic-number sn-military-level < -2)
	(civilian-population > 55)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(goal strategy-goal flush)
	(building-type-count town-center >= tc-level-two)
	(goal unit-goal default-ranged)
	(or
		(or
			(civ-selected frankish)
			(civ-selected persian)
		)
		(or
			(civ-selected spanish)
			(civ-selected teutonic)
		)
	)
	(building-type-count-total stable < 2)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center >= tc-level-five)
	(building-type-count-total stable < 8)
	(goal unit-goal knight)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-defined DEATH-MATCH
(defrule
	(or
		(building-type-count-total stable < 1)
		(or
			(building-type-count-total blacksmith > 0)
			(current-age >= castle-age)
		)
	)
	(or
		(or
			(building-type-count-total stable < 6)
			(civ-selected hun)
		)
		(building-type-count-total siege-workshop > 0)
	)
	(building-type-count-total stable < number-stables)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-defined FEUDAL-AGE-END
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total stable < 4)
	(wood-amount >= 175)
	(building-type-count-total blacksmith > 0)
	(goal unit-goal scout-cavalry)
=>
	(set-goal increase-town-size-goal stable)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if; NOT AZTEC
#end-if; NOT INCAN
#end-if; NOT MAYAN
#load-if-defined INCAN-CIV
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total archery-range < 2)
	(building-type-count-total monastery > 0)
	(or
		(strategic-number sn-infantry-threat > 1)
		(players-civ focus-player gothic)
	)
	(or
		(building-type-count-total archery-range < 1)
		(building-type-count-total siege-workshop > 0)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total barracks > 2)
	(goal unit-goal eagle-warrior)
	(players-unit-type-count every-enemy eagle-warrior-line < 6)
	(strategic-number sn-cavalry-threat == 0)
	(wood-amount > 350)
	(food-amount > 150)
	(gold-amount < 50)
	(building-type-count-total archery-range < 2)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock > water-mixed)
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total town-center >= tc-level-three)
	(or
		(building-type-count-total archery-range < 1)
		(and
			(building-type-count-total town-center >= tc-level-four)
			(or
				(research-completed ri-crossbow)
				(civ-selected hun)
			)
		)
	)
	(building-type-count-total archery-range < 2)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal strategy-goal flush)
	(or
		(goal enemy-goal flush)
		(and
			(current-age-time > 200)
			(wood-amount > 270)
		)
	)
	(building-type-count-total archery-range < 3)
	(strategic-number sn-current-age == feudal)
	(building-type-count-total blacksmith > 0)
	(wood-amount >= 220)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(current-age == feudal-age)
	(nor 
		(goal unit-goal knight)
		(and
			(goal unit-goal scout-cavalry)
			(goal strategy-goal rush)
		)
	)
	(building-type-count-total market < 1)
	(building-type-count-total archery-range < 1)
	(building-type-count barracks > 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total market > 0)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count-total university > 0)
	(building-type-count-total archery-range < 1)
	(building-type-count barracks > 0)
	(wood-amount >= 175)
	(nand
		(goal unit-goal scorpion)
		(up-research-status c: ri-heavy-scorpion < research-complete)
	)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal strategy-goal flush)
	(building-type-count-total archery-range < 2)
	(building-type-count-total blacksmith > 0)
	(or
		(strategic-number sn-archer-threat > 1)
		(and
			(players-building-type-count any-enemy archery-range > 1)
			(goal enemy-goal skirmisher); enemy probably going for xbow rush
		)
	)
	(strategic-number sn-infantry-threat == 0)
	(strategic-number sn-cavalry-threat == 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal strategy-goal flush)
	(building-type-count barracks > 0)
	(or
		(goal unit-goal archer)
		(goal unit-goal skirmisher)
	)
	(or
		(and
			(building-type-count-total blacksmith > 0)
			(wood-amount >= 235)
		)
		(building-type-count-total archery-range < 2)
	)
	(building-type-count-total archery-range < 3)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal strategy-goal flush)
	(building-type-count barracks > 0)
	(building-type-count-total archery-range < 2)
	(research-available ri-man-at-arms)
	(or
		(building-type-count-total stable == 0)
		(strategic-number sn-current-age >= fc-transit)
	)
	(building-type-count-total archery-range < 3)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(goal strategy-goal rush)
	(goal unit-goal archer)
	(building-type-count-total archery-range == 2)
	(or
		(building-type-count-total town-center < tc-level-four)
		(building-type-count-total siege-workshop < 1)
	)
=>
	(up-jump-rule 1)
)
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total blacksmith > 0)
	(or
		(goal strategy-goal rush)
		(goal strategy-goal flush)
	)
	(building-type-count-total archery-range < 4)
	(or
		(building-type-count-total archery-range < 3)
		(building-type-count-total town-center >= tc-level-three)
	)
	(goal unit-goal archer)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(or
		(goal unit-goal archer)
		(goal unit-goal cavalry-archer)
	)
	(goal strategy-goal boom)
	(building-type-count-total town-center >= tc-level-three)
	(or
		(building-type-count-total archery-range < 3)
		(building-type-count-total town-center >= tc-level-four)	
	)
	(building-type-count-total archery-range < 5)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined VIKING-CIV
(defrule
	(goal increase-town-size-goal 0)
	(goal unit-goal archer)
	(goal strategy-goal boom)
	(building-type-count-total town-center >= 3)
	(or
		(research-available imperial-age)
		(strategic-number sn-current-age >= ci-transit)
	)
	(building-type-count-total archery-range < 8)
	(civilian-population > 80)
	(wood-amount >= 400)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal gunpowder-class)
	(or
		(building-type-count-total archery-range < 2)
		(or
			(building-type-count-total town-center >= 2)
			(civilian-population > fourty-percent-pop)
		)
	)
	(or
		(building-type-count-total archery-range < 4)
		(civilian-population > fifty-percent-pop)
	)
	(building-type-count-total archery-range < 8)	
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(strategic-number sn-current-age >= ci-transit)
	(goal ranged-unit-type-goal gunpowder-class)
	(building-type-count-total university > 0)
	(building-type-count-total archery-range < 5)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(or
		(goal increase-town-size-goal 0)
		(goal increase-town-size-goal monastery)
	)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total town-center >= 1)
	(building-type-count-total archery-range < 2)
	(strategic-number sn-archer-threat >= 1)
	(strategic-number sn-military-level < 0)
	(goal unit-goal skirmisher)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined BYZANTINE-CIV
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total town-center >= tc-level-two)
	(or
		(building-type-count-total archery-range < 2)
		(and
			(building-type-count-total town-center >= 3)
			(strategic-number sn-cavalry-threat < 2)
		)
	)
	(or
		(strategic-number sn-cavalry-threat < 1)
		(building-type-count-total barracks > 1)
	)
	(building-type-count-total archery-range < 4)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#else
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total archery-range < 2)
	(or
		(strategic-number sn-cavalry-threat < 1)
		(building-type-count-total barracks > 1)
	)
	(goal unit-goal skirmisher)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-not-defined CELTIC-CIV
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined BRITON-CIV
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined VIKING-CIV
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal unit-goal knight)
	(goal strategy-goal rush)
	(strategic-number sn-archer-threat > 1)
	(building-type-count-total town-center < tc-level-three)
	(building-type-count-total siege-workshop > 0)
	(unit-type-count mangonel-line == 0)
	(current-age >= castle-age)
=>
	(up-jump-rule 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal unit-goal knight)
	(goal strategy-goal rush)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count siege-workshop == 0)
	(current-age >= castle-age)
	(goal anti-cavalry-threat-goal 0)
=>
	(up-jump-rule 2)
)
#end-if
#end-if
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal unit-goal knight)
	(goal strategy-goal rush)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(goal enemy-goal eagle-warrior); he will come with pikes
	)
	(or
		(research-completed ri-crossbow)
		(building-type-count-total archery-range < 1)
	)
	(building-type-count-total archery-range < 2)
	(wood-amount >= 175)
	(current-age >= castle-age)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal unit-goal knight)
	(building-type-count-total town-center >= tc-level-two)
	(or
		(or
			(goal anti-cavalry-threat-goal 1)
			(strategic-number sn-archer-threat > 1)
		)
		(or
			(goal enemy-goal flush)
			(goal enemy-goal skirmisher)
		)
	)
	(building-type-count gate < 1)
	(building-type-count-total archery-range < 1)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(goal unit-goal knight)
	(players-unit-type-count any-enemy spearman-line > 6)
	(building-type-count-total archery-range < 1)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
#end-if
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total blacksmith > 0)
	(or
		(building-type-count-total stable > 0)
		(building-type-count-total archery-range < 3)
	)
	(or
		(building-type-count-total siege-workshop > 0)
		(building-type-count-total archery-range < 4)
	)
	(building-type-count-total archery-range < 6)
	(goal unit-goal cavalry-archer)
	(goal control-goal aggressive-rush)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal unit-goal knight)
	(goal strategy-goal rush)
	(or
		(research-completed ri-elite-skirmisher)
		(unit-type-count hand-cannoneer > 2)
	)
	(building-type-count-total town-center >= tc-level-four)
	(building-type-count-total archery-range < 4)
	(not (goal control-goal my-unique-unit-line))
	(wood-amount >= 500)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#load-if-not-defined POPULATION-CAP-25
#load-if-not-defined POPULATION-CAP-50
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total university > 0)
	(building-type-count-total siege-workshop > 0)
	(research-completed ri-elite-skirmisher)
	(wood-amount > 500); excess wood
	(nand
		(player-in-game any-ally)
		(unit-type-count-total trade-cart < 25)
	)
	(nand
		(unit-type-count scorpion-line > min-number-upgrade)
		(up-research-status c: ri-heavy-scorpion < research-pending)
	)
	(building-type-count-total archery-range < 6)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
#end-if
#end-if
(defrule
	(goal increase-town-size-goal archery-range)
	(nor
		(goal ranged-unit-type-goal archer)
		(goal ranged-unit-type-goal cavalry-archer)
	)
	(wood-amount < 800)
	(building-type-count-total archery-range > 3)
	(strategic-number sn-archer-threat < 3)
=>
	(set-goal increase-town-size-goal 0)
)
#load-if-defined DEATH-MATCH
(defrule
	(game-time > 60)
	(or
		(building-type-count-total archery-range < 1)
		(building-type-count-total blacksmith > 0)
	)
	(or
		(or
			(building-type-count-total archery-range < 4)
			(civ-selected hun)
		)
		(building-type-count-total university > 0)
	)
	(building-type-count-total archery-range < number-archery-ranges)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-defined FEUDAL-AGE-END
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total archery-range < 8)
	(wood-amount >= 175)
	(building-type-count-total blacksmith > 0)
=>
	(set-goal increase-town-size-goal archery-range)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(or
		(or
			(building-type-count barracks == 0); prerequisits missing
			(current-age == dark-age)
		)
		(goal save-wood-goal 1)
	)
	(or
		(goal increase-town-size-goal archery-range)
		(goal increase-town-size-goal stable)
	)
=>
	(set-goal increase-town-size-goal 0)
)
(defrule
	(goal increase-town-size-goal 0)
	(or
		(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
		(and
			(strategic-number sn-current-age >= fc-transit)
			(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
		)
	)
	(strategic-number sn-current-age >= df-transit)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(goal strategy-goal flush)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age >= df-transit)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(goal strategy-goal rush)
	(nor
		(goal unit-goal monk)
		(or
			(goal unit-goal eagle-warrior)
			(goal unit-goal my-unique-unit-line)
		)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
	(chat-local-to-self "build a barracks")
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(goal unit-goal monk)
	(or
		(or
			(goal anti-monk-threat-goal 1)
			(strategic-number sn-cavalry-threat > 1)
		)
		(building-type-count town-center >= tc-level-three)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age >= df-transit)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(goal strategy-goal boom)
	(or
		(strategic-number sn-cavalry-threat > 1)
		(goal monk-threat-goal 1)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age >= df-transit)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(building-type-count blacksmith > 0)
	(building-type-count-total watch-tower > 1)
	(goal unit-goal my-unique-unit-line)
	(or
		(players-unit-type-count focus-player scout-cavalry > 3)
		(players-building-type-count focus-player stable > 0)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#else
(defrule
	(goal nr-map-goal 1)
	(strategic-number sn-current-age < ci-transit)
	(or
		(goal strategy-goal boom)
		(goal strategy-goal fast-imp)
	)
	(or
		(or
			(goal unit-goal knight)
			(goal unit-goal monk)
		)
		(or
			(goal unit-goal militiaman-line)
			(goal unit-goal gunpowder-class)
		)
	)
=>
	(up-jump-rule 14)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(or
		(goal unit-goal monk)
		(map-type gold-rush)
	)
	(or
		(goal anti-monk-threat-goal 1)
		(building-type-count town-center >= tc-level-three)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total town-center < tc-level-four)
	(building-type-count-total barracks > 0)
	(goal unit-goal knight)
	(goal strategy-goal boom)
=>
	(up-jump-rule 12)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total town-center < tc-level-four)
	(goal unit-goal my-unique-unit-line)
	(goal strategy-goal boom)
	(or
		(or
			(or
				(civ-selected mongol)
				(civ-selected saracen)
			)
			(civ-selected spanish)
		)
		(or
			(civ-selected italian)
			(civ-selected teutonic)
		)
	)
=>
	(up-jump-rule 11)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age >= df-transit)
	(building-type-count-total barracks < 1)
	(or
		(goal unit-goal my-unique-unit-line)
		(goal unit-goal scorpion)
	)
	(strategic-number sn-cavalry-threat > 1)
	(building-type-count-total town-center >= tc-level-two)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age >= df-transit)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(goal strategy-goal boom)
	(nor
		(or 
			(goal unit-goal my-unique-unit-line)
			(goal unit-goal scorpion)
		)
		(or
			(goal unit-goal militiaman-line)
			(goal unit-goal monk)
		)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total barracks < 3)
	(building-type-count-total blacksmith > 0)
	(building-type-count town-center > 0)
	(or
		(goal strategy-goal boom)
		(goal strategy-goal fast-imp)
	)
	(building-type-count-total siege-workshop > 0)
	(goal unit-goal eagle-warrior)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total barracks < 1)
	(building-type-count town-center > 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(goal unit-goal militiaman-line)
		(goal control-goal militiaman-line)
	)
	(up-research-status c: imperial-age >= research-available)
	(building-type-count-total barracks < 6)
	(wood-amount >= 225)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock > water-mixed)
	(up-research-status c: imperial-age >= research-available)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total market > 0)
	(or
		(building-type-count-total barracks < 1)
		(and
			(building-type-count-total town-center >= tc-level-four)
			(or
				(up-research-status c: ri-long-swordsman >= research-pending)
				(unit-available eagle-warrior-line)
			)
		)
	)
	(building-type-count-total barracks < 3)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)

#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined POPULATION-CAP-25
#load-if-not-defined POPULATION-CAP-50
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total monastery > 0)
	(building-type-count-total town-center >= tc-level-two)
	(strategic-number sn-cavalry-threat > 0)
	(or
		(building-type-count-total barracks < 2)		
		(and
			(building-type-count-total town-center >= tc-level-four)
			(research-completed ri-pikeman)
		)
	)
	(building-type-count-total barracks < 4)
	(goal unit-goal skirmisher)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined UP-1-PLAYER-TEAM
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(strategic-number sn-cavalry-threat > 1); dont care about TC if enemy has lots of knights
	)
	(or
		(building-type-count-total barracks < 2)
		(building-type-count-total town-center >= tc-level-two)
	)
	(building-type-count-total barracks < 3)
	(research-completed ri-pikeman)
	(goal strategy-goal rush)
	(unit-type-count spearman-line > 4)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#else
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(building-type-count-total barracks < 2)
	)
	(strategic-number sn-cavalry-threat > 1)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total barracks < 2)
	(research-completed ri-pikeman)
	(goal strategy-goal rush)
	(unit-type-count spearman-line > 4)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#end-if
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total town-center >= tc-level-three)
	(or
		(building-type-count-total barracks < 2)
		(building-type-count-total university > 0)
	)
	(strategic-number sn-cavalry-threat > 1)
	(building-type-count-total barracks < 3)
	(research-completed ri-pikeman)
	(unit-type-count spearman-line > 4)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total barracks < 3)
	(or
		(building-type-count-total barracks < 2)
		(building-type-count-total monastery > 0)
	)
	(goal unit-goal eagle-warrior)
	(goal strategy-goal rush)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count-total barracks < 4)
	(or
		(research-completed ri-halberdier)
		(research-completed ri-anarchy)
	)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count-total barracks < 6)
	(wood-amount > 220)
	(or
		(goal unit-goal eagle-warrior)
		(or
			(research-completed ri-halberdier)
			(research-completed ri-anarchy)
		)
	)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal barracks)
	(goal strategy-goal boom)
	(goal unit-goal eagle-warrior)
	(or
		(building-type-count-total town-center < tc-level-three)
		(building-type-count-total siege-workshop < 1)
	)
	(nand
		(and
			(players-unit-type-count focus-player scout-cavalry > 1)
			(strategic-number sn-current-age == fc-transit)
		)
		(not (player-in-game ally))
	)
=>
	(set-goal increase-town-size-goal 0)
	(disable-timer increase-town-size-timer)
)
(defrule
	(goal increase-town-size-goal barracks)
	(goal strategy-goal fast-imp)
	(building-type-count-total barracks > 0)
	(not (research-available imperial-age))
=>
	(set-goal increase-town-size-goal 0)
	(disable-timer increase-town-size-timer)
)
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(or 
		(and
			(goal enemy-goal eagle-warrior)
			(current-age == feudal-age)
		)
		(and
			(research-completed ri-elite-skirmisher)
			(building-type-count-total town-center >= tc-level-two)
		)
	)
	(building-type-count-total barracks < 2)
	(building-type-count-total blacksmith > 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(goal strategy-goal rush)
	(goal unit-goal militiaman-line)
	(building-type-count-total barracks < 2)
	(building-type-count-total blacksmith > 0)
	(research-completed ri-double-bit-axe)
	(research-completed ri-horse-collar)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal increase-town-size-goal 0)
	(or
		(and
			(goal strategy-goal flush)
			(strategic-number sn-cavalry-threat > 1)
		)
		(and
			(population-cap < 75)
			(research-completed ri-pikeman)
		)
	)
	(building-type-count-total barracks < 2)
	(building-type-count-total blacksmith > 0)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-defined UP-MICHI-STYLE
(defrule
	(goal increase-town-size-goal barracks)
	(strategic-number sn-current-age < ci-transit)
=>
	(set-goal increase-town-size-goal 0)
	(disable-timer increase-town-size-timer)
)
#end-if
#load-if-defined DEATH-MATCH
#load-if-defined DARK-AGE-START
(defrule
	(or
		(building-type-count-total barracks < 1)
		(or
			(or
				(building-type-count-total lumber-camp > 0)
				(building-type-count-total mill > 0)
			)
			(building-type-count-total blacksmith > 0)
		)
	)
	(or
		(building-type-count-total barracks < 5)
		(building-type-count-total siege-workshop > 0)
	)
	(building-type-count-total barracks < number-barracks)
	(wood-amount >= 175)
	(unit-type-count villager g:> temp-goal5)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
	(up-modify-goal temp-goal5 c:+ 1)
)
#load-if-not-defined UP-MULTIPLAYER-GAME
(defrule
	(game-time < 120)
	(goal increase-town-size-goal barracks)
=>	
	(set-goal increase-town-size-goal 2)
	(up-build place-control 0 c: barracks)
	(up-set-placement-data my-player-number villager-class c: 2)
)
#end-if
#end-if
#end-if
#load-if-defined DARK-AGE-END
(defrule
	(goal increase-town-size-goal 0)
	(building-type-count-total barracks < 12)
	(wood-amount >= 175)
	(unit-type-count-total villager > 13)
	(building-type-count-total mill > 0)
	(building-type-count-total lumber-camp > 0)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#end-if
#load-if-defined FEUDAL-AGE-END
(defrule
	(goal increase-town-size-goal 0)
	(or
		(building-type-count-total barracks < 2)
		(research-completed ri-man-at-arms)
	)
	(building-type-count-total barracks < 8)
	(wood-amount >= 175)
	(building-type-count-total blacksmith > 0)
=>
	(set-goal increase-town-size-goal barracks)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(or
		(goal increase-town-size-goal siege-workshop)
		(or
			(or
				(goal increase-town-size-goal barracks)
				(goal increase-town-size-goal archery-range)
			)
			(goal increase-town-size-goal stable)
		)
	)
	(building-type-count-total town-center == 0)
=>
	(set-goal increase-town-size-goal 0)
)
(defrule
	(wood-amount < 200)
	(or
		(goal increase-town-size-goal siege-workshop)
		(and
			(wood-amount < 175)
			(or
				(or
					(goal increase-town-size-goal barracks)
					(goal increase-town-size-goal archery-range)
				)
				(goal increase-town-size-goal stable)
			)
		)
	)
=>
	(set-goal increase-town-size-goal 0)
	(chat-local-to-self "not enough wood to build")
)
(defrule; don't build if we save
	(or
		(strategic-number sn-resource-control == 3); hand-cart upgrade
		(or
			(and
				(strategic-number sn-resource-control == ri-elite-skirmisher)
				(building-type-count archery-range > 0)
			)
			(and
				(or
					(strategic-number sn-resource-control == battering-ram)
					(goal save-wood-goal 1)
				)
				(building-type-count siege-workshop > 0)
			)
		)
	)
=>
	(set-goal increase-town-size-goal 0)
)
#end-if; NOT WONDER-RACE
(defrule
	(strategic-number sn-resource-control < 2)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(goal strategy-goal flush)
	(food-amount > 600)
	(building-type-count-total market == 0)
	(current-age-time > 30)
	(building-type-count dock > 0)
=>
	(set-strategic-number sn-resource-control 2); block training galleys
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(goal strategy-goal flush)
	(or
		(food-amount < 450)
		(current-age-time < 30)
	)
	(building-type-count dock > 0)
=>
	(up-jump-rule 1)
)
(defrule
	(goal increase-town-size-goal 0)
	(strategic-number sn-current-age == feudal)
	(or
		(building-type-count-total barracks == 0); no rax => market+bs start
		(goal strategy-goal sling)
	)
	(building-type-count-total market < 1)
	(wood-amount >= 175)
=>
	(set-goal increase-town-size-goal market)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal market)
	(or
		(or
			(building-type-count-total market > 0)
			(wood-amount < 175)
		)
		(or
			(strategic-number sn-current-age == fc-transit)
			(and
				(strategic-number sn-resource-control == ri-elite-skirmisher)
				(wood-amount < 375)
			)
		)
	)
=>
	(set-goal increase-town-size-goal 0)
)
(defrule
	(goal increase-town-size-goal barracks)
	(goal enemy-boats-goal 1)
	(goal strategy-goal flush)
	(strategic-number sn-current-age >= df-transit)
	(building-type-count-total dock < 2)
	(players-building-type-count every-enemy barracks == 0)
	(players-building-type-count every-enemy archery-range == 0)
	(players-building-type-count every-enemy stable == 0)
=>
	(set-goal increase-town-size-goal 0); skip barracks for more boats
	(disable-timer increase-town-size-timer)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(goal increase-town-size-goal 0)
	(current-age == imperial-age)
	(current-age-time > 40)
	(current-age-time < 300)
	(building-type-count-total blacksmith < 2)
	(can-afford-building blacksmith)
=>
	(enable-timer increase-town-size-timer 3)
	(set-goal increase-town-size-goal blacksmith)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(goal strategy-goal flush)
	(goal increase-town-size-goal 0)
	(building-type-count town-center > 0)
	(building-type-count-total blacksmith < 1)
	(building-type-count dock > 0)
	(can-afford-building blacksmith)
=>
	(enable-timer increase-town-size-timer 3)
	(set-goal increase-town-size-goal blacksmith)
)
(defrule
	(up-compare-goal strategy-goal != flush)
	(goal increase-town-size-goal 0)
	(food-amount > 300)
	(nand
		(goal strategy-goal sling)
		(current-age-time < 600)
	)
	(building-type-count town-center > 0)
	(or 
		(or
			(building-type-count-total stable > 0)
			(building-type-count-total archery-range > 0)
		)
		(building-type-count-total market > 0)
	)
	(building-type-count-total blacksmith < number-blacksmiths)
	(can-afford-building blacksmith)
=>
	(enable-timer increase-town-size-timer 3)
	(set-goal increase-town-size-goal blacksmith)
)
(defrule
	(goal strategy-goal flush)
	(goal increase-town-size-goal 0)
	(or
		(or
			(and
				(building-type-count-total stable > 0)
				(building-type-count-total archery-range > 0)
			)
			(research-completed ri-man-at-arms)
		)
		(or
			(building-type-count-total archery-range > 1)
			(building-type-count-total market > 0); viking navy strategy
		)
	)
	(building-type-count-total blacksmith < 1)
	(can-afford-building blacksmith)
=>
	(enable-timer increase-town-size-timer 3)
	(set-goal increase-town-size-goal blacksmith)
)
#load-if-defined DEATH-MATCH
(defrule
	(goal increase-town-size-goal blacksmith)
	(building-type-count town-center > 0)
	(can-build blacksmith)
=>
	(build blacksmith)
	(set-goal increase-town-size-goal 2)
)
#else
(defrule
	(goal increase-town-size-goal blacksmith)
	(building-type-count town-center > 0)
	(can-build blacksmith)
	(or
		(goal strategy-goal flush)
		(up-compare-goal under-attack-goal != 0)
	)
=>
	(build blacksmith)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(goal increase-town-size-goal blacksmith)
	(building-type-count town-center > 0)
	(can-build blacksmith)
=>
	(set-goal increase-town-size-goal 2)
	(set-strategic-number sn-placement-fail-delta 1)
	(set-strategic-number sn-placement-zone-size 7)
	(up-set-placement-data my-player-number lumber-camp c: 2)
	(up-build place-control 0 c: blacksmith)
)
#end-if
#end-if
(defrule
	(current-age >= castle-age)
	(or
		(goal increase-town-size-goal market)
		(goal increase-town-size-goal barracks)
	)
	(wood-amount < 400)
	(goal save-wood-goal 1)
=>
	(set-goal increase-town-size-goal 0)
)
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-target-player-number g:== threat-player-goal)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-maximum-town-size >= 60)
	(research-completed ri-heavy-scorpion)
	(gold-amount > 500)
	(wood-amount > 600)
	(building-type-count-total siege-workshop < 5)
	(can-build siege-workshop)
=>
	(build-forward siege-workshop)
)
(defrule
	(goal team-coordination-goal 1)
	(population >= del-civ-pop)
	(building-type-count siege-workshop > 4)
=>	
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 39)
	(up-find-local c: siege-workshop c: 240)
	(up-target-point 0 action-stop -1 -1)
)
#end-if
#load-if-defined GOLD-RUSH-MAP
(defrule
	(can-build castle)
	(up-pending-objects c: castle < 2)
	(game-time > 480)
	(nand
		(building-type-count-total castle == 0)
		(goal unit-goal my-unique-unit-line)
	)
	(or
		(strategic-number sn-military-level > 0)
		(goal team-coordination-goal 1)
	)
=>
	(up-copy-point p1x-goal position-center-goal)
	(up-set-target-point p1x-goal)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
	(up-build place-point 0 c: castle)
)
#end-if
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(death-match-game)
	(game-time < 300)
=>
	(set-goal increase-town-size-goal 0)
)
#end-if
(defrule
	(goal increase-town-size-goal market)
	(strategic-number sn-maximum-town-size >= 40)
	(up-can-build 0 g: increase-town-size-goal)
=>
	(up-set-placement-data focus-player -1 c: -120)
	(set-strategic-number sn-placement-zone-size 3)
	(set-strategic-number sn-placement-fail-delta 0)
	(set-strategic-number sn-placement-to-center 1)
	(up-build place-control 0 c: market)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(up-compare-goal increase-town-size-goal != town-center)
	(up-compare-goal increase-town-size-goal != 0)
	(up-compare-goal increase-town-size-goal != 2)
	(strategic-number sn-maximum-town-size >= 40); TSA going on
	(up-can-build 0 g: increase-town-size-goal); use the UP placement system to avoid spreading stuff all over the map
=>
	(up-set-placement-data my-player-number -1 c: 8)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
	(up-build place-control 0 g: increase-town-size-goal)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(building-type-count-total monastery == 0)
	(goal increase-town-size-goal monastery)
	(can-build monastery)
=>
	(build monastery)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(goal increase-town-size-goal market)
	(building-type-count-total market < 1)
	(can-build-with-escrow market)
=>
	(release-escrow wood)
	(build market)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(goal increase-town-size-goal stable)
	(can-build stable)
=>
	(build stable)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(goal increase-town-size-goal barracks)
	(can-build barracks)
=>
	(build barracks)
	(set-goal increase-town-size-goal 2)
)
(defrule
	(goal increase-town-size-goal archery-range)
	(can-build archery-range)
=>
	(build archery-range)
	(set-goal increase-town-size-goal 2)
)

#load-if-not-defined POST-IMPERIAL-AGE-START
#load-if-not-defined IMPERIAL-AGE-START
#load-if-not-defined CASTLE-AGE-START
#load-if-not-defined HIGH-RESOURCES-START
#load-if-not-defined ARENA-MAP
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(can-build watch-tower)
	(game-time > 840)
	(game-time < 960)
	(up-players-in-game ally > 2)
	(players-building-type-count any-ally barracks == 0)
	(building-type-count-total watch-tower == 0)
	(goal strategy-goal flush)
=>
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
)
(defrule
	(can-build watch-tower)
	(game-time > 840)
	(game-time < 960)
	(up-players-in-game ally > 2)
	(players-building-type-count focus-player barracks == 0)
	(building-type-count-total watch-tower == 0)
	(goal strategy-goal flush)
=>
	(set-goal increase-town-size-goal watch-tower)
	(up-assign-builders c: watch-tower c: 4)
)
(defrule
	(true)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(or
		(and
			(up-players-in-game ally < 2)
			(map-type oasis); can't tower both sides
		)
		(building-type-count gate > 0)
	)
=>
	(up-jump-rule 8)
)
(defrule
	(can-build watch-tower)
	(or
		(and
			(game-time < 1020)
			(building-type-count-total watch-tower == 0)
		)
		(and
			(goal unit-goal my-unique-unit-line)
			(strategic-number sn-current-age == fc-transit)
		)
	)
	(building-type-count-total watch-tower < 2)
	(players-military-population any-enemy > 3)
	(up-compare-goal strategy-goal != flush)
	(goal enemy-goal flush)
=>
	(set-goal increase-town-size-goal watch-tower)
	(up-assign-builders c: watch-tower c: 4)
)
(defrule
	(can-build watch-tower)
	(or
		(goal unit-goal my-unique-unit-line)
		(and
			(game-time < 990)
			(military-population < 2)
		)
	)
	(strategic-number sn-current-age <= fc-transit)
	(or
		(players-military-population focus-player > 5)
		(and
			(players-military-population any-enemy > 5)
			(up-players-in-game ally == 1)
		)
	)
	(building-type-count-total watch-tower == 0)
=>
	(set-goal increase-town-size-goal watch-tower)
	(up-assign-builders c: watch-tower c: 4)
)
(defrule
	(game-time < 1380)
	(can-build watch-tower)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(building-type-count-total watch-tower < 4)	
	(strategic-number sn-military-level < 0)	
=>
	(set-goal increase-town-size-goal watch-tower)
	(up-assign-builders c: watch-tower c: 4)
)
(defrule
	(can-build watch-tower)
	(current-age == feudal-age)
	(building-type-count-total watch-tower == 1)
	(goal unit-goal my-unique-unit-line)
=>
	(set-goal increase-town-size-goal watch-tower)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(players-military-population focus-player > 3)
	(players-building-type-count focus-player barracks > 0)
	(up-player-distance focus-player < 70)
	(game-time < 960)
	(can-build watch-tower)
	(building-type-count watch-tower == 0)
	(unit-type-count fishing-ship > 0)
=>
	(set-goal increase-town-size-goal watch-tower)
)
(defrule
	(building-type-count-total watch-tower == 0)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal archer)
	(or
		(players-unit-type-count focus-player skirmisher-line > 5)
		(strategic-number sn-archer-threat > 1)
	)
	(players-unit-type-count focus-player militiaman-line < 2)
	(goal increase-town-size-goal 0)
	(can-build watch-tower)
=>
	(set-goal increase-town-size-goal watch-tower)
)
(defrule
	(or
		(up-player-distance focus-player > 70)
		(players-building-count every-enemy == 0)
	)
	(goal increase-town-size-goal watch-tower)
=>
	(set-goal increase-town-size-goal 0)
)
(defrule
	(game-time > 750)
	(game-time < 900)
	(current-age-time < 10); slow flush
	(can-build watch-tower)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(building-type-count-total watch-tower == 0)
	(military-population < 2)
	(strategic-number sn-mill-max-distance < 30)
=>
	(set-goal increase-town-size-goal watch-tower)
	(up-assign-builders c: watch-tower c: 4)
)
#end-if; not pocket
(defrule
	(can-build watch-tower)
	(strategic-number sn-current-age == feudal)
	(building-type-count-total market > 0)
	(building-type-count-total watch-tower < 3)
	(building-type-count gate < 1)
	(goal strategy-goal sling)
=>
	(set-goal increase-town-size-goal watch-tower)
)
#end-if; not arena
#end-if; not high-res start
#end-if; not castle age start
#end-if; not imp start
#load-if-defined DEATH-MATCH
(defrule
	(game-time > 60)
=>
	(set-strategic-number sn-maximum-town-size 12)
	(disable-self)
)
(defrule
	(game-time > 120)
=>
	(set-strategic-number sn-maximum-town-size 14)
	(set-strategic-number sn-maximum-wood-drop-distance 30)
	(disable-self)
)
#load-if-not-defined FEUDAL-AGE-START
(defrule
	(current-age == feudal-age)
	(current-age-time > 20)
=>
	(set-strategic-number sn-maximum-town-size 14)
)
(defrule
	(current-age == feudal-age)
	(current-age-time > 90)
=>
	(set-strategic-number sn-maximum-town-size 16)
)
(defrule
	(current-age == feudal-age)
	(current-age-time > 180)
=>
	(set-strategic-number sn-maximum-town-size 20)
)
#end-if
#end-if
#else
#load-if-not-defined UP-MULTIPLAYER-GAME
(defrule
	(game-time < 90)
=>
	(set-strategic-number sn-maximum-town-size 8)
	(set-strategic-number sn-percent-building-cancellation 5)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-wood-drop-distance 30)
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(set-strategic-number sn-maximum-hunt-drop-distance 24)
	(set-strategic-number sn-gold-gatherer-percentage 100)
	(set-strategic-number sn-cap-civilian-builders 100)
	(set-strategic-number sn-cap-civilian-gatherers 100)(disable-self)
)
#else
(defrule
	(game-time < 90)
=>
	(set-strategic-number sn-maximum-town-size 8); don't allow to gather resources when MP lag prevents AI commands
	(set-strategic-number sn-percent-building-cancellation 5)(disable-self)
)
#end-if
(defrule
	(game-time > 90)
=>
	(set-strategic-number sn-maximum-town-size 10)(disable-self)
)
(defrule
	(game-time > 120)
=>
	(set-strategic-number sn-maximum-town-size 12)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-wood-drop-distance 30)
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(set-strategic-number sn-maximum-hunt-drop-distance 24)
	(set-strategic-number sn-gold-gatherer-percentage 100)
	(set-strategic-number sn-cap-civilian-builders 100)
	(set-strategic-number sn-cap-civilian-gatherers 100)(disable-self)
)
(defrule
	(game-time > 180)
=>
	(set-strategic-number sn-maximum-town-size 15)(disable-self)
)
(defrule
	(game-time > 240)
=>
	(set-strategic-number sn-maximum-town-size 20)
	(set-strategic-number sn-camp-max-distance 50)(disable-self)
)
(defrule
	(game-time > 300)
	(strategic-number sn-maximum-town-size < 26)
=>
	(set-strategic-number sn-maximum-town-size 26)
	(set-strategic-number sn-percent-building-cancellation 75)(disable-self)
)
(defrule
	(game-time > 480)
	(strategic-number sn-maximum-town-size < 30)
=>
	(set-strategic-number sn-maximum-town-size 30)(disable-self)
)
#end-if
#load-if-not-defined POST-IMPERIAL-AGE-START
(defrule
	(current-age >= castle-age)
	(building-type-count-total town-center >= tc-level-three)
	(unit-type-count-total villager >= fifty-percent-pop)
	(can-afford-building university)
	(building-type-count-total university == 0)
	(goal increase-town-size-goal 0)
=>
	(set-goal increase-town-size-goal university)
	(enable-timer increase-town-size-timer 3)
)
(defrule
	(goal increase-town-size-goal 0)
	(current-age >= castle-age)
	(building-type-count-total town-center > 0)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(building-type-count-total monastery > 0)
	(building-type-count-total university == 0)
	(wood-amount >= 200)
	(food-amount > 500)
	(gold-amount > 400)
=>
	(set-goal increase-town-size-goal university)
	(enable-timer increase-town-size-timer 3)
)
#load-if-not-defined TURKISH-CIV
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(or
		(goal unit-goal gunpowder-class)
		(goal ranged-unit-type-goal gunpowder-class)
	)
	(wood-amount >= 200)
	(building-type-count-total university == 0)
	(goal increase-town-size-goal 0)
=>
	(set-goal increase-town-size-goal university)
	(enable-timer increase-town-size-timer 3)
)
#end-if
(defrule
	(or
		(or 
			(building-type-count-total university > 0)
			(wood-amount < 200)
		)
		(or
			(up-compare-goal save-wood-goal != 0)
			(and
				(goal strategy-goal boom)
				(and
					(building-type-count-total town-center < tc-level-four)
					(or
						(food-amount < 600)
						(gold-amount < 500)
					)
				)
			)
		)
	)
	(goal increase-town-size-goal university)
=>
	(set-goal increase-town-size-goal 0)
)
(defrule
	(goal increase-town-size-goal university)
	(building-type-count-total university < 1)
	(can-build university)
=>
	(build university)
	(set-goal increase-town-size-goal 2)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(building-type-count-total dock < 12)
	(can-build dock)
=>
	(build dock)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(not (can-build dock))
	(unit-type-count eagle-warrior-line > 0)
	(building-type-count-total barracks == 0)
	(can-build barracks)
=>
	(build barracks)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(not (can-build dock))
	(building-type-count-total stable == 0)
	(can-build stable)
=>
	(build stable)
)
(defrule
	(building-type-count town-center > 0)
	(unit-type-count villager > villager-siege-workshop)
	(building-type-count-total siege-workshop < 3)
	(can-build siege-workshop)
=>
	(build siege-workshop)
)
(defrule
	(or 
		(and
			(unit-type-count villager >= villager-monastery)
			(or
				(goal position-goal pocket)
				(not (player-in-game any-ally))
			)
		)
		(unit-type-count villager >= 18)
	)
	(can-build monastery)
	(building-type-count-total monastery < 1)
=>
	(build monastery)
)
(defrule
	(unit-type-count villager >= 13)
	(can-build town-center)
	(building-type-count-total town-center < 6)
=>
	(build town-center)
)
#load-if-not-defined UP-MULTIPLAYER-GAME
(defrule
	(goal under-attack-goal 0)
	(unit-type-count villager g:> temp-goal5)
	(game-time < 120)
	(not (up-pending-placement c: stable))
	(can-build stable)
	(unit-type-count villager >= villager-stable)
	(building-type-count-total stable < number-stables)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: stable)
	(up-modify-goal temp-goal5 c:+ 1)
)
#load-if-defined BRITON-CIV
(defrule
	(game-time < 180)
	(unit-type-count villager g:> temp-goal5)
	(not (up-pending-placement c: archery-range))
	(can-build archery-range)
	(building-type-count-total archery-range < 4)
	(nor
		(goal meso-enemy-goal 1)
		(strategic-number sn-cavalry-threat > 0)
	)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: archery-range)
	(up-modify-goal temp-goal5 c:+ 1)
	(up-jump-rule 4)
)
#end-if
#load-if-defined JAPANESE-CIV
(defrule
	(game-time < 180)
	(unit-type-count villager g:> temp-goal5)
	(not (up-pending-placement c: archery-range))
	(can-build archery-range)
	(building-type-count-total archery-range < 4)
	(nor
		(goal meso-enemy-goal 1)
		(strategic-number sn-cavalry-threat > 0)
	)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: archery-range)
	(up-modify-goal temp-goal5 c:+ 1)
	(up-jump-rule 4)
)
#end-if
#load-if-defined KOREAN-CIV
(defrule
	(game-time < 180)
	(unit-type-count villager g:> temp-goal5)
	(not (up-pending-placement c: archery-range))
	(can-build archery-range)
	(building-type-count-total archery-range < 4)
	(nor
		(goal meso-enemy-goal 1)
		(strategic-number sn-cavalry-threat > 0)
	)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: archery-range)
	(up-modify-goal temp-goal5 c:+ 1)
	(up-jump-rule 4)
)
#end-if
#load-if-defined VIKING-CIV
(defrule
	(game-time < 180)
	(unit-type-count villager g:> temp-goal5)
	(not (up-pending-placement c: archery-range))
	(can-build archery-range)
	(building-type-count-total archery-range < 4)
	(nor
		(goal meso-enemy-goal 1)
		(strategic-number sn-cavalry-threat > 0)
	)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: archery-range)
	(up-modify-goal temp-goal5 c:+ 1)
	(up-jump-rule 4)
)
#end-if
(defrule
	(unit-type-count villager g:> temp-goal5)
	(game-time < 120)
	(goal under-attack-goal 0)
	(not (up-pending-placement c: archery-range))
	(can-build archery-range)
	(building-type-count-total archery-range < number-archery-ranges)
	(or
		(unit-type-count villager >= villager-archery-range)
		(and
			(building-type-count-total stable >= number-stables)
			(civ-selected hun)
		)
	)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: archery-range)
	(up-modify-goal temp-goal5 c:+ 1)
)
#load-if-defined GOTHIC-CIV
(defrule
	(game-time < 300)
	(or
		(game-time < 150)
		(up-pending-objects c: house < 4)
	)
	(up-pending-objects c: house < 3)
	(building-type-count-total barracks > 1)
=>
	(up-jump-rule 4)
)
#end-if
(defrule
	(unit-type-count villager g:> temp-goal5)
	(game-time < 120)
	(goal under-attack-goal 0)
	(not (up-pending-placement c: barracks))
	(can-build barracks)
	(or
		(or 
			(research-completed ri-champion)
			(civ-selected mayan)
		)
		(strategic-number sn-cavalry-threat >= 1)
	)
	(building-type-count-total barracks < number-barracks)
	(unit-type-count villager >= villager-barracks)
=>
	(up-set-placement-data my-player-number villager-class c: 2)
	(up-build place-control 0 c: barracks)
	(up-modify-goal temp-goal5 c:+ 1)
)
#else
(defrule
	(not (up-pending-placement c: stable))
	(can-build stable)
	(unit-type-count villager >= villager-stable)
	(building-type-count-total stable < number-stables)
=>
	(build stable)
)
(defrule
	(not (up-pending-placement c: archery-range))
	(can-build archery-range)
	(building-type-count-total archery-range < number-archery-ranges)
	(or
		(unit-type-count villager >= villager-archery-range)
		(and
			(building-type-count-total stable >= number-stables)
			(civ-selected hun)
		)
	)
=>
	(build archery-range)
)
(defrule
	(not (up-pending-placement c: barracks))
	(can-build barracks)
	(or
		(or 
			(research-completed ri-champion)
			(civ-selected mayan)
		)
		(strategic-number sn-cavalry-threat >= 1)
	)
	(building-type-count-total barracks < number-barracks)
	(unit-type-count villager >= villager-barracks)
=>
	(build barracks)
)

#end-if
(defrule
	(or
		(game-time > 120)
		(up-compare-goal under-attack-goal != 0)
	)
	(not (up-pending-placement c: stable))
	(can-build stable)
	(unit-type-count villager >= villager-stable)
	(building-type-count-total stable < number-stables)
=>
	(build stable)
)
(defrule
	(or
		(game-time > 120)
		(up-compare-goal under-attack-goal != 0)
	)
	(not (up-pending-placement c: archery-range))
	(can-build archery-range)
	(building-type-count-total archery-range < number-archery-ranges)
	(or
		(unit-type-count villager >= villager-archery-range)
		(and
			(building-type-count-total stable >= number-stables)
			(civ-selected hun)
		)
	)
=>
	(build archery-range)
)
(defrule
	(or
		(game-time > 120)
		(up-compare-goal under-attack-goal != 0)
	)
	(not (up-pending-placement c: barracks))
	(can-build barracks)
	(or
		(or 
			(research-completed ri-champion)
			(civ-selected mayan)
		)
		(strategic-number sn-cavalry-threat >= 1)
	)
	(building-type-count-total barracks < number-barracks)
	(unit-type-count villager >= villager-barracks)
=>
	(build barracks)
)
#load-if-not-defined DEFEND-WONDER
(defrule
	(or
		(unit-type-count villager >= 12)
		(and
			(unit-type-count villager >= 8)
			(building-type-count-total mining-camp < 3)
		)
	)
	(up-pending-objects c: mining-camp == 0)
	(building-type-count-total mining-camp < 4)
	(or 
		(resource-found gold)
		(resource-found stone)
	)
	(can-build mining-camp)
=>
	(build mining-camp)
)
#end-if
#end-if ;PIDM
(defrule
	(building-type-count town-center > 0)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-resource-control != battering-ram)
	(or
		(dropsite-min-distance wood < wood-distance-farm); make sure we don't run out of wood
		(wood-amount > 800)
	)
	(game-time > 180)
	(or
		(and
			(goal farming-goal 1)
			(up-pending-objects c: farm < 1)
		)
		(goal farming-goal 2)
	)
	(goal save-wood-goal 0)
	(up-pending-objects c: farm < 8)
	(can-build farm)
=>
	(build farm)
)

;********** UNITS **********

#load-if-defined DIFFICULTY-EASIEST
(defrule
	(current-age >= castle-age)
	(strategic-number sn-resource-control == 0)
	(or
		(current-age == castle-age)
		(population >= fourty-percent-pop)
	)
	(population-cap >= 175)
	(military-population >= 30)
	(strategic-number sn-military-level >= 0)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "hold resources on easiest")
)
#end-if
#load-if-defined DIFFICULTY-EASY
(defrule
	(current-age >= castle-age)
	(strategic-number sn-resource-control == 0)
	(or
		(current-age == castle-age)
		(population >= seventy-five-percent-pop)
	)
	(population-cap >= 175)
	(military-population >= 30)
	(strategic-number sn-military-level >= 0)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "hold resources on easy")
)
#end-if
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined DIFFICULTY-MODERATE
(defrule
	(strategic-number sn-current-age == castle)
	(strategic-number sn-resource-control == 0)
	(players-population any-ally >= eighty-five-percent-pop)
	(players-current-age any-ally == imperial-age)
	(population-cap >= 175)
	(goal under-attack-goal 0)
=>
	(set-strategic-number sn-resource-control 2)
;	(chat-to-player chat-player "save-resources = imperial-age")
)
#end-if
#end-if
#end-if
#load-if-defined DEATH-MATCH
(defrule
	(players-building-count any-enemy > 0)
	(up-player-distance any-enemy < 70)
=>
	(set-strategic-number sn-number-explore-groups 0)
	(set-strategic-number sn-total-number-explorers 0)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(up-reset-scouts)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: scout-unit c: 1)
	(up-find-remote c: villager-class c: 1)
	(up-target-objects 0 action-default -1 aggressive)
	(disable-self)
)
(defrule
	(strategic-number sn-number-explore-groups > 1)
	(town-under-attack)
=>
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
	(up-reset-scouts)
	(disable-self)
)
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(game-time < 120)
	(game-time > 10)
	(soldier-count > 1)
	(players-building-count every-enemy == 0)
=>
	(set-strategic-number sn-number-explore-groups 6)
	(set-strategic-number sn-total-number-explorers 6)
	(disable-self)
)
(defrule
	(game-time >= 180)
	(or
		(and
			(players-building-count every-enemy > 0)
			(strategic-number sn-number-explore-groups > 1)
		)
		(strategic-number sn-number-explore-groups == 0)
	)
=>
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
	(up-reset-scouts)
	(disable-self)
)
#end-if
#end-if

; *** NAVY ***
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock > water-mixed)
	(can-train transport-ship)
	(players-unit-type-count every-enemy fire-ship-line < 15)
	(players-unit-type-count every-enemy galley-line < 15)
	(strategic-number sn-resource-control != 2)
	(current-age == imperial-age)
	(unit-type-count transport-ship < 3)
	(warboat-count > 40)
	(soldier-count > 20)
=>
	(train transport-ship)
)
#load-if-defined KOREAN-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock > water-mixed)
	(can-train turtle-ship-line)
	(or
		(unit-type-count turtle-ship-line < 12)
		(can-research ri-elite-turtle-ship)
	)
=>
	(research ri-elite-turtle-ship)
	(train turtle-ship-line)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
	(can-train cannon-galleon-line)
	(warboat-count >= 20)
	(or
		(unit-type-count cannon-galleon-line < 8)
		(warboat-count >= 50)
	)
	(unit-type-count cannon-galleon-line < 16)
=>
	(train cannon-galleon-line)
)
#load-if-defined POPULATION-CAP-200
(defrule
	(game-time < 1800)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
	(strategic-number sn-resource-control < 3)
	(strategic-number sn-military-level > 0)
	(warboat-count > 15)
	(or
		(cc-players-unit-type-count 0 shore-fish > min-fishing)
		(cc-players-unit-type-count 0 sea-fish > min-fishing)
	)
	(dropsite-min-distance wood < 4)
	(civilian-population < max-civ)
	(population < max-civ-pop)
	(unit-type-count fishing-ship < 12)
	(can-train fishing-ship)
=>
	(train fishing-ship)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 2)
	(or
		(current-age == castle-age)
		(research-completed ri-fast-fire-ship)
	)
	(goal enemy-boats-goal 1)
	(or
		(and
			(warboat-count < 15)
			(up-research-status c: ri-war-galley < research-pending)
		)
		(unit-type-count fire-ship-line < 6)
	)
	(can-train fire-ship-line)
=>
	(train fire-ship-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(strategic-number sn-resource-control < 2)
	(or
		(current-age == castle-age)
		(research-completed ri-fast-fire-ship)
	)
	(unit-type-count fire-ship-line < 24)
	(warboat-count < max-fleet)
	(can-train fire-ship-line)
=>
	(train fire-ship-line)
)
(defrule
	(strategic-number sn-resource-control < 2)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(research-completed ri-war-galley)
	(warboat-count < max-fleet)
	(can-train galley-line)
=>
	(train galley-line)
)
(defrule
	(strategic-number sn-resource-control < 2)
	(or
		(goal strategy-goal flush)
		(current-age > feudal-age)
	)
	(nor 
		(research-available ri-war-galley)
		(research-available ri-galleon)
	)
	(or
		(and
			(goal enemy-boats-goal 1)
			(warboat-count < max-warships)
		)
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	)
	(warboat-count < max-fleet)
	(can-train galley-line)
=>
	(train galley-line)
)

; counter units

#load-if-not-defined TEUTONIC-CIV
(defrule
	(or
		(and
			(strategic-number sn-resource-control <= 2)
			(or
				(strategic-number sn-resource-control < 1)
				(unit-type-count scout-cavalry-line < 6)
			)
		)
		(strategic-number sn-resource-control == ri-heresy)
	)
	(or
		(food-amount > 200)
		(unit-type-count scout-cavalry-line < 6)
	)
	(goal monk-threat-goal 1)
	(not (research-available ri-light-cavalry))
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
#else
(defrule
	(nand
		(game-time > 1800)
		(players-unit-type-count every-enemy monk < 10)
	)
	(or
		(and
			(food-amount > 200)
			(strategic-number sn-resource-control < 2)
		)
		(unit-type-count scout-cavalry-line < 6)
	)
	(goal monk-threat-goal 1)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
#end-if
#load-if-defined ITALIAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(and
			(unit-type-count my-unique-unit-line < 20)
			(strategic-number sn-cavalry-threat >= 1)
		)
		(research-completed my-unique-unit-upgrade)
	)
	(gold-amount > 100)
	(can-train genoese-crossbowman)
=>
	(train genoese-crossbowman)
)
#end-if
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count spearman-line < 6)
	(current-age == feudal-age)
	(players-unit-type-count focus-player scout-cavalry-line > 3)
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(players-building-count every-enemy > 0)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count spearman-line < 6)
	(current-age == feudal-age)
	(goal threat-source-goal 947)
	(goal threat-target-goal villager-class)
	(goal threat-target-goal building-class)
	(up-compare-goal threat-time-goal < 5000)
	(players-unit-type-count any-enemy scout-cavalry-line > 1)
	(can-train spearman-line); enemy going scout flush
=>
	(train spearman-line)
)
#load-if-defined INDIAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(or
			(goal unit-goal knight)
			(research-completed ri-imperial-camel)
		)
		(unit-type-count camel-line < 12)
	)
	(can-train camel-line)
=>
	(train camel-line)
)
#end-if
#load-if-not-defined SARACEN-CIV
#load-if-not-defined TURKISH-CIV
#load-if-not-defined INDIAN-CIV
(defconst extra-anti-cav-unit spearman-line)
#else
(defconst extra-anti-cav-unit camel-line)
#end-if
#else
(defconst extra-anti-cav-unit camel-line)
#end-if
#else
(defconst extra-anti-cav-unit camel-line)
#end-if
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat >= 1)
	(or
		(players-unit-type-count any-enemy paladin > 0)
		(and
			(players-unit-type-count any-enemy camel-line > 10)
			(players-civ any-enemy saracen)
		)
	)
	(research-completed ri-halberdier)
	(unit-type-count extra-anti-cav-unit < 40)
	(food-amount > 100)
	(can-train extra-anti-cav-unit)
=>
	(train extra-anti-cav-unit)
)
#end-if
#load-if-defined SARACEN-CIV
#load-if-defined POST-IMPERIAL-AGE-START
#load-if-defined DEATH-MATCH
(defrule
	(strategic-number sn-resource-control < 1)
	(population < seventy-five-percent-pop)
	(game-time < 480)
	(can-train camel-line)
=>
	(train camel-line)
)
#end-if
#end-if
#end-if
(defrule
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat >= 1)
	(or
		(and
			(food-amount >= 85)
			(wood-amount >= 70)
		)
		(unit-type-count spearman-line < 9)
	)
	(or
		(unit-type-count spearman-line < 9)
		(strategic-number sn-cavalry-threat > 1)
	)
	(unit-type-count spearman-line < 14)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(game-time < 300)
	(or
		(players-unit-type-count focus-player knight-line > 0)
		(players-unit-type-count focus-player camel-line > 0)
	)
	(unit-type-count camel-line < high-number-upgrade)
	(can-train camel-line)
=>
	(train camel-line)
)
#end-if
#end-if ;NOT EASY
#end-if ;NOT EASIEST
(defrule
	(unit-type-count cavalry-class > 20)
	(players-unit-type-count any-enemy mangonel-line > 3)
=>
	(set-difficulty-parameter ability-to-dodge-missiles 100); don't dodge, go melee instead
)
(defrule
	(or
		(unit-type-count cavalry-class < 20)
		(players-unit-type-count every-enemy mangonel-line < 4)
	)
=>
	(set-difficulty-parameter ability-to-dodge-missiles 0); dodge
)
(defrule
	(game-time > 600)
	(or
		(and
			(civ-selected byzantine)
			(and
				(strategic-number sn-archer-threat < 4)
				(building-type-count castle > 0)
			)
		)
		(and
			(civ-selected celtic)
			(can-train heavy-scorpion)
		)
	)
=>
	(up-jump-rule 1)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-paladin)
	(or
		(or
			(and
				(players-unit-type-count every-enemy spearman-line < 25)
				(players-unit-type-count every-enemy camel-line < 20)
			)
			(and
				(civ-selected celtic)
				(gold-amount > 2000)
			)
		)
		(or
			(strategic-number sn-archer-threat > 1)
			(players-unit-type-count any-enemy scorpion-line > 10)
		)
	)
	(can-train knight-line)
=>
	(train knight-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count my-unique-unit-line < 20)
	(strategic-number sn-infantry-threat > 1)
	(gold-amount > 100)
	(or
		(or 
			(civ-selected aztec)
			(civ-selected byzantine)
		)
		(or 
			(and
				(civ-selected spanish)
				(up-research-status c: ri-chemistry < research-pending)
			)
			(research-completed ri-elite-teutonic-knight)
		)
	)
	(can-train my-unique-unit-line)
=>
	(train my-unique-unit-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train my-unique-unit-line)
	(unit-type-count my-unique-unit-line < 12)
	(goal enemy-goal eagle-warrior)
	(or
		(or 
			(civ-selected aztec)
			(civ-selected byzantine)
		)
		(civ-selected japanese)
	)
=>
	(train my-unique-unit-line)
)
#load-if-defined INCAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count slinger < 20)
	(or
		(strategic-number sn-infantry-threat > 1)
		(goal ranged-unit-type-goal slinger)
	)	
	(or
		(gold-amount > 100)
		(current-age == castle-age)
	)
	(can-train slinger)
=>
	(train slinger)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(unit-type-count villager > 75)
		(food-amount >= unique-unit-food)
	)
	(or
		(gold-amount > 100)
		(current-age == castle-age)
	)
	(or
		(unit-type-count my-unique-unit-line < 20)
		(or
			(strategic-number sn-cavalry-threat > 1)
			(unit-type-count-total archer-line > 15)
		)
	)
	(can-train kamayuk)
=>
	(train kamayuk)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count condottiero < 12)
	(players-unit-type-count any-enemy hand-cannoneer > 6)
	(gold-amount > 70)
	(or
		(unit-type-count teutonic-knight-line > high-number-upgrade)
		(civ-selected gothic)
	)
	(can-train condottiero)
=>
	(train 184)
)
#load-if-defined GOTHIC-CIV
(defrule
	(not (player-in-game any-ally))
	(research-completed ri-champion)
	(players-unit-type-count every-enemy teutonic-knight-line < 10)
	(players-unit-type-count every-enemy jaguar-man-line < 10)
	(gold-amount < 500)
=>
	(up-jump-rule 1)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(unit-type-count hand-cannoneer < 30)
		(unit-type-count trade-cart > 20)
	)
	(or
		(goal unit-goal gunpowder-class)
		(and
			(goal ranged-unit-type-goal gunpowder-class)
			(or
				(unit-type-count hand-cannoneer < 15)
				(death-match-game)
			)
		)
	)
	(gold-amount > 100)
	(can-train hand-cannoneer)
=>
	(train hand-cannoneer)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count bombard-cannon < 3)
	(goal unit-goal gunpowder-class)
	(can-train bombard-cannon)
=>
	(train bombard-cannon)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(unit-type-count-total bombard-cannon < 6)
		(goal unit-goal gunpowder-class)
	)
	(unit-type-count-total bombard-cannon < 10)
	(or
		(players-unit-type-count any-enemy mangonel-line > 5)
		(players-unit-type-count any-enemy scorpion-line > 12)
	)
	(can-train bombard-cannon)
=>
	(train bombard-cannon)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(nand
		(or
			(civ-selected teutonic)
			(civ-selected byzantine)
		)
		(unit-type-count my-unique-unit-line > 8)
	)
	(unit-type-count hand-cannoneer < 20)
	(strategic-number sn-infantry-threat >= 3)
	(gold-amount > 150)
	(can-train hand-cannoneer)
=>
	(train hand-cannoneer)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(nand
		(or
			(civ-selected teutonic)
			(civ-selected byzantine)
		)
		(unit-type-count my-unique-unit-line > 8)
	)
	(unit-type-count hand-cannoneer < 20)
	(or
		(players-unit-type-count any-enemy eagle-warrior-line > 12)
		(players-unit-type-count any-enemy huskarl-line > 4)
	)
	(can-train hand-cannoneer)
=>
	(train hand-cannoneer)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count hand-cannoneer < 15)
	(players-unit-type-count focus-player spearman-line > 20)
	(unit-type-count knight-line > 20)
	(can-train hand-cannoneer)
=>
	(train hand-cannoneer)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-long-swordsman)
	(goal enemy-goal eagle-warrior)
	(unit-type-count militiaman-line < 12)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train my-unique-unit-line)
	(or
		(current-age == castle-age)
		(gold-amount > 120)
	)
	(or
		(civ-selected mongol)
		(civ-selected briton)
	)
=>
	(train my-unique-unit-line)
)
#load-if-defined GOTHIC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train my-unique-unit-line)
	(strategic-number sn-archer-threat >= 1)
	(or
		(gold-amount > 90)
		(strategic-number sn-archer-threat >= 3)
	)
=>
	(train my-unique-unit-line)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat > 0)
	(game-time < 900)
	(can-train camel-line)
	(gold-amount > 250)
	(or
		(unit-type-count camel-line < 16)
		(strategic-number sn-cavalry-threat > 3)
	)
	(or
		(unit-type-count camel-line < 24)
		(game-time < 600)
	)
=>
	(train camel-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat > 0)
	(gold-amount > 250)
	(unit-type-count camel-line < 6)
	(nand
		(and
			(up-research-status c: ri-heavy-camel == research-available)
			(research-completed ri-halberdier)
		)
		(wood-amount > 200)
	)
	(can-train camel-line)
=>
	(train camel-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat > 2)
	(nand
		(goal unit-goal knight)
		(unit-type-count knight-line < 6)
	)
	(can-train camel-line)
	(gold-amount > 250)
	(or
		(unit-type-count camel-line < 16)
		(strategic-number sn-cavalry-threat > 3)
	)
	(unit-type-count camel-line < 24)
=>
	(train camel-line)
)
#load-if-not-defined WONDER-RACE
#load-if-defined AZTEC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(unit-type-count-total monk < 12)
	(players-unit-type-count every-enemy mangonel-line == 0)
	(players-unit-type-count every-enemy scorpion-line < 5)
	(goal anti-cavalry-threat-goal 0)
	(strategic-number sn-cavalry-threat > 1)
	(can-train monk)
	(gold-amount > 175)
=>
	(train monk)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(goal ranged-unit-type-goal monk)
	(research-completed ri-sanctity)
	(or
		(unit-type-count-total monk < 12)
		(and
			(or
				(players-unit-type-count every-enemy monk < 5)
				(research-completed ri-atonement)
			)
			(or
				(and
					(players-unit-type-count every-enemy mangonel-line == 0)
					(players-unit-type-count every-enemy scorpion-line < 5)
				)
				(research-completed ri-redemption)
			)
		)
	)
	(can-train monk)
=>
	(train monk)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(up-research-status c: ri-block-printing >= research-available)
	(gold-amount > 175)
	(or 
		(civ-selected aztec)
		(or
			(players-unit-type-count any-enemy war-elephant-line > 5)
			(players-unit-type-count any-enemy elephant-archer > 10)
		)
	)
	(or
		(goal anti-monk-threat-goal 0)
		(and
			(players-unit-type-count any-enemy war-elephant-line > 25)
			(unit-type-count monastery-class < 15)
		)
	)
	(can-train monk)
=>
	(train monk)
)
#end-if
#load-if-not-defined TURKISH-CIV
#load-if-not-defined POPULATION-CAP-25
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(current-age >= castle-age)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat >= 1)
	(food-amount >= 85)
	(up-compare-goal control-goal != shot)
	(or
		(unit-type-count spearman-line < 10)
		(and
			(research-completed ri-pikeman)
			(building-type-count-total town-center > 1)
		)
	)
	(unit-type-count camel-line < 5); don't overdo anti cavalry
	(unit-type-count mameluke-line < 5)
	(unit-type-count imperial-camel < 5)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(current-age >= castle-age)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat > 1)
	(or
		(unit-type-count spearman-line < 10)
		(research-completed ri-pikeman)
	)
	(unit-type-count spearman-line < 18)
	(unit-type-count camel-line < 12)
	(unit-type-count imperial-camel < 12)
	(unit-type-count mameluke-line < 12)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#end-if
#else
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat >= 1)
	(or
		(food-amount >= 85)
		(unit-type-count spearman-line < 6)
	)
	(or
		(unit-type-count spearman-line < 9)
		(strategic-number sn-cavalry-threat > 1)
	)
	(unit-type-count spearman-line < 14)
	(unit-type-count camel-line < 4)
	(unit-type-count imperial-camel < 4)
	(unit-type-count mameluke-line < 5)
	(research-completed ri-pikeman)
	(can-train spearman-line)
=>
	(train spearman-line)
)
(defrule
	(current-age >= castle-age)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat >= 1)
	(food-amount >= 85)
	(goal unit-goal archer)
	(or
		(unit-type-count spearman-line < 8)
		(research-completed ri-pikeman)
	)
	(gold-amount < 200)
	(can-train spearman-line)
=>
	(train spearman-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train camel-line)
	(game-time < 1800)
	(or
		(and
			(players-unit-type-count any-enemy knight-line > 3)
			(or
				(players-unit-type-count any-enemy knight-line > 5)
				(game-time < 1320)
			)
		)
		(or
			(players-building-type-count any-enemy stable > 1)
			(players-unit-type-count any-enemy cavalry-archer-line >= 6)
		)
	)
	(unit-type-count knight-line > 8); get kt first to do damage vs non-knights
	(unit-type-count camel-line < 6)
=>
	(train camel-line)
)
#end-if
#else
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-cavalry-threat >= 1)
	(food-amount >= 85)
	(research-completed ri-pikeman)
	(unit-type-count spearman-line < 4)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#end-if
#else
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train camel-line)
	(or
		(strategic-number sn-cavalry-threat >= 1)
		(players-unit-type-count any-enemy cavalry-archer-line >= 15)
	)
	(or
		(gold-amount > 100)
		(unit-type-count camel-line < 6)
	)
=>
	(train camel-line)
)
#end-if
#load-if-defined BYZANTINE-CIV
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(strategic-number sn-resource-control < 1)
	(goal unit-goal knight)
	(or
		(or
			(players-unit-type-count any-enemy knight-line >= 5); in RM train camels vs mass knights
			(or
				(players-unit-type-count any-enemy scout-cavalry-line >= 5); or vs scouts + ca
				(players-unit-type-count any-enemy cavalry-archer-line >= 15)
			)
		)
		(or
			(strategic-number sn-cavalry-threat >= 1)
			(unit-type-count camel-line < 8)
		)
	)
	(can-train camel-line)
=>
	(train camel-line)
)
#end-if
(defrule
	(game-time < 1500)
	(strategic-number sn-resource-control < 1)
	(goal unit-goal skirmisher)
	(food-amount >= 80)
	(unit-type-count spearman-line < 4)
	(strategic-number sn-current-age == castle)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-pikeman)
	(gold-amount < 50)
	(food-amount > 200)
	(wood-amount > 200)
	(or
		(or
			(civ-selected aztec)
			(civ-selected viking)
		)
		(research-completed ri-halberdier)
	)
	(unit-type-count spearman-line < 20)
	(can-train spearman-line)
=>
	(train spearman-line)
)
(defrule
	(strategic-number sn-resource-control < 3)	
	(or
		(and
			(or
				(strategic-number sn-cavalry-threat >= 1)
				(players-unit-type-count any-enemy cavalry-archer-line >= 15)
			)
			(unit-type-count camel-line > min-number-upgrade)
		)
		(or
			(civ-selected indian)
			(unit-type-count camel-line > high-number-upgrade)
		)
	)
	(can-research-with-escrow ri-heavy-camel)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-heavy-camel)
)
(defrule
	(strategic-number sn-resource-control < 1) ; anticipate knight defense
	(goal unit-goal eagle-warrior); so better make some spears and spend our gold to collect relics
	(goal enemy-goal rush)
	(or 
		(or 
			(players-civ any-enemy hun)
			(players-civ any-enemy frankish)
		)
		(or 
			(players-civ any-enemy teutonic)
			(players-civ any-enemy viking)
		)
	)
	(unit-type-count-total monk == 0)
	(building-type-count-total monastery > 0)
	(unit-type-count spearman-line < 5)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#load-if-defined AZTEC-CIV
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(players-unit-type-count focus-player militiaman-line > 4)
	(game-time < 480)
	(strategic-number sn-archer-threat < 2)
	(players-unit-type-count focus-player mangonel-line < 2)
=>
	(up-jump-rule 3)
)
#end-if
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-elite-eagle-warrior)
	(or
		(current-age-time < 300)
		(gold-amount > 114)
	)
	(nor
		(players-unit-type-count any-enemy teutonic-knight-line > 11)
		(players-unit-type-count any-enemy militiaman-line > 19)
	)
	(can-train eagle-warrior-line)
=>
	(train eagle-warrior-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(nand 
		(goal train-civ-goal ri-chain-mail)
		(gold-amount < 160)
	)
	(or
		(food-amount >= 52); one food drop left to train next villager
		(or
			(town-under-attack)
			(current-age-time < 240)
		)
	)
	(goal unit-goal eagle-warrior)
	(goal strategy-goal rush)
	(can-train eagle-warrior-line)
=>
	(train eagle-warrior-line)
)
#load-if-defined MAYAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(players-unit-type-count any-enemy huskarl-line > 3)
	(or
		(players-unit-type-count any-enemy militiaman-line < 12)
		(or
			(unit-type-count archer-line > 12)
			(unit-type-count plumed-archer-line > 12)
		)
	)
	(can-train eagle-warrior-line)
=>
	(train eagle-warrior-line)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(or 
		(gold-amount > 150)
		(current-age == castle-age)
	)
	(or
		(players-unit-type-count any-enemy monk > 5)
		(goal control-goal shot)
	)
	(unit-type-count eagle-warrior < 15)
	(can-train eagle-warrior-line)
=>
	(train eagle-warrior-line)
)
(defrule
	(goal unit-goal eagle-warrior)
	(strategic-number sn-resource-control < 1)
	(food-amount > 70)
	(gold-amount > 150)
	(unit-type-count eagle-warrior < 15)
	(can-train eagle-warrior-line)
=>
	(train eagle-warrior-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(gold-amount > 120)
	(players-unit-type-count any-enemy skirmisher-line > 15)
	(or
		(civ-selected japanese)
		(civ-selected viking)
	)
	(research-completed ri-heavy-scorpion)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-archer-threat >= 2)
	(or
		(gold-amount > 220)
		(and
			(or
				(strategic-number sn-cavalry-threat < 2)
				(or
					(unit-type-count camel-line > mid-number-upgrade)
					(unit-type-count spearman-line > high-number-upgrade)
				)
			)
			(players-unit-type-count every-enemy eagle-warrior-line < high-number-upgrade)
		)
	)
	(research-completed ri-heavy-scorpion)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(current-age == feudal-age)
	(or
		(and
			(strategic-number sn-current-age == fc-transit)
			(goal strategy-goal rush)
		)
		(and
			(building-type-count-total blacksmith > 0)
			(gold-amount > 245)
		)
	)
	(goal unit-goal archer)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-crossbow)
	(goal ranged-unit-type-goal archer)
	(nor (can-train longbowman-line)
	(can-train plumed-archer-line))
	(nor (can-train war-wagon-line)
	(goal strategy-goal boom))
	(gold-amount >= 145)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(wood-amount > 100)
	(gold-amount >= 145)
	(strategic-number sn-current-age >= fc-transit)
	(or
		(game-time < 1800)
		(civ-selected viking)
	)
	(goal ranged-unit-type-goal archer)
	(goal strategy-goal boom)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(or
		(strategic-number sn-resource-control < 1)
		(and 
			(or
				(or
					(strategic-number sn-resource-control == ri-pikeman)
					(strategic-number sn-resource-control == ri-bodkin-arrow)
				)
				(strategic-number sn-resource-control == ri-crossbow)
			)
			(gold-amount >= 145); don't stop producing archers if we need only food for our research
		)
	)
	(strategic-number sn-current-age >= fc-transit)
	(game-time < 1800)
	(goal unit-goal archer)
	(goal strategy-goal rush)
	(can-train archer-line)
=>
	(train archer-line)
)
#load-if-defined MAGYAR-CIV
(defrule
	(or
		(strategic-number sn-resource-control < 1)
		(and
			(research-available imperial-age)
			(and
				(food-amount > 1070)
				(gold-amount < 700)
			)
		)
	)
	(food-amount > 150)
	(can-train magyar-huszar)
=>
	(train magyar-huszar)
)
#end-if
#load-if-defined SLAVIC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(current-age == castle-age)
		(gold-amount > 120)
	)
	(or
		(unit-type-count villager > 75)
		(food-amount >= unique-unit-food)
	)
	(can-train boyar)
=>
	(train boyar)
)
#end-if
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined UP-MULTIPLE-ENEMIES
(defrule
	(current-age >= castle-age)
	(strategic-number sn-resource-control == 2)
	(research-completed ri-pikeman)
	(wood-amount > 400); gold shortage while getting imperial researches
	(food-amount > 400); so keep slots filled
	(gold-amount < 200); with non-gold units
	(population < max-civ-pop)
	(nand
		(research-completed ri-bracer)
		(unit-type-count spearman-line > 40)
	)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#end-if
#load-if-not-defined UP-3-PLAYER-TEAM
#load-if-not-defined UP-4-PLAYER-TEAM
#load-if-not-defined UP-5-PLAYER-TEAM
#load-if-not-defined UP-6-PLAYER-TEAM
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(research-completed ri-pikeman)
	(research-available imperial-age)
	(population < max-civ-pop)
	(or
		(food-amount > imp-food)
		(gold-amount < 300)
	)
	(food-amount > 400)
	(gold-amount < imp-gold)
	(wood-amount >= 100)
	(can-train spearman-line)
=>
	(train spearman-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(up-research-status c: ri-elite-skirmisher >= research-available)
	(research-available imperial-age)
	(population < max-civ-pop)
	(or
		(food-amount > imp-food)
		(gold-amount < 300)
	)
	(food-amount > 400)
	(gold-amount < imp-gold)
	(wood-amount >= 100)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
#end-if
#end-if
#end-if
#end-if
#end-if; NOT EASY
#end-if; NOT EASIEST
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined GOTHIC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-cavalier)
	(or 
		(research-completed ri-bloodlines)
		(research-completed ri-paladin)
	)
	(or
		(goal anti-cavalry-threat-goal 0)
		(or
			(game-time < 480)
			(and
				(civ-selected hun)
				(death-match-game)
			)
		)
	)
	(can-train knight-line)
=>
	(train knight-line)
)
#end-if
#end-if
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(game-time < 240)
	(can-train monk)
=>
	(train monk)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(goal unit-goal knight)
		(and
			(research-completed ri-paladin)
			(players-unit-type-count every-enemy spearman-line < 25)
		)
	)
	(or
		(strategic-number sn-current-age == castle)
		(and
			(gold-amount > 100)
			(nor 
				(civ-selected korean)
				(civ-selected saracen)
			)
		)
	)
	(can-train knight-line)
=>
	(train knight-line)
)
; balance resources
#load-if-defined ITALIAN-CIV
(defrule
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-resource-control != ri-heresy)
	(gold-amount > 250)
	(food-amount < 200)
	(or
		(food-amount < 80)
		(gold-amount > 400)
	)
	(can-train genoese-crossbowman); same rule as below, because my-uu-line doesn't work with aofe uu
=>
	(train genoese-crossbowman)
)
#end-if
#load-if-not-defined INCAN-CIV
#load-if-not-defined INDIAN-CIV
#load-if-not-defined MAGYAR-CIV
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined AZTEC-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined FRANKISH-CIV
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined PERSIAN-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined SPANISH-CIV
#load-if-not-defined TEUTONIC-CIV
#load-if-not-defined TURKISH-CIV
#load-if-not-defined VIKING-CIV
#load-if-not-defined BYZANTINE-CIV
(defrule
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-resource-control != ri-heresy)
	(gold-amount > 250)
	(food-amount < 200)
	(or
		(food-amount < 80)
		(gold-amount > 400)
	)
	(can-train my-unique-unit-line); ranged uu
=>
	(train my-unique-unit-line)
)
#end-if
(defrule
	(current-age >= castle-age)
	(or
		(and
			(and
				(or
					(civ-selected briton)
					(civ-selected mayan)
				)
				(goal unit-goal my-unique-unit-line)
			)
			(goal strategy-goal rush)
		)
		(building-type-count castle >= 2)
	)
=>
	(set-goal ranged-unit-type-goal 0)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(goal unit-goal knight)
	(goal strategy-goal rush)
	(building-type-count castle > 0)
	(or
		(or
			(civ-selected mongol)
			(civ-selected chinese)
		)
		(or
			(civ-selected briton)
			(or
				(civ-selected korean)
				(civ-selected italian)
			)
		)
	)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal ranged-unit-type-goal uu)
	(set-goal uu-up-goal 1)
	(disable-self)
)
(defrule
	(game-time > 20)
	(game-time < 60)
	(goal strategy-goal boom)
	(goal unit-goal my-unique-unit-line)
	(up-compare-goal ranged-unit-type-goal != scorpion)
	(up-compare-goal ranged-unit-type-goal != monk)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal ranged-unit-type-goal uu)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#load-if-not-defined INCAN-CIV
#load-if-not-defined AZTEC-CIV
#load-if-not-defined BRITON-CIV
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined ITALIAN-CIV
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined MAGYAR-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined VIKING-CIV
#load-if-defined UP-1-PLAYER-TEAM
(defrule
	(goal strategy-goal flush)
	(or
		(civilian-population >= 100)
		(civilian-population >= max-civ)
	)
	(goal unit-goal knight)
	(up-compare-goal ranged-unit-type-goal != scorpion)
	(up-compare-goal ranged-unit-type-goal != monk)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal ranged-unit-type-goal 0)
)
#end-if
(defrule
	(nand
		(research-completed ri-crossbow)
		(game-time < 2400)
	)
	(or
		(or
			(building-type-count siege-workshop > 0)
			(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
		)
		(goal strategy-goal boom)
	)
	(up-compare-goal unit-goal g:!= ranged-unit-type-goal)
	(up-compare-goal ranged-unit-type-goal != scorpion)
	(up-compare-goal ranged-unit-type-goal != monk)
	(up-compare-goal ranged-unit-type-goal != gunpowder-class)
=>
	(set-goal ranged-unit-type-goal 0)
)
#load-if-defined DEATH-MATCH
#load-if-not-defined TURKISH-CIV
(defrule
	(up-compare-goal ranged-unit-type-goal != 0)
=>
	(set-goal ranged-unit-type-goal 0)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#load-if-not-defined BRITON-CIV
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined SPANISH-CIV
#load-if-not-defined INDIAN-CIV
#load-if-not-defined ITALIAN-CIV
(defrule
	(current-age == castle-age)
	(goal hostilities-goal 1)
	(goal unit-goal knight)
	(building-type-count siege-workshop > 0)
	(up-compare-goal threat-player-goal g:!= closest-enemy-goal)
	(goal threat-source-goal ranged-unit-class)
	(strategic-number sn-archer-threat > 2)
	(up-research-status c: ri-crossbow != research-complete)
=>
	(set-goal ranged-unit-type-goal scorpion)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(goal ranged-unit-type-goal cavalry-archer)
	(or
		(gold-amount > 125)
		(goal unit-goal cavalry-archer)
	)
	(or
		(gold-amount > 100)
		(and
			(goal control-goal aggressive-rush)
			(current-age == castle-age)
		)
	)
	(or
		(dropsite-min-distance wood < 4)
		(wood-amount > 140)
	)
	(can-train cavalry-archer-line)
=>
	(train cavalry-archer-line)
)
#load-if-not-defined HUN-CIV
(defrule
	(strategic-number sn-resource-control != battering-ram)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-resource-control != ri-heresy)
	(research-completed ri-crossbow)
	(goal ranged-unit-type-goal archer)
	(wood-amount > 90)
	(gold-amount > 250)
	(food-amount < 200)
	(or
		(food-amount < 80)
		(gold-amount > 400)
	)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(strategic-number sn-resource-control != battering-ram)
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-resource-control != ri-heresy)
	(goal ranged-unit-type-goal uu)
	(wood-amount > 130)
	(gold-amount > 250)
	(food-amount < 200)
	(or
		(food-amount < 80)
		(gold-amount > 400)
	)
	(can-train my-unique-unit-line)
=>
	(train my-unique-unit-line)
)
#end-if
(defrule
	(strategic-number sn-resource-control != ri-elite-skirmisher)
	(strategic-number sn-resource-control != ri-heresy)
	(nor (can-train chu-ko-nu-line)
	(can-train mangudai-line))
	(goal ranged-unit-type-goal cavalry-archer)
	(wood-amount > 90)
	(gold-amount > 250)
	(or
		(food-amount < 200)
		(game-time < 720)
	)
	(or
		(food-amount < 80)
		(gold-amount > 400)
	)
	(can-train cavalry-archer-line)
=>
	(train cavalry-archer-line)
)
(defrule
	(current-age >= castle-age)
	(strategic-number sn-resource-control < 1)
	(goal unit-goal militiaman-line); strategy currently unused.
	(goal strategy-goal rush)
	(not (research-available ri-long-swordsman))
	(unit-type-count militiaman-line < 15)
	(game-time < 1800)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
(defrule
	(current-age == feudal-age)
	(up-compare-goal strategy-goal != flush)
=>
	(up-jump-rule 1)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(goal unit-goal militiaman-line)
		(goal control-goal militiaman-line)
	)
	(food-amount > 100)
	(or 
		(and
			(and
				(current-age == castle-age)
				(research-completed ri-long-swordsman)
			)
			(unit-type-count militiaman-line < 30)
		)
		(and
			(research-completed ri-two-handed-swordsman)
			(gold-amount > 90)
		)
	)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
#load-if-defined GOTHIC-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(food-amount > 100)
	(gold-amount > 90)
	(nand
		(game-time < 480)
		(and
			(strategic-number sn-archer-threat == 0)
			(players-unit-type-count focus-player hand-cannoneer < 8)
		)
	)
	(can-train rax-husky-line)
=>
	(train rax-husky-line)
	(set-goal unit-goal my-unique-unit-line)
)
#end-if
#load-if-defined INDIAN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train elephant-archer)
	(or
		(or
			(unit-type-count villager > 75)
			(goal train-civ-goal -1)
		)
		(food-amount >= unique-unit-food)
	)
	(or
		(current-age == castle-age)
		(gold-amount > 100)
	)
=>
	(train elephant-archer)
)
#end-if
#load-if-not-defined HUN-CIV
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train my-unique-unit-line)
	(or
		(research-completed my-unique-unit-upgrade)
		(or
			(goal unit-goal my-unique-unit-line)
			(goal control-goal my-unique-unit-line)
		)
	)
	(or
		(or
			(unit-type-count villager > 75)
			(goal train-civ-goal -1)
		)
		(food-amount >= unique-unit-food)
	)
	(or
		(current-age == castle-age)
		(gold-amount > 100)
	)
=>
	(train my-unique-unit-line)
)
(defrule
	(goal unit-goal my-unique-unit-line)
	(can-train my-unique-unit-line)	
	(or
		(and
			(unit-type-count-total my-unique-unit-line < 5)
			(civ-selected byzantine)
		)
		(civ-selected gothic)
	)
	(unit-type-count-total my-unique-unit-line < 12)
	(current-age == castle-age)
=>
	(train my-unique-unit-line)
)
#end-if
#load-if-not-defined AZTEC-CIV
#load-if-not-defined BRITON-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined TURKISH-CIV
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined ITALIAN-CIV
(defrule
	(research-completed my-unique-unit-upgrade)
	(building-type-count castle > 2)
=>
	(up-jump-rule 1)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(research-completed ri-champion)
	(food-amount > 100)
	(gold-amount > 200)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
#load-if-defined DARK-AGE-END
(defrule
	(strategic-number sn-resource-control < 1)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
#end-if
; Flush
(defrule
	(strategic-number sn-resource-control < 1)
	(goal strategy-goal flush)
	(or
		(food-amount >= 130)
		(current-age-time < 120)
	)
	(current-age == feudal-age)
	(or
		(current-age-time < 480)
		(and
			(food-amount < 500)
			(players-military-population every-enemy < 8)
		)
	)
	(not (research-completed ri-man-at-arms))
	(unit-type-count-total scout-cavalry-line < 10)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
#load-if-defined FEUDAL-AGE-END
(defrule
	(strategic-number sn-resource-control < 1)
	(food-amount >= 130)
	(current-age == feudal-age)
	(or
		(not (research-completed ri-man-at-arms))
		(players-unit-type-count focus-player skirmisher-line > 18)
	)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
#end-if
(defrule
	(goal strategy-goal flush)
	(strategic-number sn-current-age == feudal)
	(or
		(gold-amount >= 245)
		(and
			(strategic-number sn-military-level < 0)
			(and
				(food-amount < 500)
				(goal unit-goal scout-cavalry)
			)
		)
	)
	(strategic-number sn-resource-control <= 2)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(goal strategy-goal flush)
	(or
		(or
			(up-research-status c: ri-fletching >= research-pending)
			(gold-amount > 94)
		)
		(current-age-time < 180)	
	)
	(strategic-number sn-gold-gatherer-percentage >= 10)
	(strategic-number sn-current-age == feudal)
	(food-amount < 500)
	(strategic-number sn-resource-control < 1)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(current-age-time < 180)
	(strategic-number sn-resource-control < 1)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(strategic-number sn-current-age == feudal)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(or
		(strategic-number sn-gold-gatherer-percentage <= 10)
		(and
			(strategic-number sn-current-age == fc-transit)
			(goal ranged-unit-type-goal archer)
		)
	)
	(gold-amount >= 245)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(goal strategy-goal flush)
	(wood-amount > 70);dont run out of stock while spaming skirmishers
	(food-amount > 74)
	(or
		(food-amount < 700)
		(and
			(food-amount >= 825)
			(building-type-count-total blacksmith > 0)
		)
	)
	(nand
		(research-completed ri-man-at-arms)
		(goal enemy-goal eagle-warrior)
	)
	(strategic-number sn-current-age == feudal)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(goal strategy-goal flush)
	(nand
		(research-completed ri-man-at-arms)
		(goal enemy-goal eagle-warrior)
	)
	(building-type-count-total blacksmith > 0); castle age advancement is already prepared
	(current-age == feudal-age); as no resource restriction on this rule
	(strategic-number sn-military-level < 0); use this only to catch up with enemy military
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(goal strategy-goal flush)
	(goal enemy-goal flush)
	(strategic-number sn-current-age == fc-transit)
	(strategic-number sn-military-level < 1)
	(wood-amount > 250)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(current-age == feudal-age)
	(strategic-number sn-resource-control < 1)
	(goal strategy-goal flush)
	(research-completed ri-man-at-arms)
	(goal unit-goal militiaman-line)
	(or
		(goal enemy-goal eagle-warrior)
		(or 
			(current-age-time < 480)
			(and
				(strategic-number sn-current-age == fc-transit)
				(food-amount >= 260)
			)
		)
	)
	(food-amount > 100)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age == dark-age)
	(goal unit-goal militiaman-line)
	(goal strategy-goal flush)
	(strategic-number sn-current-age == df-transit)
	(food-amount > 260)
	(nand
		(civ-selected gothic)
		(not (death-match-game))
	)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
#load-if-not-defined WONDER-RACE ;Wonderrace is too fast to make gold from relics
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-resource-control < 1)
	(goal ranged-unit-type-goal monk)
	(or
		(goal enemy-goal flush)
		(and
			(up-compare-goal position-goal != pocket)
			(strategic-number sn-archer-threat > 1)
		)
	)
	(research-available ri-sanctity)
	(building-type-count monastery > 0)
=>
	(set-strategic-number sn-resource-control 2)
)
#load-if-defined AZTEC-CIV
(defrule
	(goal strategy-goal boom)
	(goal unit-goal archer)
=>
	(up-jump-rule 3)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(goal train-civ-goal 1)
	(or
		(and
			(up-compare-goal ranged-unit-type-goal != monk)
			(research-completed ri-sanctity)
		)
		(unit-type-count monk < 6)
	)
	(food-amount < 150)
	(gold-amount > 200)
	(goal anti-monk-threat-goal 0)
	(can-train monk)
=>
	(train monk)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(nand
		(goal unit-goal eagle-warrior)
		(strategic-number sn-cavalry-threat == 0)
	)
	(or
		(unit-type-count monk < 8)
		(and
			(food-amount < 600)
			(gold-amount > 750)
		)
	)
	(goal anti-monk-threat-goal 0)
	(can-train monk)
=>
	(train monk)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-current-age >= ci-transit)
	(gold-amount > 175)
	(or
		(players-unit-type-count every-enemy monk < 5)
		(research-completed ri-atonement)
	)
	(or
		(and
			(players-unit-type-count every-enemy mangonel-line == 0)
			(players-unit-type-count every-enemy scorpion-line < 5)
		)
		(research-completed ri-redemption)
	)
	(goal anti-monk-threat-goal 0)
	(can-train monk)
=>
	(train monk)
)
#end-if
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(goal ranged-unit-type-goal monk)
	(unit-type-count-total monk < 12)
	(can-train monk)
=>
	(train monk)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(goal train-civ-goal 1)
	(or
		(research-completed ri-sanctity)
		(unit-type-count monk < 6)
	)
	(research-completed ri-pikeman)
	(goal anti-monk-threat-goal 0)
	(strategic-number sn-cavalry-threat >= 1)
	(goal unit-goal skirmisher)
	(can-train monk)
=>
	(train monk)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(goal train-civ-goal 1)
	(or
		(research-completed ri-sanctity)
		(unit-type-count monk < 6)
	)
	(food-amount < 100)
	(gold-amount > 300)
	(current-age == castle-age)
	(goal anti-monk-threat-goal 0)
	(strategic-number sn-cavalry-threat > 1)
	(can-train monk)
=>
	(train monk)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(goal train-civ-goal 1)
	(or
		(research-completed ri-sanctity)
		(unit-type-count monk < 6)
	)
	(gold-amount > 200)
	(current-age == castle-age)
	(goal anti-monk-threat-goal 0)
	(strategic-number sn-cavalry-threat > 1)
	(goal unit-goal cavalry-archer)
	(goal control-goal aggressive-rush)
	(can-train monk)
=>
	(train monk)
)
(defrule
	(player-resigned every-ally)
	(players-building-type-count every-ally market == 0)
	(or (unit-type-count trade-cart > 0)
	(unit-type-count trade-cog > 0))
=>
	(delete-unit trade-cart)
	(delete-unit trade-cog)
)
#end-if ;not WONDER-RACE
#load-if-not-defined POST-IMPERIAL-AGE-START
(defrule
	(strategic-number sn-resource-control < 1)
	(food-amount > 200)
	(or
		(gold-amount < 100)
		(and
			(up-research-status c: ri-hussar >= research-pending)
			(population < max-civ-pop)
		)
	)
	(research-completed ri-light-cavalry)
	(nor
		(and
			(up-research-status c: ri-bloodlines < research-pending)
			(goal anti-cavalry-threat-goal 1)
		)
		(can-train magyar-huszar)
	)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(research-completed ri-light-cavalry)
	(research-available imperial-age)
	(not (can-train magyar-huszar))
	(food-amount > 1070)
	(gold-amount < 700)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
#else
(defrule
	(strategic-number sn-resource-control < 1)
	(population < seventy-five-percent-pop)
	(gold-amount > 1000)
	(research-completed ri-cavalier)
	(can-train knight-line)
=>
	(train knight-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(food-amount > 400)
	(gold-amount < 100)
	(not (can-train magyar-huszar))
	(research-completed ri-light-cavalry)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
#end-if	
#load-if-not-defined TURKISH-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(unit-type-count skirmisher-line < 20)
	(strategic-number sn-archer-threat >= 1)
	(or 
		(research-completed ri-elite-skirmisher)
		(unit-type-count skirmisher-line < 7)
	)
	(unit-type-count trade-cart < min-number-upgrade)
	(wood-amount > 145)
	(current-age >= castle-age)
	(nand 
		(goal position-goal pocket)
		(game-time < 1800)
	)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
#end-if
#load-if-defined UP-1-PLAYER-TEAM
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(gold-amount < 750)
	(research-completed ri-elite-skirmisher)
	(or
		(and
			(research-available my-unique-research)
			(civ-selected aztec)
		)
		(research-available ri-hussar)
	)
	(wood-amount > 250)
	(food-amount > 500)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(unit-type-count spearman-line > 14)
	(research-available ri-halberdier)
	(nor 
		(research-available ri-blast-furnace)
		(research-available ri-bracer)
	)
	(nor
		(research-available ri-plate-mail)
		(research-available ri-plate-barding)
	)
	(research-completed ri-chemistry)
	(wood-amount > 250)
	(food-amount > 350)
	(gold-amount < 600)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-current-age >= fc-transit)
	(game-time < 1320)
	(unit-type-count spearman-line < 4)
	(goal strategy-goal rush)
	(goal unit-goal skirmisher)
	(goal enemy-goal rush)
	(goal meso-enemy-goal -1)
	(can-train spearman-line); make spearman so enemy won't raid us with knights
=>
	(train spearman-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(game-time < 1800)
		(strategic-number sn-current-age >= ci-transit)
	)
	(strategic-number sn-resource-control < 1)
	(building-type-count-total town-center >= tc-level-two)
	(goal unit-goal skirmisher)
	(wood-amount > 115)
	(food-amount > 85)
	(goal strategy-goal rush)
	(unit-type-count spearman-line < 10)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-current-age >= fc-transit)
	(or
		(military-population < 4)		
		(building-type-count-total monastery > 0)
	)
	(goal strategy-goal rush)
	(goal unit-goal skirmisher)
	(food-amount > 90)
	(wood-amount > 95)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal rush)
	(goal unit-goal skirmisher)
	(or
		(goal enemy-goal skirmisher)
		(and
			(players-military-population focus-player > 4)
			(players-current-age focus-player == feudal-age)
		)
	)
	(or
		(military-population < 8)
		(strategic-number sn-military-level < 0)
	)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-current-age == fc-transit)
	(military-population < 8)
	(goal strategy-goal rush)
	(goal unit-goal skirmisher)
	(goal enemy-goal flush)
	(up-research-status c: ri-double-bit-axe >= research-pending)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(strategic-number sn-current-age == fc-transit)
		(and
			(gold-amount > 145)
			(building-type-count-total monastery > 0)
		)
	)
	(wood-amount > 85)
	(goal strategy-goal rush)
	(goal ranged-unit-type-goal archer)
	(goal unit-goal skirmisher)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(current-age == castle-age)
	(current-age-time < 400)
	(strategic-number sn-resource-control < 1)
	(goal strategy-goal rush)
	(goal unit-goal archer)
	(nor 
		(research-available ri-crossbow)
		(goal enemy-goal flush)
	)
	(or
		(or
			(players-civ any-enemy frankish)
			(players-civ any-enemy persian)
		)
		(players-civ any-enemy hun)
	)
	(unit-type-count spearman-line < 8)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(current-age == castle-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-resource-control != battering-ram)
	(strategic-number sn-archer-threat > 1)
	(unit-type-count scorpion-line < 8)
	(or
		(goal unit-goal eagle-warrior)
		(goal unit-goal monk)
	)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)
#end-if
#load-if-not-defined CHINESE-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined BRITON-CIV
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined AZTEC-CIV
(defrule
	(goal unit-goal gunpowder-class)
	(goal strategy-goal fast-imp)
=>
	(up-jump-rule 7)
)
(defrule
	(current-age == imperial-age)
	(or
		(or
			(civ-selected hun)
			(civ-selected korean)
		)
		(or
			(civ-selected saracen)
			(civ-selected spanish)
		)		
	)
=>
	(up-jump-rule 6)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal save-wood-goal 0)
	(strategic-number sn-archer-threat >= 1)
	(unit-type-count scorpion-line < 5)
	(or
		(and
			(food-amount < 50)
			(strategic-number sn-military-level < 0)
		)
		(and
			(up-research-status c: ri-elite-skirmisher < research-complete)
			(up-compare-goal unit-goal != eagle-warrior)
		)
	)
	(up-compare-goal position-goal != pocket)
	(unit-type-count mangonel-line > 1)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(players-unit-type-count any-enemy spearman-line > 20)
	(unit-type-count cavalry-class > 20)
	(unit-type-count scorpion-line < 15)
	(research-completed ri-heavy-scorpion)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-archer-threat >= 1)
	(unit-type-count scorpion-line < 12)
	(or
		(and
			(gold-amount > 150)
			(food-amount < 75)
		)
		(civ-selected turkish)
	)
	(up-compare-goal unit-goal != monk)
	(can-train scorpion-line)
	(unit-type-count mangonel-line > 1)
=>
	(train scorpion-line)
)
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined CELTIC-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(nor (goal strategy-goal fast-imp)
	(goal unit-goal default-ranged))
	(up-compare-goal unit-goal != monk)
	(strategic-number sn-resource-control < 1)
	(can-train scorpion-line)
	(unit-type-count scorpion-line < 12)
	(wood-amount > 400); excess wood and gold
	(gold-amount > 300)
	(food-amount < 200)
=>
	(train scorpion-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(not (research-completed ri-elite-skirmisher))
	(goal unit-goal monk)
	(strategic-number sn-resource-control < 1)
	(can-train scorpion-line)
	(strategic-number sn-archer-threat >= 1)
	(unit-type-count scorpion-line < 6)
	(up-compare-goal position-goal != pocket)
	(wood-amount > 200)
=>
	(train scorpion-line)
)
#end-if
#end-if
(defrule
	(or
		(or
			(civ-selected celtic)
			(civ-selected slavic)
		)
		(or
			(goal ranged-unit-type-goal scorpion)
			(goal unit-goal scorpion)
		)
	)
	(or
		(strategic-number sn-resource-control < 1)
		(and
			(strategic-number sn-resource-control == 2)
			(food-amount < 100)
		)
	)
	(population < del-civ-pop)
	(can-train scorpion-line)
	(gold-amount > scorpion-gold-min)
=>
	(train scorpion-line)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-resource-control < 1)
	(game-time < 900)
	(population < max-civ-pop)
	(gold-amount > scorpion-gold-min)
	(research-completed ri-heavy-scorpion)
	(can-train scorpion-line)
=>
	(train scorpion-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-resource-control < 1)
	(can-train archer-line)
	(research-completed ri-arbalest)
	(wood-amount > 400); excess wood and gold
	(gold-amount > 300)
	(food-amount < 200)
=>
	(train archer-line)
)
#load-if-not-defined MAGYAR-CIV
(defrule
	(strategic-number sn-resource-control == 2)
	(or
		(and
			(wood-amount < 300)
			(not (research-available ri-champion))
		)
		(goal monk-threat-goal 1)
	)
	(food-amount > 580); hussar + lc food cost
	(gold-amount < 600); hussar gold cost
	(population < max-civ-pop)
	(research-completed ri-light-cavalry)
	(can-train scout-cavalry-line)
=>
	(train scout-cavalry-line)
)
#end-if
#load-if-defined UP-1-PLAYER-TEAM
#load-if-not-defined AZTEC-CIV
#load-if-not-defined BRITON-CIV
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined SPANISH-CIV
#load-if-not-defined TURKISH-CIV
#load-if-not-defined VIKING-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(strategic-number sn-current-age >= ci-transit)
	(or
		(gold-amount <= 90)
		(goal escrow-purpose-goal ri-halberdier)
	)
	(food-amount > 200)
	(wood-amount > 200)
	(or
		(unit-type-count spearman-line < 10)
		(research-completed ri-pikeman)
	)
	(nor
		(research-completed ri-elite-teutonic-knight)
		(research-completed ri-elite-throwing-axeman)
	)
	(can-train spearman-line)
=>
	(train spearman-line)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(wood-amount > 300)
	(food-amount > 175)
	(population < max-civ-pop)
	(or
		(dropsite-min-distance gold > 5)
		(gold-amount < 100)
	)
	(unit-type-count trade-cart < min-number-upgrade)
	(research-completed ri-elite-skirmisher)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control == 2)
	(wood-amount > 400)
	(food-amount > 400)
	(unit-type-count trade-cart < min-number-upgrade)
	(or
		(dropsite-min-distance gold > 5)
		(gold-amount < 100)
	)
	(strategic-number sn-cavalry-threat > 1)
	(research-completed ri-pikeman)
	(population < max-civ-pop)
	(strategic-number sn-military-level < 1)
	(can-train spearman-line)
=>
	(train spearman-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control == 2)
	(wood-amount > 400)
	(food-amount > 400)
	(population < max-civ-pop)
	(unit-type-count trade-cart < min-number-upgrade)
	(or
		(dropsite-min-distance gold > 5)
		(gold-amount < 100)
	)
	(strategic-number sn-military-level < 1)
	(research-completed ri-elite-skirmisher)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-resource-control == 2)
	(wood-amount > 400)
	(food-amount > 400)
	(population < max-civ-pop)
	(gold-amount < 100)
	(strategic-number sn-military-level < 1)
	(research-completed ri-elite-skirmisher)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(wood-amount > 400)
	(food-amount > 400)
	(strategic-number sn-cavalry-threat > 1)
	(research-completed ri-halberdier)
	(population < del-civ-pop)
	(goal uu-up-goal 1)
	(research-available my-unique-unit-upgrade)
	(strategic-number sn-military-level < 0)
	(unit-type-count spearman-line < 30)
	(can-train spearman-line)
=>
	(train spearman-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control < 1)
	(wood-amount > 400)
	(food-amount > 400)
	(unit-type-count trebuchet-set == 0)
	(not (research-completed ri-siege-ram))
	(population < del-civ-pop)
	(research-completed ri-elite-skirmisher)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-resource-control <= 2)
	(wood-amount > 400)
	(food-amount > 400)
	(or
		(and
			(goal uu-up-goal 1)
			(research-available my-unique-unit-upgrade)
		)
		(and
			(unit-type-count knight-line > 19)
			(research-available ri-paladin)
		)
	)
	(population < del-civ-pop)
	(strategic-number sn-military-level < 0)
	(research-completed ri-elite-skirmisher)
	(can-train skirmisher-line)
=>
	(train skirmisher-line)
)
#end-if
#load-if-defined DEATH-MATCH
#load-if-defined DARK-AGE-START
(defrule
	(current-age <= castle-age)
	(or
		(and
			(strategic-number sn-military-level < 0)
			(current-age == feudal-age)
		)
		(goal ranged-unit-type-goal archer)
	)
	(can-train archer-line)
=>
	(train archer-line)
)
(defrule
	(unit-type-count militiaman-line > 20)
=>
	(research ri-man-at-arms)
	(research ri-two-handed-swordsman)
	(research ri-long-swordsman)
)
(defrule
	(strategic-number sn-current-age < castle); train militias to keep up with dark age militias
	(or
		(strategic-number sn-military-level < -1)
		(current-age <= feudal)
	)
	(can-train militiaman-line)
=>
	(train militiaman-line)
)
#end-if
#end-if

; ********** OTHER RESEARCHES **********

#load-if-defined DIFFICULTY-EASIEST
(defrule
	(current-age >= imperial-age)
	(strategic-number sn-resource-control == 2)
	(population >= fourty-percent-pop)
=>
	(set-strategic-number sn-resource-control 0)
)
#end-if
#load-if-defined MONGOL-CIV
(defrule
	(unit-type-count battering-ram-line > 3)
	(research-completed ri-siege-ram)
	(can-research my-unique-research)
=>
	(research my-unique-research)
)
#end-if

(defrule
	(strategic-number sn-current-age >= ci-transit)
	(strategic-number sn-resource-control < 1)
	(or
		(and
			(population > del-civ-pop)
			(current-age == imperial-age)
		)
		(goal unit-goal wonder)
	)
	(can-research ri-stonecutting)
=>
	(research ri-stonecutting)
)
#load-if-not-defined WONDER-RACE
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(food-amount < 300)
		(strategic-number sn-current-age >= ci-transit)
	)
	(can-research ri-ballistics)
=>
	(research ri-ballistics)
)
(defrule
	(strategic-number sn-resource-control <= 2)
	(or
		(wood-amount > 800)
		(food-amount > 800)
	)
	(or
		(current-age-time > 30)
		(gold-amount < 400)
	)
	(can-research-with-escrow ri-guilds)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-guilds)
)
(defrule
	(cc-players-unit-type-count 0 stone-mine > 5); stone left on map
	(or
		(unit-type-count 124 > 0)
		(unit-type-count 220 > 0)
	)
	(or
		(strategic-number sn-resource-control < 1)
		(strategic-number sn-stone-gatherer-percentage > 15)
	)
	(strategic-number sn-current-age >= ci-transit)
	(strategic-number sn-stone-gatherer-percentage > 4)
	(can-research ri-stone-shaft-mining)
=> 
	(research ri-stone-shaft-mining)
)
#load-if-defined UP-GAME-AGE2-X2
(defrule
	(can-research ri-cartography)
=>
	(research ri-cartography)
)
#end-if
(defrule
	(strategic-number sn-resource-control < 1)
	(current-age >= imperial-age)
	(or
		(gold-amount < 100)
		(population > del-civ-pop)
	)
	(food-amount > 400)
	(wood-amount > 400)
	(can-research ri-masonry)
=>
	(research ri-masonry)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(gold-amount < 100)
		(population > del-civ-pop)
	)
	(food-amount > 400)
	(wood-amount > 400)
	(can-research ri-architecture)
=>
	(research ri-architecture)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(gold-amount < 100)
		(population > del-civ-pop)
	)
	(food-amount > 400)
	(wood-amount > 400)
	(building-type-count castle >= 2)
	(can-research ri-hoardings)
=>
	(research ri-hoardings)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(population > del-civ-pop)
	(can-research ri-siege-engineers)
=>
	(research ri-siege-engineers)
)
(defrule
	(strategic-number sn-resource-control < 3)
	(research-completed ri-heavy-scorpion)
	(can-research-with-escrow ri-siege-engineers)
=>
	(release-escrow food)(release-escrow wood)
	(research ri-siege-engineers)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(food-amount > 1200)
	(current-age-time > 300)
	(population > del-civ-pop)
	(can-research ri-town-watch)
=>
	(research ri-town-watch)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(or
		(town-under-attack)
		(population > del-civ-pop)
	)
	(building-type-count castle > 0)
	(can-research ri-murder-holes)
=>
	(research ri-murder-holes)
)
(defrule
	(current-age == imperial-age)
	(strategic-number sn-resource-control < 1)
	(population > del-civ-pop)
	(unit-type-count infantry-class > 11)
	(can-research ri-tracking)
=>
	(research ri-tracking)
)
(defrule
	(strategic-number sn-resource-control < 1)
	(population > del-civ-pop)
	(building-type-count castle > 0)
	(can-research ri-bombard-tower)
=>
	(research ri-bombard-tower)
)
(defrule
	(strategic-number sn-resource-control == ri-heresy)
	(can-research-with-escrow ri-heresy)
=>
	(release-escrow gold)
	(research ri-heresy)
)
(defrule
	(strategic-number sn-resource-control < 2)
	(players-unit-type-count any-enemy monk > 19)
	(can-research-with-escrow ri-faith)
=>
	(release-escrow food)(release-escrow gold)
	(research ri-faith)
)
#load-if-not-defined DEFEND-WONDER
#load-if-not-defined UP-POCKET-POSITION
#load-if-not-defined UP-1-PLAYER-TEAM
#load-if-not-defined UP-2-PLAYER-TEAM
(defrule
	(game-time > 300)
	(building-type-count bombard-tower < 6)
	(game-time < 900); build them defensively early in DM
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(can-build bombard-tower)
=>
	(build bombard-tower)
)
#end-if
#end-if
#end-if
#else
(defrule
	(game-time > 300)
	(building-type-count bombard-tower < 6)
	(building-type-count wonder > 0)
	(can-build bombard-tower)
=>
	(build bombard-tower)
)
#end-if
#load-if-defined TEUTONIC-CIV
(defrule
	(research-completed ri-hoardings)
	(building-type-count castle > 1)
	(can-research my-unique-research)
=>
	(research my-unique-research)
)
#end-if
#load-if-defined TURKISH-CIV
(defrule
	(or
		(building-type-count bombard-tower > 1)
		(unit-type-count cannon-galleon-line > 3)
	)
	(can-research my-unique-research)
=>
	(research my-unique-research)
)
#end-if
(defrule
	(current-age == imperial-age)
	(unit-type-count transport-ship > 0)
	(strategic-number sn-resource-control <= 1)
=>
	(research ri-padded-archer-armor)
	(research ri-leather-archer-armor)
	(research ri-ring-archer-armor)
	(research ri-chain-mail)
	(research ri-plate-mail)
	(research ri-scale-barding)
	(research ri-chain-barding)
	(research ri-plate-barding)
	(research ri-forging)
	(research ri-iron-casting)
	(research ri-blast-furnace)
	(research ri-heavy-camel)
)

; ********** FISHING SHIPs ********** ; Put them at the end for low priority

#load-if-not-defined POPULATION-CAP-25
(defrule
	(strategic-number sn-current-age == df-transit)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(building-type-count-total mining-camp > 0)
	(up-compare-goal strategy-goal != flush)
	(can-train fishing-ship)
=>
	(train fishing-ship); make at least one extra ship
	(disable-self)
)
(defrule
	(strategic-number sn-current-age == df-transit)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(wood-amount > 325)
	(up-compare-goal strategy-goal != flush)
	(can-train fishing-ship)
=>
	(train fishing-ship); make more fishing ships during feudal transit on islands
)
(defrule
	(strategic-number sn-current-age == dark)
	(or
		(building-type-count-total lumber-camp > 0)
		(and
			(civ-selected persian)
			(unit-type-count-total fishing-ship < 3)
		)
	)
	(dropsite-min-distance wood < 4)
	(can-train fishing-ship)
=>
	(train fishing-ship)
	(set-strategic-number sn-enable-training-queue 1)
)
#end-if
#end-if ; NOT WONDER-RACE

; ********** GATHERER PERCENTAGES **********

#load-if-not-defined POST-IMPERIAL-AGE-START
#load-if-not-defined DEATH-MATCH
(defrule
	(unit-type-count villager >= 7)
=>
	(set-strategic-number sn-food-gatherer-percentage 94)
	(set-strategic-number sn-wood-gatherer-percentage 6)
	(set-strategic-number sn-gold-gatherer-percentage 0)
	(set-strategic-number sn-stone-gatherer-percentage 0)
	(set-strategic-number sn-maximum-food-drop-distance 25)
	(set-strategic-number sn-maximum-wood-drop-distance wood-distance2); enable wood gathering
	(disable-self)
)
(defrule
	(unit-type-count villager >= 8)
=>
	(set-strategic-number sn-food-gatherer-percentage 83)
	(set-strategic-number sn-wood-gatherer-percentage 17)
	(set-strategic-number sn-gold-gatherer-percentage 0)
	(set-strategic-number sn-stone-gatherer-percentage 0)
	(set-strategic-number sn-maximum-wood-drop-distance wood-distance2)
	(disable-self)
)
(defrule
	(or
		(and
			(building-type-count town-center == 0)
			(building-type-count-total town-center > 0)
		)
		(unit-type-count villager >= 9)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 74)
	(set-strategic-number sn-wood-gatherer-percentage 26)
	(set-strategic-number sn-gold-gatherer-percentage 0)
	(set-strategic-number sn-stone-gatherer-percentage 0)
	(set-strategic-number sn-maximum-wood-drop-distance wood-distance2)
	(disable-self)
)
(defrule
	(unit-type-count villager == 10)
=>
	(set-strategic-number sn-food-gatherer-percentage 76)
	(set-strategic-number sn-wood-gatherer-percentage 24)
	(disable-self)
)
(defrule
	(unit-type-count villager == 11)
=>
	(set-strategic-number sn-food-gatherer-percentage 70)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(disable-self)
)
(defrule
	(unit-type-count villager == 12)
=>
	(set-strategic-number sn-food-gatherer-percentage 65)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(disable-self)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 13)
=>
	(set-strategic-number sn-food-gatherer-percentage 60)
	(set-strategic-number sn-wood-gatherer-percentage 40)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 14)
=>
	(set-strategic-number sn-food-gatherer-percentage 65)
	(set-strategic-number sn-wood-gatherer-percentage 35)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 15)
=>
	(set-strategic-number sn-food-gatherer-percentage 71)
	(set-strategic-number sn-wood-gatherer-percentage 29)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 15)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(up-compare-goal strategy-goal != flush)
=>
	(set-strategic-number sn-food-gatherer-percentage 62)
	(set-strategic-number sn-wood-gatherer-percentage 38)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 16)
	(up-compare-goal strategy-goal != flush)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
=>
	(set-strategic-number sn-food-gatherer-percentage 67)
	(set-strategic-number sn-wood-gatherer-percentage 33)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 17)
	(up-compare-goal strategy-goal != flush)
=>
	(set-strategic-number sn-food-gatherer-percentage 62)
	(set-strategic-number sn-wood-gatherer-percentage 38)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 18)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(up-compare-goal strategy-goal != flush)
=>
	(set-strategic-number sn-food-gatherer-percentage 59)
	(set-strategic-number sn-wood-gatherer-percentage 41)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 19)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(up-compare-goal strategy-goal != flush)
=>
	(set-strategic-number sn-food-gatherer-percentage 56)
	(set-strategic-number sn-wood-gatherer-percentage 44)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 20)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(up-compare-goal strategy-goal != flush)
=>
	(set-strategic-number sn-food-gatherer-percentage 62)
	(set-strategic-number sn-wood-gatherer-percentage 38)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager == 21)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(up-compare-goal strategy-goal != flush)
=>
	(set-strategic-number sn-food-gatherer-percentage 64)
	(set-strategic-number sn-wood-gatherer-percentage 36)
)
(defrule
	(strategic-number sn-current-age == dark)
	(unit-type-count villager >= 22)
=>
	(set-strategic-number sn-food-gatherer-percentage 70)
	(set-strategic-number sn-wood-gatherer-percentage 30)
)
(defrule
	(strategic-number sn-current-age == dark)
	(or
		(and
			(civ-selected mongol)
			(up-compare-goal strategy-goal != flush)
		)
		(unit-type-count villager >= 27)
	)
	(unit-type-count villager >= 22)
=>
	(set-strategic-number sn-food-gatherer-percentage 60)
	(set-strategic-number sn-wood-gatherer-percentage 40)
)
#else
(defrule
	(unit-type-count villager >= 12)
=>
	(set-strategic-number sn-maximum-food-drop-distance 15)
	(set-strategic-number sn-maximum-gold-drop-distance 14)
	(set-strategic-number sn-maximum-stone-drop-distance 14)
	(set-strategic-number sn-maximum-hunt-drop-distance 24)
	(set-strategic-number sn-cap-civilian-builders 100)
	(set-strategic-number sn-cap-civilian-gatherers 100)(disable-self))
#end-if ;DM
#load-if-defined LOW-RESOURCES-START
(defrule
	(strategic-number sn-current-age == df-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 37)
	(set-strategic-number sn-gold-gatherer-percentage 13)
	(set-strategic-number sn-maximum-hunt-drop-distance 20)
)
(defrule
	(or
		(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
		(building-type-count-total mining-camp == 0)
	)
	(strategic-number sn-current-age == df-transit);will overwrite rule above
	(goal strategy-goal flush)
	(or
		(up-compare-goal unit-goal != militiaman-line)
		(civ-selected gothic)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 60)
	(set-strategic-number sn-gold-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == df-transit)
	(unit-type-count fishing-ship > 2)
	(building-type-count-total mining-camp > 0)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 55)
	(set-strategic-number sn-gold-gatherer-percentage 10)
)
(defrule
	(strategic-number sn-current-age == df-transit)
	(goal strategy-goal flush)
	(goal unit-goal scout-cavalry)	
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 10)
)
#load-if-defined SARACEN-CIV
(defrule
	(strategic-number sn-current-age == df-transit)
	(goal strategy-goal rush)
	(goal unit-goal monk)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 0)
)
#end-if
#end-if
(defrule
	(or
		(and
			(regicide-game)
			(strategic-number sn-current-age == df-transit)
		)
		(strategic-number sn-current-age == feudal)
	)
	(nand
		(goal strategy-goal flush)
		(building-type-count-total blacksmith == 0)
	)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 48)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 19)
)
(defrule
	(up-compare-goal strategy-goal != sling)
	(up-compare-goal strategy-goal != flush)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-current-age == feudal)
	(or
		(and
			(current-age-time < 20)
			(food-amount < 600)
		)
		(strategic-number sn-food-gatherer-percentage == 50)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 10)
	(set-strategic-number sn-stone-gatherer-percentage 0)
	(up-jump-rule 20)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal scout-cavalry)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 15)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 48)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 7)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal scout-cavalry)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 55)
	(set-strategic-number sn-wood-gatherer-percentage 29)
	(set-strategic-number sn-gold-gatherer-percentage 16)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 55)
	(set-strategic-number sn-wood-gatherer-percentage 34)
	(set-strategic-number sn-gold-gatherer-percentage 11)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(or
		(and
			(goal unit-goal militiaman-line)
			(goal enemy-goal flush)
		)
		(strategic-number sn-cavalry-threat > 1)
	)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 55)
	(set-strategic-number sn-wood-gatherer-percentage 29)
	(set-strategic-number sn-gold-gatherer-percentage 16)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(goal unit-goal archer)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 20)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(current-age-time < 300)
	(goal control-goal belated-flush-defense)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 5)
)
#load-if-not-defined UP-MICHI-STYLE
#load-if-not-defined KOREAN-CIV ;Korean have best stone miners in feudal, but they lack other resources, so don't mine too early with them
(defrule
	(strategic-number sn-current-age == feudal)
	(goal unit-goal my-unique-unit-line)
	(nor 
		(goal strategy-goal flush)
		(goal strategy-goal sling)
	)
	(dropsite-min-distance stone < 6)
	(stone-amount < 700)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
#end-if
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(unit-type-count fishing-ship > 3)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 55)
	(set-strategic-number sn-gold-gatherer-percentage 15)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(or
		(strategic-number sn-archer-threat > 2)
		(goal unit-goal skirmisher)
	)
	(building-type-count-total archery-range > 1)
	(building-type-count-total blacksmith > 0)
	(building-type-count-total mining-camp > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 55)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 10)
)
(defrule
	(or
		(and
			(strategic-number sn-current-age == feudal)
			(goal strategy-goal sling)
		)
		(and
			(strategic-number sn-wood-gatherer-percentage == 100); end of rebuild code
			(building-type-count town-center > 0); use this in case not other percentages are loaded for some reason.
		)
	)
	(game-time > 1000)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(or
		(unit-type-count-total 229 >= 1); dead miners detected
		(unit-type-count-total 221 >= 1)
	)
	(stone-amount < 125)
	(strategic-number sn-stone-gatherer-percentage == 0)
=>
	(up-modify-sn sn-gold-gatherer-percentage c:- 5)
	(up-modify-sn sn-stone-gatherer-percentage c:+ 5)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(or
		(unit-type-count-total 219 >= 1); dead lumberjacks
		(unit-type-count-total 228 >= 1)
	)
	(stone-amount < 125)
	(strategic-number sn-stone-gatherer-percentage == 0)
=>
	(up-modify-sn sn-wood-gatherer-percentage c:- 5)
	(up-modify-sn sn-stone-gatherer-percentage c:+ 5)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal sling)
	(game-time > 1800)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 30)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal monk)
	(not (civ-selected saracen))
	(strategic-number sn-current-age == fc-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)	
	(strategic-number sn-current-age == fc-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal unit-goal eagle-warrior)
	(goal control-goal shot)
	(strategic-number sn-current-age >= fc-transit)
	(building-type-count-total town-center < tc-level-three)
	(stone-amount >= 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal unit-goal eagle-warrior)
	(goal control-goal shot)
	(strategic-number sn-current-age >= fc-transit)
	(building-type-count-total town-center < tc-level-three)
	(stone-amount < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 27)
	(set-strategic-number sn-wood-gatherer-percentage 49)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal boom)
	(or
		(strategic-number sn-current-age == fc-transit)
		(and
			(goal unit-goal eagle-warrior)
			(building-type-count-total monastery == 0)
		)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(or
		(goal strategy-goal rush)
		(goal strategy-goal boom)
	)
	(or
		(goal unit-goal skirmisher)
		(or
			(goal unit-goal militiaman-line)
			(goal unit-goal scorpion)
		)
	)
	(strategic-number sn-current-age == fc-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 10)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal boom)
	(or
		(and
			(goal unit-goal archer)
			(building-type-count gate > 0)
		)
		(goal unit-goal scorpion)
	)
	(or
		(strategic-number sn-current-age == fc-transit)
		(and
			(current-age == castle-age)
			(building-type-count-total town-center < 3)
		)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 60)
	(set-strategic-number sn-gold-gatherer-percentage 10)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#load-if-defined VIKING-CIV
(defrule
	(goal unit-goal knight)
	(up-compare-goal strategy-goal != flush)
	(strategic-number sn-current-age >= fc-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#end-if
(defrule
	(goal strategy-goal rush)
	(or
		(goal unit-goal scout-cavalry)
		(goal control-goal shot)
	)
	(current-age == feudal-age)
	(building-type-count-total stable > 0)
	(building-type-count-total blacksmith > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 55)
	(set-strategic-number sn-wood-gatherer-percentage 32)
	(set-strategic-number sn-gold-gatherer-percentage 13)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(or
		(and
			(goal strategy-goal rush)
			(goal unit-goal cavalry-archer)
		)
		(goal strategy-goal flush)
	)
	(strategic-number sn-current-age == fc-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 32)
	(set-strategic-number sn-wood-gatherer-percentage 48)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == fc-transit)
	(goal unit-goal archer)
	(goal strategy-goal flush)
=>
	(set-strategic-number sn-food-gatherer-percentage 29)
	(set-strategic-number sn-wood-gatherer-percentage 42)
	(set-strategic-number sn-gold-gatherer-percentage 29)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == fc-transit)
	(goal unit-goal archer)
	(goal strategy-goal rush)
=>
	(set-strategic-number sn-food-gatherer-percentage 41)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 24)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal boom)
	(goal unit-goal knight)
	(strategic-number sn-current-age >= fc-transit)
	(or
		(stone-amount >= 100)
		(dropsite-min-distance stone > 5)
	)
	(building-type-count-total town-center < tc-level-three)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 8)
	(set-strategic-number sn-stone-gatherer-percentage 0)
	(up-jump-rule 2)
)
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age >= fc-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 47)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal unit-goal knight)
	(goal strategy-goal flush)
	(building-type-count-total town-center < tc-level-three)
=>
	(set-strategic-number sn-food-gatherer-percentage 43)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 19)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#load-if-not-defined HIGH-RESOURCES-START
#load-if-defined BRITON-CIV
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(building-type-count-total castle == 0)
	(goal unit-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 6)
	(set-strategic-number sn-stone-gatherer-percentage 30)
)
#else
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(building-type-count-total castle == 0)
	(goal unit-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 31)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 6)
	(set-strategic-number sn-stone-gatherer-percentage 30)
)
#end-if
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(building-type-count-total castle == 0)
	(unit-type-count fishing-ship > 5)
	(goal unit-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 5)
	(set-strategic-number sn-wood-gatherer-percentage 55)
	(set-strategic-number sn-gold-gatherer-percentage 10)
	(set-strategic-number sn-stone-gatherer-percentage 30)
)
#end-if
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal boom)
	(goal position-goal pocket)
	(building-type-count gate > 0)
	(building-type-count-total castle == 0)
	(goal unit-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 5)
	(set-strategic-number sn-stone-gatherer-percentage 10)
)
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(unit-type-count villager > 50)
	(building-type-count-total castle == 0)
	(goal unit-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 26)
	(set-strategic-number sn-stone-gatherer-percentage 10)
)
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal fast-imp)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 32)
	(set-strategic-number sn-gold-gatherer-percentage 18)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal fast-imp)
	(goal unit-goal monk)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 25)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#load-if-defined SARACEN-CIV
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(or
		(and
			(goal unit-goal monk)
			(building-type-count-total monastery < 3)
		)
		(goal unit-goal scorpion)
	)
	(unit-type-count villager > 20)
	(game-time < 1800)
=>
	(set-strategic-number sn-food-gatherer-percentage 20)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 23)
)
(defrule
	(goal unit-goal monk)
	(building-type-count-total monastery > 2)
	(or
		(commodity-selling-price stone > 104)
		(building-type-count-total castle == 0)
	)
	(unit-type-count villager > 20)
	(game-time < 1800)
=>
	(set-strategic-number sn-food-gatherer-percentage 25)
	(set-strategic-number sn-wood-gatherer-percentage 20)
	(set-strategic-number sn-gold-gatherer-percentage 30)
	(set-strategic-number sn-stone-gatherer-percentage 25)
)
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(building-type-count market > 0)
	(building-type-count-total archery-range < 3)
	(building-type-count-total stable < 2)
	(building-type-count-total monastery < 3)
	(commodity-selling-price stone > 104)
	(unit-type-count villager > 20)
	(game-time < 1800)
=>
	(set-strategic-number sn-food-gatherer-percentage 10)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 20)
)
#end-if ; SARACEN-CIV
(defrule
	(or
		(strategic-number sn-current-age == ci-transit)
		(and
			(current-age == imperial-age)
			(and
				(building-type-count-total archery-range < 5)
				(goal unit-goal gunpowder-class)
			)
		)
	)
	(goal strategy-goal fast-imp)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(game-time < 1800)
	(strategic-number sn-current-age >= ci-transit)
	(goal strategy-goal fast-imp)
	(goal unit-goal gunpowder-class)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(or
		(building-type-count-total town-center == 1)
		(goal enemy-fortifications-goal 1)
	)
	(strategic-number sn-current-age >= ci-transit)
	(goal strategy-goal fast-imp)
	(building-type-count-total archery-range > 2)
	(goal unit-goal gunpowder-class)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 27)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(strategic-number sn-current-age == fc-transit)
	(goal unit-goal wonder)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 25)
	(set-strategic-number sn-gold-gatherer-percentage 40)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(strategic-number sn-cavalry-threat >= 1)
	(unit-type-count villager > 20)
	(building-type-count-total monastery > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 37)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(or
		(strategic-number sn-current-age == fc-transit)
		(and
			(current-age == castle-age)
			(current-age-time < 120)
		)
	)
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(strategic-number sn-cavalry-threat == 0)
	(building-type-count-total barracks > 2)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 26)
	(set-strategic-number sn-gold-gatherer-percentage 34)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(strategic-number sn-cavalry-threat == 0)
	(building-type-count-total monastery > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 34)
	(set-strategic-number sn-wood-gatherer-percentage 26)
	(set-strategic-number sn-gold-gatherer-percentage 40)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == fc-transit)
	(goal unit-goal my-unique-unit-line)
	(dropsite-min-distance stone < 6)
	(stone-amount < 700)
	(building-type-count castle > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 34)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 6)
	(research ri-stone-mining); debug, dba and hc are blocked
)
#load-if-not-defined VIKING-CIV
(defrule
	(goal strategy-goal rush)
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(current-age-time < 300)
=>
	(set-strategic-number sn-food-gatherer-percentage 48)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#end-if
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center < tc-level-three)
	(stone-amount >= 100)
	(goal unit-goal archer)
	(goal strategy-goal boom)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal cavalry-archer)
	(goal control-goal aggressive-rush)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(building-type-count-total stable < 2)
=>
	(set-strategic-number sn-food-gatherer-percentage 27)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 35)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal cavalry-archer)
	(goal control-goal aggressive-rush)
	(strategic-number sn-cavalry-threat > 1)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(building-type-count-total stable < 2)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 27)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal cavalry-archer)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total archery-range > 2)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 28)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(or
		(and
			(goal unit-goal archer)
			(stone-amount >= 100)
		)
		(goal unit-goal scorpion)
	)
	(building-type-count-total stable == 0)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 28)
	(set-strategic-number sn-wood-gatherer-percentage 42)
	(set-strategic-number sn-gold-gatherer-percentage 30)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal archer)
	(strategic-number sn-current-age == castle)
	(stone-amount >= 100)
	(unit-type-count villager > 20)
	(building-type-count-total town-center == 1)
	(or
		(or
			(research-completed ri-pikeman)
			(strategic-number sn-archer-threat > 1)
		)
		(goal position-goal pocket)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 42)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal archer)
	(building-type-count-total stable == 0)
	(strategic-number sn-current-age == castle)
	(stone-amount >= 100)
	(unit-type-count villager > 20)
	(building-type-count-total town-center > 1)
	(or
		(or
			(research-completed ri-pikeman)
			(strategic-number sn-archer-threat > 1)
		)
		(goal position-goal pocket)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(or
		(strategic-number sn-current-age == fc-transit)
		(or
			(research-available ri-bodkin-arrow)
			(and
				(research-available ri-pikeman)
				(strategic-number sn-cavalry-threat > 0)
			)
		)
	)
	(strategic-number sn-current-age == castle)
	(goal unit-goal archer)	
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 42)
	(set-strategic-number sn-gold-gatherer-percentage 21)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#end-if
(defrule
	(goal strategy-goal flush)
	(goal unit-goal archer)
	(unit-type-count villager > 20)
	(strategic-number sn-current-age == castle)
=>
	(set-strategic-number sn-food-gatherer-percentage 20)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 30)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(or
		(goal strategy-goal flush)
		(goal strategy-goal rush)
	)
	(goal unit-goal archer)
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone < 5)
	(strategic-number sn-current-age >= castle)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 32)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 24)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal archer)
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone < 5)
	(strategic-number sn-current-age >= castle)
	(current-age-time < 600)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(or
		(goal strategy-goal flush)
		(goal strategy-goal rush)
	)
	(goal unit-goal archer)
	(building-type-count-total town-center >= tc-level-four)
	(dropsite-min-distance stone < 5)
	(strategic-number sn-current-age == castle)
=>
	(set-strategic-number sn-food-gatherer-percentage 43)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 27)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal strategy-goal flush)
	(goal unit-goal cavalry-archer)
	(unit-type-count villager > 20)
	(building-type-count-total town-center >= tc-level-two)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 37)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal unit-goal cavalry-archer)
	(unit-type-count villager > 20)
	(building-type-count-total town-center == tc-level-two)
	(stone-amount < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 37)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(or
		(goal strategy-goal flush)
		(research-completed ri-bodkin-arrow)
	)
	(or
		(goal unit-goal archer)
		(goal unit-goal cavalry-archer)
	)
	(stone-amount < 100)
	(building-type-count-total town-center < tc-level-three)
	(strategic-number sn-current-age == castle)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 20)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 30)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
#load-if-not-defined CELTIC-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(goal unit-goal skirmisher)
	(up-compare-goal control-goal != my-unique-unit-line)
	(building-type-count-total town-center < 3)
	(stone-amount >= 100)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 46)
	(set-strategic-number sn-gold-gatherer-percentage 14)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal unit-goal skirmisher)
	(up-compare-goal control-goal != my-unique-unit-line)
	(strategic-number sn-current-age >= castle)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(stone-amount < 100)
	)
	(dropsite-min-distance stone < 5)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 13)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal skirmisher)
	(up-compare-goal control-goal != my-unique-unit-line)
	(strategic-number sn-current-age >= castle)
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone < 5)
	(unit-type-count villager > fourty-percent-pop)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 42)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal skirmisher)
	(up-compare-goal control-goal != my-unique-unit-line)
	(strategic-number sn-current-age >= castle)
	(building-type-count-total town-center >= tc-level-four)
	(dropsite-min-distance stone < 5)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 19)
	(set-strategic-number sn-stone-gatherer-percentage 1)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(building-type-count-total town-center >= tc-level-two)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 32)
	(set-strategic-number sn-gold-gatherer-percentage 23)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(stone-amount < 100)
	(unit-type-count villager > 20)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 43)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#load-if-not-defined GOTHIC-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(unit-type-count villager > 20)
	(goal control-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 31)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
#end-if
#else
(defrule
	(goal strategy-goal rush)
	(goal unit-goal skirmisher)
	(strategic-number sn-current-age >= castle)
	(building-type-count-total town-center < 3)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 48)
	(set-strategic-number sn-gold-gatherer-percentage 14)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal flush)
	(dropsite-min-distance stone < 6)
	(strategic-number sn-current-age >= castle)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 39)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 13)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(goal unit-goal militiaman-line)
	(dropsite-min-distance stone < 6)
	(strategic-number sn-current-age >= castle)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(goal unit-goal militiaman-line)
	(dropsite-min-distance stone < 6)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > fifty-percent-pop)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 27)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#end-if
#load-if-defined CELTIC-CIV
(defrule
	(goal unit-goal militiaman-line)
	(dropsite-min-distance stone < 6)
	(strategic-number sn-current-age >= ci-transit)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 23)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#end-if
#end-if
#load-if-defined GOTHIC-CIV
(defrule
	(goal strategy-goal flush)
	(goal unit-goal militiaman-line)
	(building-type-count-total town-center >= tc-level-three)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(current-age == imperial-age)
	(building-type-count castle > 0)
	(building-type-count-total town-center >= tc-level-three)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage uu-food2)
	(set-strategic-number sn-wood-gatherer-percentage uu-wood2)
	(set-strategic-number sn-gold-gatherer-percentage uu-gold2)
	(set-strategic-number sn-stone-gatherer-percentage uu-stone2)
)
#end-if
(defrule
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(building-type-count town-center >= tc-level-two)
	(stone-amount >= 100)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal boom)
	(or
		(goal unit-goal archer)
		(goal unit-goal scorpion)
	)
	(building-type-count-total town-center < tc-level-three)
	(stone-amount < 100); gather stone when TC disrupted
=>
	(set-strategic-number sn-food-gatherer-percentage 23)
	(set-strategic-number sn-wood-gatherer-percentage 46)
	(set-strategic-number sn-gold-gatherer-percentage 28)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(goal strategy-goal boom)
	(goal unit-goal scorpion)
	(building-type-count-total town-center >= tc-level-three)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 49)
	(set-strategic-number sn-gold-gatherer-percentage 14)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal strategy-goal boom)
	(or
		(goal unit-goal archer)
		(and
			(goal unit-goal scorpion)
			(research-completed ri-light-cavalry)
		)
	)
	(building-type-count-total town-center >= tc-level-three)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal strategy-goal boom)
	(or
		(goal unit-goal archer)
		(and
			(goal unit-goal scorpion)
			(research-completed ri-light-cavalry)
		)
	)
	(current-age-time < 600)
	(building-type-count-total town-center >= tc-level-three)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal boom)
	(goal unit-goal archer)
	(or
		(building-type-count-total town-center >= tc-level-four)
		(current-age-time > 300)
	)
	(strategic-number sn-current-age >= castle-age)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 18)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal strategy-goal boom)
	(goal unit-goal archer)
	(strategic-number sn-current-age >= ci-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 26)
	(set-strategic-number sn-wood-gatherer-percentage 42)
	(set-strategic-number sn-gold-gatherer-percentage 30)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal scorpion)
	(strategic-number sn-current-age >= ci-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 26)
	(set-strategic-number sn-wood-gatherer-percentage 44)
	(set-strategic-number sn-gold-gatherer-percentage 28)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal strategy-goal fast-imp)
	(strategic-number sn-current-age >= fc-transit)
	(goal strategy-goal eagle-warrior)
=>
	(set-strategic-number sn-food-gatherer-percentage 44)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 26)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#load-if-defined AZTEC-CIV
(defrule
	(goal strategy-goal boom)
	(goal unit-goal archer)
	(building-type-count-total town-center >= tc-level-three)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 46)
	(set-strategic-number sn-gold-gatherer-percentage 12)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#end-if
#load-if-defined INDIAN-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-three)
	(goal strategy-goal fast-imp)
	(goal unit-goal gunpowder-class)
=>
	(set-strategic-number sn-food-gatherer-percentage 55)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#end-if
#load-if-defined PERSIAN-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-four)
	(dropsite-min-distance stone <= 6)
	(goal control-goal my-unique-unit-line)
	(building-type-count-total castle == 0)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 10)
)
#end-if
(defrule
	(goal strategy-goal rush)
	(or
		(goal unit-goal eagle-warrior)
		(goal unit-goal militiaman-line)
	)
	(strategic-number sn-current-age >= castle)
	(dropsite-min-distance stone <= 6)
	(or
		(building-type-count-total town-center >= tc-level-two)
		(and
			(stone-amount < 100)
			(research-completed ri-pikeman)
		)
	)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 26)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(or
		(strategic-number sn-cavalry-threat == 0)
		(strategic-number sn-current-age >= ci-transit)
	)
	(dropsite-min-distance stone <= 6)
	(civilian-population > fifty-percent-pop)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 34)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal eagle-warrior)
	(strategic-number sn-cavalry-threat == 0)
	(strategic-number sn-current-age == ci-transit)
	(building-type-count town-center == 3)
=>
	(set-strategic-number sn-food-gatherer-percentage 41)
	(set-strategic-number sn-wood-gatherer-percentage 26)
	(set-strategic-number sn-gold-gatherer-percentage 30)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#load-if-defined AZTEC-CIV
(defrule
	(goal unit-goal militiaman-line)
	(strategic-number sn-current-age >= ci-transit)
	(game-time < 2400)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 46)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 19)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#end-if
#load-if-not-defined BYZANTINE-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined SLAVIC-CIV
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 4)
	(nor (goal unit-goal archer)
	(goal unit-goal knight))
	(goal control-goal my-unique-unit-line)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 41)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 18)
	(set-strategic-number sn-stone-gatherer-percentage 14)
)
#else
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 4)
	(goal unit-goal scorpion)
	(goal control-goal my-unique-unit-line)
	(building-type-count-total castle >= 0)
	(unit-type-count villager > 20)
	(strategic-number sn-current-age >= castle)
=>
	(set-strategic-number sn-food-gatherer-percentage 34)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 27)
	(set-strategic-number sn-stone-gatherer-percentage 6)
)
#end-if ;SLAVIC-CIV
#else
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 4)
	(goal control-goal my-unique-unit-line)
	(or
		(strategic-number sn-current-age == castle)
		(building-type-count-total castle == 0)
	)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 39)
	(set-strategic-number sn-gold-gatherer-percentage 16)
	(set-strategic-number sn-stone-gatherer-percentage 10)
)
#end-if ;KOREAN-CIV
#else
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 4)
	(goal control-goal my-unique-unit-line)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 39)
	(set-strategic-number sn-gold-gatherer-percentage 12)
	(set-strategic-number sn-stone-gatherer-percentage 13)
)
#end-if ;CELTIC-CIV
#else
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 4)
	(up-compare-goal unit-goal != knight)
	(goal control-goal my-unique-unit-line)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 37)
	(set-strategic-number sn-gold-gatherer-percentage 13)
	(set-strategic-number sn-stone-gatherer-percentage 15)
)
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 4)
	(goal control-goal my-unique-unit-line)
	(strategic-number sn-current-age == castle)
	(goal unit-goal skirmisher)
	(goal enemy-goal skirmisher)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 39)
	(set-strategic-number sn-wood-gatherer-percentage 41)
	(set-strategic-number sn-gold-gatherer-percentage 5)
	(set-strategic-number sn-stone-gatherer-percentage 15)
)
#end-if ;BYZANTINE-CIV
#load-if-not-defined KOREAN-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total castle == 1)
	(goal unit-goal my-unique-unit-line)
	(unit-type-count villager > 20)
	(nand
		(building-type-count-total town-center < tc-level-three)
		(wood-amount > 400)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 18)
	(set-strategic-number sn-stone-gatherer-percentage 7)
)
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-three)
	(goal unit-goal default-ranged)
	(goal control-goal my-unique-unit-line)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 36)
	(set-strategic-number sn-gold-gatherer-percentage 21)
	(set-strategic-number sn-stone-gatherer-percentage 6)
)
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count-total castle == 1)
	(goal unit-goal my-unique-unit-line)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#else
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total castle == 1)
	(goal unit-goal my-unique-unit-line)
	(unit-type-count villager > 20)
	(nand
		(wood-amount > 300)
		(stone-amount < 100)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 31)
	(set-strategic-number sn-wood-gatherer-percentage 52)
	(set-strategic-number sn-gold-gatherer-percentage 13)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
#end-if
#load-if-not-defined BRITON-CIV
#load-if-not-defined CHINESE-CIV
#load-if-not-defined KOREAN-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined MONGOL-CIV
#load-if-not-defined UP-MICHI-STYLE ; food uu
(defrule
	(strategic-number sn-current-age == feudal)
	(goal unit-goal my-unique-unit-line)
	(goal strategy-goal boom)
	(dropsite-min-distance stone < 6)
	(stone-amount < 600)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 15)
)
#end-if
#load-if-not-defined TEUTONIC-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total castle >= 1)
	(goal unit-goal my-unique-unit-line)
	(unit-type-count villager > 20)
	(nand
		(building-type-count-total town-center == tc-level-two)
		(wood-amount > 400)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total castle >= 1)
	(goal unit-goal my-unique-unit-line)
	(unit-type-count villager > 20)
	(current-age-time > 420)
=>
	(set-strategic-number sn-food-gatherer-percentage 46)
	(set-strategic-number sn-wood-gatherer-percentage 34)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#load-if-defined SLAVIC-CIV
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-two)
	(goal unit-goal my-unique-unit-line)
	(unit-type-count villager > 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 34)
	(set-strategic-number sn-gold-gatherer-percentage 21)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#else
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-five)
	(unit-type-count villager > 20)
	(civilian-population < max-civ)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 16)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
#end-if
(defrule
	(goal unit-goal my-unique-unit-line)
	(building-type-count-total castle > 1)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage uu-food2)
	(set-strategic-number sn-wood-gatherer-percentage uu-wood2)
	(set-strategic-number sn-gold-gatherer-percentage uu-gold2)
	(set-strategic-number sn-stone-gatherer-percentage uu-stone2)
)
#load-if-defined CELTIC-CIV
(defrule
	(or
		(research-completed my-unique-unit-upgrade)
		(research-completed ri-champion)
	)
	(research-completed ri-halberdier)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
#end-if
(defrule
	(current-age == castle-age)
	(goal unit-goal my-unique-unit-line)
	(building-type-count-total castle > 1)
	(unit-type-count villager > 20)
	(or
		(or 
			(civ-selected byzantine)
			(civ-selected spanish)
		)
		(or
			(civ-selected japanese)
			(civ-selected gothic)
		)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 45)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#load-if-defined BYZANTINE-CIV
(defrule
	(up-compare-goal strategy-goal != boom)
	(goal uu-up-goal 1)
	(goal unit-goal my-unique-unit-line)
	(building-type-count-total castle > 1)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 44)
	(set-strategic-number sn-wood-gatherer-percentage 28)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(goal strategy-goal boom)
	(strategic-number sn-current-age == ci-transit)
	(goal unit-goal my-unique-unit-line)
	(building-type-count-total castle > 1)
	(unit-type-count villager > 20)	
=>
	(set-strategic-number sn-food-gatherer-percentage 24)
	(set-strategic-number sn-wood-gatherer-percentage 48)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#end-if
#load-if-not-defined TEUTONIC-CIV
(defrule
	(goal strategy-goal boom)
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(building-type-count town-center >= tc-level-three)
	(building-type-count-total town-center < tc-level-five)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 46)
	(set-strategic-number sn-wood-gatherer-percentage 42)
	(set-strategic-number sn-gold-gatherer-percentage 10)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal strategy-goal boom)
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count-total town-center < tc-level-five)
	(unit-type-count villager > 20)
	(or
		(current-age-time > 600)
		(building-type-count-total siege-workshop > 0)
	)
	(civilian-population < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 47)
	(set-strategic-number sn-wood-gatherer-percentage 36)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age >= castle)
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 6)
	(goal control-goal my-unique-unit-line)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 41)
	(set-strategic-number sn-wood-gatherer-percentage 34)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 10)
)
#else
(defrule
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal knight)
	(dropsite-min-distance stone <= 6)
	(building-type-count castle == 0)
	(building-type-count-total town-center >= tc-level-three)
	(goal control-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 44)
	(set-strategic-number sn-wood-gatherer-percentage 28)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 6)
)
(defrule
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal knight)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center >= tc-level-three)
	(goal control-goal my-unique-unit-line)
	(civilian-population < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 46)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 18)
	(set-strategic-number sn-stone-gatherer-percentage 6)
)
#end-if
(defrule
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal knight)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center >= tc-level-four)
	(up-compare-goal control-goal != my-unique-unit-line)
	(civilian-population < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 48)
	(set-strategic-number sn-wood-gatherer-percentage 31)
	(set-strategic-number sn-gold-gatherer-percentage 18)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal knight)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center == tc-level-three)
	(up-compare-goal control-goal != my-unique-unit-line)
	(civilian-population < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 47)
	(set-strategic-number sn-wood-gatherer-percentage 26)
	(set-strategic-number sn-gold-gatherer-percentage 24)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#load-if-not-defined KOREAN-CIV
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age >= castle)
	(building-type-count-total castle > 0)
	(goal control-goal my-unique-unit-line)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
#else
(defrule
	(goal unit-goal knight)
	(building-type-count-total town-center >= tc-level-three)
	(building-type-count-total castle > 0)
	(goal control-goal my-unique-unit-line)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 33)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 8)
)
#end-if
#load-if-not-defined VIKING-CIV
(defrule
	(goal strategy-goal rush)
	(goal unit-goal knight)
	(or
		(up-compare-goal control-goal != boom)
		(current-age-time < 300)
	)
	(strategic-number sn-current-age == castle)
	(current-age-time < 420)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center >= tc-level-three)
	(up-compare-goal control-goal != my-unique-unit-line)
	(civilian-population < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 49)
	(set-strategic-number sn-wood-gatherer-percentage 34)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#else
(defrule
	(goal unit-goal knight)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center >= tc-level-three)
	(civilian-population < 100)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 43)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
#end-if
(defrule
	(up-compare-goal strategy-goal != boom)
	(goal unit-goal knight)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center < tc-level-three)
	(stone-amount < 100)
	(civilian-population < 100)
=>
	(set-strategic-number sn-food-gatherer-percentage 43)
	(set-strategic-number sn-wood-gatherer-percentage 31)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(civilian-population >= 100)
	(research-available imperial-age)
	(up-research-status c: ri-crossbow != research-complete)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 37)
	(set-strategic-number sn-wood-gatherer-percentage 24)
	(set-strategic-number sn-gold-gatherer-percentage 37)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(or
		(civilian-population >= 100)
		(civilian-population > max-civ)
	)
	(research-available imperial-age)
	(research-completed ri-crossbow)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 34)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 37)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(goal strategy-goal flush)
	(or
		(civilian-population >= 100)
		(civilian-population > max-civ)
	)
	(research-available imperial-age)
	(up-compare-goal control-goal != my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 23)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(or
		(game-time > 2400)
		(strategic-number sn-current-age >= ci-transit)
	)
	(goal unit-goal knight)
	(up-compare-goal control-goal != my-unique-unit-line)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 32)
	(set-strategic-number sn-gold-gatherer-percentage 28)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(goal strategy-goal flush)
	(stone-amount < 100)
	(building-type-count-total town-center < tc-level-three)
	(not (goal control-goal my-unique-unit-line))
	(unit-type-count villager > 20)
	(current-age-time < 600)
=>
	(set-strategic-number sn-food-gatherer-percentage 44)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 18)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#else; => teutonic
(defrule
	(goal strategy-goal boom)
	(building-type-count-total town-center >= tc-level-two)
	(building-type-count-total castle > 0)
	(goal unit-goal my-unique-unit-line)
=>
	(set-strategic-number sn-food-gatherer-percentage 53)
	(set-strategic-number sn-wood-gatherer-percentage 24)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(building-type-count-total town-center < tc-level-three)
	(stone-amount < 100)
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(strategic-number sn-stone-gatherer-percentage < 10); if we started gathering stone don't stop or keep retasking
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 28)
	(set-strategic-number sn-gold-gatherer-percentage 21)
	(set-strategic-number sn-stone-gatherer-percentage 1)
)
(defrule
	(building-type-count-total town-center == tc-level-three)
	(current-age-time < 600)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(strategic-number sn-stone-gatherer-percentage < 10); if we started gathering stone don't stop or keep retasking
=>
	(set-strategic-number sn-food-gatherer-percentage 53)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(current-age-time > 600)
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(strategic-number sn-stone-gatherer-percentage < 10); if we started gathering stone don't stop or keep retasking
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 26)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(or
		(building-type-count-total town-center == tc-level-three)
		(stone-amount < 100)
	)
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(goal strategy-goal boom)
=>
	(set-strategic-number sn-food-gatherer-percentage 49)
	(set-strategic-number sn-wood-gatherer-percentage 31)
	(set-strategic-number sn-gold-gatherer-percentage 16)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
(defrule
	(building-type-count-total town-center == tc-level-three)
	(building-type-count-total barracks > 1)
	(research-completed ri-pikeman)
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(strategic-number sn-stone-gatherer-percentage < 10)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 32)
	(set-strategic-number sn-gold-gatherer-percentage 15)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(building-type-count-total town-center >= tc-level-four)
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(strategic-number sn-stone-gatherer-percentage < 10)
=>
	(set-strategic-number sn-food-gatherer-percentage 48)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 23)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-current-age >= castle)
	(or
		(building-type-count-total castle > 0)
		(strategic-number sn-stone-gatherer-percentage < 10)
	)
	(civilian-population >= max-civ)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 31)
	(set-strategic-number sn-gold-gatherer-percentage 31)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-archer-threat < 3)
	(goal anti-cavalry-threat-goal 1)
	(up-research-status c: ri-heavy-scorpion < research-pending)
	(unit-type-count villager > 20)
	(goal unit-goal knight)
	(building-type-count-total castle < 1)
=>
	(set-strategic-number sn-food-gatherer-percentage 44)
	(set-strategic-number sn-wood-gatherer-percentage 25)
	(set-strategic-number sn-gold-gatherer-percentage 21)
	(set-strategic-number sn-stone-gatherer-percentage 10)
)
#end-if
#end-if ;teutonic civ
(defrule
	(goal strategy-goal rush)
	(goal unit-goal cavalry-archer)
	(goal control-goal aggressive-rush)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(building-type-count-total stable < 2)
	(building-type-count-total archery-range > 3)
	(strategic-number sn-cavalry-threat < 2)
=>
	(set-strategic-number sn-food-gatherer-percentage 24)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 38)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal strategy-goal rush)
	(goal unit-goal cavalry-archer)
	(goal control-goal aggressive-rush)
	(strategic-number sn-current-age == castle)
	(unit-type-count villager > 20)
	(building-type-count-total stable < 2)
	(building-type-count-total archery-range > 3)
	(or
		(strategic-number sn-cavalry-threat > 1)
		(research-completed ri-elite-skirmisher)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 24)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#load-if-defined UP-2-PLAYER-GAME
#load-if-not-defined TEUTONIC-CIV
(defrule
	(goal unit-goal knight)
	(strategic-number sn-current-age == castle)
	(game-time < 1800)
	(unit-type-count villager > 20)
	(goal monk-threat-goal 1)
=>
	(set-strategic-number sn-food-gatherer-percentage 58); too much food actually, but we want early light cavalry
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 12)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#end-if
(defrule	
	(strategic-number sn-current-age == castle)
	(goal strategy-goal boom)
	(goal unit-goal knight)
	(unit-type-count villager > 20)
	(or
		(strategic-number sn-archer-threat > 0)
		(building-type-count-total wall-class > 10)
	)
	(building-type-count-total siege-workshop == 0); get a siege work shop early in arena
	(unit-type-count 980 == 0); still no stone gatherers, so keep percentage from castle transit
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 48)
	(set-strategic-number sn-gold-gatherer-percentage 10)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#end-if
(defrule
	(goal unit-goal cavalry-archer)
	(strategic-number sn-current-age == castle)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center < tc-level-three)
	(stone-amount < 100)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
(defrule
	(goal unit-goal cavalry-archer)
	(strategic-number sn-current-age == castle)
	(dropsite-min-distance stone <= 6)
	(building-type-count-total town-center >= tc-level-three)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 43)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal cavalry-archer)
	(strategic-number sn-current-age == castle)
	(building-type-count stable > 1)
	(dropsite-min-distance stone <= 6)
	(or
		(building-type-count-total town-center >= tc-level-three)
		(stone-amount < 100)
	)
	(goal enemy-goal eagle-warrior)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 47)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 24)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal cavalry-archer)
	(unit-type-count villager > 95)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 32)
	(set-strategic-number sn-gold-gatherer-percentage 29)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(unit-type-count villager > 75)
	(goal unit-goal cavalry-archer)
	(goal strategy-goal boom)
	(strategic-number sn-current-age == castle)
	(up-research-status c: imperial-age < research-available)
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 36)
	(set-strategic-number sn-gold-gatherer-percentage 26)
	(set-strategic-number sn-stone-gatherer-percentage 2)
)
(defrule
	(goal unit-goal cavalry-archer)
	(unit-type-count villager > 95)
	(building-type-count-total castle > 0)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 29)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal control-goal shot)
	(building-type-count-total town-center >= tc-level-three)
	(dropsite-min-distance stone <= 6)
	(unit-type-count villager > 20)
	(building-type-count-total castle == 0)
	(or
		(goal unit-goal knight)
		(research-completed ri-elite-eagle-warrior)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 35)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(dropsite-min-distance stone <= 4)
	(strategic-number sn-current-age == ci-transit)
	(goal strategy-goal fast-imp)
	(goal unit-goal knight)
	(civilian-population >= max-civ)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 22)
	(set-strategic-number sn-gold-gatherer-percentage 40)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)

#load-if-not-defined AZTEC-CIV
#load-if-not-defined FRANKISH-CIV
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined ITALIAN-CIV
#load-if-not-defined MAYAN-CIV
#load-if-not-defined SARACEN-CIV
(defrule
	(up-compare-goal unit-goal != my-unique-unit-line)
	(goal control-goal my-unique-unit-line)
	(strategic-number sn-current-age >= ci-transit)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage ci-uu-switch-food)
	(set-strategic-number sn-wood-gatherer-percentage ci-uu-switch-wood)
	(set-strategic-number sn-gold-gatherer-percentage ci-uu-switch-gold)
	(set-strategic-number sn-stone-gatherer-percentage ci-uu-switch-stone)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#load-if-defined SPANISH-CIV
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal flush)
	(gold-amount > 170)
	(building-type-count blacksmith > 0)
=>
	(set-strategic-number sn-food-modifier-percentage 4)
	(set-strategic-number sn-wood-modifier-percentage 3)
	(set-strategic-number sn-gold-modifier-percentage -7); don't go for archer with spanish, they can't upgrade to xbow
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(unit-type-count fishing-ship > 1)
	(strategic-number sn-current-age == fc-transit)
=>
	(up-get-player-fact my-player-number unit-type-count fishing-ship temp-goal)
	(up-modify-goal temp-goal c:* 3)
	(set-goal temp-goal2 0)
	(up-modify-goal temp-goal2 g:- temp-goal)
	(up-modify-sn sn-food-modifier-percentage g:== temp-goal2)
	(up-modify-sn sn-wood-modifier-percentage g:== temp-goal)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(goal strategy-goal sling)
	(or
		(players-civ any-ally aztec)
		(or
			(players-civ any-ally mayan)
			(players-civ any-ally slavic)
		)
	)
	(players-current-age any-ally == imperial-age)
	(players-building-type-count every-ally castle < 1)
=>
	(set-strategic-number sn-food-modifier-percentage -7)
	(set-strategic-number sn-stone-modifier-percentage 7)
)
(defrule
	(current-age >= castle-age)
	(unit-type-count villager > 85)
	(unit-type-count fishing-ship > 4)
	(wood-amount < 200)
	(food-amount > 600)
=>
	(set-strategic-number sn-food-modifier-percentage -5)
	(set-strategic-number sn-wood-modifier-percentage 5)
)
#load-if-defined UP-2-PLAYER-GAME
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(players-building-type-count any-enemy watch-tower > 4)
	(game-time < 1800)
	(current-age >= castle-age)
	(strategic-number sn-gold-gatherer-percentage > 19)
=>
	(set-strategic-number sn-wood-modifier-percentage 10)
	(set-strategic-number sn-gold-modifier-percentage -10)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(building-type-count-total siege-workshop > 0)
	(building-type-count-total watch-tower > 0)
	(building-type-count-total castle == 0)
	(goal unit-goal my-unique-unit-line)
=>
	(set-strategic-number sn-wood-modifier-percentage -15)
	(set-strategic-number sn-gold-modifier-percentage 15)
)
#end-if
#load-if-defined LOW-RESOURCES-START
(defrule
	(stone-amount < 200)
	(strategic-number sn-current-age >= fc-transit)
	(building-type-count-total watch-tower > 0)
	(strategic-number sn-stone-gatherer-percentage == 0)
	(dropsite-min-distance stone < 6)
=>
	(set-strategic-number sn-wood-modifier-percentage -4)
	(set-strategic-number sn-stone-modifier-percentage 4)
)
#end-if
(defrule
	(game-time > 2700)
	(unit-type-count trade-cart >= 40)
	(or
		(goal ranged-unit-type-goal uu)
		(goal ranged-unit-type-goal scorpion)
	)
=>
	(set-strategic-number sn-wood-modifier-percentage 15)
	(set-strategic-number sn-gold-modifier-percentage -15)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(building-type-count-total castle == 0)
	(or
		(or
			(goal enemy-fortifications-goal 1)
			(players-building-type-count any-enemy castle > 0)
		)
		(or
			(population-cap < 100)
			(civ-selected magyar)
		)
	)
	(strategic-number sn-stone-gatherer-percentage == 0)
=>
	(set-strategic-number sn-food-modifier-percentage -3)
	(set-strategic-number sn-wood-modifier-percentage -3)
	(set-strategic-number sn-stone-modifier-percentage 6)
)
(defrule
	(building-type-count-total town-center < 2)
	(building-type-count-total dock > 0)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(strategic-number sn-current-age == castle)
	(stone-amount > 100)
	(not(goal unit-goal my-unique-unit-line))
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 48)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(or
		(not (research-available imperial-age))
		(stone-amount < 100)
	)
	(not(goal unit-goal my-unique-unit-line))
	(building-type-count-total dock > 0)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(strategic-number sn-current-age == castle)
	(dropsite-min-distance stone < 6)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 47)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(or
		(strategic-number sn-current-age == df-transit)
		(and
			(strategic-number sn-current-age == feudal)
			(not (civ-selected viking))
		)
	)
	(up-compare-goal strategy-goal != boom)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
=>
	(set-strategic-number sn-food-gatherer-percentage 57)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 0)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == feudal)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(or
		(civ-selected viking)
		(goal strategy-goal flush)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 20)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(current-age < imperial-age)
	(strategic-number sn-current-age >= fc-transit)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(unit-type-count fishing-ship > 11)
=>
	(set-strategic-number sn-food-gatherer-percentage 0)
	(set-strategic-number sn-wood-gatherer-percentage 67)
	(set-strategic-number sn-gold-gatherer-percentage 33)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(current-age >= castle-age)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(civilian-population < 61)
	(dropsite-min-distance stone <= 6)
=>
	(set-strategic-number sn-food-gatherer-percentage 18)
	(set-strategic-number sn-wood-gatherer-percentage 53)
	(set-strategic-number sn-gold-gatherer-percentage 24)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
(defrule
	(civilian-population > 60)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(unit-type-count-total trade-cart < 11)
=>
	(set-strategic-number sn-food-gatherer-percentage 21)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 26)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#load-if-not-defined KOREAN-CIV
(defrule
	(game-time >= 3600)
	(unit-type-count female-gold-miner == 0); no gold miners
	(unit-type-count male-gold-miner == 0)
	(unit-type-count-total 229 == 0); no dead miners so enemy didn't kill them
	(unit-type-count-total 221 == 0); so there's no gold available
=>
	(set-strategic-number sn-food-gatherer-percentage 48)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 2)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(game-time >= 3600)
	(unit-type-count female-gold-miner == 0); no gold miners
	(unit-type-count male-gold-miner == 0)
	(unit-type-count-total 229 == 0); no dead miners so enemy didn't kill them
	(unit-type-count-total 221 == 0); so there's no gold available
	(or
		(research-completed ri-champion)
		(and
			(research-completed ri-light-cavalry)
			(research-completed ri-bloodlines)
		)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 55)
	(set-strategic-number sn-wood-gatherer-percentage 38)
	(set-strategic-number sn-gold-gatherer-percentage 2)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
#end-if
#else ; MICHI
(defrule
	(strategic-number sn-current-age >= fc-transit)
	(strategic-number sn-current-age < ci-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 0)
	(set-strategic-number sn-stone-gatherer-percentage 8)
)
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-five)
=>
	(set-strategic-number sn-food-gatherer-percentage 58)
	(set-strategic-number sn-wood-gatherer-percentage 37)
	(set-strategic-number sn-gold-gatherer-percentage 5)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age == castle)
	(building-type-count-total town-center >= tc-level-five)
	(or
		(unit-type-count-total villager > 110)
		(unit-type-count-total villager >= max-civ)
	)
	(nor
		(goal unit-goal my-unique-unit-line)
		(goal control-goal my-unique-unit-line)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 36)
	(set-strategic-number sn-gold-gatherer-percentage 22)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(or
		(goal unit-goal my-unique-unit-line)
		(goal control-goal my-unique-unit-line)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage michi-food)
	(set-strategic-number sn-wood-gatherer-percentage michi-wood)
	(set-strategic-number sn-gold-gatherer-percentage 23)
	(set-strategic-number sn-stone-gatherer-percentage 12)
)
(defrule
	(or
		(goal unit-goal knight)
		(goal unit-goal eagle-warrior)
	)
	(unit-type-count villager > 20)
	(strategic-number sn-current-age >= ci-transit)
=>
	(set-strategic-number sn-food-gatherer-percentage 38)
	(set-strategic-number sn-wood-gatherer-percentage 22)
	(set-strategic-number sn-gold-gatherer-percentage 36)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
#end-if
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal strategy-goal rush)
	(goal unit-goal monk)
	(current-age == castle-age)
	(commodity-selling-price stone < 105)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 28)
	(set-strategic-number sn-wood-gatherer-percentage 28)
	(set-strategic-number sn-gold-gatherer-percentage 44)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal unit-goal monk)
	(goal strategy-goal fast-imp)
	(current-age == castle-age)
	(commodity-selling-price stone < 105)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 32)
	(set-strategic-number sn-wood-gatherer-percentage 24)
	(set-strategic-number sn-gold-gatherer-percentage 44)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal unit-goal monk)
	(strategic-number sn-current-age >= ci-transit)
	(unit-type-count villager > 20)
=>
	(set-strategic-number sn-food-gatherer-percentage 29)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 45)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)
(defrule
	(goal unit-goal monk)
	(strategic-number sn-current-age >= ci-transit)
	(unit-type-count villager > 70)
=>
	(set-strategic-number sn-food-gatherer-percentage 28)
	(set-strategic-number sn-wood-gatherer-percentage 27)
	(set-strategic-number sn-gold-gatherer-percentage 42)
	(set-strategic-number sn-stone-gatherer-percentage 4)
)
(defrule
	(strategic-number sn-current-age == castle)
	(goal unit-goal wonder)
=>
	(set-goal train-civ-goal wonder)
	(set-strategic-number sn-food-gatherer-percentage 25)
	(set-strategic-number sn-wood-gatherer-percentage 20)
	(set-strategic-number sn-gold-gatherer-percentage 45)
	(set-strategic-number sn-stone-gatherer-percentage 10)
)
(defrule
	(strategic-number sn-current-age >= ci-transit)
	(goal unit-goal wonder)
=>
	(set-strategic-number sn-food-gatherer-percentage 0)
	(set-strategic-number sn-wood-gatherer-percentage 30)
	(set-strategic-number sn-gold-gatherer-percentage 40)
	(set-strategic-number sn-stone-gatherer-percentage 30)
	(set-strategic-number sn-wood-dropsite-distance 14)
)
#load-if-defined DEFEND-WONDER
(defrule
	(unit-type-count-total villager < twenty-five-percent-pop)
=>
	(set-strategic-number sn-food-gatherer-percentage 60)
	(set-strategic-number sn-wood-gatherer-percentage 33)
	(set-strategic-number sn-gold-gatherer-percentage 5)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#load-if-not-defined INCAN-CIV
#load-if-not-defined INDIAN-CIV
#load-if-not-defined MAGYAR-CIV
#load-if-not-defined SLAVIC-CIV
#load-if-not-defined AZTEC-CIV
#load-if-not-defined CELTIC-CIV
#load-if-not-defined FRANKISH-CIV
#load-if-not-defined GOTHIC-CIV
#load-if-not-defined HUN-CIV
#load-if-not-defined JAPANESE-CIV
#load-if-not-defined PERSIAN-CIV
#load-if-not-defined SARACEN-CIV
#load-if-not-defined SPANISH-CIV
#load-if-not-defined TEUTONIC-CIV
#load-if-not-defined TURKISH-CIV
#load-if-not-defined VIKING-CIV
(defrule
	(unit-type-count-total villager < twenty-five-percent-pop)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 8)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#end-if
#else; == POST-IMPERIAL-AGE-START
(defrule
	(timer-triggered one-minute-timer)
	(food-amount < 2500)
	(or
		(food-amount < 1500)
		(unit-type-count-total villager < fourty-percent-pop)
	)
	(wood-amount > 1500)
=>
	(set-strategic-number sn-food-gatherer-percentage 50)
	(set-strategic-number sn-wood-gatherer-percentage 10)
	(set-strategic-number sn-gold-gatherer-percentage 35)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(timer-triggered one-minute-timer)
	(food-amount < 1500)
	(wood-amount < 1500)
	(unit-type-count-total villager >= fourty-percent-pop)
=>
	(set-strategic-number sn-food-gatherer-percentage pidm-food2)
	(set-strategic-number sn-wood-gatherer-percentage pidm-wood2)
	(set-strategic-number sn-gold-gatherer-percentage pidm-gold2)
	(set-strategic-number sn-stone-gatherer-percentage pidm-stone2)
)
(defrule
	(timer-triggered one-minute-timer)
	(food-amount < 1500)
	(wood-amount < 1500)
	(unit-type-count-total villager < fourty-percent-pop)
=>
	(set-strategic-number sn-food-gatherer-percentage 47)
	(set-strategic-number sn-wood-gatherer-percentage 25)
	(set-strategic-number sn-gold-gatherer-percentage 25)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)
(defrule
	(timer-triggered one-minute-timer)
	(food-amount > 1500)
	(wood-amount < 1500)
=>
	(set-strategic-number sn-food-gatherer-percentage 14)
	(set-strategic-number sn-wood-gatherer-percentage 40)
	(set-strategic-number sn-gold-gatherer-percentage 41)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
(defrule
	(timer-triggered one-minute-timer)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(food-amount > 1500)
	(wood-amount > 1500)
=>
	(set-strategic-number sn-food-gatherer-percentage pidm-food)
	(set-strategic-number sn-wood-gatherer-percentage pidm-wood)
	(set-strategic-number sn-gold-gatherer-percentage pidm-gold)
	(set-strategic-number sn-stone-gatherer-percentage pidm-stone)
)
(defrule
	(timer-triggered one-minute-timer)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(food-amount > 1500)
=>
	(set-strategic-number sn-food-gatherer-percentage 0)
	(set-strategic-number sn-wood-gatherer-percentage 55)
	(set-strategic-number sn-gold-gatherer-percentage 40)
	(set-strategic-number sn-stone-gatherer-percentage 5)
)
#end-if


; ********** ATTACK RULES **********

#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
(defrule
	(goal enemy-fortifications-goal -1)
	(or
		(players-building-type-count any-enemy castle > 3)
		(and
			(nand
				(building-type-count gate > 0); arena map
				(unit-type-count battering-ram-line > 0)
			)
			(and
				(players-building-type-count any-enemy castle > 0)
				(players-building-type-count any-enemy stone-wall > 10)
			)
		)
	)
=>
	(set-goal enemy-fortifications-goal 1)
)
(defrule
	(goal enemy-fortifications-goal 1)
	(players-building-type-count every-enemy castle < 2)
	(or
		(players-building-type-count every-enemy stone-wall < 10)
		(and
			(building-type-count gate > 0)
			(unit-type-count battering-ram-line > 0)
		)
	)
=>
	(set-goal enemy-fortifications-goal -1)
)
(defrule
	(goal team-coordination-goal 1)
	(game-time < 1800)
	(goal unit-goal knight)
	(goal threat-source-goal monastery-class)
	(goal monk-threat-goal 1)
	(nor
		(research-completed ri-heresy)
		(unit-type-count scout-cavalry-line > 3)
	)
	(strategic-number sn-military-level <= 0)
=>
	(set-goal retreat-now-goal 1)
	(enable-timer attack-timer 90)
	(chat-to-player chat-player "retreat vs mush")
)
#end-if; not easy
#end-if; not easiest
(defrule
	(true)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(timer-triggered attack-timer)
	(strategic-number sn-military-level >= -1)
	(goal hostilities-goal 0)
	(goal ally-attacking-goal 1)
=>
	(set-goal attack-goal 4)
)
(defrule
	(strategic-number sn-military-level >= 2); military superiority so
	(strategic-number sn-current-age != fc-transit)
=>
	(set-goal attack-goal 1); attack might be an idea, check other conditions
)
#load-if-not-defined DEATH-MATCH
(defrule
	(current-age == castle-age)
	(military-population > 16)
	(strategic-number sn-resource-control < 2)
	(strategic-number sn-military-level >= 0)
	(or
		(players-current-age focus-player < 120)
		(strategic-number sn-military-level >= 1)
	)
	(goal strategy-goal flush)
	(or
		(unit-type-count skirmisher-line < 12)
		(up-research-status c: ri-elite-skirmisher >= research-pending)
	)
	(up-research-status c: ri-bodkin-arrow >= research-pending)
	(players-building-type-count focus-player castle == 0); not if enemy has a castle
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
(defrule
	(timer-triggered attack-timer)
	(game-time < 1800)
	(or
		(unit-type-count ranged-unit-class > 12)
		(unit-type-count cavalry-archer-class > 10)
	)
	(up-research-status c: ri-bodkin-arrow >= research-pending)
	(or
		(players-unit-type-count focus-player skirmisher-line < 10)
		(strategic-number sn-military-level >= 1)
	)
	(players-current-age focus-player == feudal-age)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
(defrule
	(timer-triggered attack-timer)
	(game-time < 1800)
	(unit-type-count conquistador-line > 7)
	(players-current-age focus-player == feudal-age)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
(defrule
	(timer-triggered attack-timer)
	(game-time < 1620)
	(goal unit-goal eagle-warrior)
	(players-current-age target-player == feudal-age)
	(players-unit-type-count target-player militiaman-line < 6)
	(unit-type-count eagle-warrior-line > 14)
	(research-completed ri-chain-mail)
	(strategic-number sn-military-level >= -2)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
(defrule
	(timer-triggered attack-timer)
	(game-time < 1620)
	(goal attack-goal 0)
	(goal strategy-goal rush)
	(goal unit-goal knight)
	(research-completed ri-chain-barding)
	(unit-type-count knight-line > 8)
	(players-current-age focus-player == feudal-age)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
(defrule
	(timer-triggered attack-timer)
	(game-time < 1800)
	(goal unit-goal eagle-warrior)
	(or
		(or
			(goal anti-cavalry-threat-goal 1)
			(players-unit-type-count focus-player skirmisher-line > 7)
		)
		(and
			(players-current-age focus-player == feudal-age)
			(players-unit-type-count focus-player militiaman-line < 1)
		)
	)
	(strategic-number sn-military-level >= 0)
	(unit-type-count eagle-warrior-line > 20)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
(defrule
	(goal attack-goal 0)
	(goal unit-goal monk)
	(up-research-status c: ri-block-printing != research-available)
	(up-research-status c: ri-theocracy != research-available)
	(up-research-status c: ri-illumination != research-available)
	(current-age == imperial-age)
	(unit-type-count monk > 20)
	(game-time < 2700)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
(defrule
	(goal attack-goal 0)
	(goal unit-goal scorpion)
	(goal strategy-goal rush)
	(unit-type-count scorpion-line > 20)
	(strategic-number sn-military-level >= -1)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
(defrule
	(game-time < 1500)
	(timer-triggered attack-timer)
	(goal attack-goal 0)
	(players-military-population focus-player < 2)
	(up-player-distance focus-player < 70)
	(or
		(or
			(unit-type-count scout-cavalry > 2)
			(and
				(goal strategy-goal flush)
				(military-population > 6)
			)
		)
		(unit-type-count cavalry-class > 3)
	)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
#end-if
(defrule
	(current-age == castle-age)
	(players-current-age focus-player == feudal-age)
	(strategic-number sn-military-level >= -1)
	(up-research-status c: ri-bodkin-arrow >= research-pending)
	(or
		(up-research-status c: ri-crossbow >= research-pending)
		(up-research-status c: ri-elite-skirmisher >= research-pending)
	)
	(military-population > 20)
=>
	(set-goal attack-goal 1)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
(defrule
	(goal enemy-fortifications-goal 1)
	(unit-type-count trebuchet-set < 1)
	(unit-type-count battering-ram-line < 4)
=>
	(set-goal attack-goal 0)
)
#load-if-not-defined FEUDAL-AGE-END
(defrule
	(goal attack-goal 1)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(or
		(and 
			(players-current-age focus-player == castle-age)
			(players-military-population focus-player > 10)
		)
		(strategic-number sn-resource-control == 2)
	)
	(strategic-number sn-military-level < 3)
=>
	(set-goal attack-goal 0); better go castle age and attack then
)
(defrule
	(goal attack-goal 1)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(players-building-type-count focus-player watch-tower > 0)
	(or
		(and
			(players-military-population focus-player > 9)
			(players-current-age focus-player >= castle-age)
		)
		(players-building-type-count focus-player watch-tower > 1)
	)
=>
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 1)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(unit-type-count militiaman-line < 6)
	(goal enemy-goal eagle-warrior)
	(players-military-population every-enemy > 4)
=>
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 1)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(players-unit-type-count focus-player knight-line > 8)
=>
	(set-goal attack-goal 0)
)
#load-if-defined UP-1-PLAYER-TEAM
(defrule
	(goal attack-goal 1)
	(goal strategy-goal flush)
	(strategic-number sn-cavalry-threat > 0)
	(unit-type-count spearman-line < 4)
	(unit-type-count camel-line < 4)
=>
	(set-goal attack-goal 0)
)
#end-if
(defrule
	(players-current-age focus-player > feudal-age)
	(goal attack-goal 1)
	(or 
		(and
			(goal strategy-goal flush)
			(current-age == feudal-age)
		)
		(or
			(goal unit-goal eagle-warrior)
			(goal unit-goal archer)
		)
	)
	(strategic-number sn-cavalry-threat >= 1); enemy cavalry found
	(unit-type-count spearman-line < 5); no anti cavalry
=>
	(set-goal attack-goal 0)
)
#end-if
(defrule
	(goal attack-goal 1)
	(goal strategy-goal flush)
	(current-age >= castle-age)
	(or
		(and 
			(unit-type-count spearman-line >= 6)
			(research-available ri-pikeman)
		)
		(and 
			(unit-type-count militiaman-line >= 12)
			(research-available ri-long-swordsman)
		)
	)
=>
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 1)
	(game-time < 1800)
	(goal monk-threat-goal 1)
	(unit-type-count eagle-warrior-line < 4)
	(unit-type-count scout-cavalry-line < 4)
	(nor 
		(research-completed ri-heresy)
		(goal unit-goal archer)
	)
=>
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 1)
	(up-research-status c: ri-bodkin-arrow < research-pending)
	(or
		(or
			(goal unit-goal skirmisher)
			(goal unit-goal archer)
		)
		(goal unit-goal my-unique-unit-line)
	)
	(current-age >= castle-age)
	(unit-type-count ranged-unit-class > 12)
=>
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 1)
	(goal ranged-unit-type-goal archer)
	(current-age >= castle-age)
	(unit-type-count archer > 10)
	(up-research-status c: ri-crossbow < research-pending)
=>
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 1)
	(goal unit-goal skirmisher)
	(or
		(goal enemy-goal skirmisher)
		(unit-type-count skirmisher-line > 9)
	)
	(or
		(research-available ri-elite-skirmisher)
		(research-available ri-bodkin-arrow)
	)
=>
	(set-goal attack-goal 0)
)
#load-if-not-defined CASTLE-AGE-END
(defrule
;	(strategic-number sn-military-level < 3)
	(goal attack-goal 1)
	(or
		(strategic-number sn-resource-control == ri-elite-eagle-warrior)
		(or
			(goal strategy-goal fast-imp)
			(goal strategy-goal boom)
		)
	)
	(or
		(current-age < imperial-age)
		(current-age-time < 120)
	)
=>
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 1)
	(unit-type-count monk > 20)
	(or
		(current-age < imperial-age)
		(or
			(research-available ri-theocracy)
			(research-available ri-illumination)
		)
	)
	(strategic-number sn-military-level < 3)
=>
	(set-goal attack-goal 0)
)
#end-if
(defrule
	(goal attack-goal 1)
	(or
		(players-building-count every-enemy == 0); no attack when enemy is not yet found.
		(or
			(and
				(strategic-number sn-resource-control == ri-pikeman)
				(research-available ri-pikeman)
			)
			(and
				(goal unit-goal eagle-warrior)				
				(research-available ri-chain-mail)
			)
		)
	)
=>
	(set-goal attack-goal 0)
)
(defrule
	(current-age == feudal-age)
	(goal strategy-goal flush)
	(goal enemy-goal eagle-warrior)
	(unit-type-count militiaman-line < 5)
	(goal team-coordination-goal 1)
	(players-current-age focus-player >= castle-age)
=>
	(set-goal attack-goal 0)
	(set-goal retreat-now-goal 1)
	(enable-timer attack-timer 90)
	(chat-to-player chat-player "retreat: no m@a vs eagles")
)
#load-if-defined UP-1-PLAYER-TEAM
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(or
		(current-age == feudal-age)
		(and
			(strategic-number sn-military-level < 0)
			(current-age == castle-age)
		)
	)
	(goal strategy-goal flush)
	(goal attack-goal 0)
	(goal raid-goal 0)
	(goal team-coordination-goal 1)	
=>
	(set-goal retreat-now-goal 1)
	(enable-timer attack-timer 90)
)
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age < imperial-age)
	(strategic-number sn-military-level < 0)
	(goal strategy-goal flush)
	(goal attack-goal 0)
	(goal raid-goal 0)
	(goal team-coordination-goal 1)
=>
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(up-get-focus-fact allied-goal team-coordination-goal temp-goal)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(current-age < imperial-age)
	(strategic-number sn-military-level < 0)
	(goal strategy-goal flush)
	(goal attack-goal 0)
	(goal raid-goal 0)
	(goal team-coordination-goal 1)
	(up-compare-goal temp-goal != 1)
=>
	(set-goal retreat-now-goal 1)
	(enable-timer attack-timer 90)
)
#end-if
(defrule
	(goal attack-goal 1)
	(or 
		(and
			(or
				(strategic-number sn-military-level > 1)
				(unit-type-count battering-ram-line > 1)
			)
			(unit-type-count battering-ram-line > 0)
		)
		(or
			(players-building-type-count target-player castle == 0)
			(unit-type-count trebuchet > 0)
		)
	)
	(goal enemy-fortifications-goal -1)
=>
	(set-goal attack-goal 2)
)
#load-if-defined POST-IMPERIAL-AGE-START
(defrule
	(current-age == imperial-age)
	(population > seventy-five-percent-pop)
	(players-building-count any-enemy > 0)
	(unit-type-count paladin > 30)
	(game-time < 900)
=>
	(set-goal attack-goal 2)
)
#end-if
#load-if-defined DEFEND-WONDER
(defrule
	(current-age == imperial-age)
	(unit-type-count trebuchet-set > 0)
	(strategic-number sn-military-level > 0)
	(players-building-type-count any-enemy wonder > 0)
=>
	(set-goal attack-goal 2)
)
#end-if
#load-if-defined DIFFICULTY-EASIEST
(defrule
	(current-age == imperial-age)
	(current-age-time > 600)
	(population > fourty-percent-pop)
	(strategic-number sn-military-level >= 0)
	(goal hostilities-goal 0)
=>
	(set-goal attack-goal 4)
)
#end-if
#load-if-defined DIFFICULTY-EASY
(defrule
	(current-age == imperial-age)
	(current-age-time > 300)
	(population > sixty-percent-pop)
	(strategic-number sn-military-level >= 0)
	(goal hostilities-goal 0)
=>
	(set-goal attack-goal 4)
)
#end-if
; try to push with tech advantage
(defrule
	(players-building-count target-player > 0)
	(population >= eighty-five-percent-pop)
	(up-research-status c: ri-bracer >= research-pending)
	(players-current-age target-player <= castle-age)
	(unit-type-count battering-ram-line >= 2)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands); not on islands
=>
	(set-goal attack-goal 2)
)
(defrule
	(population >= sixty-percent-pop)
	(players-building-count target-player > 0)
	(research-completed ri-elite-eagle-warrior)
	(unit-type-count eagle-warrior-line > 25)
	(players-current-age target-player <= castle-age)
	(or
		(unit-type-count battering-ram-line >= 2)
		(players-building-type-count target-player castle == 0)
	)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
=>
	(set-goal attack-goal 2)
)
(defrule
	(population >= sixty-percent-pop)
	(players-building-count target-player > 0)
	(up-research-status c: ri-cavalier >= research-pending)
	(up-research-status c: ri-plate-barding >= research-pending)
	(unit-type-count knight-line > 20)
	(players-current-age every-enemy <= castle-age)
	(players-building-type-count target-player castle == 0)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
=>
	(set-goal attack-goal 2)
)
(defrule
	(current-age == imperial-age)
	(current-age-time > 150)
	(population >= del-civ-pop)
	(players-building-count any-enemy > 0)
	(or
		(unit-type-count battering-ram-line >= 4)
		(unit-type-count trebuchet-set > 0)
	)
	(up-research-status c: ri-paladin != research-pending)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
=>
	(set-goal attack-goal 2)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
(defrule
	(or
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands); this is not the water map section here
		(and
			(or
				(dropsite-min-distance wood > 20); map resources about to run out, so prepare for big attacks only
				(cc-players-unit-type-count 0 tree-class < 20)
			)
			(or
				(population < del-civ-pop)
				(nor
					(unit-type-count battering-ram-line > 3)
					(unit-type-count trebuchet-set > 1)
				)
			)
		)
	)
=>
	(set-goal attack-goal 0)
)
(defrule
	(up-compare-goal attack-goal != 6)
	(up-compare-goal attack-goal >= 2)
	(strategic-number sn-cavalry-threat > 2)
	(or
		(and
			(unit-type-count spearman-line > 15)
			(research-available ri-halberdier)
		)
		(and
			(unit-type-count camel-line > 10)
			(research-available ri-heavy-camel)
		)
	)
	(up-research-status c: ri-paladin != research-complete)
=>
	(set-goal attack-goal 0)
)
(defrule
	(timer-triggered attack-timer)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(strategic-number sn-maximum-town-size >= 40)
	(population > eighty-five-percent-pop); keep attacking when pop his high
	(or
		(nand
			(goal enemy-fortifications-goal 1)
			(unit-type-count trebuchet-set < 1); except if the enemy has fortification and we don't have trebs
		)
		(or
			(unit-type-count battering-ram-line > 4)
			(and		
				(up-allied-goal any-ally team-coordination-goal == 1)
				(up-allied-sn any-ally sn-target-player-number s:== sn-target-player-number)
			)
		)
	)
=>
	(set-goal attack-goal 4)
)
(defrule
	(taunt-detected any-human-ally 31)
	(taunt-detected any-human-ally 36)
=>
	(acknowledge-taunt this-any-human-ally 36)
)
; React to flare
(defrule
	(or
		(taunt-detected any-human-ally 31)
		(taunt-detected any-human-ally 222)
	)
	(players-unit-type-count any-human-ally flare > 0)
=>
	(up-reset-unit c: -1)
	(up-find-player-flare this-any-human-ally patrol-point-goal)
	(set-goal split-rule-goal 24)
)
(defrule
	(players-unit-type-count any-computer-ally flare > 0)
	(goal ally-under-attack-goal 1)
	(military-population > 5)
	(goal hostilities-goal 0)
=>
	(up-get-player-fact this-any-ally player-number 0 temp-goal)
	(up-get-player-fact this-any-ally allied-goal position-goal temp-goal2)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
)
(defrule
	(players-unit-type-count focus-player flare > 0)
	(players-unit-type-count any-computer-ally flare > 0)
	(goal ally-under-attack-goal 1)
	(military-population > 5)
	(goal hostilities-goal 0)
=>
	(up-reset-unit c: -1)
	(up-find-player-flare this-any-computer-ally patrol-point-goal)
	(up-get-player-fact this-any-computer-ally player-number 0 patrol-goal)
	(set-goal split-rule-goal 24)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(players-unit-type-count any-computer-ally flare > 0)
	(up-allied-goal any-computer-ally under-attack-goal == 1)
	(or
		(up-allied-goal focus-player under-attack-goal != 1)
		(players-unit-type-count focus-player flare > 0)
	)
	(military-population > 5)
	(up-compare-goal temp-goal2 == pocket)
	(goal under-attack-goal 0)
	(goal patrol-goal -1)
=>
	(up-reset-unit c: -1)
	(up-find-player-flare this-any-computer-ally patrol-point-goal)
	(up-get-player-fact this-any-computer-ally player-number 0 patrol-goal)
	(set-goal split-rule-goal 24)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
#end-if
(defrule
	(or
		(or
			(up-point-terrain patrol-point-goal == terrain-water)
			(up-point-terrain patrol-point-goal == terrain-water-deep)
		)
		(up-point-terrain patrol-point-goal == terrain-water-medium)
	)
	(goal split-rule-goal 24)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: warship-class c: 200)
	(up-set-target-point patrol-point-goal)
	(set-goal split-rule-goal 3)
	(chat-local-to-self "target is on water")
)
(defrule
	(goal split-rule-goal 24)
	(up-point-terrain patrol-point-goal == terrain-shallows)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-exclude -1 actionid-explore orderid-relic -1)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-set-target-point patrol-point-goal)
	(set-goal split-rule-goal 3)
	(chat-local-to-self "target is mixed")
)
(defrule
	(goal split-rule-goal 24)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-exclude -1 actionid-explore orderid-relic warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-set-target-point patrol-point-goal)
	(set-goal split-rule-goal 3)
	(chat-local-to-self "target is on land")
)
(defrule
	(goal split-rule-goal 3)
	(or
		(taunt-detected any-ally 31)
		(taunt-detected any-ally 222)
	)
=>
	(up-target-point patrol-point-goal action-patrol -1 defensive)
	(set-strategic-number sn-number-attack-groups 0);block other military stuff
	(set-strategic-number sn-maximum-town-size 12)
	(enable-timer attack-timer 180)
	(enable-timer misc-micro-timer 10)
	(set-goal patrol-goal 0)
	(set-goal attack-goal 0)
	(chat-to-allies "1 I'm sending my units to the flare!")
	(acknowledge-taunt this-any-ally 31)
	(acknowledge-taunt this-any-ally 222)
)
; React to taunts: We can't help if we have no military
(defrule
	(or
		(taunt-detected any-ally 31)
		(taunt-detected any-ally 222)
	)
	(military-population < 6)
	(unit-type-count monastery-class < 4)
	(unit-type-count war-wagon-line < 3)
	(unit-type-count scorpion-line == 0)
	(unit-type-count mangonel-line == 0)
=>
	(acknowledge-taunt this-any-ally 222)
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22118)
	(chat-to-allies "I don't have enough military to help you.")
)
(defrule
	(or
		(taunt-detected any-ally 31)
		(taunt-detected any-ally 222)
	)
	(building-type-count town-center == 0)
=>
	(acknowledge-taunt this-any-ally 222)
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22118)
)
; Initialize all maps
(defrule
	(taunt-detected any-ally 31)
=>
	(up-get-player-fact this-any-ally player-number 0 temp-goal)
	(up-get-player-fact this-any-ally allied-sn sn-target-player-number temp-goal2)
	(up-get-player-fact this-any-ally allied-goal position-self-goal temp-goal3)
	(up-get-player-fact this-any-ally allied-goal position-self-y-goal temp-goal4)
	(up-get-player-fact this-any-ally allied-goal position-goal temp-goal5); works with promi, barb and jugger
	(up-modify-sn sn-focus-player-number g:= temp-goal2)
	(up-get-focus-fact player-distance 0 temp-goal6)
)
(defrule
	(taunt-detected any-ally 222)
=>
	(up-get-player-fact this-any-ally player-number 0 temp-goal)
	(up-get-player-fact this-any-ally allied-goal attacking-enemy-goal temp-goal2)
	(up-get-player-fact this-any-ally allied-goal position-self-goal temp-goal3)
	(up-get-player-fact this-any-ally allied-goal position-self-y-goal temp-goal4)
	(up-get-player-fact this-any-ally allied-goal position-goal temp-goal5)
	(up-get-player-fact this-any-ally population 0 temp-goal7)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(up-get-focus-fact player-distance 0 temp-goal6)
	(up-modify-sn sn-focus-player-number g:= temp-goal2)
)
; Help Ally: Water Maps
(defrule
	(stance-toward focus-player enemy)
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(or
		(taunt-detected any-computer-ally 222)
		(taunt-detected any-computer-ally 31)
	)
	(warboat-count > 4)
	(players-building-type-count focus-player dock > 0)
=>
	(up-modify-sn sn-target-player-number g:= temp-goal2); switch target to attacker of that ally
	(up-gather-inside c: dock c: 0)
	(set-goal attack-goal 6)
	(acknowledge-taunt this-any-ally 31)
	(acknowledge-taunt this-any-ally 222)
	(chat-to-allies-using-id 22153)
	(up-chat-data-to-player every-ally "My target is Player %d ." s: sn-target-player-number)
)
#load-if-defined DEATH-MATCH
#load-if-defined UP-POCKET-POSITION
(defrule
	(military-population > 5)
	(game-time < 300)
	(or
		(goal ally-under-attack-goal 1)
		(goal ally-attacking-goal 1)
	)
=>
	(set-goal attack-goal 4)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
#end-if
#end-if
; Help Ally: Mixed Maps
(defrule
	(taunt-detected any-ally 222)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-remote c: town-center c: 1)
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 40)
	(up-modify-sn sn-focus-player-number g:= temp-goal2)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(taunt-detected any-computer-ally 222)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(up-find-remote c: warship-class c: 5)
	(warboat-count > 5)
=>
	(up-modify-sn sn-target-player-number g:= temp-goal2); switch target to attacker of that ally
	(up-gather-inside c: dock c: 0)
	(set-goal attack-goal 6)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(acknowledge-taunt this-any-ally 222)
	(up-chat-data-to-player every-ally "help ally with boats: target player = %d" s: sn-target-player-number)
)
(defrule
	(stance-toward focus-player enemy)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(taunt-detected any-ally 31)
	(warboat-count > 4)
	(players-building-type-count focus-player dock > 0)
	(game-time < 2400)
=>
	(up-modify-sn sn-target-player-number g:= temp-goal2); switch target to attacker of that ally
	(up-gather-inside c: dock c: 0)
	(set-goal attack-goal 6)
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22153)
	(up-chat-data-to-player every-ally "Attacking Player %d with boats." s: sn-target-player-number)
)
#else
(defrule
	(taunt-detected any-computer-ally 222)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(up-find-remote c: warship-class c: 5)
	(warboat-count > 5)
	(nor
		(up-compare-goal temp-goal2 g:== closest-enemy-goal)
		(up-compare-goal temp-goal g:== closest-ally-goal)
	)
	(game-time < 1800)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(chat-to-allies-using-id 22118)
	(acknowledge-taunt this-any-ally 222); don't cross map attack
)
(defrule
	(taunt-detected any-computer-ally 222)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(up-find-remote c: warship-class c: 5)
	(warboat-count > 5)
	(or
		(up-compare-goal temp-goal2 g:== closest-enemy-goal)
		(up-compare-goal temp-goal g:== closest-ally-goal)
	)
=>
	(up-modify-sn sn-target-player-number g:= temp-goal2); switch target to attacker of that ally
	(up-gather-inside c: dock c: 0)
	(set-goal attack-goal 6)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(acknowledge-taunt this-any-ally 222)
	(up-chat-data-to-player every-ally "help ally with boats: target player = %d" s: sn-target-player-number)
)
#end-if
(defrule
	(taunt-detected any-computer-ally 222)
	(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
	(not (up-find-remote c: warship-class c: 5))
=>
	(up-chat-data-to-player every-ally "Ally is attacked by player %d 's land units" g: temp-goal2)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-jump-rule 1)
)
(defrule
	(taunt-detected any-ally 222)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(warboat-count < 6)
	(up-find-remote c: warship-class c: 5)
=>
	(chat-to-allies-using-id 22118)
	(acknowledge-taunt this-any-ally 222)
	(chat-to-allies "I have no navy to help you.")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock > water-mixed)
	(taunt-detected any-ally 222); if we could help, this taunt would have been acknowledeged by now
=>	
	(acknowledge-taunt this-any-ally 222)
	(chat-to-allies-using-id 22118)
)
; Help Ally: Land Maps
(defrule
	(or
		(taunt-detected any-computer-ally 31)
		(taunt-detected any-computer-ally 222)
	)
	(up-compare-goal temp-goal g:== closest-ally-goal)
	(players-population target-player < sixty-percent-pop); target is already weakened
	(nand
		(up-compare-goal temp-goal7 >= fourty-percent-pop)
		(players-population target-player < fourty-percent-pop)
	)
	(goal team-coordination-goal 1)
=>
	(up-jump-rule 3)
)
(defrule
	(taunt-detected any-computer-ally 222)
	(up-compare-goal patrol-goal g:== temp-goal)
=>
	(enable-timer attack-timer 180)
	(enable-timer misc-micro-timer 10)
	(acknowledge-taunt this-any-ally 222)	
)
(defrule
	(taunt-detected any-computer-ally 31)
	(up-compare-goal patrol-goal g:== temp-goal)
=>
	(acknowledge-taunt this-any-computer-ally 31)
	(set-goal attack-goal 4)
	(up-modify-sn sn-target-player-number g:= temp-goal)
	(chat-to-allies-using-id 22153)
	(set-goal patrol-goal -1)
	(up-jump-rule 2)
)
(defrule
	(or
		(goal hostilities-goal 1); already attacking or town under attack
		(goal forward-threat-goal 1)
	)
	(or
		(taunt-detected any-ally 31)
		(taunt-detected any-ally 222)
	)
=>
	(acknowledge-taunt this-any-ally 222)
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22118)
)
(defrule
	(or
		(taunt-detected any-computer-ally 31)
		(taunt-detected any-computer-ally 222)
	)
	(strategic-number sn-cavalry-threat > 2)
	(or
		(and
			(unit-type-count spearman-line > 15)
			(research-available ri-halberdier)
		)
		(and
			(unit-type-count camel-line > 10)
			(research-available ri-heavy-camel)
		)
	)
	(up-compare-goal unit-goal != knight)
=>
	(acknowledge-taunt this-any-ally 222)
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22118)
)
#load-if-not-defined UP-GROUPED-BY-TEAM
(defrule
	(taunt-detected any-computer-ally 222)
	(up-compare-goal temp-goal2 g:== closest-enemy-goal)
	(up-compare-goal temp-goal6 >= 90)
=>
	(set-goal attack-goal 4)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(chat-to-allies-using-id 22153)
	(acknowledge-taunt this-any-computer-ally 222)
	(up-chat-data-to-player every-ally "The allied player %d is too far away." g: temp-goal)
	(up-chat-data-to-player every-ally "Distance: %d tiles" g: temp-goal6)
)
(defrule
	(or
		(goal position-goal flank)
		(strategic-number sn-military-level < -2)
	)
	(or
		(strategic-number sn-military-level < -1)
		(strategic-number sn-current-age == feudal)
	)
	(strategic-number sn-military-level < 0)
	(taunt-detected any-ally 31)
=>
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22118)
	(chat-to-allies "I am too far outnumbered and can't help.")
)	
(defrule
	(or
		(game-time >= 1500)
		(goal position-goal flank)
	)
	(taunt-detected any-ally 222)
	(strategic-number sn-military-level < -2)
=>
	(acknowledge-taunt this-any-ally 222)
	(chat-to-allies-using-id 22118)
	(chat-to-allies "I am too far outnumbered and can't help.")
)
#load-if-not-defined UP-2-PLAYER-TEAM
#load-if-not-defined UP-3-PLAYER-TEAM
(defrule
	(population >= eighty-five-percent-pop)
	(taunt-detected any-ally 222)
	(up-compare-goal temp-goal g:== closest-ally-goal)
	(up-compare-goal temp-goal7 < twenty-five-percent-pop)
=>
	(set-goal attack-goal 4)
	(acknowledge-taunt this-any-ally 222)
	(chat-to-allies-using-id 22153)
)
#else
(defrule
	(or
		(population >= eighty-five-percent-pop)
		(strategic-number sn-military-level > 0)
	)
	(taunt-detected any-ally 222)
	(up-compare-goal temp-goal7 < twenty-five-percent-pop)
=>
	(set-goal attack-goal 4)
	(up-modify-sn sn-target-player-number g:= temp-goal2); help ally with UP
	(acknowledge-taunt this-any-ally 222)
	(chat-to-allies-using-id 22153)
)
#end-if
(defrule
	(taunt-detected any-ally 222)
	(up-compare-goal temp-goal6 >= 90)
=>
	(acknowledge-taunt this-any-ally 222)
	(chat-to-allies-using-id 22118)
	(up-chat-data-to-player every-ally "The allied player %d is too far away." g: temp-goal)
	(up-chat-data-to-player every-ally "Distance: %d tiles" g: temp-goal6)
)
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(current-age == feudal-age)
	(goal strategy-goal flush)
	(up-player-distance target-player > 80)
	(up-compare-goal attack-goal > 0)
	(up-compare-goal attack-goal != 6)
=>
	(set-goal attack-goal 0)
	(set-strategic-number sn-resource-control 2)
	(chat-to-player chat-player "retreat: enemy too far away")
)
(defrule
	(taunt-detected any-computer-ally 31)
	(or
		(and
			(up-compare-goal temp-goal2 g:!= closest-enemy-goal)
			(up-compare-goal temp-goal2 != -1)
		)
		(and
			(up-compare-goal temp-goal g:!= closest-ally-goal)
			(goal temp-goal2 -1)
		)
	)
=>
	(acknowledge-taunt this-any-computer-ally 31)
	(chat-to-allies-using-id 22118)
	(chat-to-allies "The target too far away.")
)
#load-if-not-defined DEATH-MATCH
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(current-age == feudal-age)
	(goal strategy-goal flush)
	(up-player-distance target-player > 90)
	(goal team-coordination-goal 1)
	(strategic-number sn-number-boat-attack-groups == 0)
=>
	(set-goal attack-goal 0)
	(set-goal retreat-now-goal 1)
	(set-strategic-number sn-resource-control 2)
	(enable-timer attack-timer 20)
	(chat-to-player chat-player "retreat: enemy too far away")
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time < 2400)
	(up-player-distance target-player > 100)
	(up-compare-goal attack-goal >= 2)
	(strategic-number sn-target-player-number g:!= closest-enemy-goal)
=>
	(set-goal attack-goal 0)
)
#else
(defrule
	(taunt-detected any-computer-ally 31)
	(up-compare-goal temp-goal g:!= closest-ally-goal)
	(up-compare-goal temp-goal2 g:!= closest-enemy-goal)
	(up-compare-goal temp-goal5 g:!= pocket)
=>
	(acknowledge-taunt this-any-computer-ally 31)
	(chat-to-allies-using-id 22118)
	(chat-to-allies "The target is too far away.")
)
#end-if
#end-if
(defrule
	(taunt-detected any-ally 222)
=>
	(up-copy-point patrol-point-goal temp-goal3)
	(set-goal split-rule-goal 6)
)
#else; => GROUPED-BY-TEAM
(defrule ; grouped by team, take own position and search for enemy in large area
	(taunt-detected any-ally 222)
=>
	(up-modify-goal patrol-goal g:= temp-goal)
	(set-strategic-number sn-focus-player-number 1)
	(set-goal split-rule-goal 15)
	(up-jump-rule 20)
)
#end-if
(defrule
	(or
		(taunt-detected any-ally 31)
		(taunt-detected any-ally 222)
	)
	(military-population < 6)
	(unit-type-count monastery-class < 4)
	(unit-type-count war-wagon-line < 3)
	(unit-type-count scorpion-line == 0)
	(unit-type-count mangonel-line == 0)
=>
	(up-jump-rule 1)
)
(defrule
	(timer-triggered five-seconds-timer)
	(goal hostilities-goal 0)
	(or
		(or
			(strategic-number sn-military-level >= -2)
			(game-time < 1650)
		)
		(and
			(current-age == imperial-age)
			(population >= max-civ-pop)
		)
	)
	(or
		(game-time < 1650)
		(goal position-goal pocket)
	)
	(goal ally-under-attack-goal 1)
	(up-allied-goal any-ally id-goal >= 0)
=>
	(set-goal split-rule-goal 51)
)
(defrule
	(timer-triggered five-seconds-timer)
	(goal hostilities-goal 0)
	(current-age == imperial-age)
	(population >= eighty-five-percent-pop)
	(goal ally-under-attack-goal 2)
	(up-allied-goal any-ally under-attack-goal == 1)
=>
	(set-goal split-rule-goal 51)
	(up-get-player-fact this-any-ally player-number 0 temp-goal8)
)
(defrule
	(up-projectile-detected projectile-town-center c:< 10000);recently moved out of TC fire
	(up-projectile-detected projectile-town-center c:> 7500)
	(goal patrol-goal -1)
	(goal ally-under-attack-goal 1)
	(up-allied-goal any-ally id-goal >= 0)
=>
	(set-goal split-rule-goal 51)
)
(defrule
	(goal ally-under-attack-goal 1)
	(goal split-rule-goal 51)
=>
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(up-modify-goal patrol-goal s:= sn-focus-player-number)
	(up-get-player-fact focus-player allied-goal position-self-goal patrol-point-goal)
	(up-get-player-fact focus-player allied-goal position-self-y-goal patrol-point-y-goal)
	(set-goal split-rule-goal 6)
	(chat-local-to-self "help ally ...")
)
(defrule
	(goal ally-under-attack-goal 2)
	(goal split-rule-goal 51)
=>
	(up-modify-sn sn-focus-player-number g:= temp-goal8)
	(up-modify-goal patrol-goal s:= sn-focus-player-number)
	(up-get-player-fact focus-player allied-goal position-self-goal patrol-point-goal)
	(up-get-player-fact focus-player allied-goal position-self-y-goal patrol-point-y-goal)
	(set-goal split-rule-goal 6)
	(chat-local-to-self "help ally ...")
)
(defrule
	(goal split-rule-goal 6)
	(goal ally-under-attack-goal 1)
	(or
		(goal patrol-point-goal -1)
		(goal patrol-point-y-goal -1)
	)
=>
    (up-get-point position-focus patrol-point-goal)
	(up-lerp-tiles patrol-point-goal position-self-goal c: -20)
)
(defrule
	(taunt-detected any-ally 31)
	(goal split-rule-goal 51)
=>
	(up-modify-goal sn-focus-player-number g:= temp-goal2)
)
(defrule
	(not (taunt-detected any-ally 31))
	(or
		(goal split-rule-goal 51)
		(and
			(goal split-rule-goal 6)
			(goal ally-under-attack-goal 1)
		)
	)
=>
	(up-modify-goal sn-focus-player-number g:= closest-enemy-goal)
)
#load-if-defined KING-OF-THE-HILL
(defrule
	(game-time > 2520)
	(strategic-number sn-military-level >= 0)
	(timer-triggered attack-timer)
=>
	(up-copy-point p1x-goal position-center-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-set-target-point p1x-goal)
	(up-target-point p1x-goal action-patrol -1 aggressive)
	(set-strategic-number sn-number-attack-groups 0);block other military stuff
	(set-strategic-number sn-maximum-town-size 32)
	(enable-timer attack-timer 10)
	(set-goal attack-goal 0)
)
(defrule
	(true); this is just here, so it matches the jump-rule count
=>
	(do-nothing)
	(disable-self)
)
#else
(defrule
	(or
		(unit-type-count trebuchet-set > 0)
		(up-compare-goal victory-time-goal < 750)
	)
	(or
		(players-building-type-count any-enemy wonder > 0)
		(enemy-captured-relics)
	)
	(up-compare-goal victory-time-goal > 0)
	(up-compare-goal victory-time-goal < 1500)
=>
	(set-goal attack-goal 2)
)
(defrule
	(up-compare-goal victory-time-goal < 1500)
	(strategic-number sn-military-level >= 0)
	(or
		(players-building-type-count any-enemy wonder > 0)
		(enemy-captured-relics)
	)
	(unit-type-count battering-ram-line > 1)
	(current-age == castle-age)
=>
	(set-goal attack-goal 2)
)
#end-if
(defrule
	(or
		(taunt-detected any-ally 31)
		(taunt-detected any-ally 222)
	)
	(goal split-rule-goal 6)
=>
	(up-get-player-fact this-any-ally player-number 0 patrol-goal)
	(chat-to-allies-using-id 22153)
	(acknowledge-taunt this-any-ally 31)
	(acknowledge-taunt this-any-ally 222)
)
(defrule
	(unit-type-count monk < 3)
	(goal split-rule-goal 6)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-filter-exclude -1 actionid-attack orderid-explore siege-unit-class)
	(up-find-local c: -1 c: 200)
	(up-target-point patrol-point-goal action-patrol -1 defensive)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: mangonel-line c: 200)
	(up-target-point patrol-point-goal action-patrol -1 defensive)
	(enable-timer attack-timer 180)
	(enable-timer misc-micro-timer 10)
	(up-chat-data-to-player chat-player "Sending help for allied player %d" g: patrol-goal)
)
(defrule
	(unit-type-count monk > 2)
	(goal split-rule-goal 6)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-filter-exclude -1 actionid-attack orderid-explore -1)
	(up-find-local c: -1 c: 200)
	(up-filter-exclude -1 actionid-convert orderid-relic -1)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point patrol-point-goal action-patrol -1 defensive)
	(enable-timer attack-timer 180)
	(enable-timer misc-micro-timer 10)
	(up-chat-data-to-player chat-player "Sending help for allied player %d" g: patrol-goal)
)
(defrule
	(up-compare-goal patrol-goal > 0)
	(up-compare-goal threat-time-goal >= 3000)
=>
	(up-modify-sn sn-focus-player-number g:= patrol-goal)
)
(defrule
	(up-compare-goal patrol-goal > 0)
	(up-compare-goal threat-time-goal >= 3000)
	(or
		(up-allied-goal focus-player under-attack-goal == 0)
		(up-allied-goal focus-player attack-goal > 0)
	)
	(up-allied-goal focus-player id-goal != barbarian)
	(up-allied-goal focus-player id-goal > 0)
=>
	(set-goal patrol-goal -1)
	(enable-timer reset-units-timer 1)
	(up-set-attack-stance -1 c: aggressive)
	(enable-timer attack-timer 1)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(chat-to-player chat-player "patrol help for AI ally not needed anymore")
)
(defrule
	(up-compare-goal threat-time-goal >= 3000)
	(up-compare-goal patrol-goal > 0)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(up-compare-goal threat-time-goal >= 3000)
	(up-compare-goal patrol-goal >= 0)
	(timer-triggered attack-timer)
=>
	(set-goal patrol-goal -1)
	(up-set-attack-stance -1 c: aggressive)
	(enable-timer reset-units-timer 1)
	(chat-to-player chat-player "patrol help timed out")
)
(defrule
	(up-compare-goal patrol-goal >= 0)
	(up-timer-status attack-timer == timer-running)
	(up-timer-status misc-micro-timer != timer-running)
=>
	(up-set-target-point patrol-point-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 20)
	(up-find-local c: -1 c: 200)
	(up-get-search-state local-total-goal)
)
(defrule
	(up-compare-goal local-last-goal < 3); none of my units are there
	(up-compare-goal patrol-goal >= 0)
	(up-timer-status attack-timer == timer-running)
	(up-timer-status misc-micro-timer != timer-running)
=>
	(enable-timer misc-micro-timer 10)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-exclude -1 actionid-explore -1 -1)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point patrol-point-goal action-patrol -1 defensive); keep moving
)
(defrule
	(up-compare-goal patrol-goal >= 0)
	(up-timer-status attack-timer == timer-running)
	(up-timer-status misc-micro-timer != timer-running)
=>
	(set-strategic-number sn-focus-player-number 1)
	(set-goal split-rule-goal 15)
)
(defrule
	(goal split-rule-goal 15)
	(player-valid focus-player)
	(stance-toward focus-player enemy); search for enemy units nearby
=>
	(up-set-target-point patrol-point-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 35)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-remote c: -1 c: 4)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-remote c: -1 c: 4)
	(up-get-search-state local-total-goal)
)
(defrule
	(goal split-rule-goal 15)
	(up-compare-goal remote-total-goal > 2)
	(player-valid focus-player)
	(stance-toward focus-player enemy)
	(up-compare-sn sn-focus-player-number g:!= closest-enemy-goal)
	(up-compare-sn sn-focus-player-number g:!= second-enemy-goal)
=>
	(up-modify-goal second-enemy-goal s:= sn-focus-player-number)
	(up-chat-data-to-player chat-player "Try to counter player %d 's units" g: second-enemy-goal)
)
(defrule
	(goal split-rule-goal 15)
	(up-compare-goal remote-total-goal > 2)
	(player-valid focus-player)
	(stance-toward focus-player enemy)
=>
	(up-filter-exclude -1 actionid-attack orderid-explore -1)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-filter-range -1 -1 -1 -1)
	(up-find-local c: -1 c: 240)
	(up-target-objects 0 action-patrol -1 -1)
	(up-chat-data-to-player chat-player "help ally against %d" s: sn-focus-player-number)
	(enable-timer misc-micro-timer 30)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-jump-rule 1)
)
(defrule
	(goal split-rule-goal 15)
	(strategic-number sn-focus-player-number < 8); go through all players
	(player-valid focus-player)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-jump-rule -4)
)
(defrule
	(taunt-detected any-ally 31)
	(or
		(strategic-number sn-current-age != ci-transit); better get some imp upgrades before trading units
		(and
			(unit-type-count cavalry-class > 12); except trade away bad kt
			(or
				(and
					(up-research-status c: ri-bloodlines < research-pending)
					(not (civ-selected frankish))
				)
				(or
					(civ-selected saracen)
					(civ-selected gothic)
				)
			)
		)
	)
=>
	(up-jump-rule 1)
)
(defrule
	(taunt-detected any-ally 31)
	(or
		(and
			(strategic-number sn-military-level < -1)			
			(population < eighty-five-percent-pop)
		)
		(and
			(players-current-age target-player == imperial-age)
			(current-age == castle-age)
		)
	)
=>
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22118)
)
(defrule
	(taunt-detected any-ally 31)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(or
		(or
			(and 
				(players-current-age focus-player == castle-age)
				(players-military-population focus-player > 10)
			)
			(strategic-number sn-resource-control == 2)
		)
		(players-military-population focus-player > 16)
	)
	(nand
		(up-compare-sn sn-focus-player-number g:== closest-enemy-goal)
		(game-time < 1620)
	)
=>
	(acknowledge-taunt this-any-ally 31)
	(chat-to-player this-any-ally "2")
)
(defrule
	(taunt-detected any-ally 31)
	(strategic-number sn-focus-player-number > 0)
	(players-building-type-count focus-player castle > 0)
	(or
		(players-building-type-count focus-player castle > 1)
		(players-building-type-count focus-player stone-wall-line > 10)
	)
	(unit-type-count trebuchet-set < 1)
	(unit-type-count battering-ram-line < 2)
=>
	(acknowledge-taunt this-any-ally 31)
	(chat-to-player this-any-ally "2")
)
(defrule
	(taunt-detected any-ally 31)
=>
	(set-goal attack-goal 4)
	(up-modify-sn sn-target-player-number g:= temp-goal2)
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22153)
)
(defrule
	(true)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(or
		(up-projectile-detected projectile-town-center c:< 20000)
		(up-projectile-detected projectile-watch-tower c:< 20000)
	)
	(players-military-population focus-player > 15)
	(not (research-completed ri-chain-barding))
=>
	(up-jump-rule 1)
)
(defrule
	(timer-triggered attack-timer)
	(game-time < 1500)
	(goal attack-goal 0)
	(goal unit-goal knight)
	(research-completed ri-scale-barding)
	(unit-type-count knight-line > 5)
	(stance-toward focus-player enemy)
	(players-current-age focus-player == feudal-age)
	(players-unit-type-count focus-player spearman-line < 6)
	(players-military-population focus-player < 26)
=>
	(set-goal attack-goal 2)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
(defrule
	(timer-triggered attack-timer)
	(game-time < 1800)
	(goal attack-goal 0)
	(goal unit-goal knight)
	(research-completed ri-scale-barding)
	(or
		(up-research-status c: ri-bloodlines >= research-pending)
		(up-research-status c: ri-chain-barding >= research-pending)
	)
	(unit-type-count knight-line > 12)
	(stance-toward focus-player enemy)
	(players-current-age focus-player == feudal-age)
	(players-unit-type-count focus-player spearman-line < 10)
=>
	(set-goal attack-goal 2)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
)
#load-if-defined DEATH-MATCH
#load-if-defined UP-4-PLAYER-TEAM
#load-if-not-defined UP-POCKET-POSITION
(defrule
	(goal under-attack-goal 0)
	(up-allied-goal any-ally team-coordination-goal == 1)
	(players-building-count every-enemy > 0)
	(game-time < 480)
=>
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
)
(defrule
	(goal under-attack-goal 0)
	(up-allied-goal focus-player team-coordination-goal == 1)
	(up-allied-sn focus-player sn-target-player-number g:== closest-enemy-goal)
	(players-building-count every-enemy > 0)
	(game-time < 480)
=>
	(set-goal attack-goal 7)
)
(defrule
	(goal under-attack-goal 0)
	(up-allied-goal any-ally team-coordination-goal == 1)
	(players-building-count every-enemy > 0)
	(game-time < 480)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
#end-if
#end-if
#else
#load-if-defined UP-POCKET-POSITION
(defrule
	(game-time < 1800)
	(population < max-civ-pop)
	(timer-triggered attack-timer)
	(goal attack-goal 0)
	(goal hostilities-goal 0)
	(up-allied-goal any-ally team-coordination-goal == 1)
	(or
		(unit-type-count cavalry-class > 4)
		(unit-type-count my-unique-unit-line > 4)
	)
=>
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(up-get-focus-fact allied-goal team-coordination-goal temp-goal)
	(up-get-focus-fact allied-sn sn-target-player-number temp-goal2)
	(set-goal split-rule-goal 18)
)
(defrule
	(goal split-rule-goal 18)
	(goal temp-goal 1)
	(up-compare-goal temp-goal g:== closest-enemy-goal)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-goal attack-goal 4)
)
#end-if
#end-if
(defrule
	(players-building-type-count any-enemy stone-wall-line > 0)
	(strategic-number sn-maximum-town-size >= 40)
	(strategic-number sn-maximum-town-size < 255)
	(up-building-type-in-town c: town-center == 0)
	(up-building-type-in-town c: castle == 0)
	(up-building-type-in-town c: watch-tower == 0)
	(up-building-type-in-town c: monastery == 0)
	(up-building-type-in-town c: market == 0)
	(up-building-type-in-town c: bombard-tower == 0)
	(up-building-type-in-town c: siege-workshop == 0)
	(up-building-type-in-town c: lumber-camp == 0)
	(up-building-type-in-town c: mining-camp == 0)
=>
	(up-modify-sn sn-maximum-town-size c:+ 5); increase TS to reach buildings behind the wall
)
(defrule
	(strategic-number sn-target-player-number < 1);debug
	(players-building-count any-enemy > 0)
=>
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
(defrule
	(game-time >= 1800)
	(population >= max-civ-pop)
	(current-age >= imperial-age)
	(players-population target-player < thirty-percent-pop); target is already weakened
	(up-compare-sn sn-target-player-number g:!= closest-enemy-goal); not cloest enemy
	(up-allied-goal any-ally attacking-enemy-goal g:== closest-enemy-goal); go for the closest enemy
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal); prevents trading players when trippling an enemy
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
)
(defrule
	(game-time >= 1800)
	(strategic-number sn-target-player-number g:== closest-enemy-goal)
	(players-population target-player < twenty-five-percent-pop)
	(or
		(players-population target-player < 10)
		(strategic-number sn-maximum-town-size > 270); not TC to destroy
	)
	(players-population any-enemy > fourty-percent-pop)
	(goal team-coordination-goal 1)
=>
	(up-modify-sn sn-focus-player-number g:= second-enemy-goal)
	(up-modify-sn sn-target-player-number g:= second-enemy-goal)
)
(defrule
	(game-time >= 1800)
	(strategic-number sn-target-player-number g:== second-enemy-goal)
	(players-population target-player > eighty-five-percent-pop)
	(population < eighty-five-percent-pop)
	(goal team-coordination-goal 1)
	(players-population any-enemy < 30)
=>
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal); their pocket is stronger than us
)
(defrule
	(strategic-number sn-target-player-number > 0)
	(or
		(or
			(not (player-in-game target-player))
			(stance-toward target-player ally); reset target player or attacks will be stuck if we set the enemy to ally
		)
		(or
			(players-building-count target-player == 0)
			(strategic-number sn-maximum-town-size == 65612); debug
		)
	)
=>
	(up-find-player enemy find-closest closest-enemy-goal)
	(up-modify-sn sn-target-player-number g:= closest-enemy-goal)
	(set-strategic-number sn-safe-town-size 255)
)
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
(defrule
	(strategic-number sn-military-level > -2)
	(current-age == feudal-age)
	(players-unit-type-count focus-player skirmisher-line > 5)
	(or
		(unit-type-count militiaman-line > 5)
		(unit-type-count scout-cavalry > 5)
	)
	(up-compare-goal position-goal != pocket)
=>
	(up-jump-rule 2)
)
(defrule
	(up-compare-goal threat-time-goal > 2000)
	(up-compare-goal threat-time-goal < 5000)
	(unit-type-count tc-arrow == 0)
	(unit-type-count watch-tower-arrow == 0)
	(strategic-number sn-military-level < 0)
	(current-age == feudal-age)
	(up-enemy-units-in-town == 0)
	(strategic-number sn-allow-civilian-defense != 3)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-set-target-point position-self-goal)
	(up-target-point position-self-goal action-move -1 aggressive)
	(enable-timer reset-units-timer 10)
)
(defrule
	(up-compare-goal threat-time-goal < 2000)
	(unit-type-count tc-arrow == 0)
	(unit-type-count watch-tower-arrow == 0)
	(strategic-number sn-military-level < -1)
	(current-age == feudal-age)
	(up-enemy-units-in-town == 0)
	(strategic-number sn-allow-civilian-defense != 3)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-set-target-point position-self-goal)
	(up-target-point position-self-goal action-move -1 no-attack)
	(enable-timer reset-units-timer 5)
)
(defrule
	(or
		(or
			(unit-type-count castle-arrow > 0)
			(unit-type-count castle-arrow-chemistry > 0)
		)
		(or
			(unit-type-count tc-arrow > 0)
			(building-type-count town-center == 0)
		)
	)
=>
	(up-jump-rule 36)
)
(defrule
	(or
		(and
			(death-match-game)
			(game-time < 900)
		)
		(and
			(or
				(players-building-type-count any-enemy wonder > 0)
				(enemy-captured-relics)
			)
			(up-compare-goal victory-time-goal < 300)
		)
	)
=>
	(up-jump-rule 35)
)
(defrule
	(up-projectile-detected projectile-castle c:< 30000)
	(or
		(unit-type-count 469 > 0); trebuchet bullets
		(unit-type-count 371 > 0)
	)
	(up-timer-status trebuchet-timer != timer-running)
	(players-building-type-count target-player castle > 2)
=>
	(set-strategic-number sn-disable-defend-groups 1)
	(enable-timer disable-defend-groups-timer 8)
	(enable-timer trebuchet-timer 8)
	(up-retreat-to 42 c: ranged-unit-class)
	(up-retreat-to 42 c: cavalry-archer-class)
	(up-retreat-to 42 c: infantry-class)
	(up-retreat-to 42 c: cavalry-class)
	(up-retreat-to 42 c: gunpowder-class)
	(up-retreat-to 42 c: monastery-class)
	(chat-to-player chat-player "guard shooting trebs")
)
; target castle as trebs seem to unpack with sn-disable-defend-groups. But with TSA on, troops run mindlessly in all time.
(defrule
	(up-timer-status trebuchet-timer == timer-running)
	(up-timer-status misc-micro-timer != timer-running)
	(unit-type-count unpacked-trebuchet > 0)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: unpacked-trebuchet c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-filter-distance c: 12 c: 17)
	(up-get-projectile-player projectile-castle temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)
(defrule
	(up-timer-status trebuchet-timer == timer-running)
	(up-timer-status misc-micro-timer != timer-running)
	(unit-type-count unpacked-trebuchet > 0)
	(up-find-remote c: castle c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-target-objects 0 action-default -1 aggressive)
	(chat-local-to-self "duc: shoot treb")
)
(defrule
	(game-time < 1380)
	(up-player-distance focus-player < 70)
	(goal strategy-goal flush)
	(strategic-number sn-military-level < -1)
	(up-compare-goal threat-time-goal < 3000)
	(up-pending-objects c: watch-tower == 0)
	(strategic-number sn-allow-civilian-defense != 3)
=>
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-filter-distance c: -1 c: 11)
	(set-goal split-rule-goal 37)
)
(defrule
	(goal split-rule-goal 37)
	(not (up-find-remote c: -1 c: 1))
=>
	(set-goal retreat-now-goal 1)
	(enable-timer attack-timer 90)
)
(defrule
	(up-player-distance focus-player < 70)
	(goal strategy-goal flush)
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(players-current-age focus-player == castle-age)
	(players-military-population focus-player > 20)
	(strategic-number sn-current-age == fc-transit)
	(unit-type-count watch-tower-arrow == 0)
=>
	(set-goal retreat-now-goal 1)
	(enable-timer attack-timer 60)
;	(chat-to-player chat-player "avoid fight until research complete")
)
(defrule
	(up-player-distance focus-player < 70)
	(goal strategy-goal flush)
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(players-current-age focus-player == castle-age)
	(players-current-age-time focus-player > 90)
	(stance-toward focus-player enemy)
	(or
		(up-research-status c: ri-pikeman == research-pending)
		(up-research-status c: ri-bodkin-arrow == research-pending)
	)
	(current-age-time < 90)
	(up-enemy-units-in-town == 0)
	(not (death-match-game))
	(unit-type-count watch-tower-arrow == 0)
=>
	(set-goal retreat-now-goal 1)
	(enable-timer attack-timer 60)
;	(chat-to-player chat-player "avoid fight until research complete2")
)
(defrule
	(up-players-in-game ally == 0)
	(game-time < 1800)
	(up-compare-goal threat-time-goal < 2000)
	(up-compare-goal threat-player-goal != 0)
	(strategic-number sn-military-level < -1)
	(unit-type-count watch-tower-arrow == 0)
	(goal under-attack-goal 0)
	(unit-type-count mangonel-line == 0)
	(up-timer-status reset-units-timer != timer-running)
	(goal attack-goal 0)
	(goal raid-goal 0)
	(players-current-age-time every-enemy > 180)
	(strategic-number sn-allow-civilian-defense != 3)
=>
	(set-goal retreat-now-goal 1)
	(set-goal attack-goal 0)
	(enable-timer attack-timer 90)
)
(defrule
	(up-players-in-game ally == 0)
	(game-time < 2400)
	(up-compare-goal threat-time-goal < 2000)
	(up-compare-goal threat-player-goal != 0)
	(strategic-number sn-military-level < -2)
	(unit-type-count watch-tower-arrow == 0)
	(goal under-attack-goal 0)
	(unit-type-count mangonel-line < 3)
	(up-timer-status reset-units-timer != timer-running)
	(goal attack-goal 0)
	(goal raid-goal 0)
	(strategic-number sn-allow-civilian-defense != 3)
=>
	(set-goal retreat-now-goal 1)
	(set-goal attack-goal 0)
	(enable-timer attack-timer 90)
)
(defrule
	(game-time < 1800)
	(goal team-coordination-goal 1)
	(players-building-type-count target-player town-center > 0)
=>
	(up-modify-sn sn-focus-player-number s:= sn-target-player-number)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-remote c: town-center c: 1)
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 9)
)
(defrule
	(game-time < 1800)
	(goal team-coordination-goal 1)
	(players-building-type-count target-player town-center > 0)
	(up-find-local c: spearman-line c: 1)
=>
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p2x-goal)
	(up-lerp-tiles p2x-goal p1x-goal c: -5)
	(up-target-point p2x-goal action-move -1 aggressive)
	(chat-local-to-self "move spearman out of TC range")
)

; Retreat from fortification fire
(defrule
	(current-age == feudal-age)
	(strategic-number sn-military-level < 2)
	(up-projectile-detected projectile-watch-tower c:< 3000)
=>
	(up-get-projectile-player projectile-watch-tower threat-player-goal)
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-get-fact military-population 0 temp-goal)
	(up-modify-goal temp-goal g:- temp-goal2)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(set-goal temp-goal2 watch-tower)
	(up-jump-rule 1)
)
(defrule
	(up-projectile-detected projectile-watch-tower c:< 3000)
=>
	(up-get-projectile-player projectile-watch-tower threat-player-goal)
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-get-fact military-population 0 temp-goal)
	(up-modify-goal temp-goal g:- temp-goal2)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-garrison c: 3 c: -1)
	(set-goal temp-goal2 watch-tower)
)
(defrule
	(up-projectile-detected projectile-town-center c:< 3000)
=>
	(up-get-projectile-player projectile-town-center threat-player-goal)
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-get-fact military-population 0 temp-goal)
	(up-modify-goal temp-goal g:- temp-goal2)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-garrison c: 3 c: -1)
	(set-goal temp-goal2 town-center)
)
(defrule
	(up-projectile-detected projectile-castle c:< 3000)
=>
	(up-get-projectile-player projectile-castle threat-player-goal)
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-get-fact military-population 0 temp-goal)
	(up-modify-goal temp-goal g:- temp-goal2)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include -1 actionid-attack -1 -1); find the shooting castle
	(set-goal temp-goal2 castle)
)
(defrule
	(or
		(or
			(up-timer-status retreat-timer == timer-running); issue retreat only once every few seconds to prevent regroup dance
			(and
				(up-projectile-target projectile-any == 947); don't freak out if only scout gets shot
				(unit-type-count scout-cavalry-line == 1)
			)
		)
		(or
			(or
				(up-projectile-target projectile-any == warship-class); => water section
				(up-projectile-target projectile-any == siege-unit-class); all good
			)
			(or
				(up-projectile-target projectile-castle == villager-class); => forward threat section
				(up-projectile-target projectile-any == building-class)
			)
		)
	)
=>
	(up-jump-rule 19)
)
(defrule
	(or
		(or
			(up-projectile-detected projectile-town-center c:< 2000)
			(up-projectile-detected projectile-castle c:< 2000)
		)
		(up-projectile-detected projectile-watch-tower c:< 2000)
	)
	(up-find-remote g: temp-goal2 c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object object-point-goal)
	(up-get-point-distance object-point-goal target-goal temp-goal3)
	(up-get-point-distance object-point-goal position-self-goal temp-goal8)
	(up-get-point-distance target-goal position-self-goal temp-goal7)
	(up-get-point-distance waypoint-goal position-self-goal temp-goal6)
	(set-goal split-rule-goal 4)
)

(defrule
	(goal split-rule-goal 4)
	(up-projectile-detected projectile-castle c:< 2000)
	(nand
		(or
			(research-completed ri-elite-eagle-warrior)
			(research-completed ri-paladin)
		)
		(population > seventy-five-percent-pop)
	)
	(or
		(population < eighty-five-percent-pop)
		(and
			(unit-type-count battering-ram-line == 0)
			(unit-type-count trebuchet-set == 0)
		)
	)
=>
	(set-goal split-rule-goal 5)
)
(defrule
	(goal split-rule-goal 4)
	(up-projectile-detected projectile-town-center c:< 2000)
	(up-compare-goal temp-goal >= 5)
	(unit-type-count cavalry-class > 8)
	(or
		(strategic-number sn-target-player-number g:== second-enemy-goal); raid pocket more aggressively
		(up-compare-goal temp-goal >= 10)
	)
=>
	(up-jump-rule 16)
	(up-set-defense-priority c: town-center c: 20000)
)
(defrule
	(goal split-rule-goal 4)
	(up-projectile-detected projectile-town-center c:< 2000); don't retreat vs feudal aged players 
	(up-compare-goal temp-goal >= -7)
	(unit-type-count cavalry-class > 8)
	(or
		(players-current-age focus-player == feudal-age)
		(and
			(or
				(players-unit-type-count focus-player skirmisher-line > 9)
				(players-unit-type-count focus-player archer-line > 9)
			)
			(research-completed ri-chain-barding)
		)
	)
	(goal anti-cavalry-threat-goal 0)
	(goal monk-threat-goal 0)
	(strategic-number sn-cavalry-threat == 0)
=>
	(up-jump-rule 15)
	(up-set-defense-priority c: town-center c: 20000)
)
(defrule
	(goal split-rule-goal 4)
	(population < seventy-five-percent-pop)
	(up-projectile-detected projectile-town-center c:< 2000)
	(up-compare-goal temp-goal >= 0)
	(or
		(strategic-number sn-cavalry-threat == 0)
		(or
			(up-compare-goal temp-goal >= 10)
			(research-completed ri-elite-eagle-warrior)
		)
	)
	(players-unit-type-count focus-player militiaman-line < 7)
	(unit-type-count eagle-warrior-line > 10)
=>
	(up-jump-rule 14)
	(up-set-defense-priority c: town-center c: 20000)
)
(defrule
	(goal split-rule-goal 4)
	(population < seventy-five-percent-pop)
	(up-projectile-detected projectile-town-center c:< 2000)
=>
	(set-goal split-rule-goal 5)
)
(defrule
	(population < seventy-five-percent-pop)
	(current-age < imperial-age)
	(goal split-rule-goal 4)
	(up-projectile-detected projectile-watch-tower c:< 2000)
	(or
		(current-age == feudal-age)
		(or
			(up-projectile-target projectile-watch-tower == ranged-unit-class)
			(up-projectile-target projectile-watch-tower == cavalry-archer-class)
		)
	)
=>
	(set-goal split-rule-goal 5)
)
(defrule
	(goal split-rule-goal 5)
	(unit-type-count battering-ram-line > 0)
=>
	(up-set-target-point object-point-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 15)
	(up-find-remote c: infantry-class c: 10)
	(up-find-remote c: cavalry-class c: 10)
	(up-find-remote c: siege-unit-class c: 10)
	(up-get-search-state local-total-goal)
	(set-goal split-rule-goal 13)
)
(defrule
	(goal split-rule-goal 13)
	(strategic-number sn-military-level < 2)
	(up-projectile-detected projectile-castle c:< 2000)
=>
	(set-goal retreat-now-goal 1)
	(set-goal attack-goal 0)
	(set-goal duc-control-goal -1)
	(enable-timer attack-timer 20)
	(chat-to-player chat-player "retreat out of castle fire")
	(up-jump-rule 10)
)
(defrule
	(goal split-rule-goal 13)
	(up-find-local c: battering-ram-line c: 1)
	(up-compare-goal remote-total-goal > 1); enemy has units that may kill the ram
	(strategic-number sn-military-level >= 0)
	(or
		(or
			(strategic-number sn-military-level > 1)
			(population > max-civ-pop)
		)
		(not (up-projectile-detected projectile-castle c:< 2000))
	)
=>
	(up-jump-rule 9); stay
	(up-set-defense-priority c: castle c: 20000)
	(up-set-defense-priority c: town-center c: 20000)
	(chat-local-to-self "ignore fortification fire")
)
(defrule
	(goal split-rule-goal 13)
	(or
		(not (up-find-local c: battering-ram-line c: 1))
		(up-compare-goal remote-total-goal < 2)
	)
=>
	(up-lerp-tiles object-point-goal position-self-goal c: 18)
	(up-set-defense-priority c: castle c: 20000)
	(up-set-defense-priority c: town-center c: 20000)
	(up-set-target-point object-point-goal)
	(set-strategic-number sn-maximum-town-size 20)
	(set-goal split-rule-goal 14)
)
(defrule
	(goal split-rule-goal 14)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-exclude -1 actionid-explore orderid-relic siege-unit-class)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point object-point-goal action-move -1 no-attack)
	(enable-timer reset-units-timer 5)
	(enable-timer retreat-timer 5)
	(set-goal split-rule-goal 0)
	(chat-local-to-self "duc out of fortification fire: ram")
)
(defrule
	(or
		(goal split-rule-goal 5)
		(goal split-rule-goal 13)
	)
	(up-compare-goal duc-control-goal >= move-target)
	(up-compare-goal temp-goal7 g:> temp-goal8); distance to target > distance to fortification
=>
	(up-copy-point p1x-goal object-point-goal)
	(up-copy-point p2x-goal object-point-goal)
	(up-cross-tiles p1x-goal position-self-goal c: -20)
	(up-cross-tiles p2x-goal position-self-goal c: 20)
	(up-get-point-distance p1x-goal target-goal temp-goal)
	(up-get-point-distance p2x-goal target-goal temp-goal2)
	(set-goal split-rule-goal 14)
)
(defrule
	(goal split-rule-goal 14)
	(up-compare-goal temp-goal2 g:< temp-goal)
	(nor
		(or
			(up-compare-goal p2x-goal g:>= max-coordinate-goal); avoid invalid coordinates
			(up-compare-goal p2y-goal g:>= max-coordinate-y-goal)
		)
		(or
			(or
				(goal p2x-goal -1)
				(goal p2y-goal -1)
			)
			(or
				(up-point-contains p2x-goal c: tree-class)
				(up-point-terrain p2x-goal == terrain-water)
			)
		)
	)
=>
	(up-copy-point p1x-goal p2x-goal)
)
(defrule
	(goal split-rule-goal 14)
=>
	(up-set-target-point object-point-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: 1 c: 12)
	(up-filter-exclude -1 actionid-explore orderid-relic siege-unit-class)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point p1x-goal action-move -1 no-attack)
	(enable-timer attack-timer 5)
	(enable-timer reset-units-timer 5)
	(set-strategic-number sn-disable-defend-groups 1)
	(enable-timer disable-defend-groups-timer 3)
)
(defrule
	(goal split-rule-goal 14)
=>
	(set-strategic-number sn-maximum-town-size 12)
	(enable-timer retreat-timer 3)
	(set-goal split-rule-goal 0)
	(chat-local-to-self "DUC out of fortification fire 2")
)
(defrule
	(or
		(goal split-rule-goal 5)
		(goal split-rule-goal 13)
	)
	(or
		(up-compare-goal duc-control-goal < move-target)
		(up-compare-goal temp-goal7 g:<= temp-goal8); distance to target < distance to fortification
	)
=>
	(enable-timer attack-timer 5)
	(enable-timer reset-units-timer 5)
	(enable-timer disable-defend-groups-timer 3)
	(set-strategic-number sn-disable-defend-groups 1)
	(set-goal split-rule-goal 14)
)
(defrule
	(up-compare-goal attack-goal >= 2)
	(goal split-rule-goal 14)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: 1 c: 12)
	(up-filter-exclude -1 actionid-explore orderid-relic siege-unit-class)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point position-self-goal action-move -1 no-attack)
	(set-strategic-number sn-maximum-town-size 14)
	(enable-timer retreat-timer 3)
	(up-jump-rule 1)
	(chat-local-to-self "DUC out of fortification fire 3")
)
(defrule
	(goal split-rule-goal 14)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-exclude -1 actionid-explore orderid-relic siege-unit-class)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point position-self-goal action-move -1 no-attack)
	(set-strategic-number sn-maximum-town-size 14)
	(enable-timer retreat-timer 3)
	(enable-timer attack-timer 3)
	(chat-local-to-self "DUC out of fortification fire 4")
)

; retreat imperial age attacks
(defrule
	(building-type-count town-center > 0)
	(game-time > 600)
	(strategic-number sn-maximum-town-size >= 40)
	(players-population target-player > max-civ-pop)
	(players-current-age target-player == imperial-age)
	(population < max-civ-pop)
	(current-age == imperial-age)
	(current-age-time > 150)
=>
	(up-get-point position-target p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 40)
	(set-goal split-rule-goal 40)
)
(defrule
	(goal split-rule-goal 40)
	(up-find-local c: castle c: 1)
=>
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-filter-exclude -1 actionid-explore orderid-relic -1)
	(up-find-local c: -1 c: 240)
	(set-goal split-rule-goal 41)
)
(defrule	
	(goal split-rule-goal 41)
=>
	(up-target-point p1x-goal action-move -1 no-attack)
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-maximum-town-size 32)
	(set-goal retreat-now-goal 0)
	(set-goal duc-control-goal -1)
	(enable-timer reset-units-timer 9)
	(set-goal attack-goal 0)
	(enable-timer attack-timer 30)
	(chat-to-player chat-player "full pop attack failed - retreat to forward")
)
(defrule
	(game-time > 600)
	(strategic-number sn-maximum-town-size >= 40)
	(players-population target-player > max-civ-pop)
	(players-current-age target-player == imperial-age)
	(strategic-number sn-target-player-number g:!= winning-player-goal)
	(population < max-civ-pop)
	(current-age == imperial-age)
	(current-age-time > 150)
	(building-type-count town-center > 0)
=>
	(up-retreat-now)
	(set-strategic-number sn-maximum-town-size 24)
	(enable-timer reset-units-timer 9)
	(enable-timer attack-timer 30)
	(up-set-attack-stance -1 c: no-attack)
	(set-goal duc-control-goal -1)
	(chat-to-player chat-player "full pop attack failed")
)
#load-if-defined DEATH-MATCH
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 1000)
	(unit-type-count trebuchet-set < 1)
	(unit-type-count battering-ram-line == 0)
	(up-projectile-detected projectile-castle c:< 2000)
	(up-projectile-target projectile-castle != building-class)
	(up-projectile-target projectile-castle != warship-class)
	(up-projectile-target projectile-castle != villager-class)
=>
	(set-goal retreat-now-goal 1)
	(set-goal attack-goal 0)
	(enable-timer attack-timer 20)
)
#end-if
#end-if ; not easy/easiest
#end-if
(defrule
	(or
		(strategic-number sn-number-civilian-militia > 0)
		(up-timer-status retreat-timer == timer-running)
	)
=>
	(set-goal retreat-now-goal 0)
	(up-jump-rule 5)
)
(defrule
	(goal retreat-now-goal 1)
	(up-compare-goal duc-control-goal > 0)
=>
	(set-goal duc-control-goal -1)
	(up-reset-unit c: -1)
)
(defrule
	(goal duc-control-goal -1)
=>
	(set-goal waypoint2-goal -1)
	(set-goal waypoint2-y-goal -1)
	(set-goal target-goal -1)
	(set-goal target-y-goal -1)
)
(defrule
	(building-type-count castle > 0)
	(goal retreat-now-goal 1)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: castle c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-filter-range -1 -1 20 -1)
	(up-filter-exclude -1 actionid-explore orderid-relic warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 240)
	(up-target-point p1x-goal action-move -1 no-attack)
	(up-jump-rule 1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal retreat-now-goal 1)
=>
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-filter-exclude -1 actionid-explore orderid-relic siege-unit-class)
	(up-filter-range -1 -1 8 -1)
	(up-find-local c: -1 c: 200)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point position-self-goal action-move -1 no-attack)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal retreat-now-goal 1)
=>
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-maximum-town-size 12)
	(set-goal retreat-now-goal 0)
	(set-strategic-number sn-disable-defend-groups 1)
	(enable-timer disable-defend-groups-timer 3)
	(enable-timer retreat-timer 3)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 8 -1)
	(up-find-local c: siege-unit-class c: 240)
	(up-target-point position-self-goal action-move -1 no-attack)
	(chat-to-player my-player-number "retreating")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
=>
	(up-jump-rule 7); TSA doesn't work on islands map
)
(defrule
	(goal position-goal pocket)
	(players-building-count any-enemy == 0)
=>
	(up-jump-rule 2); find all players first to eliminate slings
)
(defrule
	(players-building-count any-enemy > 0)
	(current-age >= feudal-age)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-send-scout group-type-land-explore position-focus)
	(enable-timer scouting-timer 40)
	(disable-self)
)
(defrule
	(up-compare-sn sn-target-player-number g:== closest-enemy-goal)
	(players-current-age any-enemy >= feudal-age)
	(game-time < 1800)
	(timer-triggered scouting-timer)
	(strategic-number sn-infantry-threat == 0)
	(strategic-number sn-archer-threat == 0)
	(strategic-number sn-cavalry-threat == 0)
	(goal monk-threat-goal 0)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-send-scout group-type-land-explore position-focus)
	(enable-timer scouting-timer 60)
)
(defrule
	(goal team-coordination-goal 1)
	(players-building-type-count any-enemy market > 0)
	(players-unit-type-count any-enemy trade-cart > 10)
=>
	(up-set-defense-priority c: market c: 3000)
)
(defrule
	(game-time > 400)
	(or
		(and
			(death-match-game)
			(game-time < 460)
		)
		(and
			(game-time > 1620)
			(game-time < 1800)
		)
	)
	(player-in-game any-ally)
=>
	(set-strategic-number sn-number-explore-groups 1)
	(up-send-scout group-type-land-explore position-corner)
)
(defrule
	(cc-players-unit-type-count 0 tree-class > 50)
	(population > seventy-five-percent-pop)
	(players-population target-player < twenty-five-percent-pop)
=>
	(set-strategic-number sn-number-explore-groups 1); search for enemy to prevent him from hiding
)
#load-if-not-defined UP-MICHI-STYLE
#load-if-not-defined BLACK-FOREST-MAP
#load-if-not-defined DEATH-MATCH
#load-if-not-defined UP-1-PLAYER-TEAM
(defrule
	(strategic-number sn-current-age < ci-transit)
	(population < eighty-five-percent-pop)
	(timer-triggered attack-timer)
	(goal attack-goal 0)
	(or
		(goal hostilities-goal 0)
		(goal threat-source-goal warship-class)
	)
	(goal raid-goal 0)
	(game-time < 2100)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands); not on islands
	(or
		(unit-type-count cavalry-class > 4)
		(military-population > 15)
	)
	(up-compare-goal unit-goal != monk)
=>
	(set-goal raid-goal 2); allow selecting a potential target for raid
)
(defrule
	(goal raid-goal 2)
	(goal ally-under-attack-goal 1)
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(strategic-number sn-military-level > -2)
=>
	(set-goal raid-goal -1)
)
#load-if-defined UP-POCKET-POSITION
(defrule
	(goal raid-goal 2)
	(up-compare-goal second-enemy-goal > 0)
=>
	(up-modify-sn sn-focus-player-number g:= second-enemy-goal)
)
(defrule
	(goal raid-goal 2)
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
=>
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-get-focus-fact unit-type-count galley-line temp-goal3)
	(up-modify-goal temp-goal2 g:- temp-goal3)
	(up-get-focus-fact unit-type-count fire-ship-line temp-goal3)
	(up-modify-goal temp-goal2 g:- temp-goal3)
	(up-get-fact military-population 0 temp-goal3)
	(up-get-fact unit-type-count warship-class temp-goal4)
	(up-modify-goal temp-goal3 g:- temp-goal4)
	(up-modify-goal temp-goal3 g:- temp-goal2)
	(set-goal split-rule-goal 7)
)
(defrule
	(strategic-number sn-focus-player-number g:== second-enemy-goal)
	(goal split-rule-goal 7)
	(unit-type-count cavalry-class > 4)
	(up-compare-goal temp-goal3 > 4)
	(or
		(up-compare-goal temp-goal2 < 1)
		(up-compare-goal temp-goal3 > 8)
	)
	(or
		(up-compare-goal temp-goal2 < 5)
		(up-compare-goal temp-goal3 > 12)
	)
	(up-compare-goal temp-goal2 < 20)
=>
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
	(up-modify-sn sn-target-player-number s:= sn-focus-player-number)
	(up-chat-data-to-player chat-player "23 Raid the pocket on my side: Player %d" s: sn-target-player-number)
)
(defrule
	(goal raid-goal 2)
=>
	(up-find-player enemy find-closest temp-goal); just overwrite them
	(up-find-next-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal); find fourth closest enemy
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-get-focus-fact unit-type-count galley-line temp-goal3)
	(up-modify-goal temp-goal2 g:- temp-goal3)
	(up-get-focus-fact unit-type-count fire-ship-line temp-goal3)
	(up-modify-goal temp-goal2 g:- temp-goal3)
	(up-get-fact military-population 0 temp-goal3)
	(up-get-fact unit-type-count warship-class temp-goal4)
	(up-modify-goal temp-goal3 g:- temp-goal4)
	(up-modify-goal temp-goal3 g:- temp-goal2)
	(set-goal split-rule-goal 11)
)
(defrule
	(goal split-rule-goal 11)
	(unit-type-count cavalry-class > 4)
	(up-compare-goal temp-goal3 > 4)
	(or
		(up-compare-goal temp-goal2 < 1)
		(up-compare-goal temp-goal3 > 8)
	)
	(or
		(up-compare-goal temp-goal2 < 5)
		(up-compare-goal temp-goal3 > 12)
	)
	(up-compare-goal temp-goal2 < 20)
=>
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
	(up-modify-sn sn-target-player-number s:= sn-focus-player-number)
	(up-chat-data-to-player chat-player "23 Raid fourth nearest Player: Player %d" s: sn-target-player-number)
)
#end-if ;pocket
(defrule
	(up-compare-goal split-rule-goal != 29)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(set-goal split-rule-goal 0)
)
(defrule
	(goal raid-goal 2)
	(game-time < 1800)
	(goal strategy-goal flush)
	(up-player-distance focus-player < 70)
	(stance-toward focus-player enemy)
	(strategic-number sn-military-level > 0)
=>
	(set-goal split-rule-goal 7)
	(set-goal raid-goal 3)
)
(defrule
	(goal raid-goal 2)
=>
	(up-get-focus-fact military-population 0 temp-goal2)
	(up-get-focus-fact unit-type-count galley-line temp-goal3)
	(up-modify-goal temp-goal2 g:- temp-goal3)
	(up-get-focus-fact unit-type-count fire-ship-line temp-goal3)
	(up-modify-goal temp-goal2 g:- temp-goal3)
	(up-get-fact military-population 0 temp-goal3)
	(up-get-fact unit-type-count warship-class temp-goal4)
	(up-modify-goal temp-goal3 g:- temp-goal4)
	(up-modify-goal temp-goal3 g:- temp-goal2)
	(set-goal split-rule-goal 7)
)
(defrule
	(unit-type-count cavalry-class > 4)
	(goal split-rule-goal 7)
	(up-compare-goal temp-goal3 > 4)
	(or
		(up-compare-goal temp-goal2 < 1)
		(up-compare-goal temp-goal3 > 8)
	)
	(or
		(up-compare-goal temp-goal2 < 5)
		(up-compare-goal temp-goal3 > 12)
	)
	(up-compare-goal temp-goal2 < 20)
=>
	(set-goal raid-goal 3)
)
(defrule
	(goal split-rule-goal 7)
	(up-compare-goal temp-goal3 > 15)
	(up-compare-goal temp-goal2 < 30)
=>
	(set-goal raid-goal 3)
)
(defrule
	(goal raid-goal 3)
	(or
		(players-building-type-count focus-player watch-tower > 0)
		(players-building-type-count focus-player castle > 0)
	)
	(or
		(players-building-type-count focus-player palisade-wall > 0)
		(or 
			(players-building-type-count focus-player fortified-wall > 0)
			(players-building-type-count focus-player stone-wall > 0)
		)
	)
	(unit-type-count battering-ram-line < 2)
=>
	(set-goal raid-goal 2)
	(set-goal attack-goal 0)
	(set-goal split-rule-goal 0)
)
(defrule
	(strategic-number sn-focus-player-number g:== closest-enemy-goal)
	(goal raid-goal 3)
=>
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
	(up-modify-sn sn-target-player-number s:= sn-focus-player-number)
	(up-chat-data-to-player chat-player "Attack the closest enemy: %d" s: sn-target-player-number)
)
#else
#load-if-not-defined UP-MULTIPLE-ENEMIES
(defrule
	(game-time < 2100)
	(goal hostilities-goal 0)
	(timer-triggered attack-timer)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands); not on islands
=>
	(set-goal raid-goal 2)
)
(defrule
	(strategic-number sn-military-level >= -1)
	(goal raid-goal 2)
	(or
		(players-unit-type-count every-enemy archer-line > 4)
		(players-unit-type-count every-enemy skirmisher-line > 4)
	)
	(players-building-type-count every-enemy castle == 0)
	(players-unit-type-count every-enemy monk < 3)
	(players-unit-type-count every-enemy spearman-line < 6)
	(research-completed ri-scale-barding)
	(unit-type-count cavalry-class > 7)
=>
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
	(chat-local-to-self "23DUC")
)
(defrule
	(strategic-number sn-military-level >= 1)
	(goal raid-goal 2)
	(goal enemy-goal eagle-warrior)
	(unit-type-count militiaman-line > 4)
=>
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
	(chat-local-to-self "23DUC")
)
(defrule
	(strategic-number sn-military-level >= 0)
	(goal raid-goal 2)
	(players-unit-type-count every-ally archer-line > 9)
	(unit-type-count elite-skirmisher > 10)
=>
	(set-goal raid-goal 1)
	(set-goal split-rule-goal 29)
	(chat-local-to-self "23DUC")
)
#end-if ; no FFA Game
#end-if ; playing alone
(defrule
	(or
		(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
		(building-type-count town-center == 0)
	)
=>
	(up-jump-rule 53); TSA/DUC is not for islands map
)
(defrule
	(goal raid-goal 1)
	(goal split-rule-goal 29)
	(unit-type-count battering-ram-line > 0)
	(up-compare-goal unit-goal != knight)
	(up-compare-goal unit-goal != eagle-warrior)
=>
	(set-goal split-rule-goal 0)
	(set-goal attack-goal 4)
	(up-set-defense-priority c: town-center c: 20000)
)
(defrule
	(or
		(or
			(unit-type-count castle-arrow > 0)
			(unit-type-count castle-arrow-chemistry > 0)
		)
		(or
			(unit-type-count tc-arrow > 0)
			(or
				(building-type-count town-center == 0)
				(goal retreat-now-goal 1)
			)
		)
	)
=>
	(up-jump-rule 2); or it won't react to town under attack
)
(defrule
	(strategic-number sn-maximum-town-size >= 40)
	(strategic-number sn-maximum-town-size < 254)
	(or
		(unit-type-count battering-ram-line > 0)
		(unit-type-count trebuchet-set > 0)
	)
	(up-building-type-in-town c: town-center == 0)
	(up-building-type-in-town c: castle == 0)
	(up-building-type-in-town c: watch-tower == 0)
	(up-building-type-in-town c: bombard-tower == 0)
=>
	(up-modify-sn sn-maximum-town-size c:+ 5)
	(up-modify-sn sn-safe-town-size s:= sn-maximum-town-size)
	(up-chat-data-to-self "TSA = %d" s: sn-maximum-town-size)
	(up-set-defense-priority c: town-center c: 20000)
	(up-set-defense-priority c: castle c: 30000)
	(set-strategic-number sn-disable-defend-groups 1)
)
(defrule
	(strategic-number sn-maximum-town-size >= 40)
	(strategic-number sn-maximum-town-size < 254)
	(or
		(players-building-type-count target-player lumber-camp > 0)
		(players-building-type-count target-player mining-camp > 0)
	)
	(up-building-type-in-town c: mining-camp == 0)
	(up-building-type-in-town c: lumber-camp == 0)
	(unit-type-count battering-ram-line == 0)
	(unit-type-count trebuchet-set == 0)
=>
	(up-modify-sn sn-maximum-town-size c:+ 5)
	(set-strategic-number sn-disable-defend-groups 1)
	(enable-timer reset-town-size-timer 62)
	(up-chat-data-to-self "TSA = %d" s: sn-maximum-town-size)
)
(defrule
	(or
		(or
			(strategic-number sn-current-age >= ci-transit)
			(population >= eighty-five-percent-pop)
		)
		(or
			(or
				(building-type-count town-center == 0)
				(goal forward-threat-goal 1)
			)
			(or
				(strategic-number sn-maximum-town-size < 40)
				(goal unit-goal monk)
			)
		)
	)
=>
	(up-jump-rule 4)
)
(defrule
	(or
		(and
			(up-projectile-detected projectile-watch-tower c:> 2000)
			(up-projectile-detected projectile-watch-tower c:< 30000)
		)
		(or
			(up-building-type-in-town c: watch-tower > 0)
			(up-building-type-in-town c: town-center > 0)
		)
	)
	(or
		(players-building-type-count target-player lumber-camp > 0)
		(players-building-type-count target-player mining-camp > 0)
	)
	(unit-type-count battering-ram-line == 0)
=>
	(set-goal split-rule-goal 29)
	(chat-local-to-self "switch to DUC")
	(up-jump-rule 2)
)
(defrule
	(strategic-number sn-maximum-town-size >= 40)
	(game-time < 1800)
	(or
		(players-building-type-count target-player lumber-camp > 0)
		(players-building-type-count target-player mining-camp > 0)
	)
	(strategic-number sn-military-level < -1)
	(goal unit-goal knight)
	(players-military-population target-player > 20)
=>
	(set-goal split-rule-goal 29)
	(chat-local-to-self "switch to DUC")
	(up-jump-rule 1)
)
(defrule
	(strategic-number sn-maximum-town-size >= 40)
	(goal unit-goal archer)
	(or
		(players-building-type-count target-player lumber-camp > 0)
		(players-building-type-count target-player mining-camp > 0)
	)
	(players-unit-type-count target-player mangonel-line < 1)
	(game-time < 2100)
=>
	(set-goal split-rule-goal 29)
	(chat-local-to-self "switch to DUC")
)
(defrule
	(goal split-rule-goal 29)
	(players-building-type-count target-player lumber-camp == 0)
	(players-building-type-count target-player mining-camp == 0)
=>
	(set-goal split-rule-goal 0)
	(up-send-scout group-type-land-explore position-target)
	(chat-local-to-self "no undefended camp found")
)
(defrule
	(goal split-rule-goal 29)
=>
	(up-modify-sn sn-focus-player-number s:= sn-target-player-number)
	(up-get-target-fact building-type-count mining-camp temp-goal)
	(up-get-target-fact building-type-count lumber-camp temp-goal2)
	(up-modify-goal temp-goal g:+ temp-goal2)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-remote c: mining-camp g: temp-goal)
	(up-find-remote c: lumber-camp g: temp-goal)
	(up-modify-goal temp-goal c:- 1); search offset
	(set-goal temp-goal2 0)
)
(defrule
	(goal split-rule-goal 29)
=>
	(up-set-target-object search-remote g:= temp-goal2)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)	
	(up-filter-distance c: -1 c: 17)
	(up-find-remote c: castle c: 1)
	(up-filter-distance c: -1 c: 11)
	(up-find-remote c: town-center c: 1)
	(up-find-remote c: watch-tower c: 1)
	(up-get-search-state local-total-goal)
	(set-goal split-rule-goal 30)
)
(defrule
	(goal split-rule-goal 30)
	(up-compare-goal remote-last-goal > 0); enemy has defenses near the camp
	(up-compare-goal temp-goal2 g:< temp-goal); not all camps checked yet
=>
	(up-modify-goal temp-goal2 c:+ 1); go to next camp
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-remote c: mining-camp g: temp-goal)
	(up-find-remote c: lumber-camp g: temp-goal)
	(set-goal split-rule-goal 29)
	(up-jump-rule -2)
)
(defrule
	(goal split-rule-goal 30)
	(up-compare-goal temp-goal2 g:== temp-goal); all camps checked
	(up-compare-goal remote-last-goal != 0); no undefended target found
=>
	(set-goal retreat-now-goal 1); cancel the attack
	(enable-timer attack-timer 90)
	(up-send-scout group-type-land-explore position-target)
	(chat-local-to-self "no undefended camp found")
)
(defrule
	(goal split-rule-goal 30)
	(goal remote-last-goal 0); no defenses found
=>
	(set-strategic-number sn-maximum-town-size 15)
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-disable-defend-groups 0)
	(enable-timer attack-timer 180); allow more time for attack
	(up-copy-point target-goal p1x-goal)
	(up-get-point-distance position-self-goal target-goal temp-goal5)
	(up-modify-goal temp-goal5 c:- 5); if it's not that far behind TC use a waypoint
	(set-goal split-rule-goal 31)
	(set-goal p1x-goal -1)
	(set-goal p2x-goal -1)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(chat-local-to-self "undefended camp found")
)
(defrule
	(players-building-type-count focus-player town-center > 0)
	(goal split-rule-goal 31)
	(up-find-remote c: town-center c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal); p1x-goal = TC, p2x-goal = defensive object
	(up-get-point-distance position-self-goal p1x-goal temp-goal6)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(players-building-type-count focus-player castle > 0)
	(goal split-rule-goal 31)
	(up-find-remote c: castle c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p2x-goal)
	(up-get-point-distance position-self-goal p2x-goal temp-goal7)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-jump-rule 2)
)
(defrule
	(players-building-type-count focus-player town-center > 1)
	(goal split-rule-goal 31)
	(up-find-remote c: town-center c: 2)
=>
	(up-set-target-object search-remote c: 1)
	(up-get-point position-object p2x-goal)
	(up-get-point-distance position-self-goal p2x-goal temp-goal7)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-jump-rule 1)
)
(defrule
	(goal split-rule-goal 31)
	(up-find-remote c: watch-tower c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p2x-goal)
	(up-get-point-distance position-self-goal p2x-goal temp-goal7)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(goal split-rule-goal 31)
	(or
		(goal p1x-goal -1); no TC found
		(up-compare-goal temp-goal5 g:< temp-goal6); target closer than their TC
	)
	(or
		(goal p2x-goal -1); no defensive object
		(up-compare-goal temp-goal5 g:< temp-goal7); target closer than their defense
	)
	(goal enemy-sighted-goal 0)
=>	
	(set-goal split-rule-goal 43); easiest case, no defenses => go to target
)
(defrule
	(goal split-rule-goal 31)
	(or
		(goal p1x-goal -1)
		(up-compare-goal temp-goal5 g:< temp-goal6)
	)
	(or
		(goal p2x-goal -1)
		(up-compare-goal temp-goal5 g:< temp-goal7)
	)
	(goal enemy-sighted-goal 1)
=>	
	(set-goal split-rule-goal 44)
)
(defrule
	(goal split-rule-goal 31)
	(up-compare-goal temp-goal5 g:>= temp-goal6); avoid TC
	(or
		(goal p2x-goal -1)
		(up-compare-goal temp-goal5 g:< temp-goal7)
	)
=>
	(up-copy-point p2x-goal p1x-goal); => use WP to move around TC (p1x-goal)
	(up-cross-tiles p1x-goal position-self-goal c: -20); check direction in which we want to avoid
	(up-cross-tiles p2x-goal position-self-goal c: 20)
	(up-get-point-distance p1x-goal target-goal temp-goal3); use shorter path
	(up-get-point-distance p2x-goal target-goal temp-goal4)
	(set-goal split-rule-goal 32)
)
(defrule
	(goal split-rule-goal 32)
	(up-compare-goal temp-goal4 g:< temp-goal3)
	(nor
		(or
			(up-compare-goal p2x-goal g:>= max-coordinate-goal); avoid invalid coordinates
			(up-compare-goal p2y-goal g:>= max-coordinate-y-goal)
		)
		(or
			(or
				(goal p2x-goal -1)
				(goal p2y-goal -1)
			)
			(or
				(up-point-contains p2x-goal c: tree-class)
				(up-point-terrain p2x-goal == terrain-water)
			)
		)
	)
=>
	(up-copy-point waypoint-goal p2x-goal)
	(set-goal split-rule-goal 33)
)
(defrule
	(goal split-rule-goal 32)
=>
	(up-copy-point waypoint-goal p1x-goal)
	(up-get-point-distance waypoint-goal position-center-goal temp-goal4)
	(set-goal split-rule-goal 33)
)
(defrule
	(goal split-rule-goal 33)
	(or
		(or
			(up-compare-goal waypoint-goal g:>= max-coordinate-goal)
			(up-compare-goal waypoint-y-goal g:>= max-coordinate-y-goal)
		)
		(or
			(or
				(goal waypoint-goal -1)
				(goal waypoint-y-goal -1)
			)
			(up-point-contains waypoint-goal c: tree-class)
		)
	)
	(up-compare-goal temp-goal4 > 5)
=>
	(up-lerp-tiles waypoint-goal position-center-goal c: 1)
	(up-get-point-distance waypoint-goal p1x-goal temp-goal4)
	(up-jump-rule -1)
)
(defrule
	(goal split-rule-goal 31); avoid defenses and TC
=>
	(up-copy-point object-point-goal p2x-goal); def
	(up-copy-point temp-goal p1x-goal); TC
	(up-cross-tiles object-point-goal position-self-goal c: -20)
	(up-cross-tiles p2x-goal position-self-goal c: 20)
	(up-get-point-distance object-point-goal target-goal temp-goal3)
	(up-get-point-distance p2x-goal target-goal temp-goal4)
	(set-goal split-rule-goal 32)
)
(defrule
	(goal split-rule-goal 32)
	(up-compare-goal temp-goal4 g:< temp-goal3)
	(nor
		(or
			(up-compare-goal p2x-goal g:>= max-coordinate-goal); avoid invalid coordinates
			(up-compare-goal p2y-goal g:>= max-coordinate-y-goal)
		)
		(or
			(or
				(goal p2x-goal -1)
				(goal p2y-goal -1)
			)
			(up-point-contains p2x-goal c: tree-class)
		)
	)
=>
	(up-copy-point waypoint-goal p2x-goal)
	(set-goal temp-goal8 20)
	(set-goal split-rule-goal 35)
)
(defrule
	(goal split-rule-goal 32)
=>
	(up-copy-point waypoint-goal object-point-goal)
	(set-goal temp-goal8 -20)
	(set-goal split-rule-goal 35)
)
(defrule
	(goal split-rule-goal 35)
=>
	(up-cross-tiles p1x-goal position-self-goal g: temp-goal8); dodge in same direction as the other object
	(up-copy-point waypoint2-goal p1x-goal)
	(up-get-point-distance waypoint2-goal position-center-goal temp-goal8)
)
(defrule
	(goal split-rule-goal 35)
	(or
		(or
			(up-compare-goal waypoint2-goal g:>= max-coordinate-goal); avoid invalid coordinates
			(up-compare-goal waypoint2-y-goal g:>= max-coordinate-y-goal)
		)
		(or
			(or
				(goal waypoint2-goal -1)
				(goal waypoint2-y-goal -1)
			)
			(up-point-contains waypoint2-goal c: tree-class)
		)
	)
	(up-compare-goal temp-goal8 > 5)
=>
	(up-lerp-tiles waypoint2-goal position-center-goal c: 1)
	(up-get-point-distance waypoint2-goal position-center-goal temp-goal8)
	(up-jump-rule -1)
)
(defrule
	(or
		(goal split-rule-goal 33)
		(goal split-rule-goal 35)
	)
	(goal enemy-sighted-goal 0)
=>
	(set-goal split-rule-goal 45)
)
(defrule
	(or
		(goal split-rule-goal 33)
		(goal split-rule-goal 35)
	)
	(goal enemy-sighted-goal 1)
=>
	(set-goal split-rule-goal 46)
)
(defrule
	(goal unit-goal knight)
	(up-compare-goal strategy-goal != flush)
=>
	(set-goal temp-goal8 cavalry-class)
	(set-goal raid-goal 4)
)
(defrule
	(up-compare-goal unit-goal != knight)
=>
	(set-goal temp-goal8 -1)
)
(defrule
	(or
		(goal duc-control-goal move-wp1)
		(goal duc-control-goal pat-wp1)
	)
=>
	(up-set-target-point waypoint-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 8)
	(up-filter-exclude -1 actionid-explore -1 warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-get-search-state local-total-goal)
)
(defrule
	(goal duc-control-goal move-wp1)
	(up-compare-goal local-last-goal > 2)
	(or
		(up-compare-goal local-last-goal > 5)
		(military-population < 6)
	)
	(up-compare-goal waypoint2-goal != -1)
=>
	(set-goal split-rule-goal 47)
)
(defrule
	(goal duc-control-goal pat-wp1)
	(up-compare-goal local-last-goal > 2)
	(or
		(up-compare-goal local-last-goal > 5)
		(military-population < 6)
	)
	(up-compare-goal waypoint2-goal != -1)
=>
	(set-goal split-rule-goal 48)
)
(defrule
	(goal duc-control-goal move-wp1)
	(up-compare-goal local-last-goal > 2)
	(or
		(up-compare-goal local-last-goal > 5)
		(military-population < 6)
	)
	(goal waypoint2-goal -1)
=>
	(set-goal split-rule-goal 43)
)
(defrule
	(goal duc-control-goal pat-wp1)
	(up-compare-goal local-last-goal > 2)
	(or
		(up-compare-goal local-last-goal > 5)
		(military-population < 6)
	)
	(goal waypoint2-goal -1)
=>
	(set-goal split-rule-goal 44)
)
(defrule
	(up-timer-status duc-timer != timer-running)
	(or
		(goal duc-control-goal move-wp1)
		(and
			(goal enemy-sighted-goal 0)
			(goal duc-control-goal pat-wp1)
		)
	)
=>
	(set-goal split-rule-goal 45)
)
(defrule
	(or
		(goal duc-control-goal move-wp1)
		(and
			(up-timer-status duc-timer != timer-running)
			(goal duc-control-goal pat-wp1)
		)
	)
	(goal enemy-sighted-goal 1)
=>
	(set-goal split-rule-goal 46)
)
(defrule
	(or
		(goal duc-control-goal move-wp2)
		(goal duc-control-goal pat-wp2)
	)
=>
	(up-set-target-point waypoint2-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 8)
	(up-filter-exclude -1 actionid-explore -1 warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-get-search-state local-total-goal)
)
(defrule
	(goal duc-control-goal move-wp2)
	(up-compare-goal local-last-goal > 2)
	(or
		(up-compare-goal local-last-goal > 5)
		(military-population < 6)
	)
=>
	(set-goal split-rule-goal 43)
)
(defrule
	(goal duc-control-goal pat-wp2)
	(up-compare-goal local-last-goal > 2)
	(or
		(up-compare-goal local-last-goal > 5)
		(military-population < 6)
	)
=>
	(set-goal split-rule-goal 44)
)
(defrule
	(up-timer-status duc-timer != timer-running)
	(or
		(goal duc-control-goal move-wp2)
		(and
			(goal enemy-sighted-goal 0)
			(goal duc-control-goal pat-wp2)
		)
	)
=>
	(set-goal split-rule-goal 47)
)
(defrule
	(or
		(goal duc-control-goal move-wp2)
		(and
			(up-timer-status duc-timer != timer-running)
			(goal duc-control-goal pat-wp2)
		)
	)
	(goal enemy-sighted-goal 1)
=>
	(set-goal split-rule-goal 48)
)
(defrule
	(up-timer-status duc-timer != timer-running)
	(or
		(goal duc-control-goal move-target)
		(and
			(goal enemy-sighted-goal 0)
			(goal duc-control-goal pat-target)
		)
	)
=>
	(set-goal split-rule-goal 43)
)
(defrule
	(or
		(goal duc-control-goal move-target)
		(and
			(up-timer-status duc-timer != timer-running)
			(goal duc-control-goal pat-target)
		)
	)
	(goal enemy-sighted-goal 1)
=>
	(set-goal split-rule-goal 44)
)

(defrule
	(goal split-rule-goal 43)
=>
	(set-goal duc-control-goal move-target)
	(up-set-target-point target-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 6 -1)
	(up-filter-exclude -1 actionid-explore orderid-relic warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-target-point target-goal action-move -1 aggressive)
	(enable-timer duc-timer 15)
	(chat-to-player chat-player "duc: move to target")
)
(defrule
	(goal split-rule-goal 44)
=>
	(set-goal duc-control-goal pat-target)
	(up-set-target-point target-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 6 -1)
	(up-filter-exclude -1 actionid-explore orderid-relic warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-target-point target-goal action-patrol -1 aggressive)
	(enable-timer duc-timer 60)
	(chat-to-player chat-player "duc: patrol to target")
)
(defrule
	(goal split-rule-goal 45)
=>
	(set-goal duc-control-goal move-wp1)
	(up-set-target-point waypoint-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 6 -1)
	(up-filter-exclude -1 actionid-explore orderid-relic warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-target-point waypoint-goal action-move -1 aggressive)
	(enable-timer duc-timer 15)
	(chat-to-player chat-player "duc: move to waypoint")
)
(defrule
	(goal split-rule-goal 46)
=>
	(set-goal duc-control-goal pat-wp1)
	(up-set-target-point waypoint-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 6 -1)
	(up-filter-exclude -1 actionid-attack orderid-explore warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-target-point waypoint-goal action-patrol -1 aggressive)
	(enable-timer duc-timer 60)
	(chat-to-player chat-player "duc: patrol to waypoint")
)
(defrule 
	(goal split-rule-goal 47)
=>
	(set-goal duc-control-goal pat-wp2)
	(up-set-target-point waypoint2-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 6 -1)
	(up-filter-exclude -1 actionid-attack orderid-explore warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-target-point waypoint2-goal action-move -1 aggressive)
	(enable-timer duc-timer 15)
	(chat-to-player chat-player "duc part 2: move to waypoint 2")
	(set-goal split-rule-goal 0)
)
(defrule 
	(goal split-rule-goal 48)
=>
	(set-goal duc-control-goal pat-wp2)
	(up-set-target-point waypoint2-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 6 -1)
	(up-filter-exclude -1 actionid-attack orderid-explore warship-class)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local g: temp-goal8 c: 240)
	(up-target-point waypoint2-goal action-patrol -1 aggressive)
	(enable-timer duc-timer 60)
	(chat-to-player chat-player "duc part 2: patrol to waypoint 2")
	(set-goal split-rule-goal 0)
)
#end-if ; not black forest
#end-if ; not DM
#end-if ; not MICHI
(defrule
	(game-time < 3600)
	(unit-type-count siege-unit-class > 1)
	(or
		(population > eighty-five-percent-pop)
		(strategic-number sn-military-level > 1)
	)
=>
	(up-set-defense-priority c: mining-camp c: 300); set camps low and go for TC/castles, to do some damage
	(up-set-defense-priority c: lumber-camp c: 300)
)
(defrule
	(up-compare-goal attack-goal >= 2)
	(taunt-detected any-human-ally 36)
=>
	(set-goal attack-goal 0)
)
#load-if-not-defined FEUDAL-AGE-START
#load-if-not-defined FEUDAL-AGE-END
(defrule
	(timer-triggered attack-timer)
	(goal strategy-goal flush)
	(players-current-age every-ally == feudal-age)
	(up-compare-goal attack-goal >= 2)
	(up-compare-goal attack-goal != 6)
=>
	(set-goal attack-goal 4)
)
(defrule
	(up-projectile-detected projectile-castle c:< 10000)
	(up-projectile-detected projectile-castle c:> 3000)
	(up-timer-status micro-timer == timer-running)
	(up-timer-status reset-units-timer == timer-running)
	(timer-triggered attack-timer)
	(up-compare-goal attack-goal >= 2)
=>	
	(set-goal attack-goal 4); restart attack and prevent spamming attack calls to allies
)
#end-if
#end-if
(defrule
	(or
		(or
			(unit-type-count castle-arrow > 0); don't retreat when enemy is fighting us in our town
			(unit-type-count castle-arrow-chemistry > 0)
		)
		(or
			(unit-type-count tc-arrow > 0)
			(or
				(strategic-number sn-disable-defend-groups == 1)
				(and
					(up-compare-goal patrol-goal >= 0)
					(unit-type-count battering-ram-line > 0)
				)
			)
		)
	)
=>
	(up-jump-rule 1)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(up-compare-goal avoid-tc-goal < 1)
	(goal team-coordination-goal 0)
	(or
		(up-projectile-detected projectile-town-center c:< 2000)
		(up-projectile-detected projectile-castle c:< 2000)
	)
	(up-player-distance every-enemy >= 50)
	(up-projectile-target projectile-any != siege-unit-class)
	(up-projectile-target projectile-any != warship-class)
	(up-projectile-target projectile-any != building-class)
	(up-projectile-target projectile-any != villager-class)
=>
	(set-goal retreat-now-goal 1)
	(chat-to-player chat-player "no attack: retreat out of fortification fire")
)
(defrule
	(or
		(and
			(up-projectile-detected projectile-fortification c:< 10000);recently moved out fortification fire
			(up-projectile-detected projectile-fortification c:> 3000)
		)
		(and
			(up-projectile-detected projectile-town-center c:< 10000)
			(up-projectile-detected projectile-town-center c:> 3000)
		)
	)
	(up-compare-goal attack-goal >= 2)
	(goal patrol-goal -1)
	(timer-triggered retreat-timer)
=>
	(disable-timer retreat-timer)
	(chat-to-player chat-player "allow attacks again")
	(set-goal attack-goal 4)
	(up-reset-unit c: -1)
	(up-set-attack-stance -1 c: aggressive)
	(set-goal duc-control-goal -1)
)
(defrule
	(or
		(and
			(up-projectile-detected projectile-watch-tower c:< 30000)
			(up-projectile-detected projectile-watch-tower c:> 3000)
		)
		(and
			(up-projectile-detected projectile-town-center c:< 30000)
			(up-projectile-detected projectile-town-center c:> 3000)
		)
	)
	(goal ally-attacking-goal 1)
	(strategic-number sn-military-level > -2)
	(timer-triggered retreat-timer)
=>
	(disable-timer retreat-timer)
	(set-goal attack-goal 4)
	(up-reset-unit c: -1)
	(up-set-attack-stance -1 c: aggressive)
	(set-goal duc-control-goal -1)
)
(defrule
	(game-time < 1800)
	(or
		(and
			(up-projectile-detected projectile-fortification c:< 10000);recently moved out fortification fire
			(up-projectile-detected projectile-fortification c:> 3000)
		)
		(and
			(up-projectile-detected projectile-town-center c:< 10000);recently moved out fortification fire
			(up-projectile-detected projectile-town-center c:> 3000)
		)
	)
	(up-compare-goal attack-goal < 2)
	(goal patrol-goal -1)
	(timer-triggered retreat-timer)
=>
	(set-goal retreat-now-goal 1)
	(set-goal duc-control-goal -1)
)
#load-if-defined DEATH-MATCH
(defrule
	(up-compare-sn sn-target-player-number g:== closest-enemy-goal)
	(players-building-count every-enemy > 0)
	(game-time < 480)
=>
	(up-get-fact military-population 0 temp-goal)
	(up-get-target-fact military-population 0 temp-goal2)
	(up-modify-goal temp-goal3 g:= temp-goal2)
	(up-modify-goal temp-goal c:* 5)
	(up-modify-goal temp-goal2 c:* 7)
	(up-modify-goal temp-goal3 c:* 6)
)
(defrule
	(game-time < 480)
	(up-compare-sn sn-target-player-number g:== closest-enemy-goal)
	(players-building-count every-enemy > 0)
	(up-compare-goal temp-goal g:>= temp-goal2)
=>
	(set-goal attack-goal 7)
)
(defrule
	(unit-type-count castle-arrow-chemistry == 0)
	(unit-type-count tc-arrow-chemistry == 0)
	(strategic-number sn-number-attack-groups > 0)
	(strategic-number sn-maximum-town-size < 40)
	(up-compare-goal attack-goal < 2)
	(strategic-number sn-military-level < 1)
	(nand
		(game-time < 480)
		(up-compare-goal temp-goal g:>= temp-goal3)
	)
=>
	(set-strategic-number sn-number-attack-groups 0)
	(set-goal retreat-now-goal 1)
)
(defrule
	(strategic-number sn-target-player-number > 0)
	(up-compare-goal attack-goal >= 2)
	(players-building-count target-player > 0)
	(or
		(game-time < 300)
		(starting-age < post-imperial-age)
	)
	(game-time < 600)
=>
	(up-set-attack-stance -1 c: aggressive)
	(set-strategic-number sn-number-attack-groups 199)
	(set-strategic-number sn-minimum-attack-group-size 1)
	(set-strategic-number sn-maximum-attack-group-size 1)
	(set-strategic-number sn-group-form-distance 5)
	(set-goal attack-goal 0)
)
#end-if
(defrule
	(or
		(or
			(up-building-type-in-town c: town-center > 0)
			(up-building-type-in-town c: castle > 0)
		)
		(or
			(up-building-type-in-town c: bombard-tower > 0)
			(up-building-type-in-town c: wonder > 0)
		)
	)
	(up-compare-goal attack-goal > 1)
	(unit-type-count unpacked-trebuchet > 0)
	(timer-triggered attack-timer)
=>
	(set-goal attack-goal 0)
	(enable-timer attack-timer 60)
	(enable-timer reset-town-size-timer 62)
	(up-chat-data-to-self "TSA = %d" s: sn-maximum-town-size)
)
(defrule
	(players-building-count target-player > 0)
	(strategic-number sn-target-player-number > 0)
	(goal attack-goal 2)
	(timer-triggered attack-timer)
	(game-time < 2700)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-modify-sn sn-focus-player-number g:= closest-ally-goal)
	(set-goal split-rule-goal 12)
	(set-goal attack-goal 0)
	(chat-to-player focus-player "31")
	(chat-to-player-using-id focus-player 22116)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
)
(defrule
	(players-building-count target-player > 0)
	(strategic-number sn-target-player-number > 0)
	(goal attack-goal 2)
	(timer-triggered attack-timer)
=>
	(set-goal split-rule-goal 12)
	(set-goal attack-goal 0)
	(chat-to-allies "31")
	(chat-to-allies-using-id 22116)
)
(defrule
	(strategic-number sn-target-player-number > 0)
	(players-building-count target-player > 0)
	(or
		(goal attack-goal 4)
		(goal attack-goal 7)
	)
	(timer-triggered attack-timer)
	(up-compare-goal threat-time-goal > 4000)
=>
	(set-goal split-rule-goal 12)
	(set-goal attack-goal 0)
	(set-strategic-number sn-disable-defend-groups 1)
	(up-set-defense-priority c: watch-tower c: 2000)
	(up-set-defense-priority c: mining-camp c: 1000)
	(up-set-defense-priority c: lumber-camp c: 1000)
)
(defrule
	(strategic-number sn-target-player-number > 0)
	(players-building-count target-player > 0)
	(goal attack-goal 7)
	(timer-triggered attack-timer)
=>
	(set-goal split-rule-goal 12)
	(set-goal attack-goal 0)
)
(defrule
	(goal attack-goal 4)
=>
	(up-get-target-fact player-distance 0 temp-goal)
	(up-modify-goal temp-goal c:max 40)
	(up-modify-sn sn-maximum-town-size g:= temp-goal)
	(up-modify-sn sn-maximum-town-size c:+ 2)
	(set-goal attack-goal 0)
	(enable-timer attack-timer 60)
	(enable-timer reset-town-size-timer 62)
	(up-chat-data-to-self "TSA = %d" s: sn-maximum-town-size)
)
(defrule
	(goal split-rule-goal 12)
=>
	(set-goal split-rule-goal 0)
	(up-get-target-fact player-distance 0 temp-goal)
	(up-modify-goal temp-goal c:max 40)
	(up-modify-sn sn-maximum-town-size g:= temp-goal)
	(up-modify-sn sn-maximum-town-size c:+ 2)
	(enable-timer attack-timer 60)
	(enable-timer reset-town-size-timer 62)
	(up-chat-data-to-self "TSA = %d" s: sn-maximum-town-size)
)
(defrule
	(up-player-distance every-enemy > 40)
	(strategic-number sn-maximum-town-size < 40); no attacks going anymore
	(strategic-number sn-disable-attack-groups == 1)
=>
	(set-strategic-number sn-disable-attack-groups 0); so reset attacking SN
)
(defrule
	(or
		(timer-triggered attack-timer); end attack
		(strategic-number sn-maximum-town-size >= 40); started a TSA instead
	)
	(up-compare-goal duc-control-goal > 0)
=>
	(up-reset-unit c: -1); reset any moving grouped units
	(set-goal duc-control-goal 0)
	(set-goal waypoint-goal -1)
	(set-goal waypoint-y-goal -1)
	(set-goal waypoint2-goal -1)
	(set-goal waypoint2-y-goal -1)
	(set-goal target-goal -1)
	(set-goal target-y-goal -1)
	(chat-to-player chat-player "duc ended")
)
#load-if-not-defined UP-2-PLAYER-GAME
(defrule
	(strategic-number sn-number-boat-attack-groups > 0)
	(players-building-count any-enemy > 0)
=>
	(up-modify-sn sn-focus-player-number s:= sn-target-player-number)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(not (up-find-remote c: dock c: 1))
	(strategic-number sn-number-boat-attack-groups > 0)
	(players-building-count any-enemy > 0)
=>
	(up-find-player enemy find-closest temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(not (up-find-remote c: dock c: 1))
	(strategic-number sn-number-boat-attack-groups > 0)
	(players-building-count any-enemy > 0)
=>
	(up-find-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(not (up-find-remote c: dock c: 1))
	(strategic-number sn-number-boat-attack-groups > 0)
	(players-building-count any-enemy > 0)
=>
	(up-find-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(not (up-find-remote c: dock c: 1))
	(strategic-number sn-number-boat-attack-groups > 0)
	(players-building-count any-enemy > 0)
=>
	(up-find-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal)
	(up-find-next-player enemy find-closest temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(strategic-number sn-number-boat-attack-groups > 0)
	(up-find-remote c: dock c: 1)
=>
	(up-modify-sn sn-target-player-number s:= sn-focus-player-number)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(strategic-number sn-target-player-number > 0)
=>
	(set-strategic-number sn-safe-town-size safe-town)
)
(defrule
	(strategic-number sn-safe-town-size < 75)
	(population >= max-civ-pop)
	(or
		(death-match-game)
		(game-time > 3000)
	)
=>
	(set-strategic-number sn-safe-town-size 75)
)
#end-if
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined DIFFICULTY-MODERATE; monk micro is from barbie
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(unit-type-count monastery-class > 4)
	(strategic-number sn-maximum-town-size >= 40)
=>
	(up-get-point position-target p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: 15 c: -1)	
	(up-modify-sn sn-focus-player-number s:= sn-target-player-number)
	(up-get-point-distance p1x-goal position-self-goal temp-goal)
	(set-goal split-rule-goal 34)
)
(defrule
	(goal split-rule-goal 34)
	(or
		(up-find-remote c: castle c: 1)
		(and
			(up-find-remote c: town-center c: 1)
			(not (up-find-local c: siege-unit-class c: 1))
		)
	)
	(up-compare-goal temp-goal > 20)
=>
	(up-lerp-tiles p1x-goal position-self-goal c: 3)
	(up-get-point-distance p1x-goal position-self-goal temp-goal)
	(up-reset-search 1 1 1 1)
	(up-jump-rule -1)
)
(defrule
	(goal split-rule-goal 34)
	(up-timer-status monk-micro-timer != timer-running)
	(unit-type-count unpacked-trebuchet == 0)
=>
	(up-filter-distance c: 9 c: -1)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-filter-exclude -1 actionid-convert orderid-relic -1)
	(up-find-local c: monastery-class c: 240)
	(up-target-point p1x-goal action-patrol -1 -1)
	(enable-timer monk-micro-timer 43)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(unit-type-count monastery-class > 4)
	(up-timer-status monk-micro-timer == timer-running)
	(up-compare-goal threat-time-goal < 3000)
	(goal threat-target-goal monastery-class)
	(up-timer-status micro-timer != timer-running)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-monk -1 -1 -1)
	(up-filter-exclude -1 actionid-convert orderid-relic -1)
	(up-find-local c: monastery-class c: 240)
	(up-target-point 0 action-stop -1 -1)		
	(enable-timer micro-timer 10)
)
(defrule
	(or
		(or
			(or
				(game-time < 720)
				(up-timer-status gather-units-timer == timer-running)
			)
			(or
				(or
					(strategic-number sn-maximum-town-size >= 40); don't use hostilities goal as newly started attacks won't have triggered it yet
					(up-compare-goal threat-time-goal < 10000)
				)
				(and
					(goal unit-goal scorpion); takes too much space and blocks gathering
					(game-time > 1800)
				)
			)
		)
		(or
			(goal forward-threat-goal 1)
			(goal hostilities-goal 1)
		)
	)
=>
	(up-jump-rule 4)
)
(defrule
	(true)
=>
	(up-copy-point p1x-goal position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(up-find-local c: castle c: 1)
=>
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-jump-rule 1)
)
(defrule
	(up-find-local c: watch-tower c: 1)
=>
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
)
(defrule
	(true)
=>
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-filter-exclude -1 actionid-explore orderid-relic warship-class)
	(up-filter-range -1 -1 6 -1)
	(up-find-local c: -1 c: 200)
	(up-target-point 0 action-move -1 aggressive)
	(enable-timer gather-units-timer 20)
)
#end-if
#end-if
#end-if
(defrule
	(timer-triggered reset-town-size-timer)
	(population >= 140)
=>
	(disable-timer reset-town-size-timer)
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-disable-attack-groups 0)
	(set-strategic-number sn-maximum-town-size 24)
	(set-strategic-number sn-safe-town-size safe-town)
)
(defrule
	(timer-triggered reset-town-size-timer)
	(population >= 90)
=>
	(disable-timer reset-town-size-timer)
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-disable-attack-groups 0)
	(set-strategic-number sn-maximum-town-size 20)
	(set-strategic-number sn-safe-town-size safe-town)
)
(defrule
	(timer-triggered reset-town-size-timer)
	(population >= 60)
=>
	(disable-timer reset-town-size-timer)
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-disable-attack-groups 0)
	(set-strategic-number sn-maximum-town-size 16)
	(set-strategic-number sn-safe-town-size safe-town)
)
(defrule
	(timer-triggered reset-town-size-timer)
=>
	(disable-timer reset-town-size-timer)
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-disable-attack-groups 0)
	(set-strategic-number sn-maximum-town-size 10)
	(set-strategic-number sn-safe-town-size safe-town)
)
(defrule
	(or
		(taunt-detected any-ally 222); if we could help, this taunt would have been acknowledeged by now
		(taunt-detected any-ally 31)
	)
=>	
	(acknowledge-taunt this-any-ally 222)
	(acknowledge-taunt this-any-ally 31)
	(chat-to-allies-using-id 22118)
	(chat-to-allies "Answering an allied request for help failed for unknown reason!")
)
#load-if-not-defined DEATH-MATCH
(defrule
	(up-compare-goal forward-threat-goal > 0)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 30)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-remote c: -1 c: 30)
	(up-get-search-state local-total-goal)
	(up-get-fact military-population 0 temp-goal3)
	(up-modify-goal temp-goal7 g:= remote-total-goal)
	(up-modify-goal temp-goal7 g:- temp-goal3)
)
(defrule
	(game-time < 1320)
	(players-building-type-count focus-player watch-tower > 0)
	(players-building-type-count focus-player palisade-wall == 0)
	(players-building-type-count focus-player stone-wall == 0)
	(players-building-type-count focus-player gate == 0)
	(goal forward-threat-goal 2)
	(up-compare-goal temp-goal7 < 10)
=>
	(up-get-focus-fact player-distance 0 temp-goal2)
	(up-modify-sn sn-maximum-town-size g:= temp-goal2)
	(up-modify-sn sn-maximum-town-size c:+ 5)
	(set-strategic-number sn-number-civilian-militia 10); send villagers to kill it
	(set-strategic-number sn-allow-civilian-offense 2)
	(set-strategic-number sn-allow-civilian-defense 3)
	(up-set-defense-priority c: watch-tower c: 10000)
	(chat-to-player chat-player "villager vs maybe forwarded tower")
)
(defrule
	(up-timer-status misc-micro-timer != timer-running)
	(game-time < 1320)
	(players-building-type-count focus-player watch-tower < 3)
	(players-building-type-count focus-player watch-tower > 0)
	(up-projectile-detected projectile-watch-tower c:< 10000)
	(players-building-type-count focus-player palisade-wall == 0)
	(players-building-type-count focus-player stone-wall == 0)
	(players-building-type-count focus-player gate == 0)
	(goal forward-threat-goal 1)
	(up-compare-goal temp-goal7 < 5)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 30)
	(set-goal split-rule-goal 54)
)
(defrule
	(goal split-rule-goal 54)
	(up-find-remote c: watch-tower c: 1)
=>
	(up-find-local c: villager-class c: 8)
	(up-target-objects 1 action-default -1 -1)
	(enable-timer misc-micro-timer 30)
	(chat-to-player chat-player "villager vs forward tower")
)
(defrule
	(strategic-number sn-number-civilian-militia != 0)
	(up-compare-goal forward-threat-goal > 0)
	(or 
		(or
			(up-compare-goal temp-goal7 > 5)
			(or
				(players-building-type-count focus-player palisade-wall > 0)
				(players-building-type-count focus-player watch-tower > 2)
			)
		)
		(or
			(players-building-type-count focus-player stone-wall > 0)
			(players-building-type-count focus-player gate > 0)
		)
	)
=>
	(set-strategic-number sn-allow-civilian-offense 0)
	(set-strategic-number sn-number-civilian-militia 0)
)
(defrule
	(strategic-number sn-number-civilian-militia != 0)
	(goal forward-threat-goal -1)
=>
	(set-strategic-number sn-allow-civilian-offense 0)
	(set-strategic-number sn-number-civilian-militia 0)
)
(defrule
	(timer-triggered five-seconds-timer)
	(goal forward-threat-goal 1)
	(or
		(goal threat-source-goal tower-class)
		(goal threat-source-goal 0); nobody is attacking us, just prevent walking in
	)
	(unit-type-count battering-ram-line == 0)
	(strategic-number sn-number-civilian-militia == 0)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-set-target-point position-self-goal)
	(up-filter-range -1 -1 -1 24)
	(up-find-local c: ranged-unit-class c: 200)
	(up-find-local c: cavalry-archer-class c: 200)
	(up-target-point position-self-goal action-move -1 no-attack)
	(enable-timer reset-units-timer 1)
)
(defrule
	(goal forward-threat-goal 1)
	(up-player-distance focus-player < 30)
	(strategic-number sn-military-level >= -2)
	(goal increase-town-size-goal 0)
	(unit-type-count battering-ram-line > 0)
=>
	(up-get-focus-fact player-distance 0 temp-goal2)
	(up-modify-sn sn-maximum-town-size g:= temp-goal2)
	(up-modify-sn sn-maximum-town-size c:+ 2)
)
(defrule
	(game-time < 1800)
	(up-projectile-detected projectile-fortification c:< 5000)
	(goal forward-threat-goal 1)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(unit-type-count battering-ram == 0)
	(goal team-coordination-goal 0)
	(strategic-number sn-allow-civilian-offense == 0)
	(strategic-number sn-allow-civilian-defense == 0)
	(goal increase-town-size-goal 0); otherwise we can't build anything and lose as well.
=>
	(set-strategic-number sn-number-attack-groups 0)
	(set-strategic-number sn-maximum-town-size 7)
	(set-strategic-number sn-task-ungrouped-soldiers 0)
	(set-strategic-number sn-disable-defend-groups 1)
	(set-strategic-number sn-enemy-sighted-response-distance 0)
	(chat-to-player chat-player "forwarded - maximum TS = 7")
)
#end-if
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands); don't mess transports on islands
	(goal team-coordination-goal 1)
	(goal unit-goal archer)
	(unit-type-count battering-ram-line > 0)
	(strategic-number sn-cavalry-threat > 0)
	(unit-type-count spearman-line < 7)
	(up-timer-status garrison-timer != timer-running)
=>
	(enable-timer garrison-timer 15)
	(set-strategic-number sn-number-garrison-units 1)
	(up-garrison battering-ram c: ranged-unit-class)
	(up-set-offense-priority c: 109 c: 5)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(goal team-coordination-goal 1)
	(unit-type-count battering-ram-line > 0)
	(up-timer-status garrison-timer != timer-running)
=>
	(enable-timer garrison-timer 15)
	(set-strategic-number sn-number-garrison-units 1)
	(up-garrison battering-ram c: infantry-class)
	(up-set-offense-priority c: 109 c: 5)
)
#load-if-not-defined VICTORY-CONQUEST
#load-if-not-defined VICTORY-TIME-LIMIT
#load-if-not-defined VICTORY-SCORE
(defrule
	(enemy-captured-relics)
=>
	(up-get-victory-data winning-player-goal victory-type-goal victory-time-goal)
	(up-modify-sn sn-target-player-number g:= winning-player-goal)
	(up-set-defense-priority c: monastery c: 3000)
	(set-strategic-number sn-special-attack-type2 monastery)
	(set-strategic-number sn-safe-town-size 45)
)
(defrule
	(players-building-type-count any-enemy wonder > 0)
=>
	(up-get-player-fact this-any-enemy player-number 0 temp-goal)
	(up-modify-sn sn-target-player-number g:= temp-goal)
	(set-strategic-number sn-special-attack-type2 wonder)
)
(defrule
	(enemy-captured-relics)
	(players-building-type-count target-player monastery > 0)
	(up-building-type-in-town c: monastery == 0)
	(nand
		(up-building-type-in-town c: castle > 0)
		(up-compare-goal victory-time-goal > 700)
	)
	(strategic-number sn-maximum-town-size >= 40)
=>
	(up-modify-sn sn-maximum-town-size c:+ 20)
	(up-chat-data-to-self "target monastery: TS = %d" s: sn-maximum-town-size)
)
(defrule
	(players-building-type-count target-player wonder > 0)
	(up-building-type-in-town c: wonder == 0)
	(nand
		(up-building-type-in-town c: castle > 0)
		(up-compare-goal victory-time-goal > 700)
	)
	(strategic-number sn-maximum-town-size >= 40)
=>
	(up-modify-sn sn-maximum-town-size c:+ 20)
	(up-chat-data-to-self "target wonder: TS = %d" s: sn-maximum-town-size)
)
#end-if
#end-if
#end-if

; NAVY MANAGEMENT
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
=>
	(up-jump-rule 13)
)
(defrule
	(warboat-count > 4)
	(or
		(game-time < 1260)
		(warboat-count > 7)
	)
	(or
		(game-time < 1620)
		(warboat-count > 15)
	)
	(or
		(game-time < 2000)
		(warboat-count > 21)
	)
	(or
		(or
			(game-time < 2400)
			(strategic-number sn-minimum-water-body-size-for-dock == water-mixed)
		)
		(warboat-count > 30)
	)
=>
	(set-goal attack-goal 6)
)
(defrule
	(up-timer-status navy-attack-timer != timer-running)
	(warboat-count > 4)
	(strategic-number sn-military-level >= -1)
	(up-allied-goal any-ally team-coordination-goal == 1)
	(up-compare-goal attack-goal != 6)
=>
	(up-get-player-fact this-any-ally allied-sn sn-target-player-number temp-goal2)
	(up-modify-sn sn-target-player-number g:= temp-goal2)
	(set-goal attack-goal 6)
	(up-chat-data-to-player every-ally "Join an ally's attack on player %d with boats." s: sn-target-player-number)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock == water-islands)
	(goal attack-goal 6)
	(timer-triggered attack-timer)
	(players-unit-type-count target-player galley-line < 15)
	(players-unit-type-count target-player fire-ship-line < 15)
	(unit-type-count transport-ship > 0)
	(soldier-count >= 20)
=>
	(set-strategic-number sn-percent-attack-soldiers 100)
	(attack-now)
	(enable-timer attack-timer 300)
	(set-strategic-number sn-special-attack-type2 -1)
)
(defrule
	(current-age >= feudal-age)
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(or
		(and
			(goal strategy-goal flush)
			(game-time > 1200)
		)
		(current-age == castle-age)
	)
=>
	(set-strategic-number sn-dock-training-filter 2)
	(disable-self)
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(building-type-count dock > 0)
	(military-population > 1)
	(unit-type-count warship-class < 24)	
	(strategic-number sn-number-boat-attack-groups == 0)
	(players-building-count any-enemy > 0)
=>
	(up-gather-inside c: dock c: 1)
)
(defrule
	(goal attack-goal 6)
	(up-timer-status navy-attack-timer != timer-running)
	(players-building-count any-enemy > 0)
=>
	(set-strategic-number sn-minimum-boat-attack-group-size 1)
	(set-strategic-number sn-maximum-boat-attack-group-size 1)
	(set-strategic-number sn-number-boat-attack-groups 200)
	(enable-timer navy-attack-timer 10)
)
(defrule
	(building-type-count dock > 0)
	(or
		(goal threat-target-goal 921); civilian boat class
		(goal threat-target-goal warship-class)
	)
=>
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 40)
	(up-find-remote c: warship-class c: 40)
	(up-get-search-state local-total-goal)
	(up-get-fact unit-type-count warship-class temp-goal2)
	(up-modify-goal temp-goal2 g:- remote-total-goal)
	(set-goal split-rule-goal 21)
)
(defrule
	(or
		(and
			(goal split-rule-goal 21)
			(up-compare-goal temp-goal2 >= -4)
		)
		(or
			(strategic-number sn-number-boat-attack-groups > 0)
			(and
				(unit-type-count warship-class >= 20)
				(goal threat-target-goal warship-class)
			)
		)
	)
=>
	(up-gather-inside c: dock c: 0)
)
(defrule
	(strategic-number sn-number-boat-attack-groups > 0)
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(up-compare-goal attack-goal != 6)
=>
	(set-strategic-number sn-minimum-boat-attack-group-size 0)
	(set-strategic-number sn-maximum-boat-attack-group-size 0)
	(set-strategic-number sn-number-boat-attack-groups 0)
)
(defrule
	(up-projectile-detected projectile-bombard-tower c:< 2000)
	(up-projectile-target projectile-castle == warship-class)
=>
	(up-get-projectile-player projectile-bombard-tower threat-player-goal)
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include -1 actionid-attack -1 -1)
	(set-goal temp-goal8 bombard-tower)
)
(defrule
	(up-projectile-detected projectile-castle c:< 2000)
	(up-projectile-target projectile-castle == warship-class)
=>
	(up-get-projectile-player projectile-castle threat-player-goal)
	(up-modify-sn sn-focus-player-number g:= threat-player-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include -1 actionid-attack -1 -1)
	(set-goal temp-goal8 castle)
)
(defrule
	(up-projectile-detected projectile-fortification c:< 2000)
	(up-projectile-target projectile-fortification == warship-class)
	(stance-toward focus-player enemy)
	(up-find-remote g: temp-goal8 c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 16)
	(up-find-local c: warship-class c: 200)
	(up-target-point position-self-goal action-move -1 no-attack)
	(chat-to-player chat-player "DUC to move ships out of fortification fire")
)
(defrule
	(or
		(and
			(up-projectile-detected projectile-bombard-tower c:< 5000)
			(up-projectile-detected projectile-bombard-tower c:> 3000)	
		)
		(and
			(up-projectile-detected projectile-castle c:< 5000)
			(up-projectile-detected projectile-castle c:> 3000)
		)
	)
	(up-projectile-target projectile-fortification == warship-class)
=>
	(up-reset-unit c: warship-class)
	(chat-to-player chat-player "reset warships")
)
(defrule
	(or
		(strategic-number sn-maximum-town-size >= 40)
		(or
			(and
				(timer-triggered under-attack-timer)
				(goal under-attack-goal 3)
			)
			(and
				(not (town-under-attack))
				(goal under-attack-goal 2)
			)
		)
	)
=>
	(disable-timer under-attack-timer)
	(set-goal under-attack-goal 0)
)

; ********** INCREASE TS CODE **********

#load-if-not-defined UP-MICHI-STYLE
#load-if-defined DEATH-MATCH
(defrule
	(up-compare-goal increase-town-size-goal != 0)
	(or
		(and
			(current-age == castle-age)
			(current-age-time < 30)
		)
		(or
			(current-age < castle-age)
			(starting-age == post-imperial-age)
		)
	)
=>
	(set-goal increase-town-size-goal 0); don't increase TS too early in DM, as there might be more building rules than available villagers!
)
#end-if
#end-if
(defrule
	(population >= del-civ-pop)
	(strategic-number sn-maximum-town-size >= 40)
	(players-population every-enemy < fourty-percent-pop)
=>
	(set-strategic-number sn-safe-town-size 255)
	(set-strategic-number sn-disable-defend-groups 0); clear the map!
)
(defrule
	(up-pending-placement c: farm)
	(strategic-number sn-maximum-town-size < 31)
	(building-type-count farm > 8)
	(up-compare-goal threat-time-goal > 30000)
=>
	(set-strategic-number sn-maximum-town-size 31)
	(enable-timer reset-town-size-timer 1)
)
(defrule
	(up-timer-status disable-defend-groups-timer != timer-running)	
=>
	(set-strategic-number sn-disable-defend-groups 0)
	(up-set-attack-stance -1 c: aggressive)
)
(defrule
	(or
		(goal increase-town-size-goal watch-tower)
		(and
			(and
				(starting-age == post-imperial-age)
				(death-match-game)
			)
			(game-time < 300)
		)
	)
=>
	(up-jump-rule 4)
)
(defrule
	(strategic-number sn-maximum-town-size < 37)
	(up-compare-goal housing-goal > 0)
	(up-pending-placement c: house)
	(wood-amount >= 30)
=>
	(up-modify-sn sn-maximum-town-size c:+ 2)
	(up-chat-data-to-player my-player-number "build town-size = %d" s: sn-maximum-town-size)
)
(defrule
	(or
		(and
			(strategic-number sn-maximum-town-size < 40)
			(goal increase-town-size-goal 2)
		)
		(goal increase-town-size-goal house)
	)
=>
	(set-goal increase-town-size-goal 0); building already placed, no need to increase TS
	(disable-timer increase-town-size-timer)
)
#load-if-defined UP-MICHI-STYLE
(defrule
	(timer-triggered increase-town-size-timer)
	(up-compare-goal increase-town-size-goal != 0)
=>
	(enable-timer increase-town-size-timer 3)
	(set-goal increase-town-size-goal 0)
	(up-modify-sn sn-maximum-town-size c:+ 2)
	(up-chat-data-to-self "build town-size = %d" s: sn-maximum-town-size)
	(up-jump-rule 1)
)
#end-if
(defrule
	(timer-triggered increase-town-size-timer)
	(or
		(strategic-number sn-maximum-town-size < 37)
		(strategic-number sn-minimum-water-body-size-for-dock >= water-islands)
	)
	(strategic-number sn-maximum-town-size < 57)
	(up-compare-goal increase-town-size-goal != 0)
	(up-compare-goal increase-town-size-goal != 2)
=>
	(enable-timer increase-town-size-timer 3)
	(set-goal increase-town-size-goal 0)
	(up-modify-sn sn-maximum-town-size c:+ 2)
	(up-chat-data-to-self "build town-size = %d" s: sn-maximum-town-size)
)
(defrule
	(population < eighty-five-percent-pop)
	(strategic-number sn-maximum-town-size > 30)
	(strategic-number sn-maximum-town-size < 40)
	(timer-triggered one-minute-timer)
=>
	(set-strategic-number sn-maximum-town-size 16)
)
(defrule
	(game-time < 20)
	(building-type-count-total town-center == 0)
=>
	(enable-timer civ-explore-timer 150)
	(disable-self)
)
(defrule
	(building-type-count-total town-center == 0)
=>
	(set-strategic-number sn-camp-max-distance 255)
	(set-strategic-number sn-maximum-town-size 255); entire map viable to place TC
	(set-strategic-number sn-mill-max-distance 255)
	(set-strategic-number sn-maximum-food-drop-distance 30)
	(set-strategic-number sn-wood-gatherer-percentage 100)
	(set-strategic-number sn-food-gatherer-percentage 0)
	(set-strategic-number sn-gold-gatherer-percentage 0)
	(set-escrow-percentage wood 100)
	(set-goal escrow-purpose-goal town-center)
)
(defrule
	(strategic-number sn-minimum-civilian-explorers > 0)
	(or
		(and
			(or
				(unit-type-count sheep > 1)
				(building-type-count-total mill > 0)
			)
			(goal civ-explore-goal food)
		)
		(timer-triggered civ-explore-timer)
	)
=>
	(disable-timer civ-explore-timer)
	(set-strategic-number sn-minimum-civilian-explorers 0)
	(set-strategic-number sn-percent-civilian-explorers 0)
	(chat-local-to-self "disable civ exploration")
	(set-goal civ-explore-goal -1)
)
(defrule
	(current-age == dark-age)
	(up-timer-status civ-explore-timer != timer-running)
	(wood-amount > 130)
	(food-amount < 50)
	(building-type-count mill == 0)
	(building-type-count lumber-camp > 0)
	(sheep-and-forage-too-far)
	(unit-type-count villager-hunter == 0)
	(unit-type-count scout-unit == 0)
=>
	(up-reset-placement c: mill)
	(set-strategic-number sn-mill-max-distance 20)
	(set-goal civ-explore-goal food)
	(set-goal split-rule-goal 53)
	(set-goal temp-goal 5)
	(set-strategic-number sn-minimum-civilian-explorers 5)
	(set-strategic-number sn-percent-civilian-explorers 100)
)
(defrule
	(game-time > 60)
	(game-time < 120)
	(building-type-count town-center >= 1)
	(building-type-count house >= 1)
	(sheep-and-forage-too-far)
=>
	(set-strategic-number sn-minimum-civilian-explorers 4)
	(set-strategic-number sn-maximum-wood-drop-distance 0)
	(set-goal split-rule-goal 53)
	(set-goal temp-goal 4)
	(disable-self)
)
(defrule
	(game-time < 1800)
	(up-timer-status civ-explore-timer != timer-running)
	(unit-type-count-total villager > 23)
	(military-population == 0)
	(or
		(dropsite-min-distance gold == -1)
		(dropsite-min-distance stone == -1)
	)
	(strategic-number sn-minimum-civilian-explorers == 0)	
=>
	(set-strategic-number sn-minimum-civilian-explorers 1)
	(set-goal split-rule-goal 53)
	(set-goal temp-goal 1)
)
(defrule
	(goal split-rule-goal 53)
=>
	(enable-timer civ-explore-timer 60)
	(up-find-local c: male-lumberjack g: temp-goal)
	(up-get-search-state local-total-goal)
	(up-modify-goal temp-goal g:- local-last-goal)
	(up-find-local c: female-lumberjack g: temp-goal)
	(up-modify-sn sn-focus-player-number g:= my-player-number-goal)
	(up-find-remote c: town-center c: 1)
	(up-target-objects 0 action-garrison -1 -1)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(chat-local-to-self "civ exploration required")
)
(defrule
	(game-time > 600)
	(building-type-count-total town-center > 0)
	(building-type-count town-center == 0)
	(building-type-count farm > 0)
	(population < 20)
=>
	(delete-building farm); delete all farm, prevent losing villagers by walking back into old town
)
(defrule
	(building-type-count lumber-camp > 0)
	(building-type-count-total town-center < 1)
	(game-time >= 20)
	(game-time < 600)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: lumber-camp c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p2x-goal)
	(up-lerp-tiles p2x-goal position-center-goal c: 5)
	(up-set-target-point p2x-goal)
	(up-find-local c: sheep c: 240)
	(up-target-point p2x-goal action-move -1 no-attack)
	(disable-self)
)
(defrule
	(game-time > 900)
	(idle-farm-count > 0)
	(up-pending-objects c: mill == 0)
=>
	(up-delete-distant-farms c: 15)
)
(defrule
	(game-time > 15)
	(resource-found wood)
	(building-type-count-total town-center == 0)
	(building-type-count-total lumber-camp == 0)
	(can-build lumber-camp)
=>
	(build lumber-camp)
	(up-assign-builders c: lumber-camp c: 6)
	(set-strategic-number sn-maximum-wood-drop-distance -1)
	(set-strategic-number sn-minimum-civilian-explorers 0)
	(set-strategic-number sn-percent-civilian-gatherers 0)
	(set-strategic-number sn-percent-civilian-builders 100)
	(set-strategic-number sn-percent-civilian-explorers 0)
	(set-strategic-number sn-allow-adjacent-dropsites 1)
)
(defrule
	(wood-amount >= 275)
	(game-time < 10)
	(up-gaia-type-count c: forage-bush >= 4)
	(building-type-count-total town-center == 0)
	(strategic-number sn-mill-max-distance == 255)
=>
	(set-strategic-number sn-town-center-placement mill)
	(disable-self)
)
(defrule
	(building-type-count-total lumber-camp > 0)
	(building-type-count-total town-center < 1)
	(can-build-with-escrow town-center)
	(game-time >= 20)
	(game-time < 600)
=>
	(release-escrow wood)
	(set-escrow-percentage wood 0)
	(up-set-placement-data my-player-number villager-class c: 1)
	(up-build place-control 0 c: town-center)
	(up-assign-builders c: lumber-camp c: 1)
	(set-strategic-number sn-placement-zone-size 30)
)
(defrule
	(game-time > 30)
	(building-type-count-total lumber-camp == 0)
	(can-build-with-escrow town-center)
	(building-type-count town-center == 0)
=>
	(up-assign-builders c: lumber-camp c: 1)
	(release-escrow wood)
	(set-escrow-percentage wood 0)
	(build town-center)
	(set-strategic-number sn-maximum-hunt-drop-distance 15)
)
(defrule
	(building-type-count-total town-center == 1)
	(up-pending-objects c: town-center == 1)
=>
	(release-escrow wood)
	(set-escrow-percentage wood 0)
	(set-goal escrow-purpose-goal 0)
	(set-strategic-number sn-mill-max-distance 31)
	(set-strategic-number sn-maximum-town-size 30)
	(set-strategic-number sn-camp-max-distance 35)
	(set-strategic-number sn-maximum-food-drop-distance 20)
	(set-strategic-number sn-maximum-hunt-drop-distance 20)
	(set-strategic-number sn-percent-civilian-explorers 0)
	(set-strategic-number sn-maximum-wood-drop-distance 25)
	(set-strategic-number sn-minimum-civilian-explorers 0)
)
(defrule
	(timer-triggered reset-units-timer)
=>
	(disable-timer reset-units-timer)
	(up-reset-unit c: ranged-unit-class)
	(up-reset-unit c: cavalry-archer-class)
	(up-reset-unit c: infantry-class)
	(up-reset-unit c: cavalry-class)
	(up-reset-unit c: gunpowder-class)
	(up-reset-unit c: monastery-class)
	(up-reset-unit c: scorpion-class)
	(up-reset-unit c: mounted-gun-class)
	(up-reset-unit c: 947)
	(up-set-attack-stance -1 c: aggressive)
	(chat-to-player chat-player "reset units timer triggered: reset")
)
#load-if-not-defined UP-SCENARIO-GAME ;don't ruin scenarios
(defrule
	(strategic-number sn-military-level >= 0)
	(population >= eighty-five-percent-pop)
	(building-type-count wall-class > 0)
	(current-age == imperial-age)
	(building-type-count-total wonder == 0)
=>
	(up-delete-objects c: 927 c: 32767); In late game walls blocking the AI are worse than the protection they offer
	(up-delete-objects c: 939 c: 32767); so we delete them all.
)
#end-if
#load-if-not-defined DEATH-MATCH
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined DIFFICULTY-MODERATE
(defrule
	(or
		(game-time > 1080)
		(or
			(unit-type-count scout-unit == 0)
			(or
				(strategic-number sn-number-explore-groups == 0)
				(current-age >= castle-age)
			)
		)
	)
	(or 
		(goal avoid-tc-goal -1)
		(game-time > 1100)
	)
=>
	(up-jump-rule 10)
)
(defrule
	(true)
=>
	(set-strategic-number sn-focus-player-number 0)
	(set-goal temp-goal2 11)
)
(defrule
	(up-compare-goal avoid-tc-goal >= 1)
=>
	(up-modify-goal temp-goal2 c:+ 3)
)
(defrule
	(strategic-number sn-focus-player-number < 8)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: scout-unit c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-filter-distance c: -1 g: temp-goal2)
	(up-get-point-distance p1x-goal position-center-goal temp-goal)
)
(defrule
	(stance-toward focus-player enemy)
	(or	
		(up-find-remote c: town-center c: 1)
		(up-find-remote c: castle c: 1)
	)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-point position-object p2x-goal)
	(up-get-point-distance p1x-goal p2x-goal temp-goal)
	(set-strategic-number sn-number-explore-groups 0)
	(set-strategic-number sn-total-number-explorers 0)
	(up-reset-scouts)
	(up-modify-goal avoid-tc-goal s:= sn-focus-player-number)
	(up-jump-rule 2)
)
(defrule
	(goal avoid-tc-goal -1)
	(player-valid focus-player)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-jump-rule -3)
)
(defrule
	(or
		(nor
			(up-find-remote c: town-center c: 1)
			(up-find-remote c: castle c: 1)
		)
		(unit-type-count scout-unit == 0)
	)
	(up-compare-goal avoid-tc-goal >= 1)
=>
	(set-goal avoid-tc-goal -1)
	(up-set-attack-stance scout-unit c: aggressive)
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
)
(defrule
	(up-compare-goal avoid-tc-goal >= 1)
=>
	(up-lerp-tiles p1x-goal p2x-goal c: -2)
	(up-cross-tiles p1x-goal p2x-goal c: -5)
	(set-goal kill-villager-goal -1)
)
(defrule
	(up-compare-goal avoid-tc-goal >= 1)
	(or
		(or
			(up-compare-goal p1x-goal g:>= max-coordinate-goal); avoid invalid coordinates
			(up-compare-goal p1y-goal g:>= max-coordinate-y-goal)
		)
		(or
			(goal p1x-goal -1)
			(goal p1y-goal -1)
		)
	)
	(up-compare-goal temp-goal > 5); don't loop infinite
=>
	(up-lerp-tiles p1x-goal position-center-goal c: 1); move to center to find viable square
	(up-get-point-distance p1x-goal position-center-goal temp-goal)
	(up-jump-rule -1)
)
(defrule
	(up-compare-goal avoid-tc-goal >= 1)
	(or
		(or
			(up-point-contains object-point-goal c: gold-mine); avoid occupied tiles
			(up-point-contains object-point-goal c: stone-mine)
		)
		(or
			(or
				(up-point-contains p1x-goal c: forage-bush)
				(up-point-contains p1x-goal c: tree-class)
			)
			(up-point-contains p1x-goal c: building-class)
		)
	)
	(up-compare-goal temp-goal > 5); don't loop infinite
=>
	(up-lerp-tiles p1x-goal position-center-goal c: 1); move to center to find viable square
	(up-get-point-distance p1x-goal position-center-goal temp-goal)
	(up-jump-rule -1)
)
(defrule
	(up-compare-goal avoid-tc-goal >= 1)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: scout-unit c: 1)
	(up-set-target-object search-local c: 0)
	(up-target-point p1x-goal action-move -1 no-attack)
)
(defrule
	(true)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
)
(defrule
	(game-time > 960)
	(up-compare-goal kill-scouts-goal < 1); The kill scouts code is from Barbie.
=>
	(up-jump-rule 7)
)
(defrule
	(goal kill-scouts-goal -1)
=>
	(set-strategic-number sn-focus-player-number 0)
)
(defrule
	(up-compare-goal kill-scouts-goal >= 1)
=>
	(up-modify-sn sn-focus-player-number g:= kill-scouts-goal)
	(up-get-point position-self p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 4)
)
(defrule
	(up-compare-goal kill-scouts-goal >= 1)
	(nor
		(up-find-remote c: scout-cavalry-line c: 1)
		(or   
			(up-find-remote c: eagle-warrior-line c: 1)
			(or
				(up-find-remote c: villager c: 1)
				(up-find-remote c: militiaman-line c: 1)
			)
		)
	)
=>
	(up-ungarrison c: town-center)
	(set-goal kill-scouts-goal -1)
)
(defrule
	(goal kill-scouts-goal -1)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 4)
)
(defrule
	(goal kill-scouts-goal -1)
	(stance-toward focus-player enemy)
	(or
		(up-find-remote c: scout-cavalry-line c: 1)
		(or   
			(up-find-remote c: eagle-warrior-line c: 1)
			(or   
				(up-find-remote c: villager-class c: 1)
				(up-find-remote c: militiaman-line c: 1)
			)
		)
	)
	(up-timer-status misc-micro-timer != timer-running)
=>
	(up-modify-goal kill-scouts-goal s:= sn-focus-player-number)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 4)
)
(defrule
	(up-timer-status misc-micro-timer != timer-running)
	(up-compare-goal kill-scouts-goal >= 1)
=>
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 4)
	(up-find-local c: villager-class c: 10)
	(up-get-search-state local-total-goal)
	(up-modify-sn sn-focus-player-number g:= my-player-number-goal)
	(up-find-remote c: town-center c: 1)
	(up-target-objects 0 action-garrison -1 -1)
	(up-chat-data-to-self "garrisoning %d villagers" g: local-last-goal)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(enable-timer misc-micro-timer 12)
)
(defrule
	(goal kill-scouts-goal -1)
	(player-valid focus-player)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-jump-rule -4)
)
(defrule
	(or
		(or
			(game-time > 900)
			(unit-type-count scout-unit != 1)
		)
		(or
			(or
				(up-compare-goal avoid-tc-goal > 0)
				(up-compare-goal deer-luring-goal > 0)
			)
			(players-unit-type-count every-enemy villager == 0)
		)
	)
	(goal kill-villager-goal -1)
=>
	(up-jump-rule 7)
)
(defrule
	(goal kill-villager-goal -1)
=>
	(set-strategic-number sn-focus-player-number 1)
)
(defrule
	(goal kill-villager-goal -1)
	(stance-toward focus-player enemy)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: scout-unit c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-get-object-data object-data-hitpoints temp-goal5)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 6)
	(up-find-remote c: villager-class c: 40)
	(up-get-search-state local-total-goal)
	(set-goal temp-goal3 0)
)
(defrule
	(goal kill-villager-goal -1)
	(stance-toward focus-player enemy)
	(up-compare-goal temp-goal3 < remote-total-goal)
	(up-compare-goal temp-goal5 >= 40); full scout
=>
	(up-set-target-object search-remote g: temp-goal3)
	(up-get-object-data object-data-hitpoints temp-goal4)
	(set-goal split-rule-goal 38)
)
(defrule
	(goal split-rule-goal 38)
	(or
		(up-compare-goal temp-goal4 < 9); low HP
		(and
			(up-compare-goal temp-goal4 < 20); 1v1 medium HP villager
			(goal remote-total-goal 1)
		)
	)
	(up-compare-goal remote-total-goal < 5); don't go into villager bulks
=>
	(up-get-object-data object-data-id object-id2-goal)
	(up-reset-scouts)
	(up-find-local c: scout-unit c: 1)
	(up-target-objects 0 action-default -1 -1)
	(up-chat-data-to-player chat-player "kill %d HP villager" g: temp-goal4)
	(set-goal kill-villager-goal 1)
	(set-strategic-number sn-number-explore-groups 0)
	(set-strategic-number sn-total-number-explorers 0)
)
(defrule
	(goal kill-villager-goal -1)
	(stance-toward focus-player enemy)
	(up-compare-goal temp-goal3 < remote-total-goal)
=>
	(up-modify-goal temp-goal3 c:+ 1)
	(set-goal split-rule-goal 0)
	(up-jump-rule -3)
)
(defrule
	(goal kill-villager-goal -1)
	(strategic-number sn-focus-player-number < 8)
=>
	(up-modify-sn sn-focus-player-number c:+ 1)
	(up-jump-rule -5)
)
(defrule
	(goal kill-villager-goal 1)
	(or
		(not (up-set-target-by-id g: object-id2-goal))
		(up-object-data object-data-hitpoints < 1)
	)
=>
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
	(set-goal kill-villager-goal -1)
)
#load-if-not-defined SCANDANAVIA-MAP ;instead mill deer there
(defrule
	(or
		(or
			(unit-type-count-total villager < 16)
			(and
				(strategic-number sn-current-age >= df-transit)
				(or
					(strategic-number sn-current-age >= fc-transit)
					(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
				)
			)
		)
		(or
				(up-compare-goal avoid-tc-goal > 0); don't run scout into TC
				(unit-type-count scout-unit == 0)
		)
	)
=>
	(up-jump-rule 16)
)
(defrule
	(not (up-set-target-by-id g: object-id2-goal))
	(up-compare-goal object-id2-goal != -1)
=>
	(set-goal object-id2-goal -1); clear memory
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
	(set-goal deer-luring-goal -1)
	(chat-local-to-self "clear deer ID")
)
(defrule
	(dropsite-min-distance deer-hunting < 32)
	(goal object-id2-goal -1)
=>
	(set-strategic-number sn-focus-player-number 0)
	(up-get-point position-self p1x-goal)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: 6 c: 35)
)
(defrule
	(dropsite-min-distance deer-hunting < 32)
	(goal object-id2-goal -1)
	(up-find-remote c: deer c: 1)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-object-data object-data-id object-id2-goal)
	(set-goal deer-luring-goal 1)
	(chat-local-to-self "select a deer")
)
(defrule
	(up-compare-goal deer-luring-goal >= 1)
	(up-set-target-by-id g: object-id2-goal)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: scout-unit c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p2x-goal);point2=scout
	(up-set-target-by-id g: object-id2-goal)
	(up-get-point position-object p1x-goal)
	(up-copy-point temp-goal6 p1x-goal);temp6,7 = deer
	(up-get-point-distance p1x-goal position-self-goal point-distance-goal)
	(up-get-point-distance p1x-goal p2x-goal temp-goal8)
	(up-lerp-tiles p1x-goal position-self-goal c: -1)
)
(defrule
	(up-compare-goal deer-luring-goal >= 1)
	(up-set-target-by-id g: object-id2-goal)
	(up-compare-goal p1x-goal != -1); no invalid coordinates
	(up-compare-goal p1y-goal != -1)
	(up-compare-goal p1x-goal g:< max-coordinate-goal)
	(up-compare-goal p1y-goal g:< max-coordinate-y-goal)
	(up-timer-status scouting-timer != timer-running); not when scout is collecting sheep
=>
	(up-reset-unit c: scout-unit)
	(up-reset-scouts)
	(set-strategic-number sn-number-explore-groups 0)
	(set-strategic-number sn-total-number-explorers 0)
	(up-get-object-data object-data-hitpoints temp-goal)
	(up-target-point p1x-goal action-move -1 -1)
	(set-goal deer-luring-goal 2)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(timer-triggered five-seconds-timer)
	(up-compare-goal deer-luring-goal >= 1)
	(up-timer-status scouting-timer != timer-running)
	(up-compare-goal p2x-goal g:< temp-goal6)
	(up-compare-goal p2y-goal g:< temp-goal7)
	(up-compare-goal split-rule-goal != 65545); avoid endless loop
=>
	(up-lerp-tiles p1x-goal position-self-goal c: -3)
	(set-goal split-rule-goal 65545)
	(up-jump-rule -2)
)
(defrule
	(up-compare-goal deer-luring-goal >= 2)
	(unit-type-count villager-forager > 3)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: mill c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-get-point-distance temp-goal6 p1x-goal temp-goal3)
)
(defrule
	(up-timer-status hunting-timer != timer-running)
	(up-compare-goal deer-luring-goal >= 2)
	(unit-type-count villager-forager > 2)
	(or 
		(up-compare-goal temp-goal3 < 7)
		(up-compare-goal point-distance-goal < 9)
	)
=>
	(up-set-target-point p1x-goal)
	(set-goal temp-goal3 4); find four villagers to kill a deer
	(set-goal temp-goal5 2); use foragers first to avoid rotting deer if it'd take all hunters from previous deer
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-jump-rule 1)
	(set-goal split-rule-goal 42)
)
(defrule
	(up-timer-status hunting-timer != timer-running)
	(up-compare-goal deer-luring-goal >= 2)
	(up-compare-goal point-distance-goal < 7)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-set-target-point position-self-goal)
	(set-goal temp-goal3 4)
	(set-goal temp-goal5 2)
	(set-goal local-last-goal 0)
	(set-goal local-total-goal 0)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(up-timer-status hunting-timer != timer-running)
	(up-compare-goal deer-luring-goal >= 2)
	(or
		(up-compare-goal point-distance-goal < 7)
		(goal split-rule-goal 42)
	)
	(up-compare-goal temp-goal5 < 6); maximum search radius for a villager to become deer hunter
	(up-compare-goal temp-goal3 > 0)
=>
	(up-modify-goal temp-goal5 c:+ 1); increase search radius
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 g: temp-goal5)
	(up-find-local c: villager-class g: temp-goal3)
	(up-get-search-state local-total-goal)
	(up-modify-goal temp-goal3 g:- local-last-goal)
	(up-jump-rule -1)
)
(defrule
	(up-timer-status hunting-timer != timer-running)
	(up-compare-goal deer-luring-goal >= 2)
	(or
		(up-compare-goal point-distance-goal < 7)
		(goal split-rule-goal 42)
	)
	(up-compare-goal local-total-goal >= 2)
	(dropsite-min-distance live-boar > 9); dont use boar fighters on deer
=>
	(up-set-target-by-id g: object-id2-goal)
	(up-target-objects 1 action-default -1 -1)
	(enable-timer hunting-timer 20)
	(chat-local-to-self "kill deer")
)
(defrule
	(up-compare-goal deer-luring-goal >= 2)
	(up-set-target-by-id g: object-id2-goal)
	(up-compare-goal temp-goal < 5);deer shot
=>
	(set-goal object-id2-goal -1)
	(set-goal deer-luring-goal -1)
	(set-strategic-number sn-number-explore-groups 1)
	(set-strategic-number sn-total-number-explorers 1)
	(chat-local-to-self "deer shot")
)
(defrule
	(building-type-count wall-class > 0)
	(up-compare-goal deer-luring-goal >= 2)
	(up-set-target-by-id g: object-id2-goal)
	(up-compare-goal temp-goal8 < 2)
=>
	(set-goal temp-goal4 0)
	(set-goal local-last-goal 0)
	(set-goal local-total-goal 0)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(building-type-count wall-class > 0)
	(up-compare-goal deer-luring-goal >= 2)
	(up-set-target-by-id g: object-id2-goal)
	(goal local-last-goal 0)
	(up-compare-goal temp-goal8 < 2)
	(up-compare-goal temp-goal4 < 2)
=>
	(up-set-target-point temp-goal6)
	(up-modify-goal temp-goal4 c:+ 1)
	(up-filter-distance c: -1 g: temp-goal4)
	(up-find-local c: wall-class c: 1);search closest wall part to the deer
	(up-get-search-state local-total-goal)
	(up-jump-rule -1)
)
(defrule
	(building-type-count wall-class > 0)
	(up-compare-goal deer-luring-goal >= 2)
	(up-set-target-by-id g: object-id2-goal)
	(up-compare-goal local-last-goal > 0)
	(up-compare-goal temp-goal8 < 2)
=>
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p2x-goal)
	(up-get-point-distance p2x-goal position-self-goal temp-goal2)
)
(defrule
	(building-type-count wall-class > 0)
	(up-compare-goal deer-luring-goal >= 2)
	(up-set-target-by-id g: object-id2-goal)
	(goal local-last-goal 1)
	(up-compare-goal point-distance-goal g:>= temp-goal2); deer is farther away than wall
	(up-compare-goal temp-goal8 < 2)
	(up-compare-goal temp-goal4 < 5)
	(goal cut-point2-goal -1)
	(goal cut-point2-y-goal -1)
=>
	(up-target-point p2x-goal action-delete -1 -1)
	(up-copy-point cut-point2-goal p2x-goal)
	(chat-local-to-self "delete wall for deer lure")
	(disable-self)
)
#end-if
(defrule
	(timer-triggered one-minute-timer)
	(game-time < 1200)
	(idle-farm-count > 0)
	(unit-type-count villager-forager > 5)
	(up-compare-goal kill-scouts-goal < 1)
	(up-compare-goal threat-time-goal > 3000)
=>
	(up-retask-gatherers 907 c: 1)
	(chat-local-to-self "retask forager")
)
;send spears to guard camps
(defrule
	(timer-triggered five-seconds-timer)
	(up-compare-goal threat-time-goal > 5000)
	(game-time < 1500)
	(unit-type-count spearman-line > 0)
	(goal hostilities-goal 0)
=>
	(up-get-fact unit-type-count spearman-line temp-goal)
	(up-get-fact building-type-count mining-camp temp-goal2)
	(up-get-fact building-type-count lumber-camp temp-goal3)
	(up-modify-goal temp-goal2 g:+ temp-goal3)
	(set-goal temp-goal3 0)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(set-goal split-rule-goal 39)
)
(defrule
	(goal split-rule-goal 39)
	(up-compare-goal temp-goal3 g:< temp-goal)
	(up-compare-goal temp-goal3 g:< temp-goal2)
=>
	(up-find-local c: lumber-camp g: temp-goal)
	(up-find-local c: mining-camp g: temp-goal)
	(up-set-target-object search-local g: temp-goal3)
	(up-get-point position-object p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-modify-goal temp-goal3 c:+ 1)
	(set-goal temp-goal4 0)
)
(defrule
	(goal split-rule-goal 39)
	(up-compare-goal temp-goal3 g:< temp-goal)
	(up-compare-goal temp-goal3 g:< temp-goal2)
	(up-compare-goal temp-goal4 g:< temp-goal3)
=>
	(up-reset-search 0 1 0 0)
	(up-find-local c: spearman-line c: 1)
	(up-modify-goal temp-goal4 c:+ 1)
	(up-jump-rule -1)
)
(defrule
	(goal split-rule-goal 39)
	(up-compare-goal temp-goal3 g:< temp-goal)
	(up-compare-goal temp-goal3 g:< temp-goal2)
=>
	(up-target-point p1x-goal action-move -1 aggressive)
	(up-jump-rule -3)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
)
(defrule
	(current-age == dark-age)
	(building-type-count town-center > 0)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-set-target-point position-self-goal)
	(up-filter-distance c: -1 c: 33)
	(up-find-local c: sheep c: 8)
	(up-get-search-state local-total-goal)
	(up-modify-goal sheep-count-last-goal g:= sheep-count-goal)
	(up-modify-goal sheep-count-goal g:= local-total-goal)
	(up-modify-goal temp-goal g:= sheep-count-goal)
	(up-modify-goal temp-goal g:- sheep-count-last-goal)
	(up-modify-goal temp-goal c:max 0)
	(up-modify-goal sheep-count-goal g:+ temp-goal)
)
; Sheep Eating Code is from Scripter 64's Meleon AI
; modifications for sheep exploration by me
(defrule
	(or
		(or
			(game-time < 1); debug
			(game-time > 600)
		)
		(and
			(unit-type-count villager-shepherd > 9)
			(unit-type-count villager > 18)
		)
	)
=>
	(up-jump-rule 28)
)
(defrule
	(or
		(not(up-set-target-by-id g: next-sheep-goal))
		(up-compare-goal current-sheep-goal g:== next-sheep-goal)
	)
=>
	(set-goal next-sheep-goal -1)
)
(defrule
	(or
		(game-time > 120)
		(up-compare-goal next-sheep-goal g:== explore-sheep-goal)
	)
	(up-compare-goal explore-sheep-goal != -1)
=>
	(set-goal explore-sheep-goal -1)
)
(defrule
	(or
		(game-time > 120)
		(or
			(up-compare-goal next-sheep-goal g:== explore-sheep-goal)
			(up-compare-goal next-sheep-goal g:== explore-sheep2-goal)
		)
	)
	(up-compare-goal explore-sheep2-goal != -1)
=>
	(set-goal explore-sheep2-goal -1)
)
(defrule
	(true)
=>
	(up-modify-goal last-shepherd-goal g:= current-shepherd-goal)
	(up-get-fact unit-type-count villager-shepherd current-shepherd-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 32)
	(up-copy-point p1x-goal position-self-goal)
	(up-copy-point p2x-goal position-self-goal)
	(up-modify-goal p1x-goal c:+ 1); other modifiers, because position-self-goal is x-2
	(up-modify-goal p1y-goal c:- 2)
	(up-modify-goal p2x-goal c:+ 2)
	(up-modify-goal p2x-goal g:+ sheep-point-goal)
	(up-modify-goal p2y-goal c:+ 1)
	(up-jump-rule 14)
)
(defrule
	(not(up-set-target-object search-local c: 0))
=>
	(up-jump-rule 14)
)
(defrule
	(true)
=>
	(up-get-point position-object object-point-goal)
	(up-get-point-distance object-point-goal p1x-goal temp-goal2)
	(up-get-point-distance object-point-goal p2x-goal temp-goal3)
)
(defrule
	(up-object-data object-data-id g:== current-sheep-goal)
	(up-object-data object-data-id g:== next-sheep-goal)
	(up-compare-goal temp-goal2 > 8)
=>
	(set-goal temp-goal5 65545)
)
(defrule
	(goal next-sheep-goal -1)
	(up-object-data object-data-id g:!= current-sheep-goal)
	(nand
		(unit-type-count sheep > 3)
		(up-object-data object-data-id g:== explore-sheep-goal)
	)
	(nand
		(unit-type-count sheep > 4)
		(up-object-data object-data-id g:== explore-sheep2-goal)
	)
=>
	(up-get-object-data object-data-id next-sheep-goal)
)
(defrule
	(game-time < 120)
	(goal explore-sheep-goal -1)
	(up-object-data object-data-id g:!= current-sheep-goal)
	(up-object-data object-data-id g:!= next-sheep-goal)
=>
	(up-get-object-data object-data-id explore-sheep-goal)
)
(defrule
	(game-time < 120)
	(goal explore-sheep2-goal -1)
	(up-object-data object-data-id g:!= current-sheep-goal)
	(up-object-data object-data-id g:!= next-sheep-goal)
	(up-object-data object-data-id g:!= explore-sheep-goal)
=>
	(up-get-object-data object-data-id explore-sheep2-goal)
)
(defrule
	(timer-triggered five-seconds-timer)
	(game-time < 120)
	(or
		(up-object-data object-data-id g:== explore-sheep-goal)
		(up-object-data object-data-id g:== explore-sheep2-goal)
	)
	(up-compare-goal temp-goal2 < 13)	
=>
	(up-lerp-tiles object-point-goal position-self-goal c: -3)
	(set-goal temp-goal 1000)
	(set-goal temp-goal6 1)
	(set-goal temp-goal7 -1)
	(up-jump-rule 2)
)
(defrule
	(timer-triggered five-seconds-timer)
	(game-time < 120)
	(up-object-data object-data-id g:== explore-sheep-goal)
	(up-compare-goal temp-goal2 >= 13)
=>
	(up-cross-tiles object-point-goal position-self-goal c: 4)
	(set-goal temp-goal 1000)
	(set-goal temp-goal6 1)
	(set-goal temp-goal7 1)
	(up-jump-rule 1)
)
(defrule
	(timer-triggered five-seconds-timer)
	(game-time < 120)
	(up-object-data object-data-id g:== explore-sheep2-goal)
	(up-compare-goal temp-goal2 >= 13)
=>
	(up-cross-tiles object-point-goal position-self-goal c: -4)
	(set-goal temp-goal6 -1)
	(set-goal temp-goal7 1)
	(set-goal temp-goal 1000)
)
(defrule
	(timer-triggered five-seconds-timer)
	(or
		(up-point-contains object-point-goal c: gold-mine); check for occupied tiles
		(or
			(up-point-contains object-point-goal c: stone-mine)
			(or
				(up-point-contains object-point-goal c: forage-bush)
				(or
					(up-point-contains object-point-goal == tree-class)
					(up-point-terrain object-point-goal == terrain-water)
				)
			)
		)
	)
	(up-compare-goal temp-goal >= 1000)
	(up-compare-goal temp-goal < 1010)
=>
	(up-lerp-tiles object-point-goal position-self-goal g: temp-goal7)
	(up-cross-tiles object-point-goal position-self-goal g: temp-goal6)
	(up-modify-goal temp-goal c:+ 1)
	(up-jump-rule -1)
)
(defrule
	(timer-triggered five-seconds-timer)
	(or
		(or
			(up-compare-goal object-point-goal g:>= max-coordinate-goal); avoid invaild points by moving to TC
			(up-compare-goal object-point-y-goal g:>= max-coordinate-y-goal)
		)
		(or
			(up-compare-goal object-point-goal < 0)
			(up-compare-goal object-point-y-goal < 0)
		)
	)
	(up-compare-goal temp-goal >= 1000)
	(up-compare-goal temp-goal < 1010)
=>
	(up-lerp-tiles object-point-goal position-self-goal c: 1)
	(up-cross-tiles object-point-goal position-self-goal g: temp-goal6)
	(up-modify-goal temp-goal c:+ 1)
	(up-jump-rule -1)
)
(defrule
	(timer-triggered five-seconds-timer)
	(up-compare-goal temp-goal >= 1000)
	(up-compare-goal temp-goal <= 1010)
=>
	(up-target-point object-point-goal action-move -1 -1)
	(up-jump-rule 2)
	(set-goal temp-goal 0)
)
(defrule
	(timer-triggered five-seconds-timer)
	(up-object-data object-data-id g:!= current-sheep-goal)
	(up-object-data object-data-id g:!= next-sheep-goal)
	(up-object-data object-data-id g:!= explore-sheep-goal)
	(up-object-data object-data-id g:!= explore-sheep2-goal)
	(up-compare-goal temp-goal2 >= 2)
=>
	(up-target-point p1x-goal action-move -1 -1)
	(up-jump-rule 1)
)
(defrule
	(timer-triggered five-seconds-timer)
	(or
		(up-object-data object-data-id g:== current-sheep-goal)
		(up-object-data object-data-id g:== next-sheep-goal)
	)
	(up-compare-goal temp-goal3 >= 2)
=>
	(up-target-point p2x-goal action-move -1 -1)
)
(defrule
	(true)
=>
	(up-reset-search 0 1 0 0)
	(up-set-target-point position-self-goal)
	(up-find-local c: sheep c: 1)
	(up-jump-rule -15)
)
(defrule
	(goal temp-goal5 65545)
=>
	(up-jump-rule 5)
)
(defrule
	(goal switch-sheep-goal 1)
	(or
		(not(up-set-target-by-id g: current-sheep-goal))
		(up-object-data object-data-status != 2)
	)
	(up-compare-goal temp-goal2 <= 2)
	(up-compare-goal temp-goal3 <= 2)
=>
	(up-modify-goal sheep-point-goal g:neg sheep-point-goal)
	(set-goal switch-sheep-goal -1)
)
(defrule
	(up-compare-goal next-sheep-goal != -1)
	(not(up-set-target-by-id g: current-sheep-goal))
=>
	(up-modify-goal current-sheep-goal g:= next-sheep-goal)
	(set-goal switch-sheep-goal 1)
	(set-goal temp-goal 65545)
)
(defrule
	(up-timer-status misc-micro-timer != timer-running)
	(unit-type-count villager-shepherd >= 1)
	(up-set-target-by-id g: current-sheep-goal)
	(up-object-data object-data-carry < 20)
	(up-set-target-by-id g: next-sheep-goal)
	(up-object-data object-data-status == 2)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(set-goal temp-goal4 1)
	(up-find-local c: male-shepherd g: temp-goal4)
	(up-get-search-state local-total-goal)
	(up-modify-goal temp-goal4 g:- local-last-goal)
	(up-find-local c: female-shepherd g: temp-goal4)
	(up-set-target-by-id g: next-sheep-goal)
	(up-target-objects 1 action-default -1 -1)
	(enable-timer misc-micro-timer 30)
)
(defrule
	(or
		(goal temp-goal 65545)
		(up-compare-goal current-shepherd-goal g:!= last-shepherd-goal)
	)
	(up-compare-goal current-sheep-goal != -1)
	(up-set-target-by-id g: current-sheep-goal)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(set-goal temp-goal4 20)
	(up-find-local c: male-shepherd g: temp-goal4)
	(up-get-search-state local-total-goal)
	(up-modify-goal temp-goal4 g:- local-last-goal)
	(up-find-local c: female-shepherd g: temp-goal4)
	(up-set-target-by-id g: current-sheep-goal)
	(up-target-objects 1 action-default -1 -1)
)
(defrule
	(unit-type-count villager-shepherd == 0)
	(up-set-target-by-id g: current-sheep-goal)
	(up-object-data object-data-hitpoints < 1)
	(up-object-data object-data-carry > 10)
	(unit-type-count villager-hunter >= 2)
	(dropsite-min-distance live-boar > 9)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(set-goal temp-goal4 2)
	(up-find-local c: male-hunter g: temp-goal4)
	(up-get-search-state local-total-goal)
	(up-modify-goal temp-goal4 g:- local-last-goal)
	(up-find-local c: female-hunter g: temp-goal4)
	(up-set-target-by-id g: current-sheep-goal)
	(up-target-objects 1 action-default -1 -1)
)
(defrule
	(strategic-number sn-minimum-number-hunters > 0)
	(timer-triggered five-seconds-timer)
	(unit-type-count villager-shepherd > 4)
	(dropsite-min-distance boar-hunting < 6)
	(or
		(unit-type-count villager-shepherd > 5)
		(unit-type-count villager-hunter < 2)
	)
=>
	(set-goal temp-goal5 1)
	(up-find-local c: male-shepherd g: temp-goal5)
	(up-get-search-state local-total-goal)
	(up-modify-goal temp-goal5 g:- local-last-goal)
	(up-find-local c: female-shepherd g: temp-goal5)
	(up-set-target-by-id g: object-id-goal)
	(up-target-objects 1 action-default -1 -1)
	(chat-local-to-self "move shepherd to boar")
)
(defrule
	(unit-type-count villager-shepherd > 0)
	(unit-type-count sheep == 0)
	(dropsite-min-distance boar-hunting < 6)
	(up-set-target-by-id g: current-sheep-goal)
	(up-object-data object-data-carry < 2)
	(up-set-target-by-id g: object-id-goal)
	(up-object-data object-data-hitpoints < 1)
=>
	(up-find-local c: male-shepherd c: 4)
	(up-find-local c: female-shepherd c: 4)
	(up-set-target-by-id g: object-id-goal)
	(up-target-objects 1 action-default -1 -1)
	(chat-local-to-self "sheep finished, move to boar")
	(disable-self)
)
(defrule
	(unit-type-count villager-forager == 0)
	(building-type-count mill >= 1)
=>
	(up-modify-goal temp-goal s:= sn-focus-player-number)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 20)
	(up-set-target-point position-self-goal)
	(set-strategic-number sn-focus-player-number 0)
	(up-find-remote c: forage-bush c: 1)
	(disable-self)
)
(defrule
	(unit-type-count villager-forager == 0)
	(building-type-count mill >= 1)
=>
	(up-find-local c: mill c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-filter-distance c: -1 c: 4)
	(up-find-local c: villager-class c: 10)
	(up-target-objects 0 action-default -1 -1)
	(chat-local-to-self "move mill builder to forage")
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)	
	(disable-self)
)
#load-if-defined SCANDANAVIA-MAP
(defrule
	(research-completed ri-loom)
	(or
		(unit-type-count villager > 16)
		(unit-type-count villager-hunter == 0)
	)
	(dropsite-min-distance boar-hunting < 32)
	(up-compare-goal boar-hunting-goal < 1)
=>
	(set-goal split-rule-goal 52)
	(chat-local-to-self "initialize boar hunting")
)
#end-if
#load-if-defined DEATH-MATCH
(defrule
	(game-time < 600)
=>
	(up-jump-rule 14)
)
#end-if
(defrule
	(or
		(not(up-set-target-by-id g: object-id-goal))
		(and
			(timer-triggered one-minute-timer)
			(and
				(unit-type-count villager-hunter == 0)
				(dropsite-min-distance boar-hunting > 5)
			)
		)
	)
	(up-compare-goal object-id-goal != -1)
=>
	(set-goal object-id-goal -1)
	(set-goal boar-hunting-goal -1)
	(chat-local-to-self "reset boar-hunting")
)
(defrule
	(dropsite-min-distance boar-hunting > 9)
	(dropsite-min-distance live-boar > 9)
=>
	(set-strategic-number sn-minimum-number-hunters 0)
)
(defrule
	(research-available ri-loom)
	(or
		(and
			(dropsite-min-distance boar-hunting < 30)
			(dropsite-min-distance boar-hunting >= 20)
		)
		(unit-type-count-total villager > 18)
	)
	(up-object-type-count-total c: villager > 8)
=>
	(set-goal need-loom-goal 1)
)
(defrule
	(or
		(up-research-status c: ri-loom >= research-pending)
		(dropsite-min-distance boar-hunting < 20)
	)
	(up-object-type-count-total c: villager > 8)
	(dropsite-min-distance boar-hunting < 30)
	(up-compare-goal boar-hunting-goal < 1)
	(unit-type-count-total villager-hunter == 0)
=>
	(set-goal split-rule-goal 52)
	(chat-local-to-self "initialize boar hunting")
)
(defrule
	(up-set-target-by-id g: object-id-goal)
	(up-object-data object-data-carry < 90)
	(or
		(dropsite-min-distance live-boar > 19)
		(up-object-data object-data-carry < 50)
	)
	(unit-type-count villager-hunter > 0)
	(dropsite-min-distance live-boar < 30)
=>
	(set-goal split-rule-goal 52)
	(chat-local-to-self "lure a second boar while eating the first")
	(set-strategic-number sn-minimum-boar-hunt-group-size 0)
	(set-strategic-number sn-minimum-number-hunters 0)
)
(defrule
	(goal split-rule-goal 52)
=>
	(up-set-target-point position-self-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(set-strategic-number sn-focus-player-number 0)
	(up-filter-distance c: 6 c: 32)
)
(defrule
	(goal split-rule-goal 52)
	(or
		(up-find-remote c: 48 c: 1)
		(up-find-remote c: 822 c: 1)
	)
=>
	(up-set-target-object search-remote c: 0)
	(up-get-object-data object-data-id object-id-goal)	
	(set-goal boar-hunting-goal 1)
	(chat-local-to-self "search boar")
)
(defrule
	(goal boar-hunting-goal 1)
	(up-compare-goal object-id-goal != -1)
	(up-object-data object-data-action != actionid-attack);boar isn't attacking anyone
=>
	(up-reset-search 1 1 0 0)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 32)
	(up-find-local c: villager-class c: 1)
	(set-goal split-rule-goal 2)
	(up-get-fact unit-type-count villager temp-goal8)
	(set-goal temp-goal7 0)
	(chat-local-to-self "find a villager")
)
(defrule
	(goal boar-hunting-goal 1)
	(up-compare-goal object-id-goal != -1)
	(up-set-target-object search-local c: 0)
	(or
		(and
			(up-object-data object-data-hitpoints < 40)
			(research-completed ri-loom)
		)
		(up-object-data object-data-hitpoints < 25)
	)
	(up-compare-goal temp-goal7 g:<= temp-goal8); prevent game freeze, if all villagers are low
=>
	(up-modify-goal temp-goal7 c:+ 1)
	(up-reset-search 0 1 0 0); find an unwounded villager
	(up-find-local c: villager-class c: 1)
	(up-jump-rule -1)
)
(defrule
	(goal boar-hunting-goal 1)
	(up-compare-goal object-id-goal != -1)
	(up-object-data object-data-action != actionid-attack)
	(up-set-target-object search-local c: 0)
=>
	(up-set-target-by-id g: object-id-goal)
	(up-target-objects 1 action-default -1 -1);right-click the boar with the villager
	(set-goal boar-hunting-goal 2)
	(chat-local-to-self "target boar")
)
(defrule
	(goal boar-hunting-goal 2)
	(dropsite-min-distance live-boar < 8)
	(or
		(dropsite-min-distance live-boar < 6)
		(up-research-status c: ri-loom < research-complete)
	)
	(up-compare-goal object-id-goal != -1)
=>
	(set-strategic-number sn-minimum-number-hunters 5); number of villagers to kill the boar
	(up-reset-filters)
	(up-reset-search 1 1 1 1)
	(up-filter-distance c: -1 c: 14)
	(up-find-local c: villager-class c: 5)
	(up-set-target-by-id g: object-id-goal)
	(up-target-objects 1 action-default -1 -1);right-click the boar with the villager
	(set-goal boar-hunting-goal -1)
	(chat-local-to-self "kill boar")
)
(defrule
	(timer-triggered five-seconds-timer); rest of the boar hunt code is to delete walls in arena
	(building-type-count wall-class > 0)
	(unit-type-count villager-hunter == 1)
	(up-set-target-by-id g: object-id-goal)
=>
	(up-set-target-by-id g: object-id-goal)
	(up-get-point position-object p1x-goal)
	(up-set-target-point p1x-goal)
	(up-get-point-distance p1x-goal position-self-goal temp-goal7)
	(up-reset-search 1 1 1 1)
	(up-filter-distance c: -1 c: 2)
	(set-goal temp-goal8 100)
)
(defrule
	(timer-triggered five-seconds-timer)
	(building-type-count wall-class > 0)
	(unit-type-count villager-hunter == 1)
	(up-set-target-by-id g: object-id-goal)
	(up-find-local c: wall-class c: 1)
=>
	(up-set-target-object search-local c: 0)
	(up-get-point position-object p2x-goal)
	(up-get-point-distance p2x-goal position-self-goal temp-goal8)
)
(defrule
	(timer-triggered five-seconds-timer)
	(building-type-count wall-class > 0)
	(unit-type-count villager-hunter == 1)
	(up-set-target-by-id g: object-id-goal)
	(up-compare-goal temp-goal7 g:> temp-goal8)
=>	
	(up-target-point p2x-goal action-delete -1 -1)
	(up-copy-point cut-point2-goal p2x-goal)
	(chat-local-to-self "delete wall for boar hunting")
	(disable-self)
)

; DUC: loop through all villagers
(defrule
	(or
		(or
			(game-time == 0)
			(building-type-count town-center == 0)
		)
		(game-time > 1500)
	)
=>
	(up-jump-rule 21)
)
(defrule
	(true)
=>
	(up-get-fact food-amount 0 temp-goal6)
	(up-get-fact wood-amount 0 temp-goal7)
	(up-get-fact gold-amount 0 temp-goal8)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(set-strategic-number sn-focus-player-number my-player-number)
	(up-find-remote c: town-center c: 1)
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal); remove wounded villager from fighting and stop chasing drusher
	(up-find-local c: villager-class c: 1)
)
(defrule
	(up-compare-goal under-attack-goal > 0)
	(goal forward-threat-goal 0)
	(up-set-target-object search-local c: 0)
	(up-compare-goal threat-time-goal < 4000)
	(up-object-data object-data-action == actionid-attack)
	(up-timer-status reset-units-timer == timer-running)
=>
	(up-reset-unit c: -1)
	(disable-timer reset-units-timer)
	(set-strategic-number sn-allow-civilian-defense 3)
)
(defrule
	(up-compare-goal threat-time-goal > 5000)
	(up-set-target-object search-local c: 0)
	(or
		(or	
			(up-object-data object-data-target == infantry-class)
			(up-object-data object-data-target == ranged-unit-class)
		)
		(up-object-data object-data-target == 947)
	)
	(strategic-number sn-allow-civilian-defense == 3)
=>
	(up-target-point 0 action-stop -1 -1)
)
(defrule
	(goal enemy-goal drush)
	(up-set-target-object search-local c: 0)
	(up-compare-goal threat-time-goal < 4000)
	(strategic-number sn-allow-civilian-defense == 3)
	(players-unit-type-count any-enemy militiaman-line > 0)
	(or
		(up-object-data object-data-hitpoints < 18)
		(players-unit-type-count every-enemy militiaman-line < 2)
	)
	(up-object-data object-data-hitpoints < 25)
	(up-object-data object-data-action == actionid-attack)
	(or	
		(up-object-data object-data-target == infantry-class)
		(up-object-data object-data-target == 947)
	)
=>
	(up-target-objects 0 action-garrison -1 -1)
	(set-strategic-number sn-allow-civilian-defense 0)
)
(defrule
	(up-set-target-object search-local c: 0)
	(up-compare-goal threat-time-goal < 4000)
	(strategic-number sn-allow-civilian-defense == 3)
	(players-unit-type-count focus-player skirmisher-line > 1)
	(or
		(up-object-data object-data-hitpoints < 18)
		(players-unit-type-count focus-player archer-line > 0)
	)
	(up-object-data object-data-hitpoints < 25)
	(up-object-data object-data-action == actionid-attack)
	(or	
		(up-object-data object-data-target == ranged-unit-class)
		(up-object-data object-data-target == 947)
	)
=>
	(up-target-objects 0 action-garrison -1 -1)
	(set-strategic-number sn-allow-civilian-defense 0)
)
(defrule
	(up-set-target-object search-local c: 0)
	(or
		(or
			(up-object-data object-data-type == female-forager)
			(up-object-data object-data-type == male-forager)
		)
		(or
			(or
				(up-object-data object-data-type == female-shepherd)
				(up-object-data object-data-type == male-shepherd)
			)
			(or
				(up-object-data object-data-type == female-farmer)
				(up-object-data object-data-type == male-farmer)
			)
		)
	)
=>
	(up-get-object-data object-data-carry temp-goal5)
	(up-modify-goal temp-goal6 g:+ temp-goal5)
)
(defrule
	(up-set-target-object search-local c: 0)
	(or
		(up-object-data object-data-type == female-hunter)
		(up-object-data object-data-type == male-hunter)
	)
	(or
		(dropsite-min-distance deer-hunting < 8)
		(dropsite-min-distance boar-hunting < 8)
	)
=>
	(up-get-object-data object-data-carry temp-goal5)
	(up-modify-goal temp-goal6 g:+ temp-goal5)
)
(defrule
	(up-set-target-object search-local c: 0)
	(or
		(up-object-data object-data-type == female-lumberjack)
		(up-object-data object-data-type == male-lumberjack)
	)
=>
	(up-get-object-data object-data-carry temp-goal5)
	(up-modify-goal temp-goal7 g:+ temp-goal5)
)
(defrule
	(up-set-target-object search-local c: 0)
	(or
		(up-object-data object-data-type == female-gold-miner)
		(up-object-data object-data-type == male-gold-miner)
	)
=>
	(up-get-object-data object-data-carry temp-goal5)
	(up-modify-goal temp-goal8 g:+ temp-goal5)
)
(defrule
	(up-set-target-object search-local c: 0)
=>
	(up-reset-search 0 1 0 0)
	(up-find-local c: villager-class c: 1)
	(up-jump-rule -9)
)

; Force drop resources

(defrule
	(unit-type-count villager-shepherd == 0)
	(unit-type-count villager-hunter < 2)
	(research-available ri-loom)
=>
	(set-goal force-res-drop-goal -1)
)
(defrule
	(or
		(or
			(building-type-count town-center > 1)
			(food-amount >= 50)
		)
		(or
			(up-research-status c: feudal-age == research-pending)
			(or
				(up-research-status c: castle-age == research-pending)
				(up-research-status c: ri-wheel-barrow == research-pending)
			)
		)
	)
=>
	(set-goal force-res-drop-goal -1); enough food/TC busy/multi TC: disable forced food drop code
)
(defrule
	(food-amount < 50)
	(up-pending-objects c: villager == 1); villager is still queuing
	(timer-triggered villager-timer)
	(goal force-res-drop-goal -1)
=>
	(set-goal force-res-drop-goal 0)
	(enable-timer villager-timer 20); wait 20s
)
(defrule
	(food-amount < 50)
	(up-compare-goal temp-goal6 >= 50)
	(up-pending-objects c: villager < 2)
	(timer-triggered villager-timer)
	(goal force-res-drop-goal 0)
	(unit-type-count villager < 24)
=>
	(up-drop-resources food c: 1)
	(chat-local-to-self "force drop food for villager")
)
(defrule
	(food-amount < 50)
	(up-compare-goal temp-goal6 >= 50)
	(up-pending-objects c: villager < 2)
	(timer-triggered villager-timer)
	(goal force-res-drop-goal 0)
	(unit-type-count villager >= 24)
=>
	(up-drop-resources food c: 5)
	(chat-local-to-self "force drop food for villager 2")
)
(defrule
	(research-available feudal-age)
	(unit-type-count-total villager >= villager-flush)
	(or
		(goal strategy-goal flush)
		(or
			(unit-type-count fishing-ship > 3)
			(unit-type-count-total villager >= dark-age-villager)
		)
	)
	(food-amount < 500)
	(up-compare-goal temp-goal6 >= 470)
=>
	(set-goal train-civ-goal -1)
)
(defrule
	(research-available feudal-age)
	(unit-type-count-total villager >= villager-flush)
	(or
		(goal strategy-goal flush)
		(or
			(unit-type-count fishing-ship > 3)
			(unit-type-count-total villager >= dark-age-villager)
		)
	)
	(food-amount < 500)
	(up-compare-goal temp-goal6 >= 500)
=>
	(up-drop-resources food c: 1)
	(chat-local-to-self "force drop food for feudal age")
)
(defrule
	(research-available castle-age)
	(can-train villager); tc not occupied
	(up-compare-goal temp-goal6 >= 800)
	(food-amount < 800)
=>
	(up-drop-resources food c: 3)
	(chat-local-to-self "force drop food for castle age")
)
(defrule
	(research-available castle-age)
	(can-train villager); tc not occupied
	(up-compare-goal temp-goal8 >= 200)
	(gold-amount < 500)
=>
	(up-drop-resources gold c: 1)
	(chat-local-to-self "force drop gold for castle age")
)
(defrule
	(strategic-number sn-current-age == feudal)
	(current-age-time >= 13)
	(current-age-time < 60)
	(up-compare-goal temp-goal6 >= 700)
	(up-compare-goal temp-goal7 >= 150)
	(up-compare-goal temp-goal8 >= 150)
	(up-compare-goal strategy-goal != flush)
	(building-type-count-total blacksmith == 0)
=>
	(up-drop-resources wood c: 1)
	(up-assign-builders c: blacksmith c: 2)
	(chat-local-to-self "force drop wood for blacksmith")
)
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock >= water-mixed)
	(research-available castle-age)
	(up-compare-goal temp-goal6 >= 800)
	(up-compare-goal temp-goal8 < 150)
	(can-sell-commodity stone)
=>
	(sell-commodity stone)
)
#end-if
#end-if
#end-if
(defrule
	(research-completed ri-loom)
	(unit-type-count villager >= 12)
	(or
		(unit-type-count villager >= 19)
		(and
			(cc-players-unit-type-count 0 48 == 0); no boar on map
			(cc-players-unit-type-count 0 822 == 0)
		)
	)
	(up-compare-goal boar-hunting-goal < 5)
	(or
		(dropsite-min-distance boar-hunting >= 32); either boar missed or already eaten - enable deer hunting
		(difficulty > hard)
	)
=>
	(set-strategic-number sn-enable-boar-hunting 1)
	(set-strategic-number sn-minimum-number-hunters 0)
	(set-goal boar-hunting-goal 5)
)
#load-if-not-defined UP-SCENARIO-GAME ; don't ruin scenarios by deleting walls
#load-if-defined ARENA-MAP
(defrule
	(game-time < 2)
=>
	(up-copy-point p1x-goal position-self-goal)
	(up-copy-point p2x-goal position-center-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-distance c: -1 c: 13)
	(up-lerp-tiles p1x-goal p2x-goal c: -17)
	(up-set-target-point p1x-goal)
	(up-find-local c: 927 c: 240)
	(up-find-local c: 939 c: 240)
	(up-cross-tiles p1x-goal p2x-goal c: 4)
	(up-set-target-point p1x-goal)
	(up-find-local c: 927 c: 240)
	(up-find-local c: 939 c: 240)
	(up-cross-tiles p1x-goal p2x-goal c: 4)
	(up-set-target-point p1x-goal)
)
(defrule
	(game-time < 2)
=>
	(up-find-local c: 927 c: 240)
	(up-find-local c: 939 c: 240)
	(up-copy-point p1x-goal position-self-goal)
	(up-copy-point p2x-goal position-center-goal)
	(up-lerp-tiles p1x-goal p2x-goal c: -17)
	(up-cross-tiles p1x-goal p2x-goal c: -4)
	(up-set-target-point p1x-goal)
	(up-find-local c: 927 c: 240)
	(up-find-local c: 939 c: 240)
	(up-cross-tiles p1x-goal p2x-goal c: -4)
	(up-set-target-point p1x-goal)
	(up-find-local c: 927 c: 240)
	(up-find-local c: 939 c: 240)
	(up-target-point 0 action-delete -1 -1)
)
#end-if
#end-if
#load-if-defined UP-MICHI-STYLE
(defrule
	(game-time > 10)
=>
	(up-jump-rule 5)
)
(defrule
	(true)
=>
	(up-get-point position-self p1x-goal)
	(up-get-point position-mirror cut-point4-goal)
	(up-lerp-percent p1x-goal cut-point4-goal c: 50)
)
(defrule
	(up-point-contains p1x-goal c: tree-class)
=>
	(up-lerp-tiles p1x-goal position-self-goal c: 2)
	(up-jump-rule -1)
)
(defrule
	(not (up-point-contains p1x-goal c: tree-class))
=>
	(up-copy-point p2x-goal p1x-goal)
	(up-lerp-tiles p1x-goal position-self-goal c: 2)
)
(defrule
	(not (up-point-contains p1x-goal c: tree-class))
	(up-point-contains p2x-goal c: tree-class)
=>
	(up-lerp-tiles p1x-goal position-self-goal c: 2)
	(up-jump-rule -3)
)
(defrule
	(nor
		(up-point-contains p1x-goal c: tree-class)
		(up-point-contains p2x-goal c: tree-class)
	)
=>
	(up-copy-point cut-point1-goal p1x-goal)
	(disable-self)
)
(defrule
	(up-timer-status five-seconds-timer != timer-running)
	(up-compare-goal cut-goal != 4535)
	(unit-type-count mangonel-line > 0)
	(nor 
		(research-completed mangonel-upgrade-cut)
		(up-allied-goal any-computer-ally cut-goal == 4535)
	)
	(players-building-count every-enemy == 0)
	(unit-type-count 675 == 0)
	(unit-type-count 589 == 0)
=>
	(up-copy-point temp-goal cut-point1-goal)
	(up-lerp-tiles temp-goal position-self-goal c: 6)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: mangonel-line c: 2)
	(up-target-point temp-goal action-move -1 -1)
)
(defrule
	(goal cut-goal -1)
	(or
		(or
			(unit-type-count 675 > 0)
			(unit-type-count 589 > 0)
		)
		(players-building-count any-enemy > 0)
	)
=>
	(up-jump-rule 8)
)
(defrule
	(goal cut-goal 4535)
	(or
		(unit-type-count 675 > 0)
		(unit-type-count 589 > 0)
	)
	(or
		(players-unit-count any-enemy > 0)
		(players-building-count any-enemy > 0)
	)
=>
	(set-goal cut-goal -1)
	(up-set-attack-stance mangonel-line c: aggressive)
	(chat-to-player chat-player "onager lost, stop cut")
)
(defrule
	(up-compare-goal cut-goal != 4535)
	(unit-type-count mangonel-line > 0)
	(research-completed mangonel-upgrade-cut)
	(or
		(nor
			(player-valid any-human-ally)
			(up-allied-goal any-computer-ally cut-goal == 4535)
		)
		(taunt-detected any-human-ally 187)
	)
	(players-building-count every-enemy == 0)
	(unit-type-count 675 == 0)
	(unit-type-count 589 == 0)
=>
	(set-goal cut-goal 4535)
)
(defrule
	(goal cut-goal 4535)
	(up-point-contains cut-point1-goal c: tree-class)
	(up-timer-status cut-timer != timer-running)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: mangonel-line c: 4)
	(up-target-point cut-point1-goal action-ground -1 defensive)
	(enable-timer cut-timer 5)
)
(defrule
	(goal cut-goal 4535)
	(not (up-point-contains cut-point1-goal c: tree-class))
	(up-timer-status cut-timer != timer-running)
=>
	(up-copy-point cut-point2-goal cut-point1-goal)
	(up-cross-tiles cut-point2-goal position-self-goal c: 2)
)
(defrule
	(goal cut-goal 4535)
	(not (up-point-contains cut-point1-goal c: tree-class))
	(up-point-contains cut-point2-goal c: tree-class)
	(up-timer-status cut-timer != timer-running)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: mangonel-line c: 4)
	(up-target-point cut-point2-goal action-ground -1 defensive)
	(enable-timer cut-timer 5)
)
(defrule
	(goal cut-goal 4535)
	(nor 
		(up-point-contains cut-point1-goal c: tree-class)
		(up-point-contains cut-point2-goal c: tree-class)
	)
	(up-compare-goal cut-point2-goal != -1)
	(up-timer-status cut-timer != timer-running)
=>
	(up-copy-point cut-point3-goal cut-point2-goal)
	(up-cross-tiles cut-point3-goal position-self-goal c: 2)
)
(defrule
	(goal cut-goal 4535)
	(nor 
		(up-point-contains cut-point1-goal c: tree-class)
		(up-point-contains cut-point2-goal c: tree-class)
	)
	(up-point-contains cut-point3-goal c: tree-class)
	(up-timer-status cut-timer != timer-running)
=>
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-find-local c: mangonel-line c: 4)
	(up-target-point cut-point3-goal action-ground -1 defensive)
	(enable-timer cut-timer 5)
)
(defrule
	(goal cut-goal 4535)
	(nor 
		(up-point-contains cut-point1-goal c: tree-class)
		(up-point-contains cut-point2-goal c: tree-class)
	)
	(not (up-point-contains cut-point3-goal c: tree-class))
	(up-compare-goal cut-point3-goal != -1)
	(up-timer-status cut-timer != timer-running)
=>
	(up-copy-point p2x-goal cut-point4-goal)
	(up-lerp-tiles cut-point1-goal p2x-goal c: 2)
)
#end-if

; build watch towers at the end
; target points for place-point building are not saved, so it they must stay the same until the building foundation is dropped

(defrule
	(goal increase-town-size-goal watch-tower)
	(up-compare-goal strategy-goal != flush)
	(players-building-type-count focus-player dock > 0)
	(building-type-count dock > 0)
=>
	(up-assign-builders c: watch-tower c: 3)
	(up-set-placement-data my-player-number dock c: 2)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta -1)
	(up-build place-control 0 c: watch-tower)
	(set-goal increase-town-size-goal 0)
	(set-strategic-number sn-percent-building-cancellation 0)
)
(defrule
	(or
		(players-building-count every-enemy == 0)
		(or
			(up-player-distance focus-player > 90)
			(up-pending-objects c: watch-tower > 0)
		)
	)
=>
	(set-goal increase-town-size-goal 0)
)
(defrule
	(goal increase-town-size-goal watch-tower)
=>
	(up-modify-sn sn-focus-player-number g:= closest-enemy-goal)
	(up-copy-point p1x-goal position-self-goal)
	(up-get-point position-focus p2x-goal)
	(up-lerp-tiles p1x-goal p2x-goal c: 20)
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 15)
	(up-find-local c: mining-camp c: 10)
	(up-find-local c: lumber-camp c: 10)
	(up-find-local c: mill c: 2)
	(up-get-search-state local-total-goal)
	(up-modify-goal temp-goal g:= local-total-goal)
	(set-goal temp-goal2 0)
)
(defrule
	(goal increase-town-size-goal watch-tower)
	(up-compare-goal temp-goal g:> temp-goal2); not all camps checked yet
=>
	(up-set-target-object search-local g: temp-goal2)
	(up-get-point position-object object-point-goal)
	(up-set-target-point object-point-goal)
	(up-reset-search 1 1 1 1)	
	(up-filter-distance c: -1 c: 5)
)
(defrule
	(goal increase-town-size-goal watch-tower)
	(up-compare-goal temp-goal g:> temp-goal2); not all camps checked yet
	(up-find-local c: watch-tower c: 1)
=>
	(up-modify-goal temp-goal2 c:+ 1); go to next camp
	(up-set-target-point p1x-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 15)
	(up-find-local c: mining-camp c: 10)
	(up-find-local c: lumber-camp c: 10)
	(up-find-local c: mill c: 2)
	(up-jump-rule -2)
)
(defrule
	(goal increase-town-size-goal watch-tower)
	(up-compare-goal temp-goal2 g:== temp-goal); all camps checked
	(or
		(up-find-local c: watch-tower c: 1); all camps towered
		(goal temp-goal 0); no forward camp
	)
=>
	(set-goal increase-town-size-goal 0); no tower needed
)
(defrule
	(goal increase-town-size-goal watch-tower)
=>
	(up-copy-point tower-point-goal object-point-goal)
	(up-set-target-point tower-point-goal)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 1)
	(up-build place-point 0 c: watch-tower)
	(set-goal increase-town-size-goal 0)
	(set-strategic-number sn-percent-building-cancellation 0)
)
(defrule
	(strategic-number sn-maximum-town-size >= 40)
	(unit-type-count unpacked-trebuchet > 0)
	(can-build bombard-tower)
	(up-pending-objects c: bombard-tower < 2)
=>
	(up-modify-sn sn-focus-player-number s:= sn-target-player-number)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-include cmdid-military -1 -1 -1)
	(up-find-local c: 42 c: 1)
	(up-set-target-object search-local c: 0)
	(up-get-point position-object object-point-goal)
	(up-set-target-point object-point-goal)
	(up-reset-search 1 1 1 1)
	(up-reset-filters)
	(up-filter-range -1 -1 -1 20)
)
(defrule
	(strategic-number sn-maximum-town-size >= 40)
	(unit-type-count unpacked-trebuchet > 0)
	(can-build bombard-tower)
	(or
		(up-find-remote c: town-center c: 1)
		(up-find-remote c: castle c: 1)
	)
	(up-pending-objects c: bombard-tower < 2)
=>
	(up-copy-point tower-point-goal object-point-goal)
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
	(set-strategic-number sn-disable-builder-assistance 0)
	(up-build place-point 0 c: bombard-tower)
)
(defrule
	(or
		(up-pending-placement c: watch-tower)
		(up-pending-placement c: bombard-tower)
	)
	(up-compare-goal tower-point-goal != -1)
=>
	(up-set-target-point tower-point-goal); tower will get placed at the last selected target point, not the one selected while the rule fired
	(set-strategic-number sn-placement-zone-size 2)
	(set-strategic-number sn-placement-fail-delta 0)
)
(defrule
	(building-type-count gate > 0)
	(current-age == feudal-age)
	(up-compare-goal cut-point2-goal != -1)
	(building-type-count-total palisade-wall < 3)
	(up-pending-objects c: palisade-wall < 1)
	(up-can-build cut-point2-goal c: palisade-wall)
=>
	(set-strategic-number sn-placement-zone-size 1)
	(set-strategic-number sn-placement-fail-delta 1)
	(up-build place-point cut-point2-goal c: palisade-wall)
)
(defrule
	(up-pending-placement c: palisade-wall)
	(up-compare-goal cut-point2-goal != -1)
=>
	(up-set-target-point cut-point2-goal)
)
#load-if-not-defined WONDER-RACE
#load-if-not-defined NOMAD-MAP
#load-if-not-defined UP-2-PLAYER-GAME
#load-if-defined UP-POCKET-POSITION
#load-if-not-defined UP-MICHI-STYLE
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-islands)
	(game-time > 10)
=>
	(set-goal position-goal pocket)
	(set-goal strategy-goal pocket-strategy)
	(set-goal unit-goal pocket-unit)
	(set-goal ranged-unit-type-goal pocket-ranged)
	(set-goal control-goal 0)(disable-self))
#else
(defrule
	(game-time > 10)
=>
	(set-goal position-goal pocket)(disable-self))
#end-if
#else
(defrule
	(strategic-number sn-minimum-water-body-size-for-dock < water-mixed)
	(game-time > 10)
=>
	(set-goal position-goal flank)(disable-self))
#end-if
#end-if
#end-if
#end-if
#else
#load-if-not-defined UP-2-PLAYER-GAME
#load-if-defined UP-POCKET-POSITION
(defrule
	(game-time > 10)
=>
	(set-goal position-goal pocket)(disable-self))
#else
(defrule
	(game-time > 10)
=>
	(set-goal position-goal flank)(disable-self))
#end-if
#end-if
#end-if
#load-if-not-defined TEAMS-LOCKED
#load-if-not-defined UP-2-PLAYER-GAME
(defrule
	(not (stance-toward 1 ally))
	(or
		(player-resigned 1)
		(not (player-in-game 1))
	)
	(players-military-population 1 < 1)
	(players-building-type-count 1 castle < 1)
	(players-building-type-count 1 watch-tower < 1)
	(players-building-type-count 1 bombard-tower < 1)
	(players-unit-type-count 1 monk < 1)
=>
	(set-stance 1 ally)
)
(defrule
	(not (stance-toward 2 ally))
	(or
		(player-resigned 2)
		(not (player-in-game 2))
	)
	(players-military-population 2 < 1)
	(players-building-type-count 2 castle < 1)
	(players-building-type-count 2 watch-tower < 1)
	(players-building-type-count 2 bombard-tower < 1)
	(players-unit-type-count 2 monk < 1)
=>
	(set-stance 2 ally)
)
(defrule
	(not (stance-toward 3 ally))
	(or
		(player-resigned 3)
		(not (player-in-game 3))
	)
	(players-military-population 3 < 1)
	(players-building-type-count 3 castle < 1)
	(players-building-type-count 3 watch-tower < 1)
	(players-building-type-count 3 bombard-tower < 1)
	(players-unit-type-count 3 monk < 1)
=>
	(set-stance 3 ally)
)
(defrule
	(not (stance-toward 4 ally))
	(or
		(player-resigned 4)
		(not (player-in-game 4))
	)
	(players-military-population 4 < 1)
	(players-building-type-count 4 castle < 1)
	(players-building-type-count 4 watch-tower < 1)
	(players-building-type-count 4 bombard-tower < 1)
	(players-unit-type-count 4 monk < 1)
=>
	(set-stance 4 ally)
)
(defrule
	(not (stance-toward 5 ally))
	(or
		(player-resigned 5)
		(not (player-in-game 5))
	)
	(players-military-population 5 < 1)
	(players-building-type-count 5 castle < 1)
	(players-building-type-count 5 watch-tower < 1)
	(players-building-type-count 5 bombard-tower < 1)
	(players-unit-type-count 5 monk < 1)
=>
	(set-stance 5 ally)
)
(defrule
	(not (stance-toward 6 ally))
	(or
		(player-resigned 6)
		(not (player-in-game 6))
	)
	(players-military-population 6 < 1)
	(players-building-type-count 6 castle < 1)
	(players-building-type-count 6 watch-tower < 1)
	(players-building-type-count 6 bombard-tower < 1)
	(players-unit-type-count 6 monk < 1)
=>
	(set-stance 6 ally)
)
(defrule
	(not (stance-toward 7 ally))
	(or
		(player-resigned 7)
		(not (player-in-game 7))
	)
	(players-military-population 7 < 1)
	(players-building-type-count 7 castle < 1)
	(players-building-type-count 7 watch-tower < 1)
	(players-building-type-count 7 bombard-tower < 1)
	(players-unit-type-count 7 monk < 1)
=>
	(set-stance 7 ally)
)
(defrule
	(not (stance-toward 8 ally))
	(or
		(player-resigned 8)
		(not (player-in-game 8))
	)
	(players-military-population 8 < 1)
	(players-building-type-count 8 castle < 1)
	(players-building-type-count 8 watch-tower < 1)
	(players-building-type-count 8 bombard-tower < 1)
	(players-unit-type-count 8 monk < 1)
=>
	(set-stance 8 ally)
)
(defrule
	(or
		(players-stance any-human-ally neutral)
		(players-stance any-human-ally enemy)
	)
=>
	(chat-to-player-using-id this-any-human-ally 22102)
	(set-stance this-any-human-ally enemy)
)
#end-if
#end-if
#load-if-defined UP-ALLY-IN-GAME
(defrule
	(taunt-detected any-ally 42)
	(current-age == dark-age)
=>
	(chat-to-player-using-id this-any-ally 22165)
	(acknowledge-taunt this-any-ally 42)
)
(defrule
	(taunt-detected any-ally 42)
	(current-age == feudal-age)
=>
	(chat-to-player-using-id this-any-ally 22166)
	(acknowledge-taunt this-any-ally 42)
)
(defrule
	(taunt-detected any-ally 42)
	(current-age == castle-age)
=>
	(chat-to-player-using-id this-any-ally 22167)
	(acknowledge-taunt this-any-ally 42)
)
(defrule
	(taunt-detected any-ally 42)
	(current-age == imperial-age)
=>
	(chat-to-player-using-id this-any-ally 22168)
	(acknowledge-taunt this-any-ally 42)
)
#end-if
(defrule
	(taunt-detected taunt-player 249)
=>
	(chat-to-player this-any-taunt-player "My current resources are:")
	(up-get-fact food-amount 0 temp-goal)
	(up-chat-data-to-player this-any-taunt-player "Food: %d" g: temp-goal)
	(up-get-fact wood-amount 0 temp-goal)
	(up-chat-data-to-player this-any-taunt-player "Wood: %d" g: temp-goal)
	(up-get-fact gold-amount 0 temp-goal)
	(up-chat-data-to-player this-any-taunt-player "Gold: %d" g: temp-goal)
	(up-get-fact stone-amount 0 temp-goal)
	(up-chat-data-to-player this-any-taunt-player "Stone: %d" g: temp-goal)
	(up-get-fact population 0 temp-goal)
	(up-chat-data-to-player this-any-taunt-player "Pop: %d" g: temp-goal)
	(up-get-fact military-population 0 temp-goal)
	(up-chat-data-to-player this-any-taunt-player "Military pop: %d" g: temp-goal)
	(acknowledge-taunt this-any-taunt-player 249)
)
(defrule
	(taunt-detected any-human-enemy 250)
=>
	(chat-to-player this-any-human-enemy "The Horde Artificial Intelligence Script 3.9")
	(acknowledge-taunt this-any-human-enemy 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human 250)
	)
=>
	(chat-to-player this-any-computer "The Horde Artificial Intelligence Script 3.9")
	(chat-to-player this-any-human "The Horde Artificial Intelligence Script 3.9")
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal strategy-goal sling)
=>
	(chat-to-player this-any-computer "I am slinging")
	(chat-to-player this-any-human-ally "I am slinging")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal control-goal shot)
=>
	(chat-to-player this-any-computer "I am shot")
	(chat-to-player this-any-human-ally "I am shot")
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal strategy-goal rush)
=>
	(chat-to-player this-any-computer "I go fast castle age.")
	(chat-to-player this-any-human-ally "I go fast castle age.")
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal strategy-goal flush)
=>
	(chat-to-player this-any-computer "I train an army in feudal age.")
	(chat-to-player this-any-human-ally "I train an army in feudal age.")
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal strategy-goal fast-imp)
=>
	(chat-to-player this-any-computer "I go fast imperial age.")
	(chat-to-player this-any-human-ally "I go fast imperial age.")
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal strategy-goal boom)
=>
	(chat-to-player this-any-computer "I will boom.")
	(chat-to-player this-any-human-ally "I will boom.")
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal monk)
=>
	(chat-to-player this-any-computer "I focus on monks.")
	(chat-to-player this-any-human-ally "I focus on monks.")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal eagle-warrior)
=>
	(chat-to-player this-any-computer "I focus on eagle warriors.")
	(chat-to-player this-any-human-ally "I focus on eagle warriors.")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(or
		(civ-selected byzantine)
		(civ-selected indian)
	)
	(goal unit-goal knight)
=>
	(chat-to-player this-any-computer "I focus on camels.")
	(chat-to-player this-any-human-ally "I focus on camels")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal knight)
=>
	(chat-to-player this-any-computer "I focus on knights.")
	(chat-to-player this-any-human-ally "I focus on knights")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal archer)
=>
	(chat-to-player this-any-computer "I focus on archers.")
	(chat-to-player this-any-human-ally "I focus on archers")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal cavalry-archer)
=>
	(chat-to-player this-any-computer "I focus on cavalry archers.")
	(chat-to-player this-any-human-ally "I focus on cavalry archers")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal skirmisher)
=>
	(chat-to-player this-any-computer "I focus on trash units.")
	(chat-to-player this-any-human-ally "I focus on trash units")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal militiaman-line)
=>
	(chat-to-player this-any-computer "I focus on infantry.")
	(chat-to-player this-any-human-ally "I focus on infantry")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal my-unique-unit-line)
=>
	(chat-to-player this-any-computer "I focus on unique units.")
	(chat-to-player this-any-human-ally "I focus on unique units")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal scout-cavalry)
=>
	(chat-to-player this-any-computer "I focus on scouts.")
	(chat-to-player this-any-human-ally "I focus on scouts")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal gunpowder-class)
=>
	(chat-to-player this-any-computer "I focus on guns.")
	(chat-to-player this-any-human-ally "I focus on guns")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
	(goal unit-goal scorpion)
=>
	(chat-to-player this-any-computer "I focus scorpions.")
	(chat-to-player this-any-human-ally "I focus scorpions.")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(or
		(taunt-detected any-computer 250)
		(taunt-detected any-human-ally 250)
	)
=>
	(chat-to-player this-any-computer "I don't focus on any particular unit.")
	(chat-to-player this-any-human-ally "I don't focus on any particular unit.")
	(acknowledge-taunt this-any-computer 250)
	(acknowledge-taunt this-any-human-ally 250)
)
(defrule
	(taunt-detected any-human 250)
=>
	(acknowledge-taunt this-any-human 250)
)
#load-if-defined DIFFICULTY-HARDEST
#load-if-not-defined DEATH-MATCH
#load-if-not-defined DEFEND-WONDER
(defrule
	(current-age >= dark-age)
=>
	(cc-add-resource food -500)
	(cc-add-resource wood -500)
	(cc-add-resource gold -500)
	(cc-add-resource stone -500)
	(disable-self)
)
#load-if-defined NOMAD-MAP
(defrule
	(cc-players-building-type-count any-enemy town-center > 0); dont get an instant TC on LN
=>
	(cc-add-resource wood 275)
	(cc-add-resource stone 100)
)
#end-if
(defrule
	(current-age >= dark-age)
	(cc-players-building-type-count any-enemy town-center > 0)
=>
	(cc-add-resource food 300)
	(cc-add-resource wood 250)
	(cc-add-resource gold 100)
	(disable-self)
)
(defrule
	(current-age >= feudal-age)
=>
	(cc-add-resource food -350)
	(cc-add-resource wood -350)
	(cc-add-resource gold -500)
	(cc-add-resource stone -500)
	(disable-self)
)
(defrule
	(current-age >= castle-age)
=>
	(cc-add-resource food -200)
	(cc-add-resource wood -200)
	(cc-add-resource gold -500)
	(cc-add-resource stone -500)
	(disable-self)
)
(defrule
	(current-age >= castle-age)
	(unit-type-count-total villager > 22)
	(unit-type-count villager-gold > 0); only get free res if we can get them otherwise
=>
	(cc-add-resource gold 200)
	(disable-self)
)
(defrule
	(civilian-population > 15)
	(goal strategy-goal flush)
	(current-age == feudal-age)
	(timer-triggered one-minute-timer)
	(current-age-time > 120)
=>
	(cc-add-resource food 100)
	(cc-add-resource wood 70)
	(cc-add-resource gold 20)
)
(defrule
	(civilian-population >= fourty-percent-pop)
	(timer-triggered one-minute-timer)
=>
	(cc-add-resource food 100)
)
(defrule
	(civilian-population >= fourty-percent-pop)
	(timer-triggered one-minute-timer)
	(dropsite-min-distance wood < 20)
	(dropsite-min-distance wood > -1)
=>
	(cc-add-resource wood 350)
)
(defrule
	(civilian-population >= fourty-percent-pop)
	(timer-triggered one-minute-timer)
	(or
		(unit-type-count villager-gold > 0)
		(unit-type-count trade-cart > 5)
	)
=>
	(cc-add-resource gold 100)
)
#end-if
#end-if
#end-if
(defrule
	(unit-type-count-total villager == 0)
	(building-type-count town-center == 0)
	(wood-amount >= 250)
	(players-population any-ally > 70)
	(up-timer-status five-seconds-timer != timer-running)
=>
	(up-get-player-fact this-any-ally player-number 0 temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(tribute-to-player focus-player wood 100)
)
(defrule
	(unit-type-count-total villager == 0)
	(building-type-count town-center == 0)
	(food-amount >= 1)
	(players-population any-ally > 70)
	(up-timer-status five-seconds-timer != timer-running)
=>
	(up-get-player-fact this-any-ally player-number 0 temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(tribute-to-player focus-player food 1000)
)
(defrule
	(unit-type-count-total villager == 0)
	(building-type-count town-center == 0)
	(gold-amount >= 250)
	(players-population any-ally > 70)
	(up-timer-status five-seconds-timer != timer-running)
=>
	(up-get-player-fact this-any-ally player-number 0 temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(tribute-to-player focus-player gold 100)
)
(defrule
	(unit-type-count-total villager == 0)
	(building-type-count town-center == 0)
	(stone-amount >= 250)
	(players-population any-ally > 70)
	(up-timer-status five-seconds-timer != timer-running)
=>
	(up-get-player-fact this-any-ally player-number 0 temp-goal)
	(up-modify-sn sn-focus-player-number g:= temp-goal)
	(tribute-to-player focus-player stone 100)
)
(defrule
	(unit-type-count-total villager == 0)
	(or
		(and
			(food-amount < 50)
			(not (player-in-game any-ally))
		)
		(building-type-count town-center == 0)
	)
	(military-population == 0)
	(building-type-count market == 0)
	(building-type-count monastery == 0)
=>
	(resign)
)
#load-if-not-defined UP-SCENARIO-GAME
(defrule
	(game-time > 600)
	(goal ffa-game-goal -1)
	(or
		(player-resigned any-human-enemy)
		(and
			(player-resigned any-computer-enemy)
			(not(player-in-game any-computer-enemy))
		)
	)
=>
	(chat-to-all "gg")
	(disable-self)
)
(defrule
	(game-time > 600)
	(goal ffa-game-goal -1)
	(population < fifty-percent-pop)
	(players-population every-ally < fifty-percent-pop)
	(or
		(player-resigned any-ally)
		(and
			(population < twenty-five-percent-pop)
			(players-population any-enemy > eighty-five-percent-pop)
		)
	)
=>
	(chat-to-all "gg")
	(resign)
)
#end-if
(defrule
	(up-timer-status five-seconds-timer != timer-running)
=>
	(enable-timer five-seconds-timer 5)
)
(defrule
	(up-timer-status one-minute-timer != timer-running)
=>
	(set-strategic-number sn-food-modifier-percentage 0)
	(set-strategic-number sn-wood-modifier-percentage 0)
	(set-strategic-number sn-gold-modifier-percentage 0)
	(set-strategic-number sn-stone-modifier-percentage 0)
	(enable-timer one-minute-timer 60)
)
(defrule
	(taunt-detected any-computer 248)
=>
	(up-chat-data-to-all "patrol target: %d" g: patrol-goal)
	(up-chat-data-to-all "focus-player: %d" s: sn-focus-player-number)
	(up-chat-data-to-all "target-player: %d" s: sn-target-player-number)
	(up-chat-data-to-all "second-enemy: %d" g: second-enemy-goal)
	(up-chat-data-to-all "hostility level: %d" g: hostilities-goal)
	(up-chat-data-to-all "duc status: %d" g: duc-control-goal)
	(up-chat-data-to-all "military level: %d" s: sn-military-level)
	(up-chat-data-to-all "maximum town size: %d" s: sn-maximum-town-size)
	(up-chat-data-to-all "disable-attack-groups = %d" s: sn-disable-attack-groups)
	(up-chat-data-to-all "disable-defend-groups = %d" s: sn-disable-defend-groups)
	(acknowledge-taunt this-any-computer 248)
)
(defrule	
	(taunt-detected any-computer 247)
=>
	(up-chat-data-to-all "Food: %d %" s: sn-food-gatherer-percentage)
	(up-chat-data-to-all "Wood: %d %" s: sn-wood-gatherer-percentage)
	(up-chat-data-to-all "Gold: %d %" s: sn-gold-gatherer-percentage)
	(up-chat-data-to-all "Stone: %d %" s: sn-stone-gatherer-percentage)
	(acknowledge-taunt this-any-computer 247)
)
